# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  generate_java = true
  sources = [
    "overscroll_behavior.mojom",
    "touch_action.mojom",
  ]

  public_deps = [ "//mojo/public/mojom/base" ]

  touch_action_typemap = {
    types = [
      {
        mojom = "cc.mojom.TouchAction"
        cpp = "::cc::TouchAction"
      },
    ]
    traits_headers = [ "//cc/ipc/cc_param_traits_macros.h" ]
    traits_public_deps = [ "//cc/ipc" ]
  }

  overscroll_behavior_typemap = {
    types = [
      {
        mojom = "cc.mojom.OverscrollBehavior"
        cpp = "::cc::OverscrollBehavior"
      },
    ]
    traits_headers = [ "//cc/ipc/cc_param_traits_macros.h" ]
    traits_public_deps = [ "//cc/ipc" ]
  }

  cpp_typemaps = [
    overscroll_behavior_typemap,
    touch_action_typemap,
  ]
  blink_cpp_typemaps = [
    overscroll_behavior_typemap,
    touch_action_typemap,
  ]
}
