# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")

source_set("lib") {
  sources = [
    "directory_impl.cc",
    "directory_impl.h",
    "file_impl.cc",
    "file_impl.h",
    "file_system_impl.cc",
    "file_system_impl.h",
    "lock_table.cc",
    "lock_table.h",
    "util.cc",
    "util.h",
  ]

  deps = [
    "//base",
    "//components/filesystem/public/interfaces",
    "//mojo/common",
    "//mojo/common:common_base",
    "//mojo/platform_handle",
    "//mojo/shell/public/cpp",
    "//mojo/shell/public/interfaces",
    "//url",
  ]
}

mojo_native_application("filesystem") {
  sources = [
    "file_system_app.cc",
    "file_system_app.h",
    "main.cc",
  ]

  deps = [
    ":lib",
    "//base",
    "//components/filesystem/public/interfaces",
    "//mojo/common",
    "//mojo/platform_handle:for_shared_library",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//mojo/services/tracing/public/cpp",
    "//mojo/shell/public/cpp",
  ]

  data_deps = [
    ":manifest",
  ]
}

mojo_application_manifest("manifest") {
  application_name = "filesystem"
  source = "manifest.json"
}

mojo_native_application("apptests") {
  output_name = "filesystem_apptests"

  testonly = true

  sources = [
    "directory_impl_unittest.cc",
    "file_impl_unittest.cc",
    "files_test_base.cc",
    "files_test_base.h",
  ]

  deps = [
    "//base",
    "//components/filesystem/public/interfaces",
    "//mojo/common",
    "//mojo/platform_handle:for_shared_library",
    "//mojo/public/cpp/bindings",
    "//mojo/shell/public/cpp:test_support",
  ]

  data_deps = [
    ":apptest_manifest",
    ":filesystem",
  ]
}

mojo_application_manifest("apptest_manifest") {
  application_name = "filesystem_apptests"
  source = "apptest_manifest.json"
}
