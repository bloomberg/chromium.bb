# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}
import("//third_party/protobuf/proto_library.gni")

static_library("prefetch") {
  sources = [
    "add_unique_urls_task.cc",
    "add_unique_urls_task.h",
    "generate_page_bundle_request.cc",
    "generate_page_bundle_request.h",
    "generate_page_bundle_task.cc",
    "generate_page_bundle_task.h",
    "get_operation_request.cc",
    "get_operation_request.h",
    "get_operation_task.cc",
    "get_operation_task.h",
    "offline_metrics_collector.h",
    "prefetch_dispatcher.h",
    "prefetch_dispatcher_impl.cc",
    "prefetch_dispatcher_impl.h",
    "prefetch_gcm_app_handler.cc",
    "prefetch_gcm_app_handler.h",
    "prefetch_gcm_handler.h",
    "prefetch_item.cc",
    "prefetch_item.h",
    "prefetch_network_request_factory.h",
    "prefetch_network_request_factory_impl.cc",
    "prefetch_network_request_factory_impl.h",
    "prefetch_proto_utils.cc",
    "prefetch_proto_utils.h",
    "prefetch_request_fetcher.cc",
    "prefetch_request_fetcher.h",
    "prefetch_service.h",
    "prefetch_service_impl.cc",
    "prefetch_service_impl.h",
    "prefetch_types.cc",
    "prefetch_types.h",
    "suggested_articles_observer.cc",
    "suggested_articles_observer.h",
  ]

  public_deps = [
    ":proto",
  ]

  deps = [
    "//base",
    "//components/gcm_driver",
    "//components/gcm_driver/common",
    "//components/keyed_service/core",
    "//components/ntp_snippets",
    "//components/offline_pages/core",
    "//components/offline_pages/core:switches",
    "//components/version_info",
    "//google_apis",
    "//net:net",
    "//url",
  ]
}

static_library("test_support") {
  testonly = true
  sources = [
    "prefetch_request_test_base.cc",
    "prefetch_request_test_base.h",
    "prefetch_service_test_taco.cc",
    "prefetch_service_test_taco.h",
    "task_test_base.cc",
    "task_test_base.h",
    "test_offline_metrics_collector.h",
    "test_prefetch_dispatcher.cc",
    "test_prefetch_dispatcher.h",
    "test_prefetch_gcm_handler.cc",
    "test_prefetch_gcm_handler.h",
    "test_prefetch_network_request_factory.cc",
    "test_prefetch_network_request_factory.h",
  ]

  deps = [
    ":prefetch",
    "//base",
    "//components/gcm_driver/instance_id",
    "//components/keyed_service/core",
    "//components/offline_pages/core",
    "//components/version_info:channel",
    "//net:test_support",
  ]
}

proto_library("proto") {
  sources = [
    "proto/any.proto",
    "proto/offline_pages.proto",
    "proto/operation.proto",
    "proto/status.proto",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "generate_page_bundle_request_unittest.cc",
    "generate_page_bundle_task_unittest.cc",
    "get_operation_request_unittest.cc",
    "get_operation_task_unittest.cc",
    "prefetch_dispatcher_impl_unittest.cc",
    "prefetch_gcm_app_handler_unittest.cc",
    "prefetch_item_unittest.cc",
    "prefetch_network_request_factory_impl_unittest.cc",
    "prefetch_request_fetcher_unittest.cc",
    "prefetch_request_operation_response_unittest.cc",
    "suggested_articles_observer_unittest.cc",
  ]

  deps = [
    ":prefetch",
    ":test_support",
    "//components/gcm_driver/instance_id",
    "//components/offline_pages/core",
    "//components/offline_pages/core:switches",
    "//components/offline_pages/core:test_support",
    "//components/version_info:channel",
    "//net:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]
}
