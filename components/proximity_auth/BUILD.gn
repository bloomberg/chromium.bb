# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

static_library("proximity_auth") {
  sources = [
    "bluetooth_connection.cc",
    "bluetooth_connection.h",
    "bluetooth_connection_finder.cc",
    "bluetooth_connection_finder.h",
    "bluetooth_util.cc",
    "bluetooth_util.h",
    "bluetooth_util_chromeos.cc",
    "cryptauth_enroller_factory_impl.cc",
    "cryptauth_enroller_factory_impl.h",
    "messenger.h",
    "messenger_impl.cc",
    "messenger_impl.h",
    "messenger_observer.h",
    "metrics.cc",
    "metrics.h",
    "proximity_auth_client.h",
    "proximity_auth_pref_manager.cc",
    "proximity_auth_pref_manager.h",
    "proximity_auth_pref_names.cc",
    "proximity_auth_pref_names.h",
    "proximity_auth_system.cc",
    "proximity_auth_system.h",
    "proximity_monitor.h",
    "proximity_monitor_impl.cc",
    "proximity_monitor_impl.h",
    "proximity_monitor_observer.h",
    "remote_device_life_cycle.h",
    "remote_device_life_cycle_impl.cc",
    "remote_device_life_cycle_impl.h",
    "remote_status_update.cc",
    "remote_status_update.h",
    "screenlock_bridge.cc",
    "screenlock_bridge.h",
    "screenlock_state.h",
    "switches.cc",
    "switches.h",
    "throttled_bluetooth_connection_finder.cc",
    "throttled_bluetooth_connection_finder.h",
    "unlock_manager.cc",
    "unlock_manager.h",
  ]

  deps = [
    "//base",
    "//components/cryptauth",
    "//components/cryptauth/ble",
    "//components/prefs",
    "//components/proximity_auth/ble",
    "//components/proximity_auth/logging",
    "//components/signin/core/account_id:account_id",
    "//device/bluetooth",
    "//net",
  ]

  # TODO(https://crbug.com/562683): This whitelists a circular include
  # dependency between this target and the following targets which should not
  # exist.
  allow_circular_includes_from = [ "//components/proximity_auth/ble" ]

  if (is_chromeos) {
    deps += [ "//chromeos" ]
  }
}

static_library("test_support") {
  testonly = true

  sources = [
    "mock_proximity_auth_client.cc",
    "mock_proximity_auth_client.h",
  ]

  public_deps = [
    ":proximity_auth",
  ]

  deps = [
    "//base",
    "//components/cryptauth:test_support",
    "//components/proximity_auth/logging",
    "//testing/gmock",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "bluetooth_connection_finder_unittest.cc",
    "bluetooth_connection_unittest.cc",
    "messenger_impl_unittest.cc",
    "proximity_auth_pref_manager_unittest.cc",
    "proximity_auth_system_unittest.cc",
    "proximity_monitor_impl_unittest.cc",
    "remote_device_life_cycle_impl_unittest.cc",
    "remote_status_update_unittest.cc",
    "throttled_bluetooth_connection_finder_unittest.cc",
    "unlock_manager_unittest.cc",
  ]

  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  deps = [
    ":proximity_auth",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//components/cryptauth:test_support",
    "//components/cryptauth:unit_tests",
    "//components/prefs:test_support",
    "//components/proximity_auth/ble:unit_tests",
    "//components/proximity_auth/logging",
    "//components/proximity_auth/logging:unit_tests",
    "//device/bluetooth:mocks",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (is_chromeos) {
    deps += [ "//chromeos" ]
  }
}

# Note: This is a convenience target for ease of rapid iteration during
# development. It is not executed on any try or build bots.
if (!is_chromeos) {
  test("proximity_auth_unittests") {
    sources = [
      "run_all_unittests.cc",
    ]
    deps = [
      ":unit_tests",
      "//base",
      "//base/test:test_support",
    ]
  }
}
