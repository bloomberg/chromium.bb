# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//testing/test.gni")

source_set("exo") {
  sources = [
    "buffer.cc",
    "buffer.h",
    "display.cc",
    "display.h",
    "keyboard.cc",
    "keyboard.h",
    "keyboard_delegate.h",
    "pointer.cc",
    "pointer.h",
    "pointer_delegate.h",
    "shared_memory.cc",
    "shared_memory.h",
    "shell_surface.cc",
    "shell_surface.h",
    "sub_surface.cc",
    "sub_surface.h",
    "surface.cc",
    "surface.h",
    "surface_delegate.h",
    "surface_observer.h",
    "touch.cc",
    "touch.h",
    "touch_delegate.h",
  ]

  deps = [
    "//ash",
    "//base",
    "//cc",
    "//gpu",
    "//gpu/command_buffer/client:gles2_interface",
    "//skia",
    "//ui/aura",
    "//ui/compositor",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gl",
    "//ui/views",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "test/exo_test_base.cc",
    "test/exo_test_base.h",
    "test/exo_test_helper.cc",
    "test/exo_test_helper.h",
  ]

  deps = [
    "//ash:test_support",
    "//base",
    "//gpu",
    "//skia",
    "//testing/gtest",
    "//ui/aura",
    "//ui/compositor",
    "//ui/gfx/geometry",
    "//ui/wm:wm",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "buffer_unittest.cc",
    "display_unittest.cc",
    "keyboard_unittest.cc",
    "pointer_unittest.cc",
    "shared_memory_unittest.cc",
    "shell_surface_unittest.cc",
    "sub_surface_unittest.cc",
    "surface_unittest.cc",
    "touch_unittest.cc",
  ]

  deps = [
    ":exo",
    ":test_support",
    "//ash",
    "//base",
    "//cc",
    "//components/user_manager",
    "//gpu/command_buffer/client:gles2_interface",
    "//skia",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/aura",
    "//ui/events:dom_keycode_converter",
    "//ui/events:test_support",
    "//ui/gfx",
    "//ui/keyboard",
    "//ui/message_center",
    "//ui/views",
  ]

  if (use_ozone) {
    deps += [ "//ui/ozone" ]
  }
}

test("exo_unittests") {
  sources = [
    "test/run_all_unittests.cc",
  ]
  deps = [
    ":unit_tests",
    "//ash:test_support",
    "//base",
    "//base/test:test_support",
  ]
  if (is_linux) {
    deps += [ "//components/exo/wayland:unit_tests" ]
  }
}
