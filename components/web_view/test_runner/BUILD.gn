# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/tools/bindings/mojom.gni")

executable("test_runner") {
  output_name = "layout_test_runner"
  testonly = true

  sources = [
    "launcher.cc",
    "launcher.h",
    "main.cc",
    "register_local_aliases.cc",
  ]

  deps = [
    "//base",
    "//build/config/sanitizers:deps",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//mojo/shell/runner/host:lib",
    "//mojo/shell/standalone:lib",
    "//mojo/shell/standalone:register_local_aliases_fwd",
    "//url",
  ]

  data_deps = [
    ":web_view_test_runner",
    "//components/html_viewer",
    "//tools/imagediff",
  ]

  if (is_linux) {
    data_deps += [
      "//breakpad:dump_syms($host_toolchain)",
      "//breakpad:minidump_stackwalk($host_toolchain)",
    ]
  }
}

mojo_native_application("web_view_test_runner") {
  testonly = true

  sources = [
    "test_runner_application_delegate.cc",
    "test_runner_application_delegate.h",
    "test_runner_main.cc",
  ]

  deps = [
    "//base",
    "//components/html_viewer/public/interfaces",
    "//components/mus/public/cpp",
    "//components/test_runner",
    "//components/web_view/public/cpp",
    "//components/web_view/public/interfaces",
    "//components/web_view/test_runner/public/interfaces",
    "//mojo/common",
    "//mojo/converters/geometry",
    "//mojo/shell/public/cpp",
    "//ui/gfx/geometry",
    "//url",
  ]
}
