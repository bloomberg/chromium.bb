# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")

source_set("switches") {
  sources = [
    "web_view_switches.cc",
    "web_view_switches.h",
  ]
}

source_set("lib") {
  sources = [
    "client_initiated_frame_connection.cc",
    "client_initiated_frame_connection.h",
    "find_controller.cc",
    "find_controller.h",
    "frame.cc",
    "frame.h",
    "frame_connection.cc",
    "frame_connection.h",
    "frame_devtools_agent.cc",
    "frame_devtools_agent.h",
    "frame_devtools_agent_delegate.h",
    "frame_tree.cc",
    "frame_tree.h",
    "frame_tree_delegate.cc",
    "frame_tree_delegate.h",
    "frame_user_data.h",
    "frame_utils.cc",
    "frame_utils.h",
    "local_find_options.cc",
    "local_find_options.h",
    "navigation_controller.cc",
    "navigation_controller.h",
    "navigation_entry.cc",
    "navigation_entry.h",
    "pending_web_view_load.cc",
    "pending_web_view_load.h",
    "reload_type.h",
    "url_request_cloneable.cc",
    "url_request_cloneable.h",
    "web_view_application_delegate.cc",
    "web_view_application_delegate.h",
    "web_view_impl.cc",
    "web_view_impl.h",
  ]

  deps = [
    ":switches",
    "//base",
    "//components/clipboard/public/interfaces",
    "//components/devtools_service/public/cpp",
    "//components/devtools_service/public/interfaces",
    "//components/mus/public/cpp",
    "//components/mus/public/interfaces",
    "//components/resource_provider/public/interfaces",
    "//components/web_view/test_runner/public/interfaces",
    "//mojo/application/public/cpp:sources",
    "//mojo/application/public/interfaces",
    "//mojo/common:common_base",
    "//mojo/common:url_type_converters",
    "//mojo/converters/geometry",
    "//mojo/services/network/public/interfaces",
    "//mojo/services/tracing/public/interfaces",
    "//third_party/mojo/src/mojo/public/cpp/bindings",
    "//url",
  ]

  public_deps = [
    "//components/web_view/public/interfaces",
  ]

  if (is_linux && !is_android) {
    deps += [ "//components/font_service/public/interfaces" ]
  }
}

mojo_native_application("web_view") {
  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
    "//mojo/application/public/cpp",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "test_frame_tree_delegate.cc",
    "test_frame_tree_delegate.h",
  ]
  deps = [
    ":lib",
    "//base",
    "//testing/gtest",
  ]
}

mojo_native_application("apptests") {
  output_name = "web_view_apptests"
  testonly = true

  sources = [
    "frame_apptest.cc",
    "web_view_apptest.cc",
  ]

  deps = [
    ":lib",
    ":test_support",
    "//base",
    "//base/test:test_config",
    "//components/mus/public/cpp/tests:test_support",
    "//components/mus/public/interfaces",
    "//components/mus/public/cpp",
    "//components/web_view/public/cpp",
    "//components/web_view/public/interfaces",
    "//mojo/application/public/cpp:sources",
    "//mojo/application/public/cpp:test_support",
    "//mojo/util:filename_util",
    "//ui/mojo/geometry:interfaces",
    "//ui/mojo/geometry:util",
    "//url:url",
  ]

  data_deps = [
    ":web_view",
  ]
}
