# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("common") {
  sources = [
    "activation_decision.h",
    "activation_level.cc",
    "activation_level.h",
    "activation_list.cc",
    "activation_list.h",
    "activation_scope.cc",
    "activation_scope.h",
    "activation_state.cc",
    "activation_state.h",
    "document_load_statistics.h",
    "document_subresource_filter.cc",
    "document_subresource_filter.h",
    "first_party_origin.cc",
    "first_party_origin.h",
    "indexed_ruleset.cc",
    "indexed_ruleset.h",
    "load_policy.h",
    "memory_mapped_ruleset.cc",
    "memory_mapped_ruleset.h",
    "scoped_timers.h",
    "time_measurements.h",
  ]

  public_deps = [
    "//components/subresource_filter/core/common/flat:indexed_ruleset",
    "//components/url_pattern_index",
  ]

  deps = [
    "//base",
    "//net",
    "//third_party/flatbuffers",
    "//third_party/protobuf:protobuf_lite",
    "//url",
  ]
}

static_library("test_support") {
  testonly = true
  sources = [
    "test_ruleset_creator.cc",
    "test_ruleset_creator.h",
    "test_ruleset_utils.cc",
    "test_ruleset_utils.h",
  ]
  deps = [
    ":common",
    "//base",
    "//net",
    "//testing/gtest",
    "//third_party/protobuf:protobuf_lite",
  ]
}

static_library("tools_lib") {
  sources = [
    "tools/filter_tool.cc",
    "tools/filter_tool.h",
    "tools/indexing_tool.cc",
    "tools/indexing_tool.h",
  ]
  deps = [
    ":common",
    "//base",
    "//components/url_pattern_index:util",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "document_subresource_filter_unittest.cc",
    "first_party_origin_unittest.cc",
    "indexed_ruleset_unittest.cc",
    "scoped_timers_unittest.cc",
    "tools/filter_tool_unittest.cc",
    "tools/indexing_tool_unittest.cc",
  ]
  deps = [
    ":common",
    ":test_support",
    ":tools_lib",
    "//base",
    "//base/test:test_support",
    "//components/url_pattern_index:test_support",
    "//testing/gtest",
    "//third_party/protobuf:protobuf_lite",
    "//url",
  ]
}

if (is_linux || is_mac || is_win) {
  executable("subresource_filter_tool") {
    sources = [
      "tools/filter_tool_main.cc",
    ]
    deps = [
      ":tools_lib",
      "//base",
      "//build/config:exe_and_shlib_deps",
    ]
  }

  executable("subresource_indexing_tool") {
    sources = [
      "tools/indexing_tool_main.cc",
    ]
    deps = [
      ":tools_lib",
      "//base",
      "//build/config:exe_and_shlib_deps",
    ]
  }
}
