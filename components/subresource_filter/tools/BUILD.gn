# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromecast_build.gni")

static_library("tools_lib") {
  sources = [
    "filter_tool.cc",
    "filter_tool.h",
    "indexing_tool.cc",
    "indexing_tool.h",
  ]
  deps = [
    "../core/common",
    "//base",
    "//components/url_pattern_index:util",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "filter_tool_unittest.cc",
    "indexing_tool_unittest.cc",
  ]

  deps = [
    ":tools_lib",
    "../core/common",
    "../core/common:test_support",
    "rule_parser:unit_tests",
    "ruleset_converter:unit_tests",
    "//base",
    "//base/test:test_support",
    "//components/url_pattern_index:test_support",
    "//testing/gtest",
    "//third_party/protobuf:protobuf_lite",
    "//url",
  ]
}
if (!is_ios && !is_android && !is_chromecast && !is_fuchsia) {
  executable("subresource_filter_tool") {
    # Production code should not depend on this.
    testonly = true
    sources = [
      "filter_tool_main.cc",
    ]
    deps = [
      ":tools_lib",
      "//base",
      "//build/config:exe_and_shlib_deps",
    ]
  }

  executable("subresource_indexing_tool") {
    # Production code should not depend on this.
    testonly = true
    sources = [
      "indexing_tool_main.cc",
    ]
    deps = [
      ":tools_lib",
      "//base",
      "//build/config:exe_and_shlib_deps",
    ]
  }

  group("subresource_filter_tools") {
    # Production code should not depend on this.
    testonly = true
    deps = [
      ":subresource_filter_tool",
      ":subresource_indexing_tool",
      "ruleset_converter",
    ]
  }
}
