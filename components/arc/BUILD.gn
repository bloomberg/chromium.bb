# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

static_library("arc") {
  sources = [
    "audio/arc_audio_bridge.cc",
    "audio/arc_audio_bridge.h",
    "bluetooth/bluetooth_struct_traits.cc",
    "bluetooth/bluetooth_struct_traits.h",
    "bluetooth/bluetooth_type_converters.cc",
    "bluetooth/bluetooth_type_converters.h",
    "clipboard/arc_clipboard_bridge.cc",
    "clipboard/arc_clipboard_bridge.h",
    "crash_collector/arc_crash_collector_bridge.cc",
    "crash_collector/arc_crash_collector_bridge.h",
    "file_system/file_system_struct_traits.cc",
    "file_system/file_system_struct_traits.h",
    "ime/arc_ime_bridge.h",
    "ime/arc_ime_bridge_impl.cc",
    "ime/arc_ime_bridge_impl.h",
    "ime/arc_ime_service.cc",
    "ime/arc_ime_service.h",
    "intent_helper/activity_icon_loader.cc",
    "intent_helper/activity_icon_loader.h",
    "intent_helper/arc_intent_helper_bridge.cc",
    "intent_helper/arc_intent_helper_bridge.h",
    "intent_helper/arc_intent_helper_observer.h",
    "intent_helper/font_size_util.cc",
    "intent_helper/font_size_util.h",
    "intent_helper/intent_constants.cc",
    "intent_helper/intent_constants.h",
    "intent_helper/intent_filter.cc",
    "intent_helper/intent_filter.h",
    "intent_helper/link_handler_model.cc",
    "intent_helper/link_handler_model.h",
    "intent_helper/page_transition_util.cc",
    "intent_helper/page_transition_util.h",
    "lock_screen/arc_lock_screen_bridge.cc",
    "lock_screen/arc_lock_screen_bridge.h",
    "metrics/arc_metrics_service.cc",
    "metrics/arc_metrics_service.h",
    "midis/arc_midis_bridge.cc",
    "midis/arc_midis_bridge.h",
    "net/arc_net_host_impl.cc",
    "net/arc_net_host_impl.h",
    "obb_mounter/arc_obb_mounter_bridge.cc",
    "obb_mounter/arc_obb_mounter_bridge.h",
    "power/arc_power_bridge.cc",
    "power/arc_power_bridge.h",
    "rotation_lock/arc_rotation_lock_bridge.cc",
    "rotation_lock/arc_rotation_lock_bridge.h",
    "storage_manager/arc_storage_manager.cc",
    "storage_manager/arc_storage_manager.h",
    "video_accelerator/video_accelerator_struct_traits.cc",
    "video_accelerator/video_accelerator_struct_traits.h",
    "volume_mounter/arc_volume_mounter_bridge.cc",
    "volume_mounter/arc_volume_mounter_bridge.h",
  ]

  public_deps = [
    ":arc_base",
    ":prefs",
  ]

  deps = [
    "//ash:ash",
    "//ash/public/cpp:ash_public_cpp",
    "//base",
    "//chromeos",
    "//chromeos:login_manager_proto",
    "//chromeos:power_manager_proto",
    "//components/exo",
    "//components/google/core/browser",
    "//components/onc",
    "//components/prefs",
    "//components/session_manager/core",
    "//components/signin/core/account_id",
    "//components/user_manager",
    "//device/bluetooth",
    "//google_apis",
    "//mojo/edk/system",
    "//skia",
    "//third_party/re2:re2",
    "//ui/aura",
    "//ui/base:base",
    "//ui/base/ime",
    "//ui/display/manager",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
    "//ui/keyboard:keyboard",
    "//url:url",
  ]
}

static_library("prefs") {
  sources = [
    "arc_export.h",
    "arc_prefs.cc",
    "arc_prefs.h",
  ]

  defines = [ "ARC_IMPLEMENTATION" ]

  deps = [
    "//components/prefs",
  ]
}

static_library("arc_base") {
  # TODO(hidehiko): Revisit here and move back some files to "arc"
  # on completion to move ArcSession task to ArcSessionManager.
  sources = [
    "arc_bridge_host_impl.cc",
    "arc_bridge_host_impl.h",
    "arc_bridge_service.cc",
    "arc_bridge_service.h",
    "arc_browser_context_keyed_service_factory_base.h",
    "arc_data_remover.cc",
    "arc_data_remover.h",
    "arc_features.cc",
    "arc_features.h",
    "arc_instance_mode.cc",
    "arc_instance_mode.h",
    "arc_service_manager.cc",
    "arc_service_manager.h",
    "arc_session.cc",
    "arc_session.h",
    "arc_session_runner.cc",
    "arc_session_runner.h",
    "arc_stop_reason.cc",
    "arc_stop_reason.h",
    "arc_util.cc",
    "arc_util.h",
    "instance_holder.h",
  ]

  deps = [
    "//base",
    "//chromeos",
    "//components/keyed_service/content",
    "//components/prefs",
    "//components/signin/core/account_id",
    "//components/user_manager",
    "//mojo/edk/system",
    "//ui/aura",
  ]

  public_deps = [
    ":arc_bindings",
    ":prefs",
  ]
}

mojom("arc_bindings") {
  sources = [
    "common/accessibility_helper.mojom",
    "common/app.mojom",
    "common/arc_bridge.mojom",
    "common/audio.mojom",
    "common/auth.mojom",
    "common/bitmap.mojom",
    "common/bluetooth.mojom",
    "common/boot_phase_monitor.mojom",
    "common/cast_receiver.mojom",
    "common/cert_store.mojom",
    "common/clipboard.mojom",
    "common/crash_collector.mojom",
    "common/enterprise_reporting.mojom",
    "common/file_system.mojom",
    "common/ime.mojom",
    "common/intent_helper.mojom",
    "common/kiosk.mojom",
    "common/lock_screen.mojom",
    "common/metrics.mojom",
    "common/midis.mojom",
    "common/net.mojom",
    "common/notifications.mojom",
    "common/obb_mounter.mojom",
    "common/oemcrypto.mojom",
    "common/oemcrypto_daemon.mojom",
    "common/policy.mojom",
    "common/power.mojom",
    "common/print.mojom",
    "common/process.mojom",
    "common/protected_buffer_manager.mojom",
    "common/rotation_lock.mojom",
    "common/scale_factor.mojom",
    "common/screen_rect.mojom",
    "common/storage_manager.mojom",
    "common/tracing.mojom",
    "common/tts.mojom",
    "common/video.mojom",
    "common/video_common.mojom",
    "common/video_decode_accelerator.mojom",
    "common/video_encode_accelerator.mojom",
    "common/voice_interaction_arc_home.mojom",
    "common/voice_interaction_framework.mojom",
    "common/volume_mounter.mojom",
    "common/wallpaper.mojom",
  ]

  public_deps = [
    "//mojo/common:common_custom_types",
    "//ui/gfx/geometry/mojo",
  ]
}

static_library("arc_test_support") {
  testonly = true
  sources = [
    "test/fake_app_instance.cc",
    "test/fake_app_instance.h",
    "test/fake_arc_session.cc",
    "test/fake_arc_session.h",
    "test/fake_bluetooth_instance.cc",
    "test/fake_bluetooth_instance.h",
    "test/fake_file_system_instance.cc",
    "test/fake_file_system_instance.h",
    "test/fake_intent_helper_instance.cc",
    "test/fake_intent_helper_instance.h",
    "test/fake_notifications_instance.cc",
    "test/fake_notifications_instance.h",
    "test/fake_policy_instance.cc",
    "test/fake_policy_instance.h",
    "test/fake_voice_interaction_framework_instance.cc",
    "test/fake_voice_interaction_framework_instance.h",
    "test/fake_wallpaper_instance.cc",
    "test/fake_wallpaper_instance.h",
  ]

  public_deps = [
    ":arc",
  ]

  deps = [
    "//base",
    "//mojo/common:common_base",
    "//mojo/edk/embedder:headers",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "arc_data_remover_unittest.cc",
    "arc_session_runner_unittest.cc",
    "arc_util_unittest.cc",
    "bluetooth/bluetooth_struct_traits_unittest.cc",
    "bluetooth/bluetooth_type_converters_unittest.cc",
    "ime/arc_ime_service_unittest.cc",
    "intent_helper/activity_icon_loader_unittest.cc",
    "intent_helper/arc_intent_helper_bridge_unittest.cc",
    "intent_helper/font_size_util_unittest.cc",
    "intent_helper/intent_filter_unittest.cc",
    "intent_helper/link_handler_model_unittest.cc",
    "intent_helper/page_transition_util_unittest.cc",
  ]

  deps = [
    ":arc_test_support",
    "//ash/public/cpp:ash_public_cpp",
    "//base",
    "//base/test:test_support",
    "//chromeos",
    "//chromeos:test_support_without_gmock",
    "//components/prefs:test_support",
    "//components/signin/core/account_id",
    "//components/user_manager",
    "//components/user_manager:test_support",
    "//device/bluetooth",
    "//mojo/public/cpp/system:system",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/aura",
    "//ui/aura:test_support",
    "//ui/base:test_support",
    "//ui/base/ime",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
    "//url:url",
  ]
}
