# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# CryptAuth is a component which manages data about devices associated with a
# user's account. This component both sends data about the current device and
# requets data about associated devices.

assert(is_chromeos, "CryptAuth is Chrome OS only")

static_library("cryptauth") {
  sources = [
    "authenticator.cc",
    "authenticator.h",
    "background_eid_generator.cc",
    "background_eid_generator.h",
    "connection.cc",
    "connection.h",
    "cryptauth_service.cc",
    "cryptauth_service.h",
    "data_with_timestamp.cc",
    "data_with_timestamp.h",
    "device_to_device_authenticator.cc",
    "device_to_device_authenticator.h",
    "device_to_device_initiator_helper.cc",
    "device_to_device_initiator_helper.h",
    "device_to_device_secure_context.cc",
    "device_to_device_secure_context.h",
    "foreground_eid_generator.cc",
    "foreground_eid_generator.h",
    "raw_eid_generator.h",
    "raw_eid_generator_impl.cc",
    "raw_eid_generator_impl.h",
    "secure_channel.cc",
    "secure_channel.h",
    "secure_context.h",
    "session_keys.cc",
    "session_keys.h",
    "wire_message.cc",
    "wire_message.h",
  ]

  deps = [
    ":secure_message",
    "//base",
    "//chromeos",
    "//chromeos/components/multidevice",
    "//chromeos/components/proximity_auth/logging",
    "//chromeos/services/device_sync",
    "//chromeos/services/device_sync/proto:util",
    "//components/cryptauth/ble",
    "//components/gcm_driver",
    "//components/gcm_driver/common",
    "//components/prefs",
    "//components/version_info",
    "//crypto",
    "//google_apis",
    "//net",
    "//services/identity/public/cpp",
  ]

  public_deps = [
    "//chromeos/services/device_sync/proto",
  ]

  # TODO (hansberry): Resolve this.
  allow_circular_includes_from = [ "//components/cryptauth/ble" ]
}

static_library("secure_message") {
  sources = [
    "secure_message_delegate.cc",
    "secure_message_delegate.h",
    "secure_message_delegate_impl.cc",
    "secure_message_delegate_impl.h",
  ]

  deps = [
    "//base",
    "//chromeos",
    "//chromeos/components/proximity_auth/logging",
  ]

  public_deps = [
    "//chromeos/services/device_sync/proto",
  ]
}

static_library("secure_message_test_support") {
  sources = [
    "fake_secure_message_delegate.cc",
    "fake_secure_message_delegate.h",
  ]

  deps = [
    ":secure_message",
    "//base",
  ]
}

static_library("test_support") {
  testonly = true

  sources = [
    "device_to_device_responder_operations.cc",
    "device_to_device_responder_operations.h",
    "fake_authenticator.cc",
    "fake_authenticator.h",
    "fake_background_eid_generator.cc",
    "fake_background_eid_generator.h",
    "fake_connection.cc",
    "fake_connection.h",
    "fake_cryptauth_service.cc",
    "fake_cryptauth_service.h",
    "fake_secure_channel.cc",
    "fake_secure_channel.h",
    "fake_secure_context.cc",
    "fake_secure_context.h",
    "mock_foreground_eid_generator.cc",
    "mock_foreground_eid_generator.h",
  ]

  public_deps = [
    ":cryptauth",
    "//chromeos/components/multidevice",
    "//chromeos/services/device_sync/proto",
  ]

  deps = [
    ":secure_message",
    "//base",
    "//chromeos/components/proximity_auth/logging",
    "//chromeos/services/device_sync:test_support",
    "//testing/gmock",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "background_eid_generator_unittest.cc",
    "connection_unittest.cc",
    "device_to_device_authenticator_unittest.cc",
    "device_to_device_operations_unittest.cc",
    "device_to_device_secure_context_unittest.cc",
    "fake_secure_message_delegate_unittest.cc",
    "foreground_eid_generator_unittest.cc",
    "raw_eid_generator_impl_unittest.cc",
    "secure_channel_unittest.cc",
    "session_keys_unittest.cc",
    "wire_message_unittest.cc",
  ]

  deps = [
    ":cryptauth",
    ":secure_message",
    ":secure_message_test_support",
    ":test_support",
    "//base/test:test_support",
    "//chromeos/components/multidevice",
    "//chromeos/components/multidevice:test_support",
    "//chromeos/services/device_sync/proto:util",
    "//components/cryptauth/ble:unit_tests",
    "//components/gcm_driver:test_support",
    "//components/prefs:test_support",
    "//google_apis:test_support",
    "//net:test_support",
    "//services/identity/public/cpp:test_support",
    "//testing/gtest",
  ]
}
