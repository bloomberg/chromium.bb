# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/libfuzzer/fuzzer_test.gni")

component("cbor") {
  sources = [
    "cbor_binary.h",
    "cbor_reader.cc",
    "cbor_reader.h",
    "cbor_values.cc",
    "cbor_values.h",
    "cbor_writer.cc",
    "cbor_writer.h",
  ]

  defines = [ "CBOR_IMPLEMENTATION" ]

  deps = [
    "//base",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "cbor_reader_unittest.cc",
    "cbor_values_unittest.cc",
    "cbor_writer_unittest.cc",
  ]

  deps = [
    ":cbor",
    "//base",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

fuzzer_test("cbor_reader_fuzzer") {
  sources = [
    "cbor_reader_fuzzer.cc",
  ]
  deps = [
    ":cbor",
    "//base",
  ]
  seed_corpus = "cbor_reader_fuzzer_corpus/"
  libfuzzer_options = [ "max_len=65535" ]
}
