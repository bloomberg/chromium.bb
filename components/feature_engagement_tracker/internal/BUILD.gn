# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

static_library("internal") {
  visibility = [
    ":*",
    "//components/feature_engagement_tracker",
    "//components/feature_engagement_tracker/test:test_support",
  ]

  sources = [
    "availability_model.h",
    "availability_model_impl.cc",
    "availability_model_impl.h",
    "availability_store.cc",
    "availability_store.h",
    "chrome_variations_configuration.cc",
    "chrome_variations_configuration.h",
    "condition_validator.cc",
    "condition_validator.h",
    "configuration.cc",
    "configuration.h",
    "editable_configuration.cc",
    "editable_configuration.h",
    "feature_config_condition_validator.cc",
    "feature_config_condition_validator.h",
    "feature_config_storage_validator.cc",
    "feature_config_storage_validator.h",
    "feature_engagement_tracker_impl.cc",
    "feature_engagement_tracker_impl.h",
    "in_memory_store.cc",
    "in_memory_store.h",
    "init_aware_model.cc",
    "init_aware_model.h",
    "model.h",
    "model_impl.cc",
    "model_impl.h",
    "never_availability_model.cc",
    "never_availability_model.h",
    "never_condition_validator.cc",
    "never_condition_validator.h",
    "never_storage_validator.cc",
    "never_storage_validator.h",
    "once_condition_validator.cc",
    "once_condition_validator.h",
    "persistent_store.cc",
    "persistent_store.h",
    "single_invalid_configuration.cc",
    "single_invalid_configuration.h",
    "stats.cc",
    "stats.h",
    "storage_validator.h",
    "store.h",
    "system_time_provider.cc",
    "system_time_provider.h",
    "time_provider.h",
  ]

  public_deps = [
    "//components/feature_engagement_tracker/internal/proto",
  ]

  deps = [
    "//base",
    "//components/feature_engagement_tracker/public",
    "//components/keyed_service/core",
    "//components/leveldb_proto",
  ]

  if (is_android) {
    sources += [
      "android/feature_engagement_tracker_impl_android.cc",
      "android/feature_engagement_tracker_impl_android.h",
      "android/feature_engagement_tracker_jni_registrar.cc",
    ]

    deps += [ ":jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true

  visibility = [ "//components/feature_engagement_tracker:unit_tests" ]

  # IMPORTANT NOTE: When adding new tests, also remember to update the list of
  # tests in //components/feature_engagement_tracker/components_unittests.filter
  sources = [
    "availability_model_impl_unittest.cc",
    "availability_store_unittest.cc",
    "chrome_variations_configuration_unittest.cc",
    "condition_validator_unittest.cc",
    "configuration_unittest.cc",
    "editable_configuration_unittest.cc",
    "feature_config_condition_validator_unittest.cc",
    "feature_config_storage_validator_unittest.cc",
    "feature_engagement_tracker_impl_unittest.cc",
    "in_memory_store_unittest.cc",
    "init_aware_model_unittest.cc",
    "model_impl_unittest.cc",
    "never_availability_model_unittest.cc",
    "never_condition_validator_unittest.cc",
    "never_storage_validator_unittest.cc",
    "once_condition_validator_unittest.cc",
    "persistent_store_unittest.cc",
    "single_invalid_configuration_unittest.cc",
    "system_time_provider_unittest.cc",
  ]

  deps = [
    ":internal",
    "//base/test:test_support",
    "//components/feature_engagement_tracker/internal/test:test_support",
    "//components/feature_engagement_tracker/public",
    "//components/leveldb_proto:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

if (is_android) {
  android_library("internal_java") {
    visibility = [ "//components/feature_engagement_tracker:feature_engagement_tracker_java" ]

    java_files = [ "android/java/src/org/chromium/components/feature_engagement_tracker/internal/FeatureEngagementTrackerImpl.java" ]

    deps = [
      "//base:base_java",
      "//components/feature_engagement_tracker/public:public_java",
    ]
  }

  generate_jni("jni_headers") {
    visibility = [ ":*" ]
    sources = [
      "android/java/src/org/chromium/components/feature_engagement_tracker/internal/FeatureEngagementTrackerImpl.java",
    ]
    jni_package = "components/feature_engagement_tracker/internal"
  }
}
