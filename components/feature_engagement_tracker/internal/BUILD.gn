# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

static_library("internal") {
  visibility = [
    ":*",
    "//components/feature_engagement_tracker",
  ]

  sources = [
    "condition_validator.h",
    "configuration.cc",
    "configuration.h",
    "editable_configuration.cc",
    "editable_configuration.h",
    "feature_constants.cc",
    "feature_engagement_tracker_impl.cc",
    "feature_engagement_tracker_impl.h",
    "feature_list.cc",
    "feature_list.h",
    "in_memory_store.cc",
    "in_memory_store.h",
    "model.h",
    "model_impl.cc",
    "model_impl.h",
    "once_condition_validator.cc",
    "once_condition_validator.h",
    "store.h",
  ]

  deps = [
    "//base",
    "//components/feature_engagement_tracker/public",
    "//components/keyed_service/core",
  ]

  if (is_android) {
    sources += [
      "android/feature_engagement_tracker_impl_android.cc",
      "android/feature_engagement_tracker_impl_android.h",
      "android/feature_engagement_tracker_jni_registrar.cc",
    ]

    deps += [ ":jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true

  visibility = [ "//components/feature_engagement_tracker:unit_tests" ]

  sources = [
    "editable_configuration_unittest.cc",
    "feature_engagement_tracker_impl_unittest.cc",
    "in_memory_store_unittest.cc",
    "model_impl_unittest.cc",
    "once_condition_validator_unittest.cc",
  ]

  deps = [
    ":internal",
    "//base/test:test_support",
    "//testing/gtest",
  ]
}

if (is_android) {
  android_library("internal_java") {
    visibility = [ "//components/feature_engagement_tracker:feature_engagement_tracker_java" ]

    java_files = [ "android/java/src/org/chromium/components/feature_engagement_tracker/internal/FeatureEngagementTrackerImpl.java" ]

    deps = [
      "//base:base_java",
      "//components/feature_engagement_tracker/public:public_java",
    ]
  }

  generate_jni("jni_headers") {
    visibility = [ ":*" ]
    sources = [
      "android/java/src/org/chromium/components/feature_engagement_tracker/internal/FeatureEngagementTrackerImpl.java",
    ]
    jni_package = "components/feature_engagement_tracker/internal"
  }
}
