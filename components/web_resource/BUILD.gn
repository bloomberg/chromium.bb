# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("web_resource") {
  sources = [
    "eula_accepted_notifier.cc",
    "eula_accepted_notifier.h",
    "notification_promo.cc",
    "notification_promo.h",
    "promo_resource_service.cc",
    "promo_resource_service.h",
    "resource_request_allowed_notifier.cc",
    "resource_request_allowed_notifier.h",
    "web_resource_pref_names.cc",
    "web_resource_pref_names.h",
    "web_resource_service.cc",
    "web_resource_service.h",
    "web_resource_switches.cc",
    "web_resource_switches.h",
  ]

  if (is_android || is_ios) {
    sources += [
      "notification_promo_mobile_ntp.cc",
      "notification_promo_mobile_ntp.h",
    ]
  }

  deps = [
    "//base",
    "//components/google/core/browser",
    "//components/pref_registry",
    "//components/prefs",
    "//components/version_info",
    "//net",
    "//ui/base",
  ]
}

static_library("test_support") {
  sources = [
    "resource_request_allowed_notifier_test_util.cc",
    "resource_request_allowed_notifier_test_util.h",
  ]

  deps = [
    ":web_resource",
    "//base",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "eula_accepted_notifier_unittest.cc",
    "promo_resource_service_unittest.cc",
    "resource_request_allowed_notifier_unittest.cc",
  ]

  if (is_android || is_ios) {
    sources += [ "promo_resource_service_mobile_ntp_unittest.cc" ]
  }

  deps = [
    ":test_support",
    ":web_resource",
    "//base",
    "//components/prefs:test_support",
    "//components/version_info",
    "//net:test_support",
    "//testing/gtest",
    "//third_party/icu",
  ]
}
