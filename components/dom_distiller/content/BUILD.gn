# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (!is_ios) {
  # GYP version: components/dom_distiller.gypi:dom_distiller_content
  static_library("content_browser") {
    sources = [
      "browser/distillable_page_utils.cc",
      "browser/distillable_page_utils.h",
      "browser/distiller_javascript_utils.cc",
      "browser/distiller_javascript_utils.h",
      "browser/distiller_page_web_contents.cc",
      "browser/distiller_page_web_contents.h",
      "browser/dom_distiller_viewer_source.cc",
      "browser/dom_distiller_viewer_source.h",
      "browser/web_contents_main_frame_observer.cc",
      "browser/web_contents_main_frame_observer.h",
    ]

    public_deps = [
      "//components/dom_distiller/core",
    ]
    deps = [
      "//base",
      "//components/resources",
      "//components/strings",
      "//content/public/browser",
      "//net",
      "//skia",
      "//sync",
      "//ui/gfx",
      "//url",
    ]

    if (is_android) {
      sources += [
        "browser/distillable_page_utils_android.cc",
        "browser/distillable_page_utils_android.h",
      ]
      deps += [ ":jni_headers" ]
    }
  }

  if (is_android) {
    import("//build/config/android/rules.gni")

    generate_jni("jni_headers") {
      sources = [
        "../android/java/src/org/chromium/components/dom_distiller/content/DistillablePageUtils.java",
      ]
      jni_package = "dom_distiller_content"
    }
  }
}
