# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (!is_ios) {
  # GYP version: components/dom_distiller.gypi:dom_distiller_content
  static_library("content") {
    sources = [
      "distillable_page_utils.cc",
      "distillable_page_utils.h",
      "distiller_javascript_utils.cc",
      "distiller_javascript_utils.h",
      "distiller_page_web_contents.cc",
      "distiller_page_web_contents.h",
      "dom_distiller_viewer_source.cc",
      "dom_distiller_viewer_source.h",
      "web_contents_main_frame_observer.cc",
      "web_contents_main_frame_observer.h",
    ]

    public_deps = [
      "//components/dom_distiller/core",
    ]
    deps = [
      "//base",
      "//components/resources",
      "//components/strings",
      "//content/public/browser",
      "//net",
      "//skia",
      "//sync",
      "//ui/gfx",
      "//url",
    ]

    if (is_android) {
      sources += [
        "distillable_page_utils_android.cc",
        "distillable_page_utils_android.h",
      ]
      deps += [ ":jni_headers" ]
    }
  }

  if (is_android) {
    import("//build/config/android/rules.gni")

    generate_jni("jni_headers") {
      sources = [
        "../android/java/src/org/chromium/components/dom_distiller/content/DistillablePageUtils.java",
      ]
      jni_package = "dom_distiller_content"
    }
  }
}
