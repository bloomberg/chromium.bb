# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//ppapi/buildflags/buildflags.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "font_service_app.cc",
    "font_service_app.h",
    "fontconfig_matching.cc",
    "fontconfig_matching.h",
  ]

  deps = [
    "//base",
    "//components/services/font/public/mojom",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//ppapi/buildflags:buildflags",
    "//third_party/fontconfig",
    "//ui/gfx",
  ]

  public_deps = [ "//skia" ]

  if (is_linux && enable_plugins) {
    deps += [ ":ppapi_fontconfig_matching" ]
  }
}

if (is_linux && enable_plugins) {
  source_set("ppapi_fontconfig_matching") {
    sources = [
      "ppapi_fontconfig_matching.cc",
      "ppapi_fontconfig_matching.h",
    ]

    deps = [
      "//base:base",
      "//ppapi/buildflags:buildflags",
      "//ppapi/c",
    ]
  }
}

test("font_service_unittests") {
  sources = [ "font_loader_unittest.cc" ]

  deps = [
    ":lib",
    "//base",
    "//base/test:test_support",
    "//components/services/font/public/cpp",
    "//components/services/font/public/mojom",
    "//mojo/core/test:run_all_unittests",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//ppapi/buildflags:buildflags",
    "//skia",
    "//testing/gtest",
  ]

  if (enable_plugins) {
    deps += [
      "//ppapi/c",
      "//third_party:freetype_harfbuzz",
    ]
  }
}
