# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
}

# TODO(crbug.com/753619): Enable crash reporting on Fuchsia.
assert(!is_fuchsia)

source_set("browser") {
  sources = [
    "child_exit_observer_android.cc",
    "child_exit_observer_android.h",
    "child_process_crash_observer_android.cc",
    "child_process_crash_observer_android.h",
    "crash_dump_manager_android.cc",
    "crash_dump_manager_android.h",
    "crash_metrics_reporter_android.cc",
    "crash_metrics_reporter_android.h",
  ]

  deps = [
    "//base",
    "//components/crash/content/app",
    "//content/public/browser",
    "//content/public/common",
    "//third_party/breakpad:client",
  ]

  if (is_linux || is_android) {
    set_sources_assignment_filter([])

    # Want this file on both Linux and Android.
    sources += [
      "crash_handler_host_linux.cc",
      "crash_handler_host_linux.h",
    ]
    deps += [ "//third_party/breakpad:client" ]
    if (!is_chromeos) {
      deps += [ "//third_party/crashpad/crashpad/client" ]
    }
  }

  # This is not in the GYP build but this target includes breakpad client
  # headers, so add the dependency here.
  if ((is_posix && !is_ios) || is_fuchsia) {
    configs += [ "//third_party/breakpad:client_config" ]
    public_configs = [ "//third_party/breakpad:client_config" ]
  }

  if (is_android) {
    deps += [ "//components/crash/android:jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "crash_dump_manager_android_unittest.cc",
    "crash_metrics_reporter_android_unittest.cc",
  ]
  deps = [
    ":browser",
    "//base/test:test_support",
    "//testing/gtest",
  ]
}
