# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("common") {
  public_deps = [
    ":crash_keys",
  ]

  if (is_mac || is_ios) {
    public_deps += [ ":crash_objc" ]
  }
}

static_library("crash_keys") {
  visibility = [ ":*" ]

  sources = [
    "crash_keys.cc",
    "crash_keys.h",
  ]

  deps = [
    "//base",
  ]
}

if (is_mac || is_ios) {
  component("crash_objc") {
    visibility = [ ":common" ]

    sources = [
      "assertion_handler.h",
      "assertion_handler.mm",
      "crash_export.h",
      "objc_zombie.h",
      "objc_zombie.mm",
    ]

    defines = [ "CRASH_CORE_COMMON_IMPLEMENTATION" ]

    deps = [
      ":crash_keys",
      "//base",
    ]

    libs = [ "Foundation.framework" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "crash_keys_unittest.cc",
  ]

  deps = [
    ":common",
    "//base",
    "//testing/gtest",
  ]

  if (is_mac || is_ios) {
    sources += [
      "assertion_handler_unittest.mm",
      "objc_zombie_unittest.mm",
    ]
  }
}
