# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("common") {
  public_deps = [
    ":crash_key",
    ":crash_key_utils",
  ]

  if (is_mac || is_ios) {
    public_deps += [ ":zombies" ]
  }
}

use_crashpad = is_mac || is_win
use_stubs = is_fuchsia

# Crashpad's annotation system can store data on a per-module basis (i.e.,
# in different shared libraries in the component build) without issue. The
# Breakpad implementation uses a static global variable, so ensure there is
# only one instance of the symbol in the component build by making this
# target a component.
if (use_stubs || use_crashpad) {
  crash_key_target_type = "static_library"
} else {
  crash_key_target_type = "component"
}
target(crash_key_target_type, "crash_key") {
  sources = [
    "crash_export.h",
    "crash_key.cc",
    "crash_key.h",
    "crash_key_base_support.cc",
    "crash_key_base_support.h",
  ]

  defines = [ "CRASH_CORE_COMMON_IMPLEMENTATION" ]

  # This target is not always a component, depending on the implementation.
  # When it is not a component, annotating functions with the standard
  # CRASH_EXPORT macro causes linking errors on Windows (clients of this target
  # expect it to be dllimport but it is linked statically). Instead, provide a
  # wrapper macro CRASH_KEY_EXPORT that only evaluates to CRASH_EXPORT if this
  # target is really a component.
  if (crash_key_target_type == "component") {
    defines += [ "CRASH_KEY_EXPORT=CRASH_EXPORT" ]
  }

  deps = [
    "//base",
  ]

  if (use_crashpad) {
    sources += [ "crash_key_crashpad.cc" ]
    deps += [ "//third_party/crashpad/crashpad/client" ]
  } else if (use_stubs) {
    sources += [ "crash_key_stubs.cc" ]
  } else {
    include_dirs = [ "//third_party/breakpad/breakpad/src" ]

    if (is_ios) {
      sources += [ "crash_key_breakpad_ios.mm" ]

      configs += [ "//build/config/compiler:enable_arc" ]
    } else {
      sources += [
        "crash_key_breakpad.cc",
        "crash_key_internal.h",
      ]
    }

    deps += [ "//third_party/breakpad:client" ]
  }
}

static_library("crash_key_utils") {
  visibility = [ ":*" ]

  sources = [
    "crash_keys.cc",
    "crash_keys.h",
  ]

  deps = [
    ":crash_key",
    "//base",
  ]
}

if (is_mac || is_ios) {
  component("zombies") {
    visibility = [ ":common" ]

    sources = [
      "objc_zombie.h",
      "objc_zombie.mm",
    ]

    defines = [ "CRASH_CORE_COMMON_IMPLEMENTATION" ]

    deps = [
      ":crash_key",
      "//base",
    ]

    libs = [ "Foundation.framework" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "crash_key_unittest.cc",
    "crash_keys_unittest.cc",
  ]

  deps = [
    ":common",
    "//base",
    "//testing/gtest",
  ]

  if (is_mac || is_ios) {
    sources += [ "objc_zombie_unittest.mm" ]
  }

  if (!is_mac && !is_win && !is_fuchsia) {
    include_dirs = [ "//third_party/breakpad/breakpad/src/" ]
    sources += [ "crash_key_breakpad_unittest.cc" ]
  }

  if (is_fuchsia) {
    sources -= [ "crash_key_unittest.cc" ]
  }
}
