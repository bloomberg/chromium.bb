# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

# GYP: //components/enhanced_bookmarks.gypi:enhanced_bookmarks
source_set("enhanced_bookmarks") {
  sources = [
    "bookmark_image_service.cc",
    "bookmark_image_service.h",
    "bookmark_server_cluster_service.cc",
    "bookmark_server_cluster_service.h",
    "bookmark_server_search_service.cc",
    "bookmark_server_search_service.h",
    "bookmark_server_service.cc",
    "bookmark_server_service.h",
    "enhanced_bookmark_model.cc",
    "enhanced_bookmark_model.h",
    "enhanced_bookmark_model_observer.h",
    "enhanced_bookmark_utils.cc",
    "enhanced_bookmark_utils.h",
    "image_record.h",
    "image_store.cc",
    "image_store.h",
    "image_store_util.cc",
    "image_store_util.h",
    "image_store_util_ios.mm",
    "item_position.cc",
    "item_position.h",
    "metadata_accessor.cc",
    "metadata_accessor.h",
    "persistent_image_store.cc",
    "persistent_image_store.h",
    "pref_names.cc",
    "pref_names.h",
  ]

  deps = [
    "//base",
    "//components/bookmarks/browser",
    "//components/enhanced_bookmarks/proto",
    "//skia",
    "//sql",
    "//ui/gfx",
    "//url",
  ]

  if (is_ios) {
    sources -= [ "image_store_util.cc" ]
  }
}

if (is_android) {
  # GYP: //components/enhanced_bookmarks.gypi:enhanced_bookmarks_launch_location_srcjar
  java_cpp_enum("enhanced_bookmarks_launch_location_srcjar") {
    sources = [
      "enhanced_bookmark_utils.h",
    ]
    outputs = [
      "org/chromium/chrome/browser/enhanced_bookmarks/LaunchLocation.java",
    ]
  }
}

# GYP: //components/enhanced_bookmarks.gypi:enhanced_bookmarks_test_support
source_set("test_support") {
  testonly = true
  sources = [
    "test_image_store.cc",
    "test_image_store.h",
  ]

  deps = [
    ":enhanced_bookmarks",
    "//skia",
    "//testing/gtest",
  ]
}
