# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

precache_config_settings_url =
    "http://www.gstatic.com/chrome/wifiprefetch/precache_config"
precache_manifest_url_prefix =
    "http://www.gstatic.com/chrome/wifiprefetch/precache_manifest_"

config("precache_config") {
  defines = [
    "PRECACHE_CONFIG_SETTINGS_URL=\"$precache_config_settings_url\"",
    "PRECACHE_MANIFEST_URL_PREFIX=\"$precache_manifest_url_prefix\"",
  ]
}

static_library("core") {
  sources = [
    "precache_database.cc",
    "precache_database.h",
    "precache_fetcher.cc",
    "precache_fetcher.h",
    "precache_switches.cc",
    "precache_switches.h",
    "precache_url_table.cc",
    "precache_url_table.h",
    "url_list_provider.h",
  ]

  # Note the GYP build sets this as direct dependent settings, but this is
  # only used to share the settings with the unit tests. Instead, we just
  # set this config for the necessary targets manually.
  configs += [ ":precache_config" ]

  deps = [
    ":proto",
    "//base",
    "//url",
  ]
}

proto_library("proto") {
  sources = [ "proto/precache.proto" ]
}
