# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//testing/test.gni")

source_set("test_support") {
  testonly = true

  sources = [
    "window_server_test_base.cc",
    "window_server_test_base.h",
  ]

  deps = [
    "//base",
    "//base/test:test_config",
    "//components/mus/public/cpp",
    "//mojo/application/public/cpp:sources",
    "//mojo/application/public/cpp:test_support",
    "//testing/gtest",
  ]
}

test("mojo_view_manager_lib_unittests") {
  sources = [
    "run_all_unittests.cc",
    "window_server_test_suite.cc",
    "window_server_test_suite.h",
    "window_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//components/mus/public/cpp",
    "//mojo/application/public/cpp",
    "//testing/gtest",
    "//third_party/mojo/src/mojo/public/cpp/system",
    "//third_party/mojo/src/mojo/edk/system",
    "//ui/gfx/geometry",
    "//ui/mojo/geometry:interfaces",
  ]

  if (use_x11) {
    deps += [ "//ui/gfx/x" ]
  }
}
