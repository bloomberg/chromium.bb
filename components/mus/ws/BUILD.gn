# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//testing/test.gni")
import("//mojo/public/mojo_application.gni")

source_set("lib") {
  sources = [
    "access_policy.h",
    "access_policy_delegate.h",
    "client_connection.cc",
    "client_connection.h",
    "connection_manager.cc",
    "connection_manager.h",
    "connection_manager_delegate.cc",
    "connection_manager_delegate.h",
    "default_access_policy.cc",
    "default_access_policy.h",
    "display_manager.cc",
    "display_manager.h",
    "display_manager_delegate.h",
    "display_manager_factory.h",
    "event_dispatcher.cc",
    "event_dispatcher.h",
    "event_dispatcher_delegate.h",
    "focus_controller.cc",
    "focus_controller.h",
    "focus_controller_delegate.h",
    "focus_controller_observer.h",
    "operation.cc",
    "operation.h",
    "server_window.cc",
    "server_window.h",
    "server_window_delegate.h",
    "server_window_drawn_tracker.cc",
    "server_window_drawn_tracker.h",
    "server_window_drawn_tracker_observer.h",
    "server_window_observer.h",
    "server_window_surface.cc",
    "server_window_surface.h",
    "server_window_surface_manager.cc",
    "server_window_surface_manager.h",
    "server_window_tracker.h",
    "window_coordinate_conversions.cc",
    "window_coordinate_conversions.h",
    "window_finder.cc",
    "window_finder.h",
    "window_manager_access_policy.cc",
    "window_manager_access_policy.h",
    "window_manager_factory_service.cc",
    "window_manager_factory_service.h",
    "window_tree_factory.cc",
    "window_tree_factory.h",
    "window_tree_host_connection.cc",
    "window_tree_host_connection.h",
    "window_tree_host_delegate.h",
    "window_tree_host_impl.cc",
    "window_tree_host_impl.h",
    "window_tree_impl.cc",
    "window_tree_impl.h",
  ]

  deps = [
    "//base",
    "//cc",
    "//cc/surfaces",
    "//cc/surfaces:surface_id",
    "//components/mus/common",
    "//components/mus/gles2",
    "//components/mus/public/interfaces",
    "//components/mus/surfaces",
    "//mojo/common:common_base",
    "//mojo/converters/geometry",
    "//mojo/converters/ime",
    "//mojo/converters/input_events",
    "//mojo/converters/surfaces",
    "//mojo/public/cpp/bindings:callback",
    "//mojo/services/tracing/public/cpp",
    "//mojo/shell/public/cpp",
    "//ui/base",
    "//ui/events",
    "//ui/events/platform",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gl",
    "//ui/mojo/geometry:interfaces",
    "//ui/platform_window",
    "//ui/platform_window:platform_impls",
  ]

  if (use_ozone) {
    deps += [ "//ui/ozone:ozone" ]
  }
}

source_set("test_support") {
  testonly = true

  sources = [
    "test_change_tracker.cc",
    "test_change_tracker.h",
  ]

  deps = [
    "//base",
    "//components/mus/common",
    "//components/mus/public/cpp",
    "//components/mus/public/interfaces",
    "//mojo/common",
    "//mojo/public/cpp/bindings:bindings",
    "//ui/mojo/geometry:interfaces",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":apptests",
    ":window_manager_unittests",
    "//components/mus/public/cpp/tests:mojo_view_manager_lib_unittests",
  ]
}

test("window_manager_unittests") {
  sources = [
    "event_dispatcher_unittest.cc",
    "focus_controller_unittest.cc",
    "server_window_drawn_tracker_unittest.cc",
    "server_window_surface_manager_test_api.cc",
    "server_window_surface_manager_test_api.h",
    "test_server_window_delegate.cc",
    "test_server_window_delegate.h",
    "transient_windows_unittest.cc",
    "window_coordinate_conversions_unittest.cc",
    "window_finder_unittest.cc",
    "window_tree_unittest.cc",
  ]

  deps = [
    ":lib",
    ":test_support",
    "//base",
    "//base/test:test_config",
    "//components/mus/common",
    "//components/mus/public/cpp",
    "//components/mus/public/interfaces",
    "//components/mus/surfaces",
    "//mojo/converters/geometry",
    "//mojo/converters/input_events",
    "//mojo/converters/transform",
    "//mojo/environment:chromium",
    "//mojo/gles2",
    "//mojo/platform_handle",
    "//mojo/public/cpp/bindings:bindings",
    "//mojo/shell/public/interfaces",
    "//testing/gtest",
    "//third_party/mojo/src/mojo/edk/test:run_all_unittests",
    "//ui/events",
    "//ui/gfx",
    "//ui/gfx:test_support",
    "//ui/gfx/geometry",
    "//ui/mojo/geometry:interfaces",
  ]

  if (!is_android) {  # TODO(GYP) Enable on Android when osmesa links.
    deps += [ "//third_party/mesa:osmesa" ]
  }
}

mojo_native_application("apptests") {
  output_name = "mus_apptests"
  testonly = true

  sources = [
    "window_manager_client_apptest.cc",
    "window_tree_apptest.cc",
  ]

  deps = [
    ":test_support",
    "//base",
    "//base/test:test_config",
    "//components/mus/common",
    "//components/mus/public/cpp",
    "//components/mus/public/cpp/tests:test_support",
    "//components/mus/public/interfaces",
    "//mojo/converters/geometry",
    "//mojo/shell/public/cpp:sources",
    "//mojo/shell/public/cpp:test_support",
    "//ui/gfx:test_support",
    "//ui/gfx/geometry",
    "//ui/mojo/geometry:interfaces",
    "//ui/mojo/geometry:util",
  ]

  data_deps = [
    ":lib",
  ]
}
