# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//testing/test.gni")

component("mus_common") {
  sources = [
    "event_matcher_util.cc",
    "event_matcher_util.h",
    "event_param_traits.cc",
    "event_param_traits.h",
    "event_param_traits_macros.h",
    "gpu_type_converters.cc",
    "gpu_type_converters.h",
    "mus_common_export.h",
    "switches.cc",
    "switches.h",
    "transient_window_utils.h",
    "types.h",
    "util.h",
  ]

  defines = [ "MUS_COMMON_IMPLEMENTATION" ]

  deps = [
    "//components/mus/public/interfaces",
    "//gpu/config",
    "//gpu/ipc/common:command_buffer_traits",
    "//ipc:ipc",
    "//mojo/public/c/system:for_component",
    "//ui/events:events",
    "//ui/gfx/ipc/geometry",
  ]

  public_deps = [
    "//ui/base",
  ]
}

source_set("run_all_shelltests") {
  testonly = true

  sources = [
    "run_all_shelltests.cc",
  ]

  deps = [
    "//base/test:test_support",
    "//mojo/edk/system",
    "//services/shell/background:main",
  ]

  if (use_ozone) {
    deps += [ "//ui/ozone" ]
  }
}

test("mus_common_unittests") {
  sources = [
    "event_param_traits_unittest.cc",
    "gpu_type_converters_unittest.cc",
  ]
  public_deps = [
    ":mus_common",
  ]
  deps = [
    ":run_all_shelltests",
    "//base",
    "//base/test:test_config",
    "//components/mus/public/interfaces",
    "//ipc:ipc",
    "//mojo/edk/test:test_support",
    "//testing/gtest",
    "//ui/events:events",
    "//ui/gfx:test_support",
    "//ui/gfx/ipc",
  ]
}

mojo_application_manifest("mus_common_unittests_app_manifest") {
  application_name = "mus_common_unittests_app"
  source = "mus_common_unittests_app_manifest.json"
}
