# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
}

config("implementation") {
  defines = [ "SESSIONS_IMPLEMENTATION" ]
}

if (!is_ios) {
  # GYP version: components/sessions.gypi:sessions_content
  component("sessions") {
    sources = [
      "content/content_live_tab.cc",
      "content/content_live_tab.h",
      "content/content_platform_specific_tab_data.cc",
      "content/content_platform_specific_tab_data.h",
      "content/content_serialized_navigation_builder.cc",
      "content/content_serialized_navigation_builder.h",
      "content/content_serialized_navigation_driver.cc",
      "content/content_serialized_navigation_driver.h",
    ]

    configs += [ ":implementation" ]

    public_deps = [
      ":shared",
      "//content/public/browser",
    ]

    deps = [
      "//base",
      "//base/third_party/dynamic_annotations",
      "//ui/base",
      "//url",
    ]
  }
} else {
  source_set("sessions") {
    sources = [
      "ios/ios_serialized_navigation_builder.cc",
      "ios/ios_serialized_navigation_builder.h",
      "ios/ios_serialized_navigation_driver.cc",
      "ios/ios_serialized_navigation_driver.h",
    ]

    public_deps = [
      ":shared",
    ]

    deps = [
      "//base",
      # '../ios/web/ios_web.gyp:ios_web',  TODO(GYP) iOS.
    ]
  }
}

# Sources shared between the content and iOS implementations.
source_set("shared") {
  visibility = [ ":*" ]

  sources = [
    "base_session_service.cc",
    "base_session_service.h",
    "base_session_service_commands.cc",
    "base_session_service_commands.h",
    "base_session_service_delegate.h",
    "core/live_tab.cc",
    "core/live_tab.h",
    "core/serialized_navigation_driver.h",
    "core/session_constants.cc",
    "core/session_constants.h",
    "core/tab_restore_service.cc",
    "core/tab_restore_service.h",
    "core/tab_restore_service_client.cc",
    "core/tab_restore_service_client.h",
    "core/tab_restore_service_delegate.h",
    "core/tab_restore_service_helper.cc",
    "core/tab_restore_service_helper.h",
    "core/tab_restore_service_observer.h",
    "serialized_navigation_entry.cc",
    "serialized_navigation_entry.h",
    "session_backend.cc",
    "session_backend.h",
    "session_command.cc",
    "session_command.h",
    "session_id.cc",
    "session_id.h",
    "session_service_commands.cc",
    "session_service_commands.h",
    "session_types.cc",
    "session_types.h",
  ]

  if (is_android) {
    sources += [
      "core/in_memory_tab_restore_service.cc",
      "core/in_memory_tab_restore_service.h",
    ]
  } else {
    sources += [
      "core/persistent_tab_restore_service.cc",
      "core/persistent_tab_restore_service.h",
    ]
  }

  configs += [ ":implementation" ]

  public_deps = [
    "//components/keyed_service/core",
    "//sync/protocol",
  ]

  deps = [
    "//base",
    "//skia",
    "//sync",
    "//ui/base",
    "//ui/gfx",
    "//url",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "serialized_navigation_entry_test_helper.cc",
    "serialized_navigation_entry_test_helper.h",
  ]

  public_deps = [
    ":sessions",
  ]
  deps = [
    "//skia",
    "//sync",
    "//testing/gtest",
  ]

  if (!is_android && !is_ios) {
    sources += [
      "base_session_service_test_helper.cc",
      "base_session_service_test_helper.h",
    ]
  }
}

if (!is_ios && !is_android) {
  source_set("unit_tests") {
    testonly = true
    sources = [
      "content/content_serialized_navigation_builder_unittest.cc",
      "content/content_serialized_navigation_driver_unittest.cc",
      "ios/ios_serialized_navigation_builder_unittest.cc",
      "ios/ios_serialized_navigation_driver_unittest.cc",
      "serialized_navigation_entry_unittest.cc",
      "session_backend_unittest.cc",
      "session_types_unittest.cc",
    ]
    deps = [
      ":sessions",
      ":test_support",
      "//base/test:test_support",
      "//testing/gtest",
    ]
  }
}
