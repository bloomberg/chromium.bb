# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

android_library("java") {
  sources = [
    "java/src/org/chromium/components/external_intents/AuthenticatorNavigationInterceptor.java",
    "java/src/org/chromium/components/external_intents/ExternalIntentsFeatureList.java",
    "java/src/org/chromium/components/external_intents/ExternalIntentsSwitches.java",
    "java/src/org/chromium/components/external_intents/ExternalNavigationDelegate.java",
    "java/src/org/chromium/components/external_intents/ExternalNavigationHandler.java",
    "java/src/org/chromium/components/external_intents/ExternalNavigationParams.java",
    "java/src/org/chromium/components/external_intents/InterceptNavigationDelegateClient.java",
    "java/src/org/chromium/components/external_intents/InterceptNavigationDelegateImpl.java",
    "java/src/org/chromium/components/external_intents/RedirectHandler.java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  deps = [
    ":java_resources",
    "//base:base_java",
    "//base:jni_java",
    "//components/embedder_support/android:util_java",
    "//components/navigation_interception/android:navigation_interception_java",
    "//content/public/android:content_java",
    "//third_party/android_deps:androidx_annotation_annotation_java",
    "//ui/android:ui_java",
    "//url:gurl_java",
  ]
}

android_resources("java_resources") {
  sources = []
  custom_package = "org.chromium.components.external_intents"
  deps = [ "//components/browser_ui/strings/android:browser_ui_strings_grd" ]
}

generate_jni("jni_headers") {
  sources = [
    "java/src/org/chromium/components/external_intents/ExternalIntentsFeatureList.java",
    "java/src/org/chromium/components/external_intents/InterceptNavigationDelegateImpl.java",
  ]
}

static_library("android") {
  sources = [
    "external_intents_feature_list.cc",
    "external_intents_feature_list.h",
    "intercept_navigation_delegate_impl.cc",
  ]

  deps = [
    ":jni_headers",
    "//base",
    "//components/navigation_interception",
    "//content/public/browser",
  ]
}

android_library("javatests") {
  testonly = true

  sources = [
    "javatests/src/org/chromium/components/external_intents/ExternalNavigationHandlerTest.java",
    "javatests/src/org/chromium/components/external_intents/RedirectHandlerTest.java",
  ]
  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//content/public/test/android:content_java_test_support",
    "//third_party/android_deps:androidx_core_core_java",
    "//third_party/android_sdk/androidx_browser:androidx_browser_java",
    "//ui/android:ui_java",
  ]
}
