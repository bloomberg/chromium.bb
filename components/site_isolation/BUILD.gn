# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//build/config/chrome_build.gni")

declare_args() {
  # Normally, only official Chrome Android builds will use a Google-internal
  # list of isolated origins defined below.
  #
  # You can set the variable 'use_internal_isolated_origins' to true to use this
  # Google-internal list of isolated origins even in a developer build.  Setting
  # this variable explicitly to true will cause your build to fail if the
  # internal file is missing.
  use_internal_isolated_origins = false

  if (is_chrome_branded && is_android) {
    use_internal_isolated_origins = true
  }
}

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "USE_INTERNAL_ISOLATED_ORIGINS=$use_internal_isolated_origins" ]
}

source_set("site_isolation") {
  sources = [
    "preloaded_isolated_origins.cc",
    "preloaded_isolated_origins.h",
  ]

  if (use_internal_isolated_origins) {
    sources += [
      "internal/google_chrome_isolated_origins.cc",
      "internal/google_chrome_isolated_origins.h",
    ]
  }

  deps = [
    ":buildflags",
    "//base",
    "//content/public/browser",
    "//url",
  ]
}
