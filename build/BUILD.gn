# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/compiler/compiler.gni")

source_set("buildflag_header_h") {
  sources = [
    "buildflag.h",
  ]

  if (current_toolchain == default_toolchain && current_cpu == "arm64" &&
      optimize_for_size) {
    # This dependency (and the associated action below) is designed to clear
    # the ThinLTO cache if the state of |arm64_allow_outlining| changes when
    # optimizing for size on arm64.
    #
    # See crbug.com/931297 for more information.
    deps = [
      ":check_thinlto_cache($default_toolchain)",
    ]
  }
}

if (current_toolchain == default_toolchain && current_cpu == "arm64" &&
    optimize_for_size) {
  action("check_thinlto_cache") {
    script = "//build/check_and_clear_thinlto_cache_on_outlining_change.py"

    outputs = [
      "$target_out_dir/arm64_outlining_status.stamp",
    ]

    args = [
      "--cache-location",
      "thinlto-cache",
      "--stampfile",
      rebase_path(outputs[0], root_build_dir),
    ]
    if (arm64_allow_outlining) {
      args += [ "--allowing-outlining" ]
    }
  }
}
