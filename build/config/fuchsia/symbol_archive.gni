# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_fuchsia)

# Creates a tarball of binaries' debug data, structured according
# to the ".build_ids" convention used by the symbolizer and GNU GDB.
#
# Parameters:
#   ids_txt: The "ids.txt" file which lists the relative paths to unstripped
#            executables and libraries, along with their build IDs.
#   archive_name: The path to the compressed tarball that will be
#                   generated.
template("symbol_archive") {
  action(target_name) {
    _ids_txt = invoker.ids_txt
    _build_ids = invoker.archive_name

    script = "//build/config/fuchsia/build_symbol_archive.py"

    inputs = [
      _ids_txt,
    ]

    outputs = [
      _build_ids,
    ]

    deps = invoker.deps

    args = [
      rebase_path(_ids_txt),
      rebase_path(_build_ids),
      "--eu-strip",
      rebase_path("//buildtools/third_party/eu-strip/bin/eu-strip"),
    ]
  }
}
