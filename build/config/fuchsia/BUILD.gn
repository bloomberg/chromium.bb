# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/config.gni")
import("//build/config/sysroot.gni")

assert(is_fuchsia)
assert(is_posix)

config("compiler") {
  sdk_version_file = rebase_path("$fuchsia_sdk/.hash")
  sdk_version = read_file(sdk_version_file, "trim string")
  defines = [
    # To force full builds after SDK updates in case of ABI changes.
    "FUCHSIA_SDK_VERSION=$sdk_version",

    # TODO(scottmg): Temporary during 3-sided rolls. https://crbug.com/765754.
    # When this is set, defines/typedefs are in effect to convert "mx_"s to
    # "zx_"s.
    "CHROMIUM_ROLLING_MAGENTA_TO_ZIRCON=1",
  ]
  cflags = []
  ldflags = []
  if (current_cpu == "arm64") {
    cflags += [ "--target=aarch64-fuchsia" ]
    ldflags += [ "--target=aarch64-fuchsia" ]
  } else if (current_cpu == "x64") {
    cflags += [
      "--target=x86_64-fuchsia",

      # TODO(scottmg): I screwed up in
      # https://chromium.googlesource.com/v8/v8.git/+/aabb893a3259c72a78920771280d0832411c92c1
      # (and put #undef mx_ zx_ rather than just #undef mx_). It took 4 days to
      # get that landed upstream, so instead of fixing and waiting again (since
      # that code will be deleted right after this lands), simply turn off
      # this warning temporarily. https://crbug.com/765754.
      "-Wno-extra-tokens",
    ]
    ldflags += [ "--target=x86_64-fuchsia" ]
  } else {
    assert(false, "Unsupported architecture")
  }
  asmflags = cflags

  ldflags += [
    # TODO(thakis): Once Fuchsia's libclang_rt.builtin no longer has upstream
    # patches, we might want to make tools/clang/scripts/update.py build it
    # and bundle it with the clang package instead of using the library from
    # the SDK, https://crbug.com/724204
    # Note: Intentionally 6.0.0 instead of $clang_version because the clang
    # version of the toolchain_libs directory in the Fuchsia SDK can be
    # different from the version of Chromium's clang.
    "-resource-dir",
    rebase_path(fuchsia_sdk, root_build_dir) + "/toolchain_libs/clang/6.0.0",

    # The stack defaults to 256k on Fuchsia (see
    # https://fuchsia.googlesource.com/zircon/+/master/system/private/zircon/stack.h#9),
    # but on other platforms it's much higher, so a variety of code assumes more
    # will be available. Raise to 8M which matches e.g. macOS.
    "-Wl,-z,stack-size=0x800000",

    # We always want fdio or else e.g. stdio wouldn't be initialized if fdio
    # happens to not be directly referenced. The common POSIX-y compiler setup
    # uses -Wl,--as-needed which drops it if it's simply "-lfdio" from a libs
    # setting. Disable --as-needed, add fdio, and then set back to --as-needed.
    # https://crbug.com/731217.
    "-Wl,--no-as-needed",
    "-lfdio",
    "-Wl,--as-needed",
  ]

  libs = [ "zircon" ]
}
