# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_chromeos)

template("generate_vm_runner_script") {
  _cros_board = getenv("SDK_BOARD")
  _cros_sdk = getenv("SDK_VERSION")

  _vm_image_path = "//build/cros_cache/chrome-sdk/tarballs/${_cros_board}+${_cros_sdk}+chromiumos_qemu_image.tar.xz/chromiumos_qemu_image.bin"
  _qemu_dir = "//build/cros_cache/chrome-sdk/tarballs/${_cros_board}+${_cros_sdk}+app-emulation/qemu-2.6.0-r2.tbz2/"

  action(target_name) {
    forward_variables_from(invoker, [ "testonly" ])

    script = "//build/chromeos/create_vm_test_script.py"

    outputs = [
      invoker.generated_script,
    ]

    data = [
      invoker.generated_script,
      "//build/chromeos/",
      "//build/cros_cache/chrome-sdk/misc/",
      "//third_party/chromite/",
      _vm_image_path,
      _qemu_dir,
    ]

    # Arguments used at build time by the runner script generator.
    args = [
      "--script-output-path",
      rebase_path(invoker.generated_script, root_build_dir),
      "--output-directory",
      rebase_path(root_out_dir, root_build_dir),
      "--test-exe",
      rebase_path(invoker.test_exe, root_build_dir),
      "--runtime-deps-path",
      rebase_path(invoker.runtime_deps_file, root_build_dir),
      "--cros-cache",
      rebase_path("//build/cros_cache/", root_build_dir),
      "--board",
      _cros_board,
    ]
  }
}
