# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/build/config.gni")
import("//ios/public/provider/chrome/browser/build_config.gni")
import("//testing/test.gni")

# All tests needs to be listed in that target to be built as part of
# "gn_all" target (i.e. by the bots).
group("all_tests") {
  testonly = true
  deps = [
    ":ios_chrome_perftests",
    ":ios_chrome_unittests",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "block_cleanup_test.h",
    "block_cleanup_test.mm",
    "ios_chrome_scoped_testing_chrome_browser_provider.h",
    "ios_chrome_scoped_testing_chrome_browser_provider.mm",
    "ios_chrome_scoped_testing_chrome_browser_state_manager.cc",
    "ios_chrome_scoped_testing_chrome_browser_state_manager.h",
    "ios_chrome_scoped_testing_local_state.cc",
    "ios_chrome_scoped_testing_local_state.h",
    "ios_chrome_unit_test_suite.h",
    "ios_chrome_unit_test_suite.mm",
    "scoped_key_window.h",
    "testing_application_context.h",
    "testing_application_context.mm",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//components/content_settings/core/common",
    "//components/network_time",
    "//components/prefs",
    "//components/prefs:test_support",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/browser_state:browser_state_impl",
    "//ios/chrome/browser/prefs:browser_prefs",
    "//ios/public/provider/chrome/browser",
    "//ios/public/provider/chrome/browser:test_support",
    "//ios/web",
    "//ios/web:test_support",
    "//net",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/base",
    "//url",
  ]
}

source_set("run_all_unittests") {
  testonly = true
  sources = [
    "run_all_unittests.cc",
  ]
  deps = [
    ":test_support",
    "//base",
    "//base/test:test_support",
  ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "google_toolbox_unittest.mm",
  ]
  deps = [
    "//base",
    "//testing/gtest",
    "//third_party/google_toolbox_for_mac",
  ]
}

test("ios_chrome_perftests") {
  deps = [
    # Ensure that all perf tests are run, use fake hooks and pack resources.
    ":run_all_unittests",
    "//ios/chrome/app:tests_fake_hook",
    ios_packed_resources_target,

    # Add perf_tests target here.
    "//ios/chrome/browser/ui:perf_tests",
    "//ios/chrome/browser/ui/ntp:perf_tests",
    "//ios/chrome/browser/ui/stack_view:perf_tests",
    "//ios/chrome/browser/web:perf_tests",
  ]

  assert_no_deps = ios_assert_no_deps
}

test("ios_chrome_unittests") {
  deps = [
    # Ensure that all unit tests are run, use fake hooks and pack resources.
    ":run_all_unittests",
    "//ios/chrome/app:tests_fake_hook",
    ios_packed_resources_target,

    # Add unit_tests target here.
    ":unit_tests",
    "//ios/chrome/app:unit_tests",
    "//ios/chrome/app/application_delegate:unit_tests",
    "//ios/chrome/app/safe_mode:unit_tests",
    "//ios/chrome/app/spotlight:unit_tests",
    "//ios/chrome/browser:unit_tests",
    "//ios/chrome/browser/autofill:unit_tests",
    "//ios/chrome/browser/browsing_data:unit_tests",
    "//ios/chrome/browser/content_suggestions:unit_tests",
    "//ios/chrome/browser/crash_report:unit_tests",
    "//ios/chrome/browser/device_sharing:unit_tests",
    "//ios/chrome/browser/favicon:unit_tests",
    "//ios/chrome/browser/find_in_page:unit_tests",
    "//ios/chrome/browser/geolocation:unit_tests",
    "//ios/chrome/browser/itunes_links:unit_tests",
    "//ios/chrome/browser/metrics:unit_tests",
    "//ios/chrome/browser/metrics:unit_tests_internal",
    "//ios/chrome/browser/native_app_launcher:unit_tests",
    "//ios/chrome/browser/native_app_launcher:unit_tests_internal",
    "//ios/chrome/browser/net:unit_tests",
    "//ios/chrome/browser/omaha:unit_tests",
    "//ios/chrome/browser/passwords:unit_tests",
    "//ios/chrome/browser/payments:unit_tests",
    "//ios/chrome/browser/reading_list:unit_tests",
    "//ios/chrome/browser/sessions:unit_tests",
    "//ios/chrome/browser/signin:unit_tests",
    "//ios/chrome/browser/snapshots:unit_tests",
    "//ios/chrome/browser/ssl:unit_tests",
    "//ios/chrome/browser/store_kit:unit_tests",
    "//ios/chrome/browser/sync:unit_tests",
    "//ios/chrome/browser/tabs:unit_tests",
    "//ios/chrome/browser/translate:unit_tests",
    "//ios/chrome/browser/u2f:unit_tests",
    "//ios/chrome/browser/ui:unit_tests",
    "//ios/chrome/browser/ui/activity_services:unit_tests",
    "//ios/chrome/browser/ui/alert_coordinator:unit_tests",
    "//ios/chrome/browser/ui/authentication:unit_tests",
    "//ios/chrome/browser/ui/autofill/cells:unit_tests",
    "//ios/chrome/browser/ui/bookmarks:unit_tests",
    "//ios/chrome/browser/ui/bookmarks/cells:unit_tests",
    "//ios/chrome/browser/ui/collection_view:unit_tests",
    "//ios/chrome/browser/ui/collection_view/cells:unit_tests",
    "//ios/chrome/browser/ui/commands:unit_tests",
    "//ios/chrome/browser/ui/content_suggestions:unit_tests",
    "//ios/chrome/browser/ui/content_suggestions/cells:unit_tests",
    "//ios/chrome/browser/ui/content_suggestions/identifier:unit_tests",
    "//ios/chrome/browser/ui/context_menu:unit_tests",
    "//ios/chrome/browser/ui/contextual_search:unit_tests",
    "//ios/chrome/browser/ui/dialogs:unit_tests",
    "//ios/chrome/browser/ui/dialogs:unit_tests_internal",
    "//ios/chrome/browser/ui/downloads:unit_tests",
    "//ios/chrome/browser/ui/elements:unit_tests",
    "//ios/chrome/browser/ui/fancy_ui:unit_tests",
    "//ios/chrome/browser/ui/first_run:unit_tests",
    "//ios/chrome/browser/ui/history:unit_tests",
    "//ios/chrome/browser/ui/icons:unit_tests",
    "//ios/chrome/browser/ui/infobars:unit_tests",
    "//ios/chrome/browser/ui/keyboard:unit_tests",
    "//ios/chrome/browser/ui/main:unit_tests",
    "//ios/chrome/browser/ui/ntp:unit_tests",
    "//ios/chrome/browser/ui/omnibox:unit_tests",
    "//ios/chrome/browser/ui/payments:unit_tests",
    "//ios/chrome/browser/ui/payments/cells:unit_tests",
    "//ios/chrome/browser/ui/promos:unit_tests",
    "//ios/chrome/browser/ui/reading_list:unit_tests",
    "//ios/chrome/browser/ui/settings:unit_tests",
    "//ios/chrome/browser/ui/settings/cells:unit_tests",
    "//ios/chrome/browser/ui/side_swipe:unit_tests",
    "//ios/chrome/browser/ui/stack_view:unit_tests",
    "//ios/chrome/browser/ui/static_content:unit_tests",
    "//ios/chrome/browser/ui/tab_switcher:unit_tests",
    "//ios/chrome/browser/ui/tabs:unit_tests",
    "//ios/chrome/browser/ui/toolbar:unit_tests",
    "//ios/chrome/browser/ui/tools_menu:unit_tests",
    "//ios/chrome/browser/ui/util:unit_tests",
    "//ios/chrome/browser/ui/voice:unit_tests",
    "//ios/chrome/browser/update_client:unit_tests",
    "//ios/chrome/browser/voice:unit_tests",
    "//ios/chrome/browser/web:unit_tests",
    "//ios/chrome/browser/web:unit_tests_internal",
    "//ios/chrome/browser/web_resource:unit_tests",
    "//ios/chrome/browser/web_state_list:unit_tests",
    "//ios/chrome/common:unit_tests",
    "//ios/chrome/search_widget_extension:unit_tests",
    "//ios/chrome/test/base:unit_tests",
    "//ios/shared/chrome/browser/ui/broadcaster:unit_tests",
    "//ios/shared/chrome/browser/ui/browser_list:unit_tests",
    "//ios/shared/chrome/browser/ui/commands:unit_tests",
    "//ios/shared/chrome/browser/ui/coordinators:unit_tests",
  ]

  assert_no_deps = ios_assert_no_deps
}
