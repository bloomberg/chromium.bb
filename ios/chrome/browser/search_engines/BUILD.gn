# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/web/js_compile.gni")
import("//rlz/buildflags/buildflags.gni")

source_set("search_engines") {
  sources = [
    "search_engine_observer_bridge.h",
    "search_engine_observer_bridge.mm",
    "search_engine_tab_helper.h",
    "search_engine_tab_helper.mm",
    "search_engines_util.cc",
    "search_engines_util.h",
    "template_url_fetcher_factory.cc",
    "template_url_fetcher_factory.h",
    "template_url_service_client_impl.cc",
    "template_url_service_client_impl.h",
    "template_url_service_factory.cc",
    "template_url_service_factory.h",
    "ui_thread_search_terms_data.cc",
    "ui_thread_search_terms_data.h",
  ]
  deps = [
    "//base",
    "//components/country_codes",
    "//components/favicon/ios",
    "//components/google/core/common",
    "//components/history/core/browser",
    "//components/keyed_service/core",
    "//components/keyed_service/ios",
    "//components/omnibox/browser",
    "//components/prefs",
    "//components/search",
    "//components/search_engines",
    "//components/version_info",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/google",
    "//ios/chrome/browser/history",
    "//ios/chrome/browser/webdata_services",
    "//ios/chrome/common",
    "//ios/web",
    "//net",
    "//rlz/buildflags",
    "//ui/base",
    "//url",
  ]
  configs += [ "//build/config/compiler:enable_arc" ]

  if (enable_rlz) {
    deps += [ "//components/rlz" ]
  }
}

source_set("extension_search_engine_data_updater") {
  sources = [
    "extension_search_engine_data_updater.h",
    "extension_search_engine_data_updater.mm",
  ]

  deps = [
    ":search_engines",
    "//base",
    "//components/search_engines",
    "//ios/chrome/common/app_group",
  ]
  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "extension_search_engine_data_updater_unittest.mm",
    "search_engine_js_unittest.mm",
    "search_engine_tab_helper_unittest.mm",
  ]
  deps = [
    ":extension_search_engine_data_updater",
    ":search_engine_js",
    ":search_engines",
    "//base:base",
    "//base/test:test_support",
    "//components/favicon/ios",
    "//components/search_engines",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/chrome/browser/favicon:favicon",
    "//ios/chrome/browser/web:test_support",
    "//ios/chrome/common/app_group",
    "//ios/web",
    "//ios/web/public/test",
    "//ios/web/public/test/fakes",
    "//net:test_support",
    "//testing/gtest",
  ]
}

js_compile_checked("search_engine_js") {
  sources = [ "resources/search_engine.js" ]
}
