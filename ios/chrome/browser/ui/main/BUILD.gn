# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/build/chrome_build.gni")

source_set("scene") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "scene_controller.h",
    "scene_controller.mm",
    "scene_delegate.h",
    "scene_delegate.mm",
    "scene_state.h",
    "scene_state.mm",
  ]

  deps = [
    ":main",
    "//base",
    "//ios/chrome/browser/tabs:tabs",
    "//ios/chrome/browser/ui/commands:commands",
    "//ios/chrome/browser/ui/tab_grid",
    "//ios/chrome/browser/ui/util:multiwindow_util",
  ]

  libs = [ "UIKit.framework" ]
}

source_set("main") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "browser_interface_provider.h",
    "browser_view_wrangler.h",
    "browser_view_wrangler.mm",
    "bvc_container_view_controller.h",
    "bvc_container_view_controller.mm",
  ]
  deps = [
    "//base",
    "//components/translate/core/browser",
    "//ios/chrome/app/resources:launchscreen_xib",
    "//ios/chrome/browser",
    "//ios/chrome/browser/app_launcher",
    "//ios/chrome/browser/autofill:autofill_internal",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/browsing_data",
    "//ios/chrome/browser/crash_report:crash_report_internal",
    "//ios/chrome/browser/device_sharing",
    "//ios/chrome/browser/download",
    "//ios/chrome/browser/main",
    "//ios/chrome/browser/reading_list",
    "//ios/chrome/browser/sessions",
    "//ios/chrome/browser/sessions:serialisation",
    "//ios/chrome/browser/store_kit",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/tabs:tabs_internal",
    "//ios/chrome/browser/ui/alert_coordinator",
    "//ios/chrome/browser/ui/app_launcher",
    "//ios/chrome/browser/ui/autofill",
    "//ios/chrome/browser/ui/browser_container",
    "//ios/chrome/browser/ui/browser_view",
    "//ios/chrome/browser/ui/commands",
    "//ios/chrome/browser/ui/coordinators:chrome_coordinators",
    "//ios/chrome/browser/ui/download",
    "//ios/chrome/browser/ui/page_info:coordinator",
    "//ios/chrome/browser/ui/print",
    "//ios/chrome/browser/ui/qr_scanner:coordinator",
    "//ios/chrome/browser/ui/reading_list",
    "//ios/chrome/browser/ui/recent_tabs",
    "//ios/chrome/browser/ui/snackbar",
    "//ios/chrome/browser/ui/translate",
    "//ios/chrome/browser/url_loading",
    "//ios/chrome/browser/web",
    "//ios/chrome/browser/web:tab_helper_delegates",
    "//ios/chrome/browser/web:web_internal",
    "//ios/chrome/browser/web_state_list",
    "//ios/public/provider/chrome/browser",
    "//net",
  ]

  if (ios_enable_multi_window) {
    deps += [ "//ios/chrome/app/resources:base_scene_storyboard" ]
  }

  libs = [ "UIKit.framework" ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "browser_view_wrangler_unittest.mm",
    "scene_controller_unittest.mm",
  ]
  deps = [
    ":main",
    ":scene",
    "//base",
    "//base/test:test_support",
    "//components/bookmarks/test",
    "//ios/chrome/browser/bookmarks",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/ui/browser_view",
    "//ios/chrome/test:block_cleanup_test",
    "//ios/web/public/test",
    "//testing/gtest",
  ]
}
