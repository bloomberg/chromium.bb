# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/build/config.gni")
import("//ios/third_party/earl_grey/ios_eg_test.gni")

ios_eg_test("ios_web_shell_test") {
  sources = [
    "web_shell_navigation_egtest.mm",
  ]

  deps = [
    "//base",
    "//ios/web:test_support",
    "//ios/web/shell",
    "//ios/web/shell/test:earl_grey_test_support",

    # All shared libraries must have the sanitizer deps to properly link in
    # asan mode (this target will be empty in other cases).
    "//build/config/sanitizers:deps",
  ]

  bundle_deps = [ ":bundle" ]

  configs += [ "//build/config/compiler:enable_arc" ]

  assert_no_deps = ios_assert_no_deps
}

source_set("earl_grey_test_support") {
  testonly = true

  deps = [
    "//base",
    "//base/test:test_support",
    "//ios/testing/earl_grey:earl_grey_support",
    "//ios/third_party/earl_grey",
    "//ios/web",
    "//ios/web:earl_grey_test_support",
    "//ios/web:test_support",
    "//ios/web/shell",
    "//url",
  ]

  sources = [
    "app/navigation_test_util.h",
    "app/navigation_test_util.mm",
    "app/web_shell_test_util.h",
    "app/web_shell_test_util.mm",
    "app/web_view_interaction_test_util.h",
    "app/web_view_interaction_test_util.mm",
    "earl_grey/shell_base_test_case.h",
    "earl_grey/shell_base_test_case.mm",
    "earl_grey/shell_matchers.h",
    "earl_grey/shell_matchers.mm",
  ]
}

bundle_data("bundle") {
  visibility = [ ":ios_web_shell_test_host" ]
  sources = [
    "http_server_files/basic_navigation_test.html",
  ]
  outputs = [
    "{{bundle_resources_dir}}/{{source_root_relative_dir}}/" +
        "{{source_file_part}}",
  ]
}
