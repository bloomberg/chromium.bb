# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/build/config.gni")
import("//build/config/ios/rules.gni")
import("//testing/test.gni")

group("all_tests") {
  testonly = true
  deps = [
    ":ios_web_view_inttests",
  ]
}

test("ios_web_view_inttests") {
  testonly = true
  sources = [
    "web_view_kvo_inttest.mm",
    "web_view_restorable_state_inttest.mm",
    "web_view_test.h",
    "web_view_test.mm",
  ]

  deps = [
    ":test_support",
    "//base",
    "//base/test:run_all_unittests",
    "//ios/testing:ios_test_support",
    "//ios/web_view:web_view+link",
    "//net",
    "//net:test_support",
    "//testing/gtest",
  ]

  bundle_deps = [ "//ios/web_view:web_view+bundle" ]

  configs += [ "//build/config/compiler:enable_arc" ]

  assert_no_deps = ios_assert_no_deps
}

source_set("test_support") {
  testonly = true
  sources = [
    # Explicitly reference imported headers from web_view library to avoid
    # depending on the framework (as it is statically linked).
    "//ios/web_view/public/cwv_web_view.h",
    "//ios/web_view/public/cwv_web_view_configuration.h",
    "observer.h",
    "observer.mm",
    "web_view_test_util.h",
    "web_view_test_util.mm",
  ]

  deps = [
    "//base:base",
    "//ios/testing:ios_test_support",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]
}
