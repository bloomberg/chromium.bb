# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

component("common") {
  output_name = "blimp_common"
  sources = [
    "blimp_common_export.h",
    "blob_cache/blob_cache.h",
    "blob_cache/id_util.cc",
    "blob_cache/id_util.h",
    "blob_cache/in_memory_blob_cache.cc",
    "blob_cache/in_memory_blob_cache.h",
    "compositor/blimp_image_serialization_processor.cc",
    "compositor/blimp_image_serialization_processor.h",
    "compositor/blimp_task_graph_runner.cc",
    "compositor/blimp_task_graph_runner.h",
    "compositor/webp_decoder.cc",
    "compositor/webp_decoder.h",
    "create_blimp_message.cc",
    "create_blimp_message.h",
    "logging.cc",
    "logging.h",
    "protocol_version.h",
  ]

  defines = [ "BLIMP_COMMON_IMPLEMENTATION=1" ]

  deps = [
    "//base",
    "//blimp/common/proto",
    "//cc",
    "//crypto",
    "//skia",
    "//third_party/libwebp",
    "//ui/gfx/geometry",
    "//ui/gl",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "blob_cache/id_util_unittest.cc",
    "blob_cache/in_memory_blob_cache_unittest.cc",
    "create_blimp_message_unittest.cc",
    "logging_unittest.cc",
  ]

  deps = [
    ":common",
    "//base",
    "//blimp/common/proto",
    "//blimp/net:test_support",
    "//crypto",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
