# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

import("//blimp/client/core/config.gni")

# This target is here to let //chrome depend on //blimp/client/public
# and automatically get the correct implementation of //blimp/client/core
# depending on whether enable_blimp_client is set or not.
group("core_shim") {
  visibility = [ "//blimp/client/public" ]

  if (enable_blimp_client) {
    deps = [
      ":core",
    ]
  } else {
    deps = [
      ":dummy_core",
    ]
  }
}

group("core") {
  visibility = [
    "//blimp/client/*",
    "//blimp/test/*",
  ]

  deps = [
    ":context",
    ":switches",
    "//blimp/client/core/compositor",
    "//blimp/client/core/contents",
    "//blimp/client/core/geolocation",
    "//blimp/client/core/render_widget",
    "//blimp/client/core/session",
    "//blimp/client/core/settings",
  ]
}

source_set("dummy_core") {
  visibility = [ ":*" ]

  sources = [
    "dummy_blimp_client_context.cc",
    "dummy_blimp_client_context.h",
  ]

  deps = [
    "//base",
    "//blimp/client/public:public_headers",
    "//url",
  ]

  if (is_android) {
    sources += [
      "android/dummy_blimp_client_context_android.cc",
      "android/dummy_blimp_client_context_android.h",
      "android/dummy_blimp_jni_registrar.cc",
    ]

    deps += [ ":dummy_core_jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "blimp_client_context_impl_unittest.cc",
  ]

  deps = [
    ":context",
    "//blimp/client/core/compositor:unit_tests",
    "//blimp/client/core/contents:unit_tests",
    "//blimp/client/core/geolocation:unit_tests",
    "//blimp/client/core/render_widget:unit_tests",
    "//blimp/client/core/session:unit_tests",
    "//blimp/client/public:public_headers",
    "//blimp/client/support:test_support",
    "//blimp/client/test",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("context") {
  sources = [
    "blimp_client_context_impl.cc",
    "blimp_client_context_impl.h",
  ]

  public_deps = [
    "//base",
    "//blimp/client/core/contents",
    "//blimp/client/core/session",
    "//blimp/client/public:public_headers",
    "//url",
  ]

  deps = [
    ":switches",
    "//blimp/client/core/settings",
  ]

  if (is_android) {
    sources += [
      "android/blimp_client_context_impl_android.cc",
      "android/blimp_client_context_impl_android.h",
      "android/blimp_jni_registrar.cc",
    ]

    deps += [
      ":context_jni_headers",
      "//blimp/client/core/settings",
      "//ui/android",
    ]
  }
}

source_set("switches") {
  visibility = [
    "//blimp/client/*",
    "//blimp/engine:browser_tests",
  ]

  sources = [
    "blimp_client_switches.cc",
    "blimp_client_switches.h",
  ]
}

if (is_android) {
  # This target is here to let //chrome depend on
  # //blimp/client/public:public_java and automatically get the correct
  # implementation of //blimp/client/core depending on whether
  # enable_blimp_client is set or not.
  java_group("core_shim_java") {
    visibility = [ "//blimp/client/public/*" ]

    if (enable_blimp_client) {
      deps = [
        ":core_java",
      ]
    } else {
      deps = [
        ":dummy_core_java",
      ]
    }
  }

  java_group("core_java") {
    visibility = [
      "//blimp/client/*",
      "//blimp:blimp_unittests_java_deps",
    ]

    deps = [
      ":context_java",
      ":switches_java",
      "//blimp/client/core/contents:contents_java",
      "//blimp/client/core/settings:settings_java",
    ]
  }

  android_library("dummy_core_java") {
    visibility = [ ":*" ]

    java_files = [ "android/java/src/org/chromium/blimp/core/DummyBlimpClientContext.java" ]

    deps = [
      "//base:base_java",
      "//blimp/client/public:public_headers_java",
      "//ui/android:ui_java",
    ]
  }

  android_library("context_java") {
    visibility = [ "//blimp/client/*" ]

    java_files = [ "android/java/src/org/chromium/blimp/core/BlimpClientContextImpl.java" ]

    deps = [
      ":common_java",
      ":switches_java",
      "//base:base_java",
      "//blimp/client/core/contents:contents_java",
      "//blimp/client/core/settings:settings_java",
      "//blimp/client/public:public_headers_java",
      "//ui/android:ui_java",
    ]
  }

  android_library("common_java") {
    visibility = [ "//blimp/client/*" ]

    java_files = [ "android/java/src/org/chromium/blimp/core/settings/PreferencesUtil.java" ]

    deps = [
      "//base:base_java",
      "//blimp/client/public:public_headers_java",
    ]
  }

  android_library("switches_java") {
    visibility = [ "//blimp/client/*" ]

    java_files =
        [ "android/java/src/org/chromium/blimp/core/BlimpClientSwitches.java" ]
  }

  generate_jni("context_jni_headers") {
    visibility = [ ":*" ]

    sources = [
      "android/java/src/org/chromium/blimp/core/BlimpClientContextImpl.java",
    ]

    jni_package = "blimp/client/core"
  }

  generate_jni("dummy_core_jni_headers") {
    visibility = [ ":*" ]

    sources = [
      "android/java/src/org/chromium/blimp/core/DummyBlimpClientContext.java",
    ]

    jni_package = "blimp/client/core"
  }
}
