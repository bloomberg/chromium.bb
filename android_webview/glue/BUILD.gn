# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("generate_resource_rewriter.gni")

webview_framework_ijar =
    "$root_out_dir/lib.java/webview/frameworks.interface.jar"

# This variable shared between 'glue' and 'glue_resource_rewriter' because
# ResourceRewrite.java need to be generated according 'glue' deps
glue_library_deps = [
  "//android_webview:android_webview_java",
  "//android_webview:resources",
  "//base:base_java",
  "//content/public/android:content_java",
  "//content/public/android:content_java_resources",
  "//components/web_contents_delegate_android:web_contents_delegate_android_java_resources",
  "//ui/android:ui_java",
  "//ui/android:ui_java_resources",
]

generate_interface_jar("framework_ijar") {
  input_jar = "../../third_party/android_platform/webview/frameworks_6.0.jar"
  output_jar = webview_framework_ijar
}

generate_resource_rewriter("glue_resource_rewriter") {
  # Change deps? please modify glue_library_deps variable.
  deps = glue_library_deps
  package_name = "com.android.webview.chromium"
}

android_library("glue") {
  # Change deps? please modify glue_library_deps variable.
  deps = glue_library_deps
  srcjar_deps = [ ":glue_resource_rewriter" ]

  alternative_android_sdk_ijar = webview_framework_ijar
  alternative_android_sdk_ijar_dep = "//android_webview/glue:framework_ijar"

  java_files = [
    "java/src/com/android/webview/chromium/ContentSettingsAdapter.java",
    "java/src/com/android/webview/chromium/CookieManagerAdapter.java",
    "java/src/com/android/webview/chromium/DrawGLFunctor.java",
    "java/src/com/android/webview/chromium/GeolocationPermissionsAdapter.java",
    "java/src/com/android/webview/chromium/GraphicsUtils.java",
    "java/src/com/android/webview/chromium/LicenseActivity.java",
    "java/src/com/android/webview/chromium/LicenseContentProvider.java",
    "java/src/com/android/webview/chromium/ResourcesContextWrapperFactory.java",
    "java/src/com/android/webview/chromium/WebBackForwardListChromium.java",
    "java/src/com/android/webview/chromium/WebHistoryItemChromium.java",
    "java/src/com/android/webview/chromium/WebIconDatabaseAdapter.java",
    "java/src/com/android/webview/chromium/WebMessagePortAdapter.java",
    "java/src/com/android/webview/chromium/WebStorageAdapter.java",
    "java/src/com/android/webview/chromium/WebViewChromiumFactoryProvider.java",
    "java/src/com/android/webview/chromium/WebViewChromium.java",
    "java/src/com/android/webview/chromium/WebViewContentsClientAdapter.java",
    "java/src/com/android/webview/chromium/WebViewDatabaseAdapter.java",
    "java/src/com/android/webview/chromium/WebViewDelegateFactory.java",
  ]
}
