# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

generate_jni("apk_jni_headers") {
  sources = [
    "java/src/com/android/webview/chromium/WebViewApkApplication.java",
  ]
}

# Monochrome uses a different copy of LicenseContentProvider.java.
# This one is used in Trichrome and SystemWebview.
android_library("webview_license_provider_java") {
  java_files =
      [ "java/src/com/android/webview/chromium/LicenseContentProvider.java" ]
  deps = [
    ":apk_java",
    "//base:base_java",
    "//components/about_ui/android:aboutui_java",
  ]
}

# Contains the native dependencies used by LicenseContentProvider via JNI.
# This should be included in the shared library of any APK that depends on
# :webview_license_provider_java.
group("webview_license_provider") {
  deps = [
    "//components/about_ui",
  ]
}

# Contains classes needed by the webview apk, but not used when loading the apk
# as a library.
android_library("apk_java") {
  java_files = [
    "java/src/com/android/webview/chromium/LicenseActivity.java",
    "java/src/com/android/webview/chromium/WebViewApkApplication.java",
  ]
  deps = [
    "//android_webview:android_webview_locale_config_java",
    "//android_webview:common_commandline_java",
    "//base:base_java",
    "//base:jni_java",
    "//components/embedder_support/android:application_java",
    "//ui/android:ui_java",
  ]
  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

source_set("apk") {
  sources = [
    "webview_apk_application.cc",
  ]
  deps = [
    ":apk_jni_headers",
    "//android_webview/common",
    "//base",
  ]
}
