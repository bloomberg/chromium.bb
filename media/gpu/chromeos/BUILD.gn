# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//media/gpu/args.gni")

assert(use_v4l2_codec || use_vaapi)
source_set("chromeos") {
  # TODO(crbug.com/1006266): consider using |use_chromeos_video_acceleration|.
  defines = [ "MEDIA_GPU_IMPLEMENTATION" ]
  sources = [
    "chromeos_video_decoder_factory.cc",
    "chromeos_video_decoder_factory.h",
  ]

  deps = [
    "//base",
    "//media",
    "//media/gpu:buildflags",
    "//media/gpu:command_buffer_helper",
    "//media/gpu:common",
  ]

  if (use_vaapi || use_v4l2_codec) {
    deps += [ "//media/gpu/linux:common" ]
  }

  if (use_vaapi) {
    deps += [ "//media/gpu/vaapi" ]
  }

  if (use_v4l2_codec) {
    deps += [ "//media/gpu/v4l2" ]
  }
}

# Although fourcc is platform-agnostic; however, it is restricted by the above
# assert(use_v4l2_codec || use_vaapi).
source_set("fourcc") {
  defines = [ "MEDIA_GPU_IMPLEMENTATION" ]
  sources = [
    "fourcc.cc",
    "fourcc.h",
  ]

  deps = [
    "//base",
    "//media",
    "//media/gpu:buildflags",
    "//media/gpu:common",
  ]
}

source_set("unit_tests") {
  testonly = true
  deps = [
    ":fourcc",
    "//base",
    "//media/gpu:buildflags",
    "//testing/gtest",
  ]
  sources = [
    "fourcc_unittests.cc",
  ]
}
