# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//build/config/arm.gni")
import("//build/config/ui.gni")
import("//media/media_options.gni")

assert(is_android)

source_set("android") {
  sources = [
    "audio_decoder_job.cc",
    "audio_decoder_job.h",
    "browser_cdm_factory_android.cc",
    "demuxer_android.h",
    "demuxer_stream_player_params.cc",
    "demuxer_stream_player_params.h",
    "media_codec_bridge.cc",
    "media_codec_bridge.h",
    "media_decoder_job.cc",
    "media_decoder_job.h",
    "media_drm_bridge.cc",
    "media_drm_bridge.h",
    "media_jni_registrar.cc",
    "media_jni_registrar.h",
    "media_player_android.cc",
    "media_player_android.h",
    "media_player_bridge.cc",
    "media_player_bridge.h",
    "media_player_listener.cc",
    "media_player_listener.h",
    "media_player_manager.h",
    "media_resource_getter.cc",
    "media_resource_getter.h",
    "media_source_player.cc",
    "media_source_player.h",
    "media_url_interceptor.h",
    "video_decoder_job.cc",
    "video_decoder_job.h",
    "webaudio_media_codec_bridge.cc",
    "webaudio_media_codec_bridge.h",
    "webaudio_media_codec_info.h",
  ]
  configs += [ "//media:media_config" ]
  deps = [
    ":media_jni_headers",
    "//third_party/widevine/cdm:version_h",
  ]
}

source_set("unittests") {
  testonly = true
  sources = [
    "media_codec_bridge_unittest.cc",
    "media_drm_bridge_unittest.cc",
    "media_source_player_unittest.cc",
  ]
  deps = [
    ":android",
    "//media/base:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/widevine/cdm:version_h",
  ]
  configs += [ "//media:media_config" ]
}

generate_jni("media_jni_headers") {
  sources = [
    "java/src/org/chromium/media/AudioManagerAndroid.java",
    "java/src/org/chromium/media/AudioRecordInput.java",
    "java/src/org/chromium/media/MediaCodecBridge.java",
    "java/src/org/chromium/media/MediaDrmBridge.java",
    "java/src/org/chromium/media/MediaPlayerBridge.java",
    "java/src/org/chromium/media/MediaPlayerListener.java",
    "java/src/org/chromium/media/UsbMidiDeviceAndroid.java",
    "java/src/org/chromium/media/UsbMidiDeviceFactoryAndroid.java",
    "java/src/org/chromium/media/WebAudioMediaCodecBridge.java",
  ]
  jni_package = "media"
}

generate_jni("video_capture_jni_headers") {
  sources = [
    "java/src/org/chromium/media/VideoCapture.java",
    "java/src/org/chromium/media/VideoCaptureFactory.java",
  ]
  jni_package = "media"
}

java_cpp_template("media_android_imageformat_list") {
  package_name = "org/chromium/media"
  sources = [
    "java/src/org/chromium/media/ImageFormat.template",
  ]
  inputs = [
    "//media/video/capture/android/imageformat_list.h"
  ]
}

android_library("media_java") {
  deps = [
    "//base:base_java",
  ]

  srcjar_deps = [
    ":media_android_imageformat_list",
  ]

  DEPRECATED_java_in_dir = "java/src"
}
