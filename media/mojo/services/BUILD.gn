# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//media/media_options.gni")
import("//testing/test.gni")
import("//third_party/mojo/src/mojo/public/mojo_application.gni")

# Target naming conventions:
# - converters: C++/Mojo type converters.
# - proxy: C++ implementations supported by mojo services.
# - service: Mojo interface implementations.
# - unittests: Unit tests for a particular class/file.
# - test: Tests for a particular app, e.g. media.

config("enable_media_mojo_renderer_config") {
  if (enable_media_mojo_renderer) {
    defines = [ "ENABLE_MEDIA_MOJO_RENDERER" ]
  }
}

source_set("converters") {
  sources = [
    "media_type_converters.cc",
    "media_type_converters.h",
  ]

  deps = [
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/common",
    "//mojo/converters/geometry",
    "//mojo/environment:chromium",
    "//third_party/mojo/src/mojo/public/c/system:for_component",
    "//third_party/mojo_services/src/geometry/public/interfaces",
  ]
}

source_set("cdm_proxy") {
  deps = [
    ":converters",
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//third_party/mojo/src/mojo/public/interfaces/application",
    "//third_party/mojo/src/mojo/public/c/system:for_component",
  ]

  sources = [
    "mojo_cdm.cc",
    "mojo_cdm.h",
    "mojo_type_trait.h",
  ]
}

source_set("cdm_service") {
  deps = [
    ":converters",
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//third_party/mojo/src/mojo/public/interfaces/application",
    "//third_party/mojo/src/mojo/public/c/system:for_component",
  ]

  sources = [
    "mojo_cdm_promise.cc",
    "mojo_cdm_promise.h",
    "mojo_cdm_service.cc",
    "mojo_cdm_service.h",
    "mojo_type_trait.h",
  ]

  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]
}

# media::Renderer implementation using mojo::MediaRenderer.
source_set("renderer_proxy") {
  sources = [
    "mojo_demuxer_stream_impl.cc",
    "mojo_demuxer_stream_impl.h",
    "mojo_renderer_factory.cc",
    "mojo_renderer_factory.h",
    "mojo_renderer_impl.cc",
    "mojo_renderer_impl.h",
  ]

  public_configs = [ ":enable_media_mojo_renderer_config" ]

  deps = [
    ":converters",
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//third_party/mojo/src/mojo/public/c/system:for_component",
    "//third_party/mojo/src/mojo/public/interfaces/application",
  ]
}

# MediaRenderer service; without a specific config.
source_set("renderer_service_generic") {
  sources = [
    "demuxer_stream_provider_shim.cc",
    "demuxer_stream_provider_shim.h",
    "mojo_demuxer_stream_adapter.cc",
    "mojo_demuxer_stream_adapter.h",
    "mojo_renderer_service.cc",
    "mojo_renderer_service.h",
    "renderer_config.cc",
    "renderer_config.h",
  ]

  deps = [
    ":converters",
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//media:shared_memory_support",
    "//mojo/common",
  ]
}

# MediaRenderer service with a default config.
source_set("renderer_service") {
  sources = [
    "renderer_config_default.cc",
  ]

  public_configs = [ ":enable_media_mojo_renderer_config" ]

  deps = [
    ":renderer_service_generic",
    "//base",
    "//media",
    "//media:shared_memory_support",
  ]
}

test("media_mojo_unittests") {
  sources = [
    "media_type_converters_unittest.cc",
  ]

  deps = [
    ":converters",
    "//base",
    "//base/test:test_support",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/environment:chromium",
    "//testing/gtest",
    "//third_party/mojo/src/mojo/edk/system",
    "//third_party/mojo/src/mojo/edk/test:run_all_unittests",
  ]
}

if (!is_component_build) {
  mojo_native_application("media") {
    sources = [
      "mojo_media_application.cc",
    ]

    deps = [
      ":renderer_service",
      "//mojo/application",
      "//third_party/mojo/src/mojo/public/c/system:for_shared_library",
    ]
  }

  # Note, this 'test' must be loaded via mojo_shell as an app:
  #
  #   out/Debug/mojo_shell mojo:media_test
  #
  mojo_native_application("media_test") {
    testonly = true

    deps = [
      "//media/test:mojo_pipeline_integration_tests",
    ]
  }
}

group("services") {
  deps = [
    ":cdm_proxy",
    ":renderer_proxy",
  ]

  if (!is_component_build) {
    deps += [ ":media" ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":media_mojo_unittests",
  ]

  if (!is_component_build) {
    deps += [ ":media_test" ]
  }
}
