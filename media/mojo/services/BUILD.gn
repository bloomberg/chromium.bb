# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//media/media_options.gni")
import("//testing/test.gni")
import("//mojo/public/mojo_application.gni")

# Target naming conventions:
# - converters: C++/Mojo type converters.
# - proxy: C++ implementations supported by mojo services.
# - service: Mojo interface implementations.
# - unittests: Unit tests for a particular class/file.
# - test: Tests for a particular app, e.g. media.

config("enable_mojo_media_config") {
  assert(enable_mojo_media == "none" || enable_mojo_media == "browser" ||
         enable_mojo_media == "gpu" || enable_mojo_media == "utility")

  if (enable_mojo_media != "none") {
    defines = [ "ENABLE_MOJO_MEDIA" ]
  }

  if (enable_mojo_media == "browser") {
    defines += [ "ENABLE_MOJO_MEDIA_IN_BROWSER_PROCESS" ]
  } else if (enable_mojo_media == "gpu") {
    defines += [ "ENABLE_MOJO_MEDIA_IN_GPU_PROCESS" ]
  } else if (enable_mojo_media == "utility") {
    defines += [ "ENABLE_MOJO_MEDIA_IN_UTILITY_PROCESS" ]
  }
}

source_set("converters") {
  sources = [
    "media_type_converters.cc",
    "media_type_converters.h",
  ]

  deps = [
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/common",
    "//mojo/converters/geometry",
    "//mojo/environment:chromium",
    "//mojo/public/c/system:for_component",
    "//ui/mojo/geometry:interfaces",
  ]
}

# Implementations of media C++ interfaces using corresponding mojo services.
source_set("proxy") {
  sources = [
    "mojo_cdm.cc",
    "mojo_cdm.h",
    "mojo_cdm_factory.cc",
    "mojo_cdm_factory.h",
    "mojo_decryptor.cc",
    "mojo_decryptor.h",
    "mojo_demuxer_stream_impl.cc",
    "mojo_demuxer_stream_impl.h",
    "mojo_renderer_factory.cc",
    "mojo_renderer_factory.h",
    "mojo_renderer_impl.cc",
    "mojo_renderer_impl.h",
    "mojo_type_trait.h",
  ]

  public_configs = [ ":enable_mojo_media_config" ]

  deps = [
    ":converters",
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//mojo/public/c/system:for_component",
    "//mojo/shell/public/interfaces",
  ]
}

source_set("media_client") {
  sources = [
    "mojo_media_client.cc",
    "mojo_media_client.h",
  ]
  if (is_android) {
    sources += [ "android_mojo_media_client.cc" ]
  } else {
    sources += [ "default_mojo_media_client.cc" ]
  }

  public_configs = [ ":enable_mojo_media_config" ]

  deps = [
    "//base",
    "//media",
    "//media:shared_memory_support",
  ]
}

source_set("cdm_service") {
  deps = [
    ":converters",
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//mojo/public/c/system:for_component",
    "//mojo/shell/public/interfaces",
  ]

  sources = [
    "mojo_cdm_promise.cc",
    "mojo_cdm_promise.h",
    "mojo_cdm_service.cc",
    "mojo_cdm_service.h",
    "mojo_cdm_service_context.cc",
    "mojo_cdm_service_context.h",
    "mojo_decryptor_service.cc",
    "mojo_decryptor_service.h",
    "mojo_type_trait.h",
  ]

  if (is_android) {
    sources += [
      "mojo_provision_fetcher.cc",
      "mojo_provision_fetcher.h",
    ]
  }

  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]
}

source_set("renderer_service") {
  sources = [
    "demuxer_stream_provider_shim.cc",
    "demuxer_stream_provider_shim.h",
    "mojo_demuxer_stream_adapter.cc",
    "mojo_demuxer_stream_adapter.h",
    "mojo_renderer_service.cc",
    "mojo_renderer_service.h",
  ]

  deps = [
    ":converters",
    "//base",
    "//media",
    "//media:shared_memory_support",
    "//media/mojo/interfaces",
    "//mojo/common",
  ]
}

source_set("application") {
  sources = [
    "mojo_media_application.cc",
    "mojo_media_application.h",
    "service_factory_impl.cc",
    "service_factory_impl.h",
  ]

  public_configs = [ ":enable_mojo_media_config" ]

  deps = [
    ":cdm_service",
    ":media_client",
    ":renderer_service",
    "//base",
    "//mojo/public/c/system:for_component",
    "//mojo/shell/public/cpp",
  ]
}

test("media_mojo_unittests") {
  sources = [
    "media_type_converters_unittest.cc",
  ]

  deps = [
    ":converters",
    "//base",
    "//base/test:test_support",
    "//media",
    "//media/base:test_support",
    "//media/mojo/interfaces",
    "//mojo/environment:chromium",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/mojo/src/mojo/edk/system",
    "//third_party/mojo/src/mojo/edk/test:run_all_unittests",
  ]
}

mojo_native_application("media") {
  sources = [
    "main.cc",
  ]

  deps = [
    ":application",
    "//mojo/public/c/system:for_shared_library",
  ]
}

# Note, the following tests must be loaded via mojo_runner as an app, e.g.
#
#   out/Debug/mojo_runner mojo:media_apptests
#   out/Debug/mojo_runner mojo:media_pipeline_integration_apptests
#
mojo_native_application("media_apptests") {
  testonly = true

  sources = [
    "media_apptest.cc",
  ]

  deps = [
    ":proxy",
    "//media/base:test_support",
    "//mojo/shell/public/cpp:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]

  data_deps = [
    ":media",
  ]
}

mojo_native_application("media_pipeline_integration_apptests") {
  testonly = true

  deps = [
    "//media/test:mojo_pipeline_integration_tests",
  ]

  data_deps = [
    ":media",
  ]
}

group("services") {
  public_deps = [
    ":proxy",
  ]

  if (!is_component_build) {
    public_deps += [ ":media" ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":media_mojo_unittests",
  ]

  if (!is_component_build) {
    deps += [
      ":media_apptests",
      ":media_pipeline_integration_apptests",
    ]
  }
}
