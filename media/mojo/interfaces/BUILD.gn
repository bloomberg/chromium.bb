# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("interfaces") {
  sources = [
    "audio_decoder.mojom",
    "audio_input_stream.mojom",
    "audio_output_stream.mojom",
    "audio_parameters.mojom",
    "cdm_storage.mojom",
    "content_decryption_module.mojom",
    "decryptor.mojom",
    "demuxer_stream.mojom",
    "interface_factory.mojom",
    "jpeg_decode_accelerator.mojom",
    "media_log.mojom",
    "media_types.mojom",
    "output_protection.mojom",
    "platform_verification.mojom",
    "provision_fetcher.mojom",
    "renderer.mojom",
    "video_decode_perf_history.mojom",
    "video_decode_stats_recorder.mojom",
    "video_decoder.mojom",
    "video_encode_accelerator.mojom",
    "watch_time_recorder.mojom",
  ]

  if (is_android) {
    sources += [ "media_drm_storage.mojom" ]
  }

  if (is_android) {
    sources += [ "android_overlay.mojom" ]
  }

  public_deps = [
    "//gpu/ipc/common:interfaces",
    "//mojo/common:common_custom_types",
    "//services/service_manager/public/interfaces",
    "//ui/gfx/geometry/mojo",
    "//url/mojo:url_mojom_gurl",
    "//url/mojo:url_mojom_origin",
  ]

  # TODO(xhwang): Remove media_service_mac.mojom when we support EnabledIf
  # attribute in mojom files. See https://crbug.com/676224
  if (is_mac) {
    sources += [ "media_service_mac.mojom" ]

    # TODO(xhwang): Ideally media should not worry about sandbox. Find a way to
    # remove this dependency.
    public_deps += [ "//sandbox/mac/mojom" ]
  } else {
    sources += [ "media_service.mojom" ]
  }
}

mojom("constants") {
  sources = [
    "constants.mojom",
  ]
}

mojom("remoting_common") {
  sources = [
    "remoting_common.mojom",
  ]
}

mojom("mirror_service_remoting") {
  sources = [
    "mirror_service_remoting.mojom",
  ]

  public_deps = [
    ":remoting_common",
  ]
}

mojom("remoting") {
  sources = [
    "remoting.mojom",
  ]

  public_deps = [
    ":remoting_common",
  ]
}

mojom("test_interfaces") {
  testonly = true
  sources = [
    "traits_test_service.mojom",
  ]
  public_deps = [
    ":interfaces",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "audio_decoder_config_struct_traits_unittest.cc",
    "encryption_scheme_struct_traits_unittest.cc",
    "video_decoder_config_struct_traits_unittest.cc",
    "video_frame_struct_traits_unittest.cc",
  ]

  deps = [
    "//base",
    "//media/mojo:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/gfx:test_support",
  ]
}
