# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")

group("network") {
  visibility = [ "//content/*" ]  # This is an internal content API.

  if (is_component_build) {
    public_deps = [
      "//content",
    ]
  } else {
    public_deps = [
      ":network_sources",
    ]
  }
}

jumbo_source_set("network_sources") {
  # Depend on via ":network above.
  visibility = [
    ":network",
    "//content",  # For the component build.

    # For use by StoragePartition.
    # TODO(mmenke): Remove when network service ships.
    "//content/browser",
  ]

  sources = [
    "cors/cors_url_loader.cc",
    "cors/cors_url_loader.h",
    "cors/cors_url_loader_factory.cc",
    "cors/cors_url_loader_factory.h",
    "resource_scheduler.cc",
    "resource_scheduler.h",
  ]

  configs += [ "//content:content_implementation" ]

  deps = [
    "//base",
    "//content:export",
    "//content/common",
    "//content/public/common:common_sources",
    "//net",
    "//services/network/public/cpp",
  ]
}
