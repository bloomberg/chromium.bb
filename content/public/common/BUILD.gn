# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//content/common/common.gni")

# See //content/BUILD.gn for how this works.
group("common") {
  if (is_component_build) {
    public_deps = [ "//content" ]
  } else {
    public_deps = [ ":common_sources" ]
  }
}

source_set("common_sources") {
  visibility = [ "//content/*" ]

  sources = rebase_path(content_common_gypi_values.public_common_sources,
                        ".", "//content")

  configs += [
    "//content:content_implementation",
  ]

  public_deps = [
    "//content/common",
  ]
  deps = [
    "//net",
    "//skia",
    "//third_party/WebKit/public:blink_headers",
    "//third_party/icu",
    "//ui/base",
    "//ui/gfx",
  ]

  if (!enable_plugins) {
    sources -= [
      "pepper_plugin_info.cc",
      "pepper_plugin_info.h",
    ]
  }
}
