# -*- python -*-
# Copyright (c) 2011 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#

Import('env')

env.Prepend(CPPDEFINES=['XP_UNIX'])
env.Replace(TEST_DIR='${SOURCE_ROOT}/ppapi/native_client/tests/ppapi_browser/' +
                     'ppb_pdf')

nexe = 'ppapi_ppb_pdf_%s' % env.get('TARGET_FULLARCH')
env.Alias('ppapi_ppb_pdf${PROGSUFFIX}',
          ['$STAGING_DIR/%s${PROGSUFFIX}' % nexe])

ppapi_ppb_pdf_nexe = env.ComponentProgram(
    nexe,
    ['ppapi_ppb_pdf.cc'],
    EXTRA_LIBS=['${PPAPI_LIBS}',
                'ppapi_test_lib',
                'platform',  # for CHECK
                'pthread',
                'gio',
                ])

env.Publish(nexe, 'run',
            ['ppapi_ppb_pdf.html',
             'ppapi_ppb_pdf.js'])

node = env.PPAPIBrowserTester('ppapi_ppb_pdf_browser_test.out',
                              url='ppapi_ppb_pdf.html',
                              nmfs=['${TEST_DIR}/ppapi_ppb_pdf.nmf'],
                              files=env.ExtractPublishedFiles(nexe))

env.AddNodeToTestSuite(node,
                       ['chrome_browser_tests'],
                       'run_ppapi_ppb_pdf_browser_test',
                    # Pnacl x86-64 requires a shim. However the shim
                    # is generated via IDL, and pdf header is not
                    # generated via IDL right now
                    # http://code.google.com/p/chromium/issues/detail?id=89968
                       is_broken=(
                           env.PPAPIBrowserTesterIsBroken() or
                           (env.Bit('bitcode') and env.Bit('target_x86_64'))))
