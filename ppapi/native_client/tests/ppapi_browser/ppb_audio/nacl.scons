# -*- python -*-
# Copyright (c) 2012 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
# This test uses ppapi_test_lib.

Import('env')

env.Prepend(CPPDEFINES=['XP_UNIX'])
env.Replace(TEST_DIR='${SOURCE_ROOT}/ppapi/native_client/tests/ppapi_browser/'
                     'ppb_audio')

nexe = 'ppapi_ppb_audio_${TARGET_FULLARCH}'
env.Alias('ppapi_ppb_audio${PROGSUFFIX}',
          ['$STAGING_DIR/%s${PROGSUFFIX}' % nexe])

ppapi_ppb_audio_nexe = env.ComponentProgram(
    nexe,
    [ 'ppapi_ppb_audio.cc' ],
    EXTRA_LIBS=['${PPAPI_LIBS}',
                'ppapi_test_lib',
                'platform',  # for CHECK
                'pthread',
                'gio',
               ])

dest_copy = env.Publish(nexe, 'run',
                        ['ppapi_ppb_audio.html',
                         env.File('${SCONSTRUCT_DIR}/tools/browser_tester/'
                                  'browserdata/nacltest.js')
                        ])

node = env.PPAPIBrowserTester('ppapi_ppb_audio_browser_test.out',
                              url='ppapi_ppb_audio.html',
                              nmfs=['${TEST_DIR}/ppapi_ppb_audio.nmf'],
                              files=[ ppapi_ppb_audio_nexe,
                                      env.File('ppapi_ppb_audio.html')],
                              browser_flags=['--enable-pepper-testing'])

# Though this tests passes locally, it does not pass on the Windows
# and Linux buildbots, which do not have audio devices.  Although
# StartPlayback() and StopPlayback() succeed, the audio callback is
# not called.  However, this does work on the Mac buildbots.
is_broken = env.PPAPIBrowserTesterIsBroken() or not env.Bit('host_mac')
env.AddNodeToTestSuite(node,
                       ['chrome_browser_tests'],
                       'run_ppapi_ppb_audio_browser_test',
                       is_broken=is_broken)
