# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/public/provider/chrome/browser/build_config.gni")

source_set("browser") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "browser_url_rewriter_provider.h",
    "browser_url_rewriter_provider.mm",
    "chrome_browser_provider.h",
    "chrome_browser_provider.mm",
    "overrides_provider.h",
    "overrides_provider.mm",
    "text_zoom_provider.h",
    "text_zoom_provider.mm",
  ]
  deps = [
    ":font_size_java_script_feature",
    "//base",
    "//components/metrics",
    "//ios/public/provider/chrome/browser/mailto",
    "//ios/public/provider/chrome/browser/signin",
    "//ios/web/public",
    "//ios/web/public/js_messaging",
    "//url",
  ]
  frameworks = [ "CoreLocation.framework" ]
}

source_set("font_size_java_script_feature") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "font_size_java_script_feature.h",
    "font_size_java_script_feature.mm",
  ]
  deps = [
    "//base",
    "//ios/web/public",
  ]
  public_deps = [ "//ios/web/public/js_messaging" ]
}

group("provider_api") {
  deps = [
    # The individual APIs.
    "//ios/public/provider/chrome/browser/modals:modals_api",
    "//ios/public/provider/chrome/browser/text_zoom:text_zoom_api",
  ]
}

source_set("test_support") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "test_chrome_browser_provider.h",
    "test_chrome_browser_provider.mm",
    "test_text_zoom_provider.h",
    "test_text_zoom_provider.mm",
  ]
  deps = [
    ":browser",
    "//base",
    "//ios/public/provider/chrome/browser/discover_feed",
    "//ios/public/provider/chrome/browser/distribution",
    "//ios/public/provider/chrome/browser/images:test_support",
    "//ios/public/provider/chrome/browser/mailto:test_support",
    "//ios/public/provider/chrome/browser/omaha:test_support",
    "//ios/public/provider/chrome/browser/signin",
    "//ios/public/provider/chrome/browser/signin:test_support",
    "//ios/public/provider/chrome/browser/spotlight:test_support",
    "//ios/public/provider/chrome/browser/ui",
    "//ios/public/provider/chrome/browser/user_feedback:test_support",
    "//ios/public/provider/chrome/browser/voice",
    "//ios/public/provider/chrome/browser/voice:test_support",
    "//testing/gtest",
  ]
}

source_set("test_provider_factory") {
  testonly = true
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [ "test_chrome_browser_provider_factory.mm" ]
  deps = [
    ":browser",
    ":test_support",
  ]
}

group("test_providers") {
  testonly = true
  deps = [
    # The target providing the ChromeBrowserProvider factory.
    ":test_provider_factory",

    # The individual API implementations.
    "//ios/public/provider/chrome/browser/modals:test_modals",
    "//ios/public/provider/chrome/browser/text_zoom:test_text_zoom",

    # The provider API needs to provide MaterialDesignComponent API (as the
    # internal provider provides an alternate implementation).
    "//ios/third_party/material_components_ios:material_components_ios+bundle",
    "//ios/third_party/material_components_ios:material_components_ios+link",
  ]
}
