# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//ios/build/chrome_build.gni")
import("//ios/chrome/features.gni")

source_set("application_delegate") {
  sources = [
    "memory_warning_helper.h",
    "memory_warning_helper.mm",
  ]
  deps = [
    "//base",
    "//components/previous_session_info",
    "//ios/chrome/browser/crash_report",
  ]
  frameworks = [ "Foundation.framework" ]

  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "app_state_unittest.mm",
    "memory_warning_helper_unittest.mm",
    "metric_kit_subscriber_unittest.mm",
    "metrics_mediator_unittest.mm",
    "url_opener_params_unittest.mm",
    "url_opener_unittest.mm",
    "user_activity_handler_unittest.mm",
  ]
  deps = [
    ":application_delegate",
    ":application_delegate_internal",
    ":metric_kit_subscriber",
    ":tab_opening",
    ":test_support",
    ":url_opener_params",
    "//base",
    "//base/test:test_support",
    "//components/handoff",
    "//components/metrics",
    "//components/previous_session_info",
    "//ios/chrome/app",
    "//ios/chrome/app:app_internal",
    "//ios/chrome/app:enterprise_app_agent",
    "//ios/chrome/app:mode",
    "//ios/chrome/app:safe_mode_app_state_agent",
    "//ios/chrome/app/spotlight",
    "//ios/chrome/app/startup",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/chrome/browser/crash_report",
    "//ios/chrome/browser/device_sharing",
    "//ios/chrome/browser/geolocation",
    "//ios/chrome/browser/main:public",
    "//ios/chrome/browser/main:test_support",
    "//ios/chrome/browser/metrics",
    "//ios/chrome/browser/ntp_snippets",
    "//ios/chrome/browser/signin",
    "//ios/chrome/browser/signin:test_support",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/u2f",
    "//ios/chrome/browser/ui/commands",
    "//ios/chrome/browser/ui/main",
    "//ios/chrome/browser/ui/main:scene",
    "//ios/chrome/browser/ui/main/test",
    "//ios/chrome/browser/ui/safe_mode",
    "//ios/chrome/browser/ui/settings",
    "//ios/chrome/browser/ui/settings:settings_root",
    "//ios/chrome/browser/url_loading",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/browser/web_state_list:test_support",
    "//ios/chrome/common/app_group",
    "//ios/chrome/common/intents",
    "//ios/chrome/test:test_support",
    "//ios/chrome/test/ocmock",
    "//ios/chrome/test/providers/app_distribution",
    "//ios/public/provider/chrome/browser:test_support",
    "//ios/public/provider/chrome/browser/app_distribution:app_distribution_api",
    "//ios/public/provider/chrome/browser/user_feedback:test_support",
    "//ios/testing:block_swizzler",
    "//ios/testing:ocmock_support",
    "//ios/testing:open_url_context",
    "//ios/web",
    "//ios/web/public/test",
    "//ios/web/public/test/fakes",
    "//net:test_support",
    "//testing/gtest",
    "//third_party/breakpad:client",
    "//third_party/ocmock",
    "//ui/base",
    "//url",
  ]
  frameworks = [ "CoreSpotlight.framework" ]
}

source_set("tab_opening") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "tab_opening.h",
    "tab_switching.h",
  ]
  deps = [
    "//base",
    "//ios/chrome/app",
    "//ios/chrome/app:mode",
    "//ios/chrome/browser/main:public",
    "//ui/base",
  ]
  frameworks = [
    "CoreSpotlight.framework",
    "UIKit.framework",
  ]
}

source_set("url_opener_params") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "url_opener_params.h",
    "url_opener_params.mm",
  ]
  deps = [ "//base" ]
}

source_set("metric_kit_subscriber") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "metric_kit_subscriber.h",
    "metric_kit_subscriber.mm",
  ]
  frameworks = [ "MetricKit.framework" ]
  deps = [
    "//base",
    "//components/crash/core/app",
    "//components/crash/core/common:reporter_running_ios",
    "//components/version_info",
    "//ios/chrome/browser/crash_report",
  ]
}

source_set("app_state_header") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "app_state.h",
    "app_state_agent.h",
    "app_state_observer.h",
  ]
  public_deps = [
    "//ios/chrome/browser/ui/main:scene_state_observer",
    "//ios/chrome/browser/ui/scoped_ui_blocker",
  ]
}

source_set("observing_app_state_agent") {
  configs += [ "//build/config/compiler:enable_arc" ]

  sources = [
    "observing_app_state_agent.h",
    "observing_app_state_agent.mm",
  ]

  frameworks = [ "UIKit.framework" ]

  deps = [ "//base" ]

  public_deps = [
    ":app_state_header",
    "//ios/chrome/browser/ui/main:scene_state_header",
    "//ios/chrome/browser/ui/main:scene_state_observer",
  ]
}

source_set("application_delegate_internal") {
  configs += [ "//build/config/compiler:enable_arc" ]
  public_deps = [ ":app_state_header" ]
  sources = [
    "app_state.mm",
    "browser_launcher.h",
    "metrics_mediator.h",
    "metrics_mediator.mm",
    "startup_information.h",
    "tab_switching.h",
    "url_opener.h",
    "url_opener.mm",
    "user_activity_handler.h",
    "user_activity_handler.mm",
  ]
  deps = [
    ":application_delegate",
    ":metric_kit_subscriber",
    ":tab_opening",
    ":url_opener_params",
    "//base",
    "//base/ios",
    "//build:branding_buildflags",
    "//components/crash/core/common",
    "//components/feature_engagement",
    "//components/handoff",
    "//components/metrics",
    "//components/prefs",
    "//components/previous_session_info",
    "//components/search_engines",
    "//components/ukm/ios:ukm_reporting_ios_util",
    "//ios/chrome/app",
    "//ios/chrome/app:mode",
    "//ios/chrome/app/spotlight",
    "//ios/chrome/app/startup",
    "//ios/chrome/app/startup:ios_enable_sandbox_dump_buildflags",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/browsing_data",
    "//ios/chrome/browser/crash_report",
    "//ios/chrome/browser/device_sharing",
    "//ios/chrome/browser/feature_engagement",
    "//ios/chrome/browser/geolocation",
    "//ios/chrome/browser/main:public",
    "//ios/chrome/browser/metrics",
    "//ios/chrome/browser/metrics:metrics_internal",
    "//ios/chrome/browser/policy:policy_util",
    "//ios/chrome/browser/search_engines",
    "//ios/chrome/browser/signin",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/u2f",
    "//ios/chrome/browser/ui/authentication",
    "//ios/chrome/browser/ui/browser_view",
    "//ios/chrome/browser/ui/commands",
    "//ios/chrome/browser/ui/content_suggestions:feature_flags",
    "//ios/chrome/browser/ui/default_promo:utils",
    "//ios/chrome/browser/ui/main",
    "//ios/chrome/browser/ui/main:scene",
    "//ios/chrome/browser/ui/ntp:util",
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/browser/url_loading",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/browser/web_state_list:agents",
    "//ios/chrome/browser/web_state_list:session_metrics",
    "//ios/chrome/browser/widget_kit:features",
    "//ios/chrome/common/app_group",
    "//ios/chrome/common/app_group:main_app",
    "//ios/chrome/common/credential_provider",
    "//ios/chrome/common/intents",
    "//ios/net",
    "//ios/public/provider/chrome/browser",
    "//ios/public/provider/chrome/browser/app_distribution:app_distribution_api",
    "//ios/public/provider/chrome/browser/signin",
    "//ios/public/provider/chrome/browser/user_feedback",
    "//ios/web",
    "//net",
    "//ui/base",
    "//url",
  ]

  if (ios_enable_widget_kit_extension) {
    deps += [ "//ios/chrome/browser/widget_kit" ]
  }

  frameworks = [
    "CoreSpotlight.framework",
    "UIKit.framework",
  ]
  allow_circular_includes_from = [
    "//ios/chrome/browser/url_loading",
    "//ios/chrome/browser/ui/main:scene",
  ]
}

source_set("test_support") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "app_state_testing.h",
    "fake_startup_information.h",
    "fake_startup_information.mm",
    "metrics_mediator_testing.h",
    "mock_metrickit_metric_payload.h",
    "mock_metrickit_metric_payload.mm",
    "mock_tab_opener.h",
    "mock_tab_opener.mm",
  ]
  deps = [
    ":application_delegate_internal",
    ":tab_opening",
    "//base",
    "//base/test:test_support",
    "//components/version_info",
    "//ios/chrome/app",
    "//ios/chrome/app:app_internal",
    "//ios/chrome/app:mode",
    "//ios/chrome/browser",
    "//ios/chrome/browser/url_loading",
    "//net",
    "//third_party/ocmock",
    "//ui/base",
    "//url",
  ]
  frameworks = [
    "MetricKit.framework",
    "UIKit.framework",
  ]
}
