# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ios/rules.gni")

source_set("ui") {
  sources = [
    "native_content_controller.h",
    "native_content_controller.mm",
  ]
  public_deps = [
    "//ios/chrome/browser/ui/util",
    "//ios/chrome/common/ui_util",
  ]
  deps = [
    "//base",
    "//base:i18n",
    "//components/sessions",
    "//ios/chrome/browser",
    "//ios/chrome/browser/ui/commands",
    "//ui/base",
  ]
  allow_circular_includes_from = [ "//ios/chrome/browser/ui/commands" ]
  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("feature_flags") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "ui_feature_flags.cc",
    "ui_feature_flags.h",
  ]
  deps = [
    "//base",
  ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "native_content_controller_unittest.mm",
  ]
  deps = [
    ":native_content_controller_test_xib",
    ":resources_unit_tests",
    ":ui",
    "//base",
    "//base/test:test_support",
    "//components/bookmarks/browser",
    "//components/bookmarks/test",
    "//components/omnibox/browser:test_support",
    "//components/payments/core",
    "//components/prefs:test_support",
    "//components/search_engines",
    "//components/sessions",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser",
    "//ios/chrome/browser/bookmarks",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/chrome/browser/favicon",
    "//ios/chrome/browser/find_in_page",
    "//ios/chrome/browser/main:test_support",
    "//ios/chrome/browser/search_engines",
    "//ios/chrome/browser/sessions",
    "//ios/chrome/browser/snapshots",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/ui:feature_flags",
    "//ios/chrome/browser/ui/activity_services",
    "//ios/chrome/browser/ui/alert_coordinator",
    "//ios/chrome/browser/ui/browser_container:ui",
    "//ios/chrome/browser/ui/commands",
    "//ios/chrome/browser/ui/ntp:ntp_controller",
    "//ios/chrome/browser/ui/toolbar/public",
    "//ios/chrome/browser/ui/toolbar/test",
    "//ios/chrome/browser/url_loading",
    "//ios/chrome/browser/web",
    "//ios/chrome/browser/web:web_internal",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/browser/web_state_list:test_support",
    "//ios/chrome/browser/web_state_list/web_usage_enabler",
    "//ios/chrome/test:test_support",
    "//ios/net",
    "//ios/testing:ocmock_support",
    "//ios/web/public/test",
    "//ios/web/public/test/fakes",
    "//net",
    "//net:test_support",
    "//services/network:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/ocmock",
    "//ui/base",
    "//ui/base:test_support",
    "//url",
  ]
}

bundle_data_ib_file("native_content_controller_test_xib") {
  visibility = [ ":unit_tests" ]
  testonly = true
  source = "native_content_controller_test.xib"
}

bundle_data("resources_unit_tests") {
  visibility = [ ":unit_tests" ]
  testonly = true
  sources = [
    "//ios/chrome/test/data/testbadpass.pkpass",
    "//ios/chrome/test/data/testpass.pkpass",
  ]
  outputs = [
    "{{bundle_resources_dir}}/ios/chrome/test/data/{{source_file_part}}",
  ]
}
