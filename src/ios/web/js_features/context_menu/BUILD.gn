# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/build/config.gni")
import("//ios/web/public/js_messaging/optimize_js.gni")

source_set("context_menu") {
  configs += [ "//build/config/compiler:enable_arc" ]
  deps = [
    ":all_frames_context_menu_js",
    ":main_frame_context_menu_js",
    "//base",
    "//components/url_formatter",
    "//ios/web/common",
    "//ios/web/public",
    "//ios/web/public/js_messaging",
  ]

  sources = [
    "context_menu_constants.h",
    "context_menu_constants.mm",
    "context_menu_java_script_feature.h",
    "context_menu_java_script_feature.mm",
    "context_menu_params.mm",
    "context_menu_params_utils.h",
    "context_menu_params_utils.mm",
  ]
}

optimize_js("all_frames_context_menu_js") {
  visibility = [ ":context_menu" ]

  primary_script = "resources/all_frames_context_menu.js"
  sources = [ "resources/all_frames_context_menu.js" ]
}

optimize_js("main_frame_context_menu_js") {
  visibility = [ ":context_menu" ]

  primary_script = "resources/main_frame_context_menu.js"
  sources = [ "resources/main_frame_context_menu.js" ]
}

source_set("unittests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true

  deps = [
    ":context_menu",
    "//base",
    "//base/test:test_support",
    "//components/url_formatter",
    "//ios/web/common",
    "//ios/web/common:web_view_creation_util",
    "//ios/web/js_messaging",
    "//ios/web/js_messaging:java_script_feature",
    "//ios/web/public/test",
    "//ios/web/public/test:test_fixture",
    "//ios/web/test/fakes",
    "//testing/gtest",
  ]

  sources = [
    "context_menu_java_script_feature_unittest.mm",
    "context_menu_js_unittest.mm",
    "context_menu_params_utils_unittest.mm",
  ]
}
