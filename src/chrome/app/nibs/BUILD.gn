# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/mac/rules.gni")
import("//ui/base/ui_features.gni")

assert(is_mac)

translated_xibs = [
  "AppMenu.xib",
  "DownloadItem.xib",
  "DownloadShelf.xib",
  "HungRendererDialog.xib",
  "OneClickSigninDialog.xib",
  "SaveAccessoryView.xib",
  "TaskManager.xib",
  "Toolbar.xib",
]

if (!mac_views_browser) {
  translated_xibs += [ "HttpAuthLoginSheet.xib" ]
}

untranslated_xibs = [
  "BookmarkBarFolderWindow.xib",
  "FindBar.xib",
  "InfoBar.xib",
]

mac_xib_bundle_data("chrome_xibs") {
  sources = translated_xibs + untranslated_xibs
}

action("localizer_table") {
  script = "generate_localizer.py"

  sources = translated_xibs
  table_path = "$target_gen_dir/localizer_table.h"
  outputs = [
    table_path,
  ]
  args = [
    "--output_path",
    rebase_path(table_path, root_build_dir),
  ]
  if (!use_system_xcode) {
    args += [
      "--developer_dir",
      hermetic_xcode_path,
    ]
  }
  args += rebase_path(translated_xibs, root_build_dir)
}
