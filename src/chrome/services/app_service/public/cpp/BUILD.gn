# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("app_update") {
  sources = [
    "app_registry_cache.cc",
    "app_registry_cache.h",
    "app_update.cc",
    "app_update.h",
  ]

  public_deps = [ "//chrome/services/app_service/public/mojom" ]
}

if (is_chromeos) {
  source_set("instance_update") {
    sources = [
      "instance.cc",
      "instance.h",
      "instance_registry.cc",
      "instance_registry.h",
      "instance_update.cc",
      "instance_update.h",
    ]
    deps = [
      "//content/public/browser",
      "//skia",
      "//ui/aura",
      "//ui/compositor",
    ]
  }
}

source_set("icon_loader") {
  sources = [
    "icon_cache.cc",
    "icon_cache.h",
    "icon_coalescer.cc",
    "icon_coalescer.h",
    "icon_loader.cc",
    "icon_loader.h",
  ]

  public_deps = [ "//chrome/services/app_service/public/mojom" ]
}

source_set("icon_loader_test_support") {
  sources = [
    "stub_icon_loader.cc",
    "stub_icon_loader.h",
  ]

  deps = [ ":icon_loader" ]
}

source_set("intents") {
  sources = [
    "intent_filter_util.cc",
    "intent_filter_util.h",
    "intent_util.cc",
    "intent_util.h",
  ]

  deps = [
    "//base",
    "//chrome/services/app_service/public/mojom",
    "//components/services/app_service/public/cpp:intent_util",
    "//url",
  ]
}

source_set("preferred_apps") {
  sources = [
    "preferred_apps_converter.cc",
    "preferred_apps_converter.h",
    "preferred_apps_list.cc",
    "preferred_apps_list.h",
  ]

  deps = [
    ":intents",
    "//base",
    "//chrome/services/app_service/public/mojom",
    "//url",
  ]
}

source_set("publisher") {
  sources = [
    "publisher_base.cc",
    "publisher_base.h",
  ]

  deps = [ "//chrome/services/app_service/public/mojom" ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "app_registry_cache_unittest.cc",
    "app_update_unittest.cc",
    "icon_cache_unittest.cc",
    "icon_coalescer_unittest.cc",
    "intent_test_util.cc",
    "intent_test_util.h",
    "intent_util_unittest.cc",
    "preferred_apps_converter_unittest.cc",
    "preferred_apps_list_unittest.cc",
  ]

  deps = [
    ":app_update",
    ":icon_loader",
    ":publisher",
    "//content/test:test_support",
    "//testing/gtest",
  ]

  if (is_chromeos) {
    sources += [
      "instance_registry_unittest.cc",
      "instance_update_unittest.cc",
    ]

    deps += [ ":instance_update" ]
  }
}
