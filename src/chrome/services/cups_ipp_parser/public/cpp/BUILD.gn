# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//printing/buildflags/buildflags.gni")
import("//testing/libfuzzer/fuzzer_test.gni")

if (use_cups) {
  source_set("cpp") {
    sources = [
      "ipp_converter.cpp",
      "ipp_converter.h",
    ]

    deps = [
      "//base",
      "//chrome/services/cups_ipp_parser/public/mojom",
      "//net",
      "//printing",
    ]

    configs += [ "//printing:cups" ]
  }

  # Fuzzer target only available on fuzzing builds.
  if (use_fuzzing_engine) {
    fuzzer_test("ipp_message_parser_fuzzer") {
      sources = [
        "ipp_message_parser_fuzzer.cc",
      ]
      deps = [
        ":cpp",
      ]

      dict = "ipp_message_parser_fuzzer.dict"
    }
  }
}

if (is_chromeos) {
  source_set("manifest") {
    sources = [
      "manifest.cc",
      "manifest.h",
    ]

    deps = [
      "//base",
      "//chrome:strings",
      "//chrome/services/cups_ipp_parser/public/mojom",
      "//services/service_manager/public/cpp",
    ]
  }
}

# A dummy group to make fuzz targets discoverable.
group("fuzzers") {
}
