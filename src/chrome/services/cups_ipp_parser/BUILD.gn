# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//printing/buildflags/buildflags.gni")

enable_service = use_cups && is_chromeos

source_set("cups_ipp_parser") {
  sources = [
    "cups_ipp_parser_service.cc",
    "cups_ipp_parser_service.h",
    "ipp_parser.h",
  ]

  deps = [
    "//base",
    "//chrome/services/cups_proxy/public/cpp",
    "//mojo/public/cpp/bindings",
    "//net",
  ]

  public_deps = [
    "//chrome/services/cups_ipp_parser/public/mojom",
    "//mojo/public/mojom/base",
    "//printing",
    "//services/service_manager/public/cpp",
  ]

  # We stub the implementation if libCUPS is not present.
  if (enable_service) {
    configs += [ "//printing:cups" ]
    sources += [ "ipp_parser.cc" ]
    deps += [ "//chrome/services/cups_ipp_parser/public/cpp" ]
  } else {
    sources += [ "fake_ipp_parser.cc" ]
  }
}
