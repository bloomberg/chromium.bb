# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

android_library("java") {
  visibility = [
    ":*",
    "//chrome/android:chrome_all_java",
    "//chrome/browser/ui/android/appmenu/test:*",
  ]

  sources = [
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenu.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuAdapter.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuButtonHelperImpl.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuCoordinatorFactory.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuCoordinatorImpl.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuDragHelper.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuHandlerImpl.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuItemIcon.java",
  ]

  deps = [
    ":java_resources",
    "//base:base_java",
    "//chrome/browser/android/lifecycle:java",
    "//chrome/browser/ui/android/appmenu:java",
    "//components/browser_ui/widget/android:java",
    "//third_party/android_deps:android_support_v7_appcompat_java",
    "//third_party/android_deps:androidx_annotation_annotation_java",
    "//ui/android:ui_java",
  ]
}

android_resources("java_resources") {
  custom_package = "org.chromium.chrome.browser.ui.appmenu.internal"
  sources = [
    "java/res/layout/icon_row_menu_item.xml",
    "java/res/layout/menu_item.xml",
    "java/res/layout/title_button_menu_item.xml",
    "java/res/values/dimens.xml",
    "java/res/values/ids.xml",
  ]

  # Include resource dependencies needed by :java so that resources may
  # all be accessed through the same custom_package.
  deps = [
    "//chrome/browser/ui/android/appmenu:java_resources",
    "//components/browser_ui/widget/android:java_resources",
  ]
}

android_library("javatests") {
  testonly = true

  sources = [
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuAdapterRenderTest.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuAdapterTest.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuTest.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/TestAppMenuDelegate.java",
    "java/src/org/chromium/chrome/browser/ui/appmenu/TestAppMenuPropertiesDelegate.java",
  ]
  deps = [
    ":java",
    ":test_java_resources",
    "//base:base_java",
    "//base:base_java_test_support",
    "//chrome/browser/android/lifecycle:java",
    "//chrome/browser/ui/android/appmenu:java",
    "//chrome/browser/ui/android/appmenu/test:test_support_java",
    "//chrome/test/android:chrome_java_test_support",
    "//components/browser_ui/widget/android:test_support_java",
    "//content/public/test/android:content_java_test_support",
    "//third_party/android_deps:android_support_v7_appcompat_java",
    "//third_party/android_support_test_runner:rules_java",
    "//third_party/android_support_test_runner:runner_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
    "//ui/android:ui_java_test_support",
  ]
}

android_resources("test_java_resources") {
  testonly = true

  custom_package = "org.chromium.chrome.browser.ui.appmenu.test"
  sources = [
    "test/java/res/drawable/test_ic_arrow_downward_black_24dp.xml",
    "test/java/res/drawable/test_ic_arrow_forward_black_24dp.xml",
    "test/java/res/drawable/test_ic_info_outline_black_24dp.xml",
    "test/java/res/drawable/test_ic_more_vert_black_24dp.xml",
    "test/java/res/drawable/test_ic_refresh_black_24dp.xml",
    "test/java/res/drawable/test_ic_star_border_black_24dp.xml",
    "test/java/res/drawable/test_ic_vintage_filter.xml",
    "test/java/res/layout/test_app_menu_activity_layout.xml",
    "test/java/res/layout/test_menu_footer.xml",
    "test/java/res/layout/test_menu_header.xml",
    "test/java/res/menu/test_menu.xml",
  ]
  deps = [ ":java_resources" ]
}

java_library("junit") {
  # Skip platform checks since Robolectric depends on requires_android targets.
  bypass_platform_checks = true
  testonly = true
  sources = [ "java/src/org/chromium/chrome/browser/ui/appmenu/AppMenuPopupPositionTest.java" ]
  deps = [
    ":java",
    "//base:base_junit_test_support",
  ]
}
