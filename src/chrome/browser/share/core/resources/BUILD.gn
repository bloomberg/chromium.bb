# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Generate the binary proto form of "share_targets" from the ascii proto.
action("make_share_targets_protobuf") {
  script = "gen_share_targets_proto.py"

  # The output goes in $target_gen_dir since that's where
  # chrome/browser/browser_resources.grd will look for it.

  input_filename = "share_targets.asciipb"
  output_dir = target_gen_dir
  output_basename = "desktop_sharing_hub.pb"
  python_path_root = "$root_out_dir/pyproto"
  python_path_share_proto = "$python_path_root/chrome/browser/share/proto"
  inputs = [ input_filename ]

  deps = [
    "//chrome/browser/share/proto:proto",
    "//third_party/protobuf:py_proto",
  ]

  outputs = [ "$output_dir/$output_basename" ]

  args = [
    "-w",
    "-i",
    rebase_path(input_filename, root_build_dir),
    "-d",
    rebase_path(output_dir, root_build_dir),
    "-o",
    output_basename,
    "-p",
    rebase_path(python_path_root, root_build_dir),
    "-p",
    rebase_path(python_path_share_proto, root_build_dir),
  ]
}
