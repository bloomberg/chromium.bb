# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//rlz/buildflags/buildflags.gni")

assert(enable_extensions)

source_set("music_manager_private") {
  sources = [
    "device_id.cc",
    "device_id.h",
    "device_id_chromeos.cc",
    "device_id_linux.cc",
    "device_id_mac.cc",
    "device_id_win.cc",
    "music_manager_private_api.cc",
    "music_manager_private_api.h",
  ]

  deps = [
    "//base",
    "//chrome/common/apps/platform_apps/api",
    "//content/public/browser",
    "//crypto",
    "//extensions/browser",
    "//rlz/buildflags",
  ]

  libs = []
  if (is_win) {
    libs += [ "iphlpapi.lib" ]
  }

  if (is_chromeos) {
    sources -= [ "device_id_linux.cc" ]

    deps += [ "//chromeos/cryptohome" ]
  }

  if (enable_rlz_support) {
    deps += [ "//rlz:rlz_lib" ]
  }
}
