# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//rlz/buildflags/buildflags.gni")

assert(enable_extensions)

source_set("music_manager_private") {
  sources = [
    "device_id.cc",
    "device_id.h",
    "music_manager_private_api.cc",
    "music_manager_private_api.h",
  ]

  deps = [
    "//base",
    "//chrome/common/apps/platform_apps/api",
    "//content/public/browser",
    "//crypto",
    "//extensions/browser",
    "//rlz/buildflags",
  ]

  libs = []
  if (is_win) {
    libs += [ "iphlpapi.lib" ]
  }

  if (is_chromeos_ash) {
    sources += [ "device_id_chromeos.cc" ]
  } else if (is_linux || is_chromeos_lacros) {
    sources += [ "device_id_linux.cc" ]
  } else if (is_mac) {
    sources += [ "device_id_mac.cc" ]
  } else if (is_win) {
    sources += [ "device_id_win.cc" ]
  }

  if (is_chromeos_ash) {
    deps += [ "//chromeos/cryptohome" ]
  }

  if (enable_rlz_support) {
    deps += [ "//rlz:rlz_lib" ]
  }
}
