# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("network") {
  public = [ "key_network_delegate.h" ]
  deps = [ "//base" ]

  if (is_win) {
    public += [ "win_key_network_delegate.h" ]

    sources = [ "win_key_network_delegate.cc" ]

    public_deps = [ "//components/winhttp" ]

    deps += [
      "//net",
      "//url",
    ]
  }

  if (is_linux) {
    public += [ "linux_key_network_delegate.h" ]

    sources = [ "linux_key_network_delegate.cc" ]

    deps += [
      "//mojo/public/cpp/bindings",
      "//mojo/public/cpp/platform",
      "//services/network/public/cpp",
      "//services/network/public/mojom",
      "//url",
    ]
  }
}

source_set("test_support") {
  testonly = true
  public = [ "mock_key_network_delegate.h" ]

  sources = [ "mock_key_network_delegate.cc" ]

  public_deps = [
    ":network",
    "//testing/gmock",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true

  if (is_win) {
    sources = [ "win_key_network_delegate_unittest.cc" ]

    deps = [
      ":network",
      ":test_support",
      "//base",
      "//base/test:test_support",
      "//net",
      "//services/network:test_support",
      "//testing/gtest",
    ]
  }

  if (is_linux) {
    sources = [ "linux_key_network_delegate_unittest.cc" ]

    deps = [
      ":network",
      ":test_support",
      "//base",
      "//base/test:test_support",
      "//mojo/public/cpp/bindings",
      "//net",
      "//services/network:test_support",
      "//testing/gtest",
    ]
  }
}
