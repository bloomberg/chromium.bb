# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/features.gni")

source_set("common") {
  sources = [ "attestation_utils.cc" ]

  public = [
    "attestation_service.h",
    "attestation_utils.h",
  ]

  public_deps = [
    ":types",
    "//base",
  ]
}

source_set("types") {
  public = [ "signals_type.h" ]

  public_deps = [ "//build:chromeos_buildflags" ]

  if (is_chromeos_ash) {
    public_deps += [ "//chromeos/dbus/attestation:attestation_proto" ]
  } else {
    public_deps += [ "//chrome/browser/enterprise/connectors/device_trust/attestation/common/proto:attestation_ca_proto" ]
  }
}

source_set("test_support") {
  testonly = true
  sources = [ "mock_attestation_service.cc" ]

  public = [ "mock_attestation_service.h" ]

  public_deps = [
    ":common",
    ":types",
    "//testing/gmock",
  ]
}
