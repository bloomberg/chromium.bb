import("//build/config/android/rules.gni")

android_library("java") {
  sources = [
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionIntentHandlerFactory.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionIntentHandlerImpl.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionMetrics.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionReportingJobFactory.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionReportingProviderFlushTask.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionReportingProviderImpl.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/BackgroundAttributionFlusherImpl.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/ImpressionPersistentStore.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/ImpressionPersistentStoreFileManager.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/ImpressionPersistentStoreFileManagerImpl.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/InputEventValidator.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/NoopAttributionIntentHandler.java",
  ]
  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  deps = [
    "//base:base_java",
    "//base:jni_java",
    "//chrome/android:base_module_java",
    "//chrome/browser/attribution_reporting/android:java",
    "//chrome/browser/flags:java",
    "//chrome/browser/preferences:java",
    "//chrome/browser/profiles/android:java",
    "//chrome/browser/util:java",
    "//components/background_task_scheduler:background_task_scheduler_java",
    "//components/background_task_scheduler:background_task_scheduler_task_ids_java",
    "//components/browser_ui/util/android:java",
    "//content/public/android:content_full_java",
    "//content/public/android:content_main_dex_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
  ]
}

source_set("internal") {
  sources = [ "attribution_metrics_android.cc" ]

  deps = [
    ":jni_headers",
    "//base",
  ]
}

generate_jni("jni_headers") {
  sources = [ "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionMetrics.java" ]
}

android_library("junit_tests") {
  # Skip platform checks since Robolectric depends on requires_android targets.
  bypass_platform_checks = true
  testonly = true

  sources = [
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionIntentHandlerImplTest.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionReportingProviderImplRobolectricTest.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/ImpressionPersistentStoreUnitTest.java",

    # TODO(https://crbug.com/1198308): Compile and run this test suite once Robolectric supports R.
    # "java/src/org/chromium/chrome/browser/attribution_reporting/InputEventValidatorTest.java",
  ]

  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//chrome/android:base_module_java",
    "//chrome/browser/attribution_reporting/android:java",
    "//chrome/browser/flags:java",
    "//chrome/browser/preferences:java",
    "//chrome/test/android:chrome_java_test_support",
    "//components/background_task_scheduler:factory_java",
    "//components/background_task_scheduler:public_java",
    "//third_party/android_deps:robolectric_all_java",
    "//third_party/android_support_test_runner:runner_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
  ]
}

android_library("javatests") {
  testonly = true
  sources = [
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionReportingProviderFlushTaskTest.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionReportingProviderImplTest.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/AttributionReportingProviderImplUnitTest.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/ImpressionPersistentStoreFileTest.java",
    "java/src/org/chromium/chrome/browser/attribution_reporting/InputEventValidatorManualTest.java",
  ]

  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//chrome/browser/attribution_reporting/android:java",
    "//chrome/browser/flags:java",
    "//chrome/test/android:chrome_java_test_support",
    "//components/background_task_scheduler:public_java",
    "//content/public/android:content_java",
    "//content/public/android:content_main_dex_java",
    "//content/public/test/android:content_java_test_support",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
    "//ui/android:ui_java_test_support",
  ]
}
