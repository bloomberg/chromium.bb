# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_extensions)
assert(is_chromeos)

source_set("login_screen") {
  sources = [
    "login/external_logout_request/external_logout_request_event_handler.cc",
    "login/external_logout_request/external_logout_request_event_handler.h",
    "login/external_logout_request/external_logout_request_event_handler_factory.cc",
    "login/external_logout_request/external_logout_request_event_handler_factory.h",
    "login/login_api.cc",
    "login/login_api.h",
    "login_screen_storage/login_screen_storage_api.cc",
    "login_screen_storage/login_screen_storage_api.h",
    "login_state/login_state_api.cc",
    "login_state/login_state_api.h",
    "login_state/session_state_changed_event_dispatcher.cc",
    "login_state/session_state_changed_event_dispatcher.h",
  ]

  deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//chrome/browser/profiles:profile",
    "//chrome/common:non_code_constants",
    "//chrome/common/extensions/api",
    "//chromeos/crosapi/mojom",
    "//components/keyed_service/content",
    "//content/public/browser",
    "//extensions/browser",
  ]

  if (is_chromeos_ash) {
    sources += [
      "login/external_logout_done/external_logout_done_event_handler.cc",
      "login/external_logout_done/external_logout_done_event_handler.h",
      "login/external_logout_done/external_logout_done_event_handler_factory.cc",
      "login/external_logout_done/external_logout_done_event_handler_factory.h",
    ]
    deps += [ "//chrome/browser/ash/crosapi" ]
  }

  if (is_chromeos_lacros) {
    sources += [
      "login/cleanup/browsing_data_cleanup_handler.cc",
      "login/cleanup/browsing_data_cleanup_handler.h",
      "login/cleanup/cleanup_handler.h",
      "login/cleanup/cleanup_manager.cc",
      "login/cleanup/cleanup_manager.h",
      "login/cleanup/cleanup_manager_lacros.cc",
      "login/cleanup/cleanup_manager_lacros.h",
      "login/cleanup/cleanup_manager_lacros_factory.cc",
      "login/cleanup/cleanup_manager_lacros_factory.h",
      "login/cleanup/extension_cleanup_handler.cc",
      "login/cleanup/extension_cleanup_handler.h",
    ]
    deps += [
      "//chrome/browser:browser_process",
      "//chrome/browser/browsing_data:constants",
      "//chrome/browser/profiles:profile",
      "//chrome/common:constants",
      "//chromeos/lacros",
      "//components/app_constants:app_constants",
      "//components/keyed_service/content",
      "//components/prefs:prefs",
    ]
  }
}

# TODO(jityao, b/215502706): Move tests back into chrome/test/BUILD.gn.
source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [ "login_state/login_state_apitest.cc" ]

  deps = [
    ":login_screen",
    "//chrome/test:test_support",
    "//content/test:test_support",
    "//testing/gtest",
  ]

  if (is_chromeos_ash) {
    sources += [
      "login_screen_i18n_apitest.cc",
      "login_screen_storage/login_screen_storage_apitest.cc",
      "login_state/session_state_changed_event_dispatcher_ash_apitest.cc",
      "storage_apitest.cc",
    ]

    deps += [
      "//base",
      "//components/policy/core/common:test_support",
      "//components/session_manager/core",
      "//components/version_info",
      "//dbus",
      "//extensions:test_support",
      "//extensions/browser/api/test",
      "//skia",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "login/external_logout_request/external_logout_request_event_handler_unittest.cc",
    "login_state/login_state_api_unittest.cc",
  ]

  deps = [
    ":login_screen",
    "//base",
    "//chrome/test:test_support",
    "//extensions:test_support",
    "//extensions/common:common",
    "//testing/gtest",
  ]

  if (is_chromeos_ash) {
    sources += [
      "login/external_logout_done/external_logout_done_event_handler_unittest.cc",
      "login_screen_storage/login_screen_storage_api_ash_unittest.cc",
      "login_state/session_state_changed_event_dispatcher_ash_unittest.cc",
    ]

    deps += [
      "//chrome/browser/ash/crosapi",
      "//chrome/common:non_code_constants",
      "//chrome/common/extensions/api",
      "//chromeos/crosapi/mojom",
      "//chromeos/dbus/session_manager",
      "//components/session_manager/core",
      "//content/test:test_support",
      "//dbus",
      "//extensions/browser",
      "//extensions/browser:test_support",
      "//skia",
      "//testing/gmock",
    ]
  }
}
