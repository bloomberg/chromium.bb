# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

generate_grd("build_grd") {
  grd_prefix = "quota_internals"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [
    "event_handler.js",
    "main.css",
    "main.html",
    "message_dispatcher.js",
  ]
  input_files_base_dir = rebase_path(".", "//")
}

js_type_check("closure_compile") {
  deps = [
    ":event_handler",
    ":message_dispatcher",
  ]
}

js_library("event_handler") {
  deps = [
    ":message_dispatcher",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:util.m",
    "//ui/webui/resources/js/cr:ui.m",
    "//ui/webui/resources/js/cr/ui:tabs.m",
    "//ui/webui/resources/js/cr/ui:tree.m",
  ]
}

js_library("message_dispatcher") {
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/quota_internals_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/quota_internals_resources.h",
    "grit/quota_internals_resources_map.cc",
    "grit/quota_internals_resources_map.h",
    "quota_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}
