# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

generate_grd("build_grd") {
  grd_prefix = "tab_strip"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [
    "alert_indicators/picture_in_picture_alt.svg",
    "alert_indicators/serial_port.svg",
    "alert_indicators/tab_audio_muting_rounded.svg",
    "alert_indicators/tab_audio_rounded.svg",
    "alert_indicators/tab_bluetooth_connected.svg",
    "alert_indicators/tab_hid_connected.svg",
    "alert_indicators/tab_media_capturing_with_arrow.svg",
    "alert_indicators/tab_media_recording.svg",
    "alert_indicators/tab_usb_connected.svg",
    "alert_indicators/vr_headset.svg",
    "tab_strip.html",
  ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [
    ":preprocess",
    ":preprocess_generated",
  ]
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/$preprocess_gen_manifest",
  ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [
    "custom_element.js",
    "drag_manager.js",
    "tab_strip_embedder_proxy.js",
    "tab_swiper.js",
    "tabs_api_proxy.js",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [
    "alert_indicator.js",
    "alert_indicators.js",
    "tab_group.js",
    "tab_list.js",
    "tab.js",
  ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/tab_strip_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/tab_strip_resources.h",
    "grit/tab_strip_resources_map.cc",
    "grit/tab_strip_resources_map.h",
    "tab_strip_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

js_type_check("closure_compile") {
  deps = [
    ":alert_indicator",
    ":alert_indicators",
    ":custom_element",
    ":drag_manager",
    ":tab",
    ":tab_group",
    ":tab_list",
    ":tab_strip_embedder_proxy",
    ":tab_swiper",
    ":tabs_api_proxy",
  ]
}

js_library("alert_indicator") {
  deps = [
    ":custom_element",
    ":tabs_api_proxy",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("alert_indicators") {
  deps = [
    ":alert_indicator",
    ":custom_element",
    ":tabs_api_proxy",
  ]
}

js_library("custom_element") {
}

js_library("drag_manager") {
  deps = [
    ":tab",
    ":tab_group",
    ":tabs_api_proxy",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("tabs_api_proxy") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/metrics_private.js",
  ]
}

js_library("tab") {
  deps = [
    ":alert_indicators",
    ":custom_element",
    ":tab_strip_embedder_proxy",
    ":tab_swiper",
    ":tabs_api_proxy",
    "//ui/webui/resources/js:icon.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
  ]
  externs_list = [ "$externs_path/metrics_private.js" ]
}

js_library("tab_group") {
  deps = [
    ":custom_element",
    ":tab_strip_embedder_proxy",
    ":tabs_api_proxy",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("tab_list") {
  deps = [
    ":custom_element",
    ":drag_manager",
    ":tab",
    ":tab_group",
    ":tab_strip_embedder_proxy",
    ":tabs_api_proxy",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:event_tracker.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager.m",
  ]
}

js_library("tab_strip_embedder_proxy") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("tab_swiper") {
}

html_to_js("web_components") {
  js_files = [
    "alert_indicator.js",
    "alert_indicators.js",
    "tab_group.js",
    "tab_list.js",
    "tab.js",
  ]
}
