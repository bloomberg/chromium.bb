# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_mojo_manifest = "preprocessed_mojo_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"
preprocess_tabs_manifest = "preprocessed_tabs_manifest.json"

generate_grd("build_grd") {
  grd_prefix = "tab_strip"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [
    "alert_indicators/picture_in_picture_alt.svg",
    "alert_indicators/serial_port.svg",
    "alert_indicators/tab_audio_muting_rounded.svg",
    "alert_indicators/tab_audio_rounded.svg",
    "alert_indicators/tab_bluetooth_connected.svg",
    "alert_indicators/tab_hid_connected.svg",
    "alert_indicators/tab_media_capturing_with_arrow.svg",
    "alert_indicators/tab_media_recording.svg",
    "alert_indicators/tab_usb_connected.svg",
    "alert_indicators/vr_headset.svg",
    "tab_strip.html",
  ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [
    ":preprocess",
    ":preprocess_generated",
    ":preprocess_mojo",
    ":preprocess_tabs",
  ]
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/$preprocess_mojo_manifest",
    "$target_gen_dir/$preprocess_gen_manifest",
    "$target_gen_dir/$preprocess_tabs_manifest",
  ]
}

preprocess_if_expr("preprocess_mojo") {
  deps = [ "//chrome/browser/ui/webui/tab_strip:mojo_bindings_webui_js" ]
  in_folder = "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/tab_strip/"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_mojo_manifest"
  in_files = [ "tab_strip.mojom-webui.js" ]
}

preprocess_if_expr("preprocess_tabs") {
  deps = [ "//chrome/browser/ui/webui/tabs:mojo_bindings_webui_js" ]
  in_folder = "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/tabs/"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_tabs_manifest"
  in_files = [ "tabs.mojom-webui.js" ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [
    "drag_manager.js",
    "tab_swiper.js",
    "tabs_api_proxy.js",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [
    "alert_indicator.js",
    "alert_indicators.js",
    "tab_group.js",
    "tab_list.js",
    "tab.js",
  ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/tab_strip_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/tab_strip_resources.h",
    "grit/tab_strip_resources_map.cc",
    "grit/tab_strip_resources_map.h",
    "tab_strip_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

js_type_check("closure_compile") {
  closure_flags =
      default_closure_args + mojom_js_args + [
        "js_module_root=" + rebase_path(".", root_build_dir),
        "js_module_root=" + rebase_path(
                "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/tab_strip",
                root_build_dir),
        "js_module_root=" + rebase_path(
                "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/tabs",
                root_build_dir),
      ]
  deps = [
    ":alert_indicator",
    ":alert_indicators",
    ":drag_manager",
    ":tab",
    ":tab_group",
    ":tab_list",
    ":tab_swiper",
    ":tabs_api_proxy",
  ]
}

js_library("alert_indicator") {
  deps = [
    ":tabs_api_proxy",
    "//chrome/browser/ui/webui/tabs:mojo_bindings_webui_js",
    "//ui/webui/resources/js:custom_element",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("alert_indicators") {
  deps = [
    ":alert_indicator",
    ":tabs_api_proxy",
    "//chrome/browser/ui/webui/tabs:mojo_bindings_webui_js",
    "//ui/webui/resources/js:custom_element",
  ]
}

js_library("drag_manager") {
  deps = [
    ":tab",
    ":tab_group",
    ":tabs_api_proxy",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("tabs_api_proxy") {
  deps = [
    "//chrome/browser/ui/webui/tab_strip:mojo_bindings_webui_js",
    "//ui/webui/resources/js:cr.m",
  ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/metrics_private.js",
  ]
}

js_library("tab") {
  deps = [
    ":alert_indicators",
    ":tab_swiper",
    ":tabs_api_proxy",
    "//ui/webui/resources/js:custom_element",
    "//ui/webui/resources/js:icon",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
  ]
  externs_list = [ "$externs_path/metrics_private.js" ]
}

js_library("tab_group") {
  deps = [
    ":tabs_api_proxy",
    "//ui/webui/resources/js:custom_element",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("tab_list") {
  deps = [
    ":drag_manager",
    ":tab",
    ":tab_group",
    ":tabs_api_proxy",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:custom_element",
    "//ui/webui/resources/js:event_tracker.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager.m",
  ]
}

js_library("tab_swiper") {
}

html_to_js("web_components") {
  js_files = [
    "alert_indicator.js",
    "alert_indicators.js",
    "tab_group.js",
    "tab_list.js",
    "tab.js",
  ]
}
