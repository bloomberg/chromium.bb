# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/optimize_webui.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/polymer/html_to_js.gni")

js_type_check("closure_compile") {
  is_polymer3 = true
  deps = [
    ":app",
    ":browser_proxy",
    ":customize_dialog",
    ":doodle_share_dialog",
    ":fakebox",
    ":grid",
    ":logo",
    ":realbox",
    ":realbox_button",
    ":realbox_dropdown",
    ":realbox_icon",
    ":realbox_match",
    ":theme_icon",
    ":untrusted_iframe",
    ":utils",
    ":voice_search_overlay",
  ]
}

js_library("browser_proxy") {
  deps = [
    ":background_manager",
    ":utils",
    "//chrome/browser/ui/webui/new_tab_page:mojo_bindings_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
  ]
  externs_list = [ "externs.js" ]
}

js_library("app") {
  deps = [
    ":background_manager",
    ":browser_proxy",
    ":most_visited",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:event_tracker.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("most_visited") {
  deps = [
    ":browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_toast:cr_toast.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager.m",
  ]
}

js_library("customize_dialog") {
  deps = [
    ":customize_backgrounds",
    ":customize_shortcuts",
    ":customize_themes",
    ":utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
  ]
}

js_library("customize_backgrounds") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("customize_themes") {
  deps = [
    ":grid",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button.m",
  ]
}

js_library("mini_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("customize_shortcuts") {
  deps = [
    ":browser_proxy",
    ":mini_page",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_toggle:cr_toggle.m",
  ]
}

js_library("theme_icon") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("grid") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("voice_search_overlay") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("utils") {
}

js_library("untrusted_iframe") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("fakebox") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("realbox") {
  deps = [
    ":browser_proxy",
    ":realbox_icon",
    ":utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("realbox_dropdown") {
  deps = [
    ":browser_proxy",
    ":realbox_button",
    ":realbox_match",
    ":utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("realbox_button") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("realbox_icon") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("realbox_match") {
  deps = [
    ":realbox_button",
    ":realbox_icon",
    ":utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("logo") {
  deps = [
    ":doodle_share_dialog",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
  ]
}

js_library("doodle_share_dialog") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button.m",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
  ]
}

js_library("background_manager") {
  deps = [ ":utils" ]
}

html_to_js("web_components") {
  js_files = [
    "app.js",
    "customize_backgrounds.js",
    "customize_dialog.js",
    "customize_shortcuts.js",
    "customize_themes.js",
    "doodle_share_dialog.js",
    "fakebox.js",
    "grid.js",
    "logo.js",
    "mini_page.js",
    "most_visited.js",
    "realbox_button.js",
    "realbox_dropdown.js",
    "realbox_icon.js",
    "realbox_match.js",
    "realbox.js",
    "theme_icon.js",
    "untrusted_iframe.js",
    "voice_search_overlay.js",
  ]
}

if (optimize_webui) {
  grit("unoptimized_resources") {
    source = "new_tab_page_resources.grd"

    deps = [
      "//chrome/browser/resources/new_tab_page:web_components",
      "//chrome/browser/ui/webui/new_tab_page:mojo_bindings_js",
      "//skia/public/mojom:mojom_js",
    ]

    grit_flags = [
      "-E",
      "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
    ]

    outputs = [
      "grit/new_tab_page_resources.h",
      "grit/new_tab_page_resources_map.cc",
      "grit/new_tab_page_resources_map.h",
      "new_tab_page_resources.pak",
    ]
    output_dir = "$root_gen_dir/chrome/browser/resources/new_tab_page"
  }

  unpak("unpak") {
    pak_file = "new_tab_page_resources.pak"
    out_folder = "unpak"
    deps = [ ":unoptimized_resources" ]
  }

  optimize_webui("optimized_js") {
    host = "new-tab-page"
    input = rebase_path("$target_gen_dir/unpak", root_build_dir)
    deps = [
      ":unpak",
      "//ui/webui/resources:modulize",
    ]
    js_module_in_files = [ "new_tab_page.js" ]
    js_out_files = [ "new_tab_page.rollup.js" ]
    excludes = [
      "chrome://resources/js/cr.m.js",
      "chrome://resources/mojo/mojo/public/js/mojo_bindings_lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/big_buffer.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/string16.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/text_direction.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/time.mojom-lite.js",
      "chrome://resources/mojo/url/mojom/url.mojom-lite.js",
      "new_tab_page.mojom-lite.js",
      "omnibox.mojom-lite.js",
      "skcolor.mojom-lite.js",
    ]
  }
}
