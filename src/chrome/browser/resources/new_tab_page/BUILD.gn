# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/optimize_webui.gni")
import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_grit.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/webui_features.gni")

js_type_check("closure_compile") {
  is_polymer3 = true
  closure_flags = default_closure_args + [ "browser_resolver_prefix_replacements=\"chrome://kaleidoscope/=../../chrome/browser/media/kaleidoscope/internal/resources\"" ]

  deps = [
    ":app",
    ":browser_proxy",
    ":customize_dialog",
    ":doodle_share_dialog",
    ":fakebox",
    ":iframe",
    ":lazy_load",
    ":logo",
    ":new_tab_page",
    ":one_google_bar_api",
    ":promo_browser_command_proxy",
    ":realbox",
    ":realbox_button",
    ":realbox_dropdown",
    ":realbox_icon",
    ":realbox_match",
    ":utils",
    ":voice_search_overlay",
    "modules:module_descriptor",
    "modules:module_registry",
    "modules:modules",
    "modules/dummy:foo_proxy",
  ]
}

js_library("new_tab_page") {
  deps = [
    ":app",
    ":background_manager",
    ":browser_proxy",
    ":promo_browser_command_proxy",
    ":utils",
    "modules:module_descriptor",
    "modules:module_registry",
  ]
}

js_library("lazy_load") {
  deps = [
    ":customize_dialog",
    ":middle_slot_promo",
    ":most_visited",
    ":voice_search_overlay",
    "modules/dummy:module",
    "modules/kaleidoscope:module",
  ]
}

js_library("browser_proxy") {
  deps = [
    ":background_manager",
    ":utils",
    "//chrome/browser/ui/webui/new_tab_page:mojo_bindings_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("app") {
  deps = [
    ":background_manager",
    ":browser_proxy",
    ":customize_dialog_types",
    ":one_google_bar_api",
    ":promo_browser_command_proxy",
    "modules:module_wrapper",
    "modules:modules",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_toast:cr_toast.m",
    "//ui/webui/resources/js:event_tracker.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager.m",
  ]
}

js_library("promo_browser_command_proxy") {
  deps = [
    "//chrome/browser/promo_browser_command:mojo_bindings_js_library_for_compile",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("middle_slot_promo") {
  deps = [
    ":browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("most_visited") {
  deps = [
    ":browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_toast:cr_toast.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager.m",
  ]
}

js_library("customize_dialog") {
  deps = [
    ":customize_backgrounds",
    ":customize_dialog_types",
    ":customize_shortcuts",
    ":utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_components/customize_themes",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
  ]
}

js_library("customize_dialog_types") {
}

js_library("customize_backgrounds") {
  deps = [
    ":browser_proxy",
    ":customize_dialog_types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("mini_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("customize_shortcuts") {
  deps = [
    ":browser_proxy",
    ":mini_page",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_toggle:cr_toggle.m",
  ]
}

js_library("voice_search_overlay") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button.m",
  ]
}

js_library("utils") {
}

js_library("iframe") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("fakebox") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("realbox") {
  deps = [
    ":browser_proxy",
    ":realbox_icon",
    ":utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("realbox_dropdown") {
  deps = [
    ":browser_proxy",
    ":realbox_button",
    ":realbox_match",
    ":utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("realbox_button") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("realbox_icon") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("realbox_match") {
  deps = [
    ":realbox_button",
    ":realbox_icon",
    ":utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("logo") {
  deps = [
    ":doodle_share_dialog",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
  ]
}

js_library("doodle_share_dialog") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button.m",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
  ]
}

js_library("background_manager") {
  deps = [ ":utils" ]
}

js_library("one_google_bar_api") {
}

js_library("img") {
}

html_to_js("web_components_local") {
  js_files = [
    "app.js",
    "customize_backgrounds.js",
    "customize_dialog.js",
    "customize_shortcuts.js",
    "doodle_share_dialog.js",
    "fakebox.js",
    "logo.js",
    "middle_slot_promo.js",
    "mini_page.js",
    "most_visited.js",
    "realbox_button.js",
    "realbox_dropdown.js",
    "realbox_icon.js",
    "realbox_match.js",
    "realbox.js",
    "iframe.js",
    "voice_search_overlay.js",
    "customize_modules.js",
  ]
}

group("web_components") {
  public_deps = [
    ":web_components_local",
    "modules:web_components",
  ]
}

if (optimize_webui) {
  preprocess_folder = "preprocessed"

  preprocess_grit("preprocess") {
    defines = [ "is_official_build=$is_official_build" ]
    in_folder = "./"
    out_folder = "$target_gen_dir/$preprocess_folder"
    in_files = [
      "background_manager.js",
      "browser_proxy.js",
      "img.js",
      "lazy_load.js",
      "customize_dialog_types.js",
      "new_tab_page.js",
      "one_google_bar_api.js",
      "promo_browser_command_proxy.js",
      "utils.js",
    ]
  }

  preprocess_grit("preprocess_gen") {
    deps = [ ":web_components" ]
    defines = [ "is_official_build=$is_official_build" ]
    in_folder = target_gen_dir
    out_folder = "$target_gen_dir/$preprocess_folder"
    in_files = [
      "app.js",
      "middle_slot_promo.js",
      "most_visited.js",
      "customize_dialog.js",
      "voice_search_overlay.js",
      "customize_backgrounds.js",
      "customize_modules.js",
      "customize_shortcuts.js",
      "iframe.js",
      "mini_page.js",
      "fakebox.js",
      "logo.js",
      "doodle_share_dialog.js",
      "realbox.js",
      "realbox_button.js",
      "realbox_dropdown.js",
      "realbox_icon.js",
      "realbox_match.js",
    ]
  }

  optimize_webui("optimized_js") {
    host = "new-tab-page"
    input = rebase_path("$target_gen_dir/$preprocess_folder", root_build_dir)
    deps = [
      ":preprocess",
      ":preprocess_gen",
      "../../../../ui/webui/resources:preprocess",
      "modules:preprocess",
      "modules:preprocess_gen",
    ]
    js_module_in_files = [
      "new_tab_page.js",
      "lazy_load.js",
    ]
    js_out_files = [
      "new_tab_page.rollup.js",
      "lazy_load.rollup.js",
      "shared.rollup.js",
    ]
    excludes = [
      "chrome://resources/js/cr.m.js",
      "chrome://resources/mojo/mojo/public/js/mojo_bindings_lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/big_buffer.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/string16.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/text_direction.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/time.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/unguessable_token.mojom-lite.js",
      "chrome://resources/mojo/skia/public/mojom/skcolor.mojom-lite.js",
      "chrome://resources/mojo/url/mojom/origin.mojom-lite.js",
      "chrome://resources/mojo/url/mojom/url.mojom-lite.js",
      "new_tab_page.mojom-lite.js",
      "omnibox.mojom-lite.js",
      "promo_browser_command.mojom-lite.js",
      "modules/task_module/task_module.mojom-lite.js",
      "foo.mojom-lite.js",
    ]
  }
}
