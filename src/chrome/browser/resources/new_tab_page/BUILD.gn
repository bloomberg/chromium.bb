# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/tools/optimize_webui.gni")
import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

js_type_check("closure_compile") {
  is_polymer3 = true

  closure_flags = default_closure_args + mojom_js_args

  deps = [
    ":app",
    ":browser_proxy",
    ":customize_dialog",
    ":doodle_share_dialog",
    ":iframe",
    ":lazy_load",
    ":logo",
    ":new_tab_page",
    ":one_google_bar_api",
    ":promo_browser_command_proxy",
    ":utils",
    ":voice_search_overlay",
    "modules:module_descriptor",
    "modules:module_registry",
    "modules:modules",
    "modules/dummy:foo_proxy",
    "realbox:realbox",
    "realbox:realbox_browser_proxy",
    "realbox:realbox_dropdown",
    "realbox:realbox_icon",
    "realbox:realbox_match",
  ]
}

js_library("new_tab_page") {
  deps = [
    ":app",
    ":background_manager",
    ":browser_proxy",
    ":promo_browser_command_proxy",
    ":utils",
    "modules:module_descriptor",
    "modules:module_header",
    "modules:module_registry",
    "realbox:realbox_browser_proxy",
  ]
}

js_library("lazy_load") {
  deps = [
    ":customize_dialog",
    ":middle_slot_promo",
    ":most_visited",
    ":voice_search_overlay",
    "modules/dummy:module",
  ]
}

js_library("browser_proxy") {
  deps = [
    ":background_manager",
    ":utils",
    "//chrome/browser/ui/webui/new_tab_page:mojo_bindings_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("app") {
  deps = [
    ":background_manager",
    ":browser_proxy",
    ":customize_dialog_types",
    ":one_google_bar_api",
    ":promo_browser_command_proxy",
    "modules:module_wrapper",
    "modules:modules",
    "realbox:realbox",
    "//skia/public/mojom:mojom_webui_js",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_toast:cr_toast.m",
    "//ui/webui/resources/js:event_tracker.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager.m",
  ]
}

js_library("promo_browser_command_proxy") {
  deps = [
    "//chrome/browser/promo_browser_command:mojo_bindings_js_library_for_compile",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("middle_slot_promo") {
  deps = [
    ":browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("most_visited") {
  deps = [
    ":browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_toast:cr_toast.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager.m",
  ]
}

js_library("customize_dialog") {
  deps = [
    ":customize_backgrounds",
    ":customize_dialog_types",
    ":customize_modules",
    ":customize_shortcuts",
    ":utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_components/customize_themes",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
  ]
}

js_library("customize_dialog_types") {
}

js_library("customize_backgrounds") {
  deps = [
    ":browser_proxy",
    ":customize_dialog_types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("mini_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("customize_shortcuts") {
  deps = [
    ":browser_proxy",
    ":mini_page",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_toggle:cr_toggle.m",
  ]
}

js_library("customize_modules") {
  deps = [
    ":browser_proxy",
    "modules:module_registry",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_radio_button:cr_radio_button.m",
    "//ui/webui/resources/cr_elements/cr_radio_group:cr_radio_group.m",
    "//ui/webui/resources/cr_elements/cr_toggle:cr_toggle.m",
    "//ui/webui/resources/cr_elements/policy:cr_policy_indicator.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("voice_search_overlay") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button.m",
  ]
}

js_library("utils") {
}

js_library("iframe") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("logo") {
  deps = [
    ":doodle_share_dialog",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
  ]
}

js_library("doodle_share_dialog") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button.m",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
  ]
}

js_library("background_manager") {
  deps = [
    ":utils",
    "//ui/webui/resources/js:event_tracker.m",
  ]
}

js_library("one_google_bar_api") {
}

js_library("img") {
}

html_to_js("web_components_local") {
  js_files = [
    "app.js",
    "customize_backgrounds.js",
    "customize_dialog.js",
    "customize_shortcuts.js",
    "doodle_share_dialog.js",
    "logo.js",
    "middle_slot_promo.js",
    "mini_page.js",
    "most_visited.js",
    "iframe.js",
    "voice_search_overlay.js",
    "customize_modules.js",
  ]
}

group("web_components") {
  public_deps = [
    ":web_components_local",
    "modules:web_components",
    "realbox:web_components",
  ]
}

grd_prefix = "new_tab_page"
preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

if (optimize_webui) {
  build_manifest = "build_manifest.json"
}

preprocess_if_expr("preprocess") {
  defines = [ "is_official_build=$is_official_build" ]
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [
    "background_manager.js",
    "browser_proxy.js",
    "img.js",
    "lazy_load.js",
    "customize_dialog_types.js",
    "new_tab_page.js",
    "promo_browser_command_proxy.js",
    "utils.js",
  ]
}

preprocess_if_expr("preprocess_gen") {
  deps = [ ":web_components" ]
  defines = [ "is_official_build=$is_official_build" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [
    "app.js",
    "middle_slot_promo.js",
    "most_visited.js",
    "customize_dialog.js",
    "voice_search_overlay.js",
    "customize_backgrounds.js",
    "customize_modules.js",
    "customize_shortcuts.js",
    "iframe.js",
    "mini_page.js",
    "logo.js",
    "doodle_share_dialog.js",
  ]
}

generate_grd("build_new_tab_page_mojo_grdp") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/new_tab_page_mojo_resources.grdp"
  input_files = [ "new_tab_page.mojom-lite.js" ]
  if (!is_official_build) {
    input_files += [ "foo/foo.mojom-lite.js" ]
    resource_path_rewrites = [ "foo/foo.mojom-lite.js|foo.mojom-lite.js" ]
  }
  input_files_base_dir =
      rebase_path("$root_gen_dir/chrome/browser/ui/webui/new_tab_page",
                  root_build_dir)
}

generate_grd("build_promo_browser_command_mojo_grdp") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/promo_browser_command_mojo_resources.grdp"
  input_files = [ "promo_browser_command.mojom-lite.js" ]
  input_files_base_dir =
      rebase_path("$root_gen_dir/chrome/browser/promo_browser_command",
                  root_build_dir)
}

generate_grd("build_drive_mojo_grdp") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/drive_mojo_resources.grdp"
  input_files = [ "drive.mojom-lite.js" ]
  input_files_base_dir =
      rebase_path("$root_gen_dir/chrome/browser/search/drive", root_build_dir)
  resource_path_prefix = "modules/drive"
}

generate_grd("build_task_module_mojo_grdp") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/task_module_mojo_resources.grdp"
  input_files = [ "task_module.mojom-lite.js" ]
  input_files_base_dir =
      rebase_path("$root_gen_dir/chrome/browser/search/task_module",
                  root_build_dir)
  resource_path_prefix = "modules/task_module"
}

generate_grd("build_chrome_cart_mojo_grdp") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/chrome_cart_mojo_resources.grdp"
  input_files = [ "chrome_cart.mojom-lite.js" ]
  input_files_base_dir =
      rebase_path("$root_gen_dir/chrome/browser/cart", root_build_dir)
  resource_path_prefix = "modules/cart"
}

generate_grd("build_grd") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [
    "new_tab_page.html",
    "shared_vars.css",
    "one_google_bar_api.js",
    "untrusted/background_image.html",
    "untrusted/background_image.js",
    "untrusted/image.html",
    "untrusted/one_google_bar.html",
    "untrusted/one_google_bar.js",
  ]
  input_files_base_dir = rebase_path(".", "//")

  grdp_files = [
    "$target_gen_dir/icons/resources.grdp",
    "$target_gen_dir/modules/drive/icons/resources.grdp",
    "$target_gen_dir/new_tab_page_mojo_resources.grdp",
    "$target_gen_dir/promo_browser_command_mojo_resources.grdp",
    "$target_gen_dir/realbox/realbox_mojo_resources.grdp",
    "$target_gen_dir/realbox/search_mojo_resources.grdp",
    "$target_gen_dir/task_module_mojo_resources.grdp",
    "$target_gen_dir/chrome_cart_mojo_resources.grdp",
    "$target_gen_dir/drive_mojo_resources.grdp",
  ]
  deps = [
    ":build_chrome_cart_mojo_grdp",
    ":build_drive_mojo_grdp",
    ":build_new_tab_page_mojo_grdp",
    ":build_promo_browser_command_mojo_grdp",
    ":build_task_module_mojo_grdp",
    "icons:build_grdp",
    "modules/drive/icons:build_grdp",
    "realbox:build_realbox_mojo_grdp",
    "realbox:build_search_mojo_grdp",
  ]

  if (optimize_webui) {
    deps += [ ":optimized_js" ]
    resource_path_rewrites = [
      "new_tab_page.rollup.js|new_tab_page.js",
      "lazy_load.rollup.js|lazy_load.js",
    ]
    manifest_files = [ "$target_gen_dir/$build_manifest" ]
  } else {
    deps += [
      ":preprocess",
      ":preprocess_gen",
      "modules:build_grdp",
      "realbox:build_grdp",
    ]
    grdp_files += [
      "$target_gen_dir/modules/resources.grdp",
      "$target_gen_dir/realbox/resources.grdp",
    ]
    manifest_files = [
      "$target_gen_dir/$preprocess_manifest",
      "$target_gen_dir/$preprocess_gen_manifest",
    ]
  }
}

grit("resources") {
  defines = chrome_grit_defines + [ "is_official_build=$is_official_build" ]

  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"

  deps = [
    ":build_grd",
    "//chrome/browser/cart:mojo_bindings_js",
    "//chrome/browser/promo_browser_command:mojo_bindings_js",
    "//chrome/browser/search/drive:mojo_bindings_js",
    "//chrome/browser/search/task_module:mojo_bindings_js",
    "//chrome/browser/ui/webui/new_tab_page:mojo_bindings_js",
    "//chrome/browser/ui/webui/realbox:mojo_bindings_js",
    "//chrome/common/search:mojo_bindings_js",
  ]
  if (!is_official_build) {
    deps += [ "//chrome/browser/ui/webui/new_tab_page/foo:mojo_bindings_js" ]
  }

  outputs = [
    "grit/new_tab_page_resources.h",
    "grit/new_tab_page_resources_map.cc",
    "grit/new_tab_page_resources_map.h",
    "new_tab_page_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

if (optimize_webui) {
  optimize_webui("optimized_js") {
    host = "new-tab-page"
    input = rebase_path("$target_gen_dir/$preprocess_folder", root_build_dir)
    deps = [
      ":preprocess",
      ":preprocess_gen",
      "../../../../ui/webui/resources:preprocess",
      "modules:preprocess",
      "modules:preprocess_gen",
      "realbox:preprocess",
      "realbox:preprocess_gen",
    ]
    js_module_in_files = [
      "new_tab_page.js",
      "lazy_load.js",
    ]
    out_manifest = "$target_gen_dir/$build_manifest"
    js_out_files = [
      "new_tab_page.rollup.js",
      "lazy_load.rollup.js",
      "shared.rollup.js",
    ]
    excludes = [
      "chrome://resources/js/cr.m.js",
      "chrome://resources/mojo/mojo/public/js/bindings.js",
      "chrome://resources/mojo/mojo/public/js/mojo_bindings_lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/big_buffer.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/string16.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/text_direction.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/time.mojom-lite.js",
      "chrome://resources/mojo/mojo/public/mojom/base/unguessable_token.mojom-lite.js",
      "chrome://resources/mojo/skia/public/mojom/skcolor.mojom-lite.js",
      "chrome://resources/mojo/skia/public/mojom/skcolor.mojom-webui.js",
      "chrome://resources/mojo/url/mojom/origin.mojom-lite.js",
      "chrome://resources/mojo/url/mojom/url.mojom-lite.js",
      "new_tab_page.mojom-lite.js",
      "realbox/omnibox.mojom-lite.js",
      "realbox/realbox.mojom-lite.js",
      "one_google_bar_api.js",
      "promo_browser_command.mojom-lite.js",
      "modules/drive/drive.mojom-lite.js",
      "modules/task_module/task_module.mojom-lite.js",
      "foo.mojom-lite.js",
      "modules/cart/chrome_cart.mojom-lite.js",
    ]
  }
}
