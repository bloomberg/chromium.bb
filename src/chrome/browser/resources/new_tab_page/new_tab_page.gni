# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("./modules/modules.gni")
import("./realbox/realbox.gni")

# List of files that don't need to be passed to html_to_js().
non_web_component_files =
    [
      "background_manager.ts",
      "customize_dialog_types.ts",
      "i18n_setup.ts",
      "lazy_load.ts",
      "metrics_utils.ts",
      "new_tab_page_proxy.ts",
      "new_tab_page.ts",
      "utils.ts",
      "window_proxy.ts",
    ] + modules_non_web_component_files + realbox_non_web_component_files

# Files holding a Polymer element definition and have an equivalent .html file.
web_component_files =
    [
      "app.ts",
      "customize_backgrounds.ts",
      "customize_dialog.ts",
      "customize_modules.ts",
      "customize_shortcuts.ts",
      "doodle_share_dialog.ts",
      "iframe.ts",
      "logo.ts",
      "middle_slot_promo.ts",
      "mini_page.ts",
      "voice_search_overlay.ts",
    ] + modules_web_component_files + realbox_web_component_files

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".ts", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".ts" ]
}

ts_files = web_component_files + non_web_component_files

mojo_js_files = [
  "chrome_cart.mojom-webui.js",
  "drive.mojom-webui.js",
  "new_tab_page.mojom-webui.js",
  "photos.mojom-webui.js",
  "realbox.mojom-webui.js",
  "task_module.mojom-webui.js",
]

if (!is_official_build) {
  mojo_js_files += [ "foo.mojom-webui.js" ]
}
