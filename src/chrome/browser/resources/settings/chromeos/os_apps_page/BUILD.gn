# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("../os_settings.gni")

js_type_check("closure_compile_module") {
  closure_flags = os_settings_closure_flags
  is_polymer3 = true
  deps = [
    ":android_apps_browser_proxy.m",
    ":android_apps_subpage.m",
    ":os_apps_page.m",
  ]
}

js_library("android_apps_browser_proxy.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_apps_page/android_apps_browser_proxy.m.js" ]
  deps = [ "//ui/webui/resources/js:cr.m" ]
  extra_deps = [ ":modulize" ]
}

js_library("android_apps_subpage.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_apps_page/android_apps_subpage.m.js" ]
  deps = [
    ":android_apps_browser_proxy.m",
    "..:deep_linking_behavior.m",
    "..:os_route.m",
    "../..:router",
    "../../prefs:prefs_behavior",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:i18n_behavior.m",
    "//ui/webui/resources/js/cr/ui:focus_without_ink.m",
  ]
  extra_deps = [ ":android_apps_subpage_module" ]
}

js_library("os_apps_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_apps_page/os_apps_page.m.js" ]
  deps = [
    ":android_apps_browser_proxy.m",
    ":android_apps_subpage.m",
    "..:deep_linking_behavior.m",
    "..:os_route.m",
    "../../:router",
    "../../controls:settings_dropdown_menu",
    "../../prefs:prefs_behavior",
    "../../settings_page:settings_animated_pages",
    "../localized_link:localized_link",
    "./app_management_page:constants.m",
    "./app_management_page:store_client.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
  extra_deps = [ ":os_apps_page_module" ]
  externs_list = [ "$externs_path/metrics_private.js" ]
}

group("polymer3_elements") {
  public_deps = [
    ":android_apps_subpage_module",
    ":modulize",
    ":os_apps_page_module",
  ]
}

polymer_modulizer("android_apps_subpage") {
  js_file = "android_apps_subpage.js"
  html_file = "android_apps_subpage.html"
  html_type = "dom-module"
  migrated_imports = os_settings_migrated_imports
  namespace_rewrites = os_settings_namespace_rewrites
  auto_imports = os_settings_auto_imports
}

polymer_modulizer("os_apps_page") {
  js_file = "os_apps_page.js"
  html_file = "os_apps_page.html"
  html_type = "dom-module"
  migrated_imports = os_settings_migrated_imports
  namespace_rewrites = os_settings_namespace_rewrites
  auto_imports = os_settings_auto_imports
}

import("//ui/webui/resources/tools/js_modulizer.gni")

js_modulizer("modulize") {
  input_files = [ "android_apps_browser_proxy.js" ]
  namespace_rewrites = os_settings_namespace_rewrites
}
