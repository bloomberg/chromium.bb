# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/settings/chromeos/os_settings.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("../os_settings.gni")

js_type_check("closure_compile_module") {
  closure_flags = os_settings_closure_flags
  is_polymer3 = true
  deps = [ ":os_settings_ui.m" ]
}

js_library("os_settings_ui.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_settings_ui/os_settings_ui.m.js" ]
  deps = [
    "..:metrics_recorder.m",
    "..:os_page_visibility.m",
    "..:os_route.m",
    "..:pref_to_setting_metric_converter.m",
    "../..:router",
    "../../prefs:prefs",
    "../os_settings_main:os_settings_main.m",
    "../os_toolbar:os_toolbar.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:cr_container_shadow_behavior.m",
    "//ui/webui/resources/cr_elements:find_shortcut_behavior",
    "//ui/webui/resources/cr_elements/cr_drawer:cr_drawer",
    "//ui/webui/resources/cr_elements/policy:cr_policy_indicator_behavior.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:util.m",
  ]
  extra_deps = [ ":os_settings_ui_module" ]
}

group("polymer3_elements") {
  public_deps = [ ":os_settings_ui_module" ]
}

polymer_modulizer("os_settings_ui") {
  js_file = "os_settings_ui.js"
  html_file = "os_settings_ui.html"
  html_type = "dom-module"
  migrated_imports = os_settings_migrated_imports
  auto_imports = os_settings_auto_imports + [
                   "ui/webui/resources/cr_elements/cr_drawer/cr_drawer.html|CrDrawerElement",
                   "chrome/browser/resources/settings/prefs/prefs.html|SettingsPrefsElement",
                 ]
  namespace_rewrites = os_settings_namespace_rewrites
}
