# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../os_settings.gni")

js_type_check("closure_compile") {
  deps = [
    ":main_page_behavior",
    ":os_settings_page",
    ":settings_idle_load",
  ]
}

js_library("os_settings_page") {
  deps = [
    ":main_page_behavior",
    "..:os_page_visibility",
    "..:os_route",
    "../..:router",
    "../..:search_settings",
    "../os_apps_page:android_apps_browser_proxy",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js:web_ui_listener_behavior",
  ]
  externs_list = [ "$externs_path/pending.js" ]
}

js_library("main_page_behavior") {
  deps = [
    "../..:router",
    "../../settings_page:settings_section",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:util",
  ]
  externs_list = [ "$externs_path/pending.js" ]
}

js_library("settings_idle_load") {
  deps = [ "//ui/webui/resources/js:assert" ]
  externs_list = [ "$externs_path/pending_polymer.js" ]
}

# TODO: Uncomment as the Polymer3 migration makes progress.
js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    #    ":os_settings_page.m",
    ":main_page_behavior.m",
    ":settings_idle_load.m",
  ]
}

js_library("os_settings_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_settings_page/os_settings_page.m.js" ]
  deps = [
    # TODO: Fill those in.
  ]
  extra_deps = [ ":os_settings_page_module" ]
}

js_library("main_page_behavior.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_settings_page/main_page_behavior.m.js" ]
  deps = [
    "../..:router.m",
    "../../settings_page:settings_section.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:util.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("settings_idle_load.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/os_settings_page/settings_idle_load.m.js" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
  ]
  extra_deps = [ ":settings_idle_load_module" ]
}

import("//tools/polymer/polymer.gni")

group("polymer3_elements") {
  public_deps = [
    ":modulize",
    ":os_settings_page_module",
    ":settings_idle_load_module",
  ]
}

polymer_modulizer("os_settings_page") {
  js_file = "os_settings_page.js"
  html_file = "os_settings_page.html"
  html_type = "dom-module"
}

polymer_modulizer("settings_idle_load") {
  js_file = "settings_idle_load.js"
  html_file = "settings_idle_load.html"
  html_type = "dom-module"
  migrated_imports = os_settings_migrated_imports
  auto_imports = [
    "chrome/browser/resources/settings/chromeos/ensure_lazy_loaded.html|ensureLazyLoaded",
    "ui/webui/resources/html/assert.html|assert",
    "ui/webui/resources/html/polymer.html|Polymer,html,templatize,TemplateInstanceBase",
  ]
  namespace_rewrites = os_settings_namespace_rewrites +
                       [ "Polymer.Templatize.templatize|templatize" ]
}

js_modulizer("modulize") {
  input_files = [ "main_page_behavior.js" ]
  namespace_rewrites = os_settings_namespace_rewrites
}
