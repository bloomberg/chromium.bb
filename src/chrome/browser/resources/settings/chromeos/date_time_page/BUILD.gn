# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../os_settings.gni")

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":date_time_page.m",
    ":date_time_types.m",
    ":timezone_browser_proxy.m",
    ":timezone_selector.m",
    ":timezone_subpage.m",
  ]
}

js_library("date_time_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/date_time_page/date_time_page.m.js" ]
  deps = [
    "..:deep_linking_behavior.m",
    "..:os_settings_routes.m",
    "../../prefs:prefs_behavior.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/policy:cr_policy_indicator.m",
    "//ui/webui/resources/cr_elements/policy:cr_policy_pref_indicator.m",
    "//ui/webui/resources/js:i18n_behavior.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  extra_deps = [ ":date_time_page_module" ]
}

js_library("date_time_types.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/date_time_page/date_time_types.m.js" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("timezone_browser_proxy.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/date_time_page/timezone_browser_proxy.m.js" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("timezone_selector.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/date_time_page/timezone_selector.m.js" ]
  deps = [
    ":timezone_browser_proxy.m",
    "../../controls:settings_dropdown_menu.m",
    "../../prefs:prefs_behavior.m",
    "../../prefs:prefs_types.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:load_time_data.m",
  ]
  extra_deps = [ ":timezone_selector_module" ]
}

js_library("timezone_subpage.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/chromeos/date_time_page/timezone_subpage.m.js" ]
  deps = [
    ":date_time_types.m",
    ":timezone_browser_proxy.m",
    "..:deep_linking_behavior.m",
    "..:os_settings_routes.m",
    "../..:router.m",
    "../../prefs:prefs_behavior.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  extra_deps = [ ":timezone_subpage_module" ]
}

group("polymer3_elements") {
  public_deps = [
    ":date_time_page_module",
    ":modulize",
    ":timezone_selector_module",
    ":timezone_subpage_module",
  ]
}

polymer_modulizer("date_time_page") {
  js_file = "date_time_page.js"
  html_file = "date_time_page.html"
  html_type = "dom-module"
  migrated_imports = os_settings_migrated_imports
  namespace_rewrites = os_settings_namespace_rewrites
  auto_imports = os_settings_auto_imports + [ "chrome/browser/resources/settings/chromeos/date_time_page/timezone_browser_proxy.html|TimeZoneBrowserProxyImpl,TimeZoneBrowserProxy" ]
}

polymer_modulizer("timezone_selector") {
  js_file = "timezone_selector.js"
  html_file = "timezone_selector.html"
  html_type = "dom-module"
  migrated_imports = os_settings_migrated_imports
  namespace_rewrites = os_settings_namespace_rewrites
  auto_imports = os_settings_auto_imports + [ "chrome/browser/resources/settings/chromeos/date_time_page/timezone_browser_proxy.html|TimeZoneBrowserProxyImpl,TimeZoneBrowserProxy" ]
}

polymer_modulizer("timezone_subpage") {
  js_file = "timezone_subpage.js"
  html_file = "timezone_subpage.html"
  html_type = "dom-module"
  migrated_imports = os_settings_migrated_imports
  namespace_rewrites = os_settings_namespace_rewrites
  auto_imports = os_settings_auto_imports + [
                   "chrome/browser/resources/settings/chromeos/date_time_page/date_time_types.html|TimeZoneAutoDetectMethod",
                   "chrome/browser/resources/settings/chromeos/date_time_page/timezone_browser_proxy.html|TimeZoneBrowserProxyImpl,TimeZoneBrowserProxy",
                 ]
}

js_modulizer("modulize") {
  input_files = [
    "date_time_types.js",
    "timezone_browser_proxy.js",
  ]
  namespace_rewrites = os_settings_namespace_rewrites
}
