# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")
import("../tools/optimize_webui.gni")
import("settings.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

if (optimize_webui) {
  build_manifest = "build_manifest.json"

  optimize_webui("build") {
    host = "settings"
    input = rebase_path("$target_gen_dir/$preprocess_folder", root_build_dir)
    js_module_in_files = [
      "settings.js",
      "lazy_load.js",
    ]
    js_out_files = [
      "settings.rollup.js",
      "lazy_load.rollup.js",
      "shared.rollup.js",
    ]
    out_manifest = "$target_gen_dir/$build_manifest"

    deps = [
      ":preprocess",
      ":preprocess_generated",
      "../../../../ui/webui/resources:preprocess",
    ]
    excludes = [
      "chrome://resources/js/cr.m.js",
      "chrome://resources/mojo/mojo/public/js/bindings.js",
      "chrome://resources/mojo/skia/public/mojom/skcolor.mojom-webui.js",
    ]
  }
}

generate_grd("build_grd") {
  grd_prefix = "settings"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [
    "images/cookies_banner_dark.svg",
    "images/cookies_banner.svg",
    "images/googleg_standard_clr_32px.svg",
    "images/password_check_neutral_dark.svg",
    "images/password_check_neutral.svg",
    "images/password_check_positive_dark.svg",
    "images/password_check_positive.svg",
    "images/permissions_banner_dark.svg",
    "images/permissions_banner.svg",
    "images/privacy_sandbox_banner_dark.svg",
    "images/privacy_sandbox_banner.svg",
    "images/safe_browsing_banner_dark.svg",
    "images/safe_browsing_banner.svg",
    "images/sync_banner_dark.svg",
    "images/sync_banner.svg",
    "privacy_sandbox/privacy_sandbox.html",
    "settings.html",
  ]
  input_files_base_dir = rebase_path(".", "//")

  grdp_files = [ "$target_gen_dir/privacy_sandbox/resources.grdp" ]
  deps = [ "privacy_sandbox:build_grdp" ]

  if (optimize_webui) {
    deps += [ ":build" ]
    manifest_files = [ "$target_gen_dir/$build_manifest" ]
    resource_path_rewrites = [
      "settings.rollup.js|settings.js",
      "lazy_load.rollup.js|lazy_load.js",
    ]
  } else {
    deps += [
      ":preprocess",
      ":preprocess_generated",
    ]
    manifest_files = [
      "$target_gen_dir/$preprocess_manifest",
      "$target_gen_dir/$preprocess_gen_manifest",
    ]
  }
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [
    "a11y_page/captions_browser_proxy.js",
    "about_page/about_page_browser_proxy.js",
    "appearance_page/appearance_browser_proxy.js",
    "appearance_page/fonts_browser_proxy.js",
    "autofill_page/merge_exceptions_store_copies_behavior.js",
    "autofill_page/merge_passwords_store_copies_behavior.js",
    "autofill_page/multi_store_exception_entry.js",
    "autofill_page/multi_store_id_handler.js",
    "autofill_page/multi_store_password_ui_entry.js",
    "autofill_page/password_check_behavior.js",
    "autofill_page/password_manager_proxy.js",
    "autofill_page/show_password_behavior.js",
    "chrome_cleanup_page/chrome_cleanup_proxy.js",
    "clear_browsing_data_dialog/clear_browsing_data_browser_proxy.js",
    "controls/pref_control_behavior.js",
    "controls/settings_boolean_control_behavior.js",
    "controls/settings_idle_load.js",
    "downloads_page/downloads_browser_proxy.js",
    "ensure_lazy_loaded.js",
    "extension_control_browser_proxy.js",
    "global_scroll_target_behavior.js",
    "hats_browser_proxy.js",
    "i18n_setup.js",
    "incompatible_applications_page/incompatible_applications_browser_proxy.js",
    "languages_page/languages.js",
    "languages_page/languages_browser_proxy.js",
    "languages_page/languages_settings_metrics_proxy.js",
    "lazy_load.js",
    "lifetime_browser_proxy.js",
    "metrics_browser_proxy.js",
    "on_startup_page/on_startup_browser_proxy.js",
    "on_startup_page/startup_urls_page_browser_proxy.js",
    "open_window_proxy.js",
    "page_visibility.js",
    "people_page/sync_browser_proxy.js",
    "people_page/profile_info_browser_proxy.js",
    "prefs/prefs_behavior.js",
    "prefs/prefs.js",
    "prefs/prefs_types.js",
    "prefs/pref_util.js",
    "privacy_page/privacy_page_browser_proxy.js",
    "privacy_page/security_keys_browser_proxy.js",
    "reset_page/reset_browser_proxy.js",
    "route.js",
    "router.js",
    "safety_check_page/safety_check_browser_proxy.js",
    "search_engines_page/search_engines_browser_proxy.js",
    "search_settings.js",
    "setting_id_param_util.js",
    "settings.js",
    "settings_page/main_page_behavior.js",
    "settings_routes.js",
    "site_settings/constants.js",
    "site_settings/cookie_info.js",
    "site_settings/local_data_browser_proxy.js",
    "site_settings/site_settings_behavior.js",
    "site_settings/site_settings_prefs_browser_proxy.js",
    "site_settings/website_usage_browser_proxy.js",
  ]

  if (is_chromeos_ash) {
    in_files += [
      "autofill_page/blocking_request_manager.js",
      "languages_page/languages_metrics_proxy.js",
      "people_page/account_manager_browser_proxy.js",
      "site_settings/android_info_browser_proxy.js",
    ]
  } else {
    if (!is_chromeos_lacros) {
      in_files += [
        "default_browser_page/default_browser_browser_proxy.js",
        "system_page/system_page_browser_proxy.js",
      ]
    }
    in_files += [
      "people_page/import_data_browser_proxy.js",
      "people_page/manage_profile_browser_proxy.js",
    ]
  }
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":polymer3_elements" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [
    "a11y_page/a11y_page.js",
    "a11y_page/captions_subpage.js",
    "a11y_page/live_caption_section.js",
    "about_page/about_page.js",
    "appearance_page/appearance_fonts_page.js",
    "appearance_page/appearance_page.js",
    "appearance_page/home_url_input.js",
    "autofill_page/autofill_page.js",
    "autofill_page/avatar_icon.js",
    "autofill_page/payments_list.js",
    "autofill_page/credit_card_list_entry.js",
    "autofill_page/upi_id_list_entry.js",
    "autofill_page/passwords_shared_css.js",
    "autofill_page/credit_card_edit_dialog.js",
    "autofill_page/autofill_section.js",
    "autofill_page/address_edit_dialog.js",
    "autofill_page/address_remove_confirmation_dialog.js",
    "autofill_page/password_check.js",
    "autofill_page/password_check_edit_dialog.js",
    "autofill_page/password_check_edit_disclaimer_dialog.js",
    "autofill_page/password_check_list_item.js",
    "autofill_page/password_list_item.js",
    "autofill_page/passwords_list_handler.js",
    "autofill_page/password_remove_confirmation_dialog.js",
    "autofill_page/passwords_section.js",
    "autofill_page/passwords_device_section.js",
    "autofill_page/password_edit_dialog.js",
    "autofill_page/password_move_multiple_passwords_to_account_dialog.js",
    "autofill_page/password_move_to_account_dialog.js",
    "autofill_page/password_remove_dialog.js",
    "autofill_page/passwords_export_dialog.js",
    "autofill_page/payments_section.js",
    "basic_page/basic_page.js",
    "clear_browsing_data_dialog/clear_browsing_data_dialog.js",
    "clear_browsing_data_dialog/history_deletion_dialog.js",
    "clear_browsing_data_dialog/installed_app_checkbox.js",
    "clear_browsing_data_dialog/passwords_deletion_dialog.js",
    "controls/controlled_button.js",
    "controls/controlled_radio_button.js",
    "controls/extension_controlled_indicator.js",
    "controls/password_prompt_dialog.js",
    "controls/settings_checkbox.js",
    "controls/settings_dropdown_menu.js",
    "controls/settings_radio_group.js",
    "controls/settings_slider.js",
    "controls/settings_textarea.js",
    "controls/settings_toggle_button.js",
    "downloads_page/downloads_page.js",
    "icons.js",
    "languages_page/add_languages_dialog.js",
    "languages_page/languages_page.js",
    "languages_page/languages_subpage.js",
    "settings_menu/settings_menu.js",
    "on_startup_page/on_startup_page.js",
    "on_startup_page/startup_url_dialog.js",
    "on_startup_page/startup_url_entry.js",
    "on_startup_page/startup_urls_page.js",
    "people_page/sync_account_control.js",
    "people_page/sync_encryption_options.js",
    "people_page/people_page.js",
    "people_page/signout_dialog.js",
    "people_page/sync_controls.js",
    "people_page/sync_page.js",
    "privacy_page/collapse_radio_button.js",
    "privacy_page/cookies_page.js",
    "privacy_page/do_not_track_toggle.js",
    "privacy_page/privacy_page.js",
    "privacy_page/personalization_options.js",
    "privacy_page/secure_dns.js",
    "privacy_page/secure_dns_input.js",
    "privacy_page/security_page.js",
    "privacy_page/disable_safebrowsing_dialog.js",
    "privacy_page/security_keys_bio_enroll_dialog.js",
    "privacy_page/security_keys_credential_management_dialog.js",
    "privacy_page/security_keys_pin_field.js",
    "privacy_page/security_keys_reset_dialog.js",
    "privacy_page/security_keys_set_pin_dialog.js",
    "privacy_page/security_keys_subpage.js",
    "reset_page/reset_page.js",
    "reset_page/reset_profile_dialog.js",
    "reset_page/reset_profile_banner.js",
    "safety_check_page/safety_check_child.js",
    "safety_check_page/safety_check_extensions_child.js",
    "safety_check_page/safety_check_page.js",
    "safety_check_page/safety_check_passwords_child.js",
    "safety_check_page/safety_check_safe_browsing_child.js",
    "safety_check_page/safety_check_updates_child.js",
    "search_engines_page/omnibox_extension_entry.js",
    "search_engines_page/search_engine_dialog.js",
    "search_engines_page/search_engine_entry_css.js",
    "search_engines_page/search_engine_entry.js",
    "search_engines_page/search_engines_list.js",
    "search_engines_page/search_engines_page.js",
    "search_page/search_page.js",
    "settings_main/settings_main.js",
    "settings_page/settings_animated_pages.js",
    "settings_page/settings_section.js",
    "settings_page/settings_subpage.js",
    "settings_ui/settings_ui.js",
    "settings_page_css.js",
    "settings_shared_css.js",
    "settings_vars_css.js",
    "site_favicon.js",
    "site_settings/add_site_dialog.js",
    "site_settings/all_sites.js",
    "site_settings/all_sites_icons.js",
    "site_settings/category_default_setting.js",
    "site_settings/category_setting_exceptions.js",
    "site_settings/chooser_exception_list.js",
    "site_settings/chooser_exception_list_entry.js",
    "site_settings/clear_storage_dialog_css.js",
    "site_settings/edit_exception_dialog.js",
    "site_settings/media_picker.js",
    "site_settings/pdf_documents.js",
    "site_settings/protocol_handlers.js",
    "site_settings/settings_category_default_radio_group.js",
    "site_settings/site_data.js",
    "site_settings/site_data_details_subpage.js",
    "site_settings/site_data_entry.js",
    "site_settings/site_details.js",
    "site_settings/site_details_permission.js",
    "site_settings/site_entry.js",
    "site_settings/site_list.js",
    "site_settings/site_list_entry.js",
    "site_settings/zoom_levels.js",
    "site_settings_page/recent_site_permissions.js",
    "site_settings_page/site_settings_list.js",
    "site_settings_page/site_settings_page.js",
  ]

  if (is_win) {
    in_files += [
      "chrome_cleanup_page/chrome_cleanup_page.js",
      "chrome_cleanup_page/items_to_remove_list.js",
      "incompatible_applications_page/incompatible_applications_page.js",
      "incompatible_applications_page/incompatible_application_item.js",
      "safety_check_page/safety_check_chrome_cleaner_child.js",
    ]
  }

  if (!is_mac) {
    in_files += [ "languages_page/edit_dictionary_page.js" ]
  }

  if (!is_chromeos_ash) {
    if (!is_chromeos_lacros) {
      in_files += [
        "default_browser_page/default_browser_page.js",
        "system_page/system_page.js",
      ]
    }
    in_files += [
      "people_page/import_data_dialog.js",
      "people_page/manage_profile.js",
    ]
  }
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/settings_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/settings_resources.h",
    "grit/settings_resources_map.cc",
    "grit/settings_resources_map.h",
    "settings_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

group("closure_compile") {
  deps = [
    ":closure_compile_local_module",
    "a11y_page:closure_compile_module",
    "about_page:closure_compile_module",
    "appearance_page:closure_compile_module",
    "autofill_page:closure_compile_module",
    "basic_page:closure_compile_module",
    "clear_browsing_data_dialog:closure_compile_module",
    "controls:closure_compile_module",
    "downloads_page:closure_compile_module",
    "languages_page:closure_compile_module",
    "on_startup_page:closure_compile_module",
    "people_page:closure_compile_module",
    "prefs:closure_compile_module",
    "privacy_page:closure_compile_module",
    "privacy_sandbox:closure_compile",
    "reset_page:closure_compile_module",
    "safety_check_page:closure_compile_module",
    "search_engines_page:closure_compile_module",
    "search_page:closure_compile_module",
    "settings_main:closure_compile_module",
    "settings_menu:closure_compile_module",
    "settings_page:closure_compile_module",
    "settings_ui:closure_compile_module",
    "site_settings:closure_compile_module",
    "site_settings_page:closure_compile_module",
  ]
  if (!is_chromeos_ash && !is_chromeos_lacros) {
    deps += [
      "default_browser_page:closure_compile_module",
      "system_page:closure_compile_module",
    ]
  }

  if (is_win) {
    deps += [
      "chrome_cleanup_page:closure_compile_module",
      "incompatible_applications_page:closure_compile_module",
    ]
  }

  if (is_chromeos_ash) {
    deps += [ "chromeos:closure_compile_module" ]
  }
}

js_type_check("closure_compile_local_module") {
  is_polymer3 = true
  closure_flags = settings_closure_flags
  deps = [
    ":extension_control_browser_proxy",
    ":global_scroll_target_behavior",
    ":hats_browser_proxy",
    ":i18n_setup",
    ":lazy_load",
    ":lifetime_browser_proxy",
    ":metrics_browser_proxy",
    ":open_window_proxy",
    ":page_visibility",
    ":route",
    ":router",
    ":search_settings",
    ":setting_id_param_util",
    ":settings",
    ":settings_routes",
    ":site_favicon",
  ]
}

js_library("settings") {
  deps = [
    ":hats_browser_proxy",
    ":lifetime_browser_proxy",
    ":metrics_browser_proxy",
    ":open_window_proxy",
    "about_page:about_page_browser_proxy",
    "autofill_page:multi_store_exception_entry",
    "autofill_page:multi_store_password_ui_entry",
    "autofill_page:password_manager_proxy",
    "people_page:sync_browser_proxy",
    "privacy_page:privacy_page_browser_proxy",
    "safety_check_page:safety_check_browser_proxy",
    "search_engines_page:search_engines_browser_proxy",
    "settings_ui:settings_ui",
  ]
}

js_library("extension_control_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
  externs_list = [ "$externs_path/chrome_send.js" ]
}

js_library("i18n_setup") {
  deps = [ "//ui/webui/resources/js:load_time_data.m" ]
}

js_library("global_scroll_target_behavior") {
  deps = [
    ":router",
    "//ui/webui/resources/js:promise_resolver.m",
  ]
}

js_library("hats_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("lazy_load") {
  deps = [
    "autofill_page:autofill_section",
    "autofill_page:payments_section",
    "chrome_cleanup_page:chrome_cleanup_proxy",
    "clear_browsing_data_dialog:clear_browsing_data_browser_proxy",
    "languages_page:languages_browser_proxy",
    "languages_page:languages_metrics_proxy",
    "languages_page:languages_page",
    "languages_page:languages_settings_metrics_proxy",
    "privacy_page:cookies_page",
    "privacy_page:security_page",
    "site_settings:category_setting_exceptions",
    "site_settings:local_data_browser_proxy",
    "site_settings:protocol_handlers",
    "site_settings:settings_category_default_radio_group",
    "site_settings:site_settings_prefs_browser_proxy",
    "site_settings_page",
  ]
}

js_library("lifetime_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("metrics_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("open_window_proxy") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("page_visibility") {
  deps = [ "//ui/webui/resources/js:load_time_data.m" ]
}

js_library("route") {
  deps = [
    ":page_visibility",
    ":router",
    ":settings_routes",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("router") {
  deps = [
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
  externs_list = [ "$externs_path/metrics_private.js" ]
}

js_library("search_settings") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:promise_resolver.m",
    "//ui/webui/resources/js:search_highlight_utils.m",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("setting_id_param_util") {
  deps = [
    ":router",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("settings_routes") {
  deps = [ ":router" ]
}

js_library("site_favicon") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:icon.m",
  ]
}

group("polymer3_elements") {
  public_deps = [
    # Sub-folder targets
    "a11y_page:web_components",
    "about_page:web_components",
    "appearance_page:web_components",
    "autofill_page:web_components",
    "basic_page:web_components",
    "clear_browsing_data_dialog:web_components",
    "controls:web_components",
    "downloads_page:web_components",
    "languages_page:web_components",
    "on_startup_page:web_components",
    "people_page:web_components",
    "privacy_page:polymer3_elements",
    "reset_page:web_components",
    "safety_check_page:web_components",
    "search_engines_page:web_components",
    "search_page:web_components",
    "settings_main:web_components",
    "settings_menu:web_components",
    "settings_page:web_components",
    "settings_ui:web_components",
    "site_settings:web_components",
    "site_settings_page:web_components",

    # Local targets
    ":web_components_local",
  ]

  if (!is_chromeos_ash && !is_chromeos_lacros) {
    public_deps += [
      "default_browser_page:web_components",
      "system_page:web_components",
    ]
  }

  if (is_win) {
    public_deps += [
      "chrome_cleanup_page:web_components",
      "incompatible_applications_page:web_components",
    ]
  }
}

html_to_js("web_components_local") {
  js_files = [
    "icons.js",
    "settings_vars_css.js",
    "settings_page_css.js",
    "settings_shared_css.js",
    "site_favicon.js",
  ]
}
