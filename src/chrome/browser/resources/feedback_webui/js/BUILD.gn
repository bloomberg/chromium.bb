# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_android)

preprocess_folder = "$target_gen_dir/../preprocessed/js"

ts_files = [
  "feedback.ts",
  "feedback_util.ts",
  "questionnaire.ts",
  "sys_info.ts",
  "take_screenshot.ts",
]

preprocess_if_expr("preprocess") {
  in_folder = "."
  out_folder = preprocess_folder
  in_files = ts_files
}

ts_library("build_ts") {
  root_dir = preprocess_folder
  out_dir = "$target_gen_dir/tsc"
  in_files = ts_files

  definitions = [
    "//tools/typescript/definitions/feedback_private.d.ts",
    "//tools/typescript/definitions/chrome_send.d.ts",
  ]

  deps = [ "//ui/webui/resources:library" ]
  extra_deps = [ ":preprocess" ]
}

generate_grd("build_grdp") {
  grd_prefix = "feedback_webui_js"
  out_grd = "$target_gen_dir/resources.grdp"
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  resource_path_prefix = "js"
  deps = [ ":build_ts" ]
}
