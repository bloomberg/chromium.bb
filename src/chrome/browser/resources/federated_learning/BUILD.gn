# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_mojo_manifest = "preprocessed_mojo_manifest.json"

generate_grd("build_grd") {
  grd_prefix = "floc_internals"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [
    "floc_internals.html",
    "floc_internals.css",
  ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [
    ":preprocess",
    ":preprocess_mojo",
  ]
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/$preprocess_mojo_manifest",
  ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [ "floc_internals.js" ]
}

preprocess_if_expr("preprocess_mojo") {
  deps =
      [ "//chrome/browser/ui/webui/federated_learning:mojo_bindings_webui_js" ]
  in_folder =
      "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/federated_learning/"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_mojo_manifest"
  in_files = [ "floc_internals.mojom-webui.js" ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/floc_internals_resources.h",
    "grit/floc_internals_resources_map.cc",
    "grit/floc_internals_resources_map.h",
    "floc_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

js_type_check("closure_compile") {
  closure_flags = default_closure_args + mojom_js_args + [
                    "js_module_root=" + rebase_path(".", root_build_dir),
                    "js_module_root=" + rebase_path(
                            "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/federated_learning",
                            root_build_dir),
                  ]

  deps = [ ":floc_internals" ]
}

js_library("floc_internals") {
  deps = [
    "//chrome/browser/ui/webui/federated_learning:mojo_bindings_webui_js",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:util.m",
  ]
}
