# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_linux || is_chromeos || is_win || is_mac || is_fuchsia)

generate_grd("build_grd") {
  grd_prefix = "new_tab_page_instant"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [
    "most_visited_title.css",
    "most_visited_title.html",
  ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
}

grit("resources") {
  defines = chrome_grit_defines + [ "is_official_build=$is_official_build" ]
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]
  outputs = [
    "grit/new_tab_page_instant_resources.h",
    "grit/new_tab_page_instant_resources_map.cc",
    "grit/new_tab_page_instant_resources_map.h",
    "new_tab_page_instant_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

ts_library("build_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"
  in_files = [ "most_visited_title.ts" ]
  definitions = [ "embedded_search.d.ts" ]
}
