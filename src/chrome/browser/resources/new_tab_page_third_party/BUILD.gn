# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

js_type_check("closure_compile") {
  is_polymer3 = true
  closure_flags = default_closure_args + mojom_js_args
  deps = [ ":new_tab_page_third_party" ]
}

js_library("new_tab_page_third_party") {
  deps = [
    ":browser_proxy",
    ":most_visited",
  ]
}

js_library("browser_proxy") {
  deps = [
    "//chrome/browser/ui/webui/new_tab_page_third_party:mojo_bindings_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("most_visited") {
  deps = [
    ":browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_toast:cr_toast.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager.m",
  ]
}

html_to_js("web_components_local") {
  js_files = [ "most_visited.js" ]
}

group("web_components") {
  public_deps = [ ":web_components_local" ]
}

grd_prefix = "new_tab_page_third_party"
preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [
    "browser_proxy.js",
    "new_tab_page_third_party.js",
  ]
}

preprocess_if_expr("preprocess_gen") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [ "most_visited.js" ]
}

generate_grd("build_new_tab_page_third_party_mojo_grdp") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/new_tab_page_third_party_mojo_resources.grdp"
  input_files = [ "new_tab_page_third_party.mojom-lite.js" ]
  input_files_base_dir =
      rebase_path(
          "$root_gen_dir/chrome/browser/ui/webui/new_tab_page_third_party",
          root_build_dir)
}

generate_grd("build_grd") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [ "new_tab_page_third_party.html" ]
  input_files_base_dir = rebase_path(".", "//")
  grdp_files =
      [ "$target_gen_dir/new_tab_page_third_party_mojo_resources.grdp" ]
  deps = [
    ":build_new_tab_page_third_party_mojo_grdp",
    ":preprocess",
    ":preprocess_gen",
  ]
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/$preprocess_gen_manifest",
  ]
}

grit("resources") {
  defines = chrome_grit_defines
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [
    ":build_grd",
    "//chrome/browser/ui/webui/new_tab_page_third_party:mojo_bindings_js",
  ]
  outputs = [
    "grit/new_tab_page_third_party_resources.h",
    "grit/new_tab_page_third_party_resources_map.cc",
    "grit/new_tab_page_third_party_resources_map.h",
    "new_tab_page_third_party_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}
