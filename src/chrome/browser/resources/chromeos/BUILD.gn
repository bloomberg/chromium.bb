# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//chromeos/components/chromebox_for_meetings/buildflags/buildflags.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")

assert(is_chromeos, "Only Chrome OS resources in //c/b/resources//chromeos.")

group("resources") {
  public_deps = [
    ":app_icon_resources",
    ":multidevice_setup_resources",
    "accessibility:build",
    "arc_account_picker:resources",
    "assistant_optin:resources",
    "audio:resources",
    "emoji_picker:resources",
    "launcher_internals:resources",
    "login:conditional_resources",
    "login:unconditional_resources",
    "multidevice_internals:resources",
    "network_ui:resources",
  ]
}

grit("app_icon_resources") {
  source = "app_icon/app_icon_resources.grd"
  defines = chrome_grit_defines
  outputs = [
    "grit/app_icon_resources.h",
    "grit/app_icon_resources_map.cc",
    "grit/app_icon_resources_map.h",
    "app_icon_resources_100_percent.pak",
    "app_icon_resources_200_percent.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

grit("multidevice_setup_resources") {
  multidevice_setup_gen_dir =
      "$root_gen_dir/chrome/browser/resources/chromeos/multidevice_setup"
  source = "$multidevice_setup_gen_dir/multidevice_setup_resources.grd"

  defines = chrome_grit_defines
  outputs = [
    "grit/multidevice_setup_resources.h",
    "grit/multidevice_setup_resources_map.cc",
    "grit/multidevice_setup_resources_map.h",
    "multidevice_setup_resources.pak",
  ]
  deps = [ "//chrome/browser/resources/chromeos/multidevice_setup:build_grd" ]
  enable_input_discovery_for_gn_analyze = false
  output_dir = "$root_gen_dir/chrome"
}

group("closure_compile") {
  deps = [
    ":closure_compile_local",
    "accessibility/accessibility_common:closure_compile",
    "accessibility/braille_ime:closure_compile",
    "accessibility/chromevox:closure_compile",
    "accessibility/select_to_speak:closure_compile",
    "accessibility/switch_access:closure_compile",
    "account_manager:closure_compile",
    "account_manager/components:closure_compile",
    "add_supervision:closure_compile",
    "arc_account_picker:closure_compile",
    "assistant_optin:closure_compile",
    "bluetooth_pairing_dialog:closure_compile",
    "crostini_installer:closure_compile",
    "crostini_upgrader:closure_compile",
    "edu_coexistence:closure_compile",
    "emoji_picker:closure_compile",
    "emulator:closure_compile",
    "gaia_action_buttons:closure_compile",
    "internet_config_dialog:closure_compile",
    "internet_detail_dialog:closure_compile",
    "keyboard:closure_compile",
    "keyboard:closure_compile_module",
    "login:closure_compile",
    "multidevice_internals:closure_compile",
    "multidevice_setup:closure_compile",
    "network_ui:closure_compile",
    "parent_access:closure_compile",
    "password_change:closure_compile",
    "set_time_dialog:closure_compile",
    "smb_shares:closure_compile",
    "sys_internals:closure_compile",
    "vm:closure_compile",
  ]
  if (is_cfm) {
    deps += [ "chromebox_for_meetings:closure_compile" ]
  }
}

js_type_check("closure_compile_local") {
  deps = [
    ":power",
    ":slow",
  ]
}

js_library("power") {
  deps = [
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("slow") {
  deps = [
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:util.m",
  ]
  externs_list = [ "$externs_path/chrome_send.js" ]
}
