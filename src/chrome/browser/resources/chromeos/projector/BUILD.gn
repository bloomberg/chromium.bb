# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Projector is ChromeOS only")

resources_grd_file = "$target_gen_dir/resources.grd"
generate_grd("build_grd") {
  grd_prefix = "projector"
  out_grd = resources_grd_file
  input_files = [
    "player_app.html",
    "player_app.js",
    "player_app.css",
    "selfie_cam.html",
    "selfie_cam.js",
    "selfie_cam.css",
  ]
  input_files_base_dir = rebase_path(".", "//")
}

grit("resources") {
  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = resources_grd_file
  deps = [ ":build_grd" ]

  outputs = [
    "grit/projector_resources.h",
    "grit/projector_resources_map.cc",
    "grit/projector_resources_map.h",
    "projector_resources.pak",
  ]
  output_dir = "$root_gen_dir/chromeos/projector"
}

js_type_check("closure_compile") {
  deps = [
    ":player_app",
    ":selfie_cam",
  ]
}

js_library("player_app") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("selfie_cam") {
}
