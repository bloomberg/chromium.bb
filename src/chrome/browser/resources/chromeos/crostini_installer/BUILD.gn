# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/material_web_components/build_mwc_app.gni")
import("//tools/polymer/html_to_js.gni")

js_type_check("closure_compile") {
  is_polymer3 = true
  closure_flags = default_closure_args + [
                    "js_module_root=../../chrome/browser/resources/chromeos/crostini_installer/",
                    "js_module_root=./gen/chrome/browser/resources/chromeos/crostini_installer/",
                    "browser_resolver_prefix_replacements=\"chrome://crostini-installer/=./\"",
                  ]
  deps = [
    ":app",
    ":browser_proxy",
  ]
}

build_mwc_app("crostini_installer") {
  host = "crostini-installer"
  input = rebase_path(target_gen_dir, root_build_dir)
  js_module_in_files = [ "app.js" ]
  js_out_files = [ "app.rollup.js" ]
  out_manifest = "$target_gen_dir/build_manifest.json"
  excludes = [
    "chrome://resources/mojo/mojo/public/js/mojo_bindings_lite.js",
    "chrome://crostini-installer/strings.js",
    "chrome://resources/js/load_time_data.m.js",
    "chrome://resources/js/cr.m.js",
    "chrome://crostini-installer/browser_proxy.js",
    "chrome://resources/polymer/v3_0/polymer/polymer_bundled.min.js",
  ]
  deps = [
    ":web_components",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources:library",
    "//ui/webui/resources:preprocess",
  ]
}

js_library("app") {
  deps = [
    ":browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("browser_proxy") {
  deps = [
    "//chrome/browser/ui/webui/chromeos/crostini_installer:mojo_bindings_js_library_for_compile",
    "//ui/webui/resources/js:cr.m",
  ]
}

html_to_js("web_components") {
  js_files = [ "app.js" ]
}
