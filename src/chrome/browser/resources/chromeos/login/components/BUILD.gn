# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("//tools/polymer/polymer.gni")

group("closure_compile") {
  deps = [
    ":closure_compile_local",
    "hd_iron_icon:closure_compile",
    "oobe_adaptive_dialog:closure_compile",
    "oobe_carousel:closure_compile",
    "oobe_content_dialog:closure_compile",
    "oobe_dialog_host_behavior:closure_compile",
    "oobe_focus_behavior:closure_compile",
    "oobe_i18n_behavior:closure_compile",
    "oobe_scrollable_behavior:closure_compile",
    "oobe_slide:closure_compile",
    "progress_list_item:closure_compile",
    "throbber_notice:closure_compile",
  ]
}

js_type_check("closure_compile_local") {
  uses_legacy_modules = true
  deps = [
    ":fake_oobe",
    ":html-echo",
    ":login_screen_behavior",
    ":multi_step_behavior",
    ":network_select_login",
    ":oobe_buttons",
    ":oobe_i18n_dropdown",
    ":oobe_modal_dialog",
    ":oobe_select",
    ":oobe_types",
  ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":display_manager_types.m",
    ":login_screen_behavior.m",
    ":multi_step_behavior.m",
  ]
}

###############################
# Closure compiler libraries below

js_library("oobe_types") {
}

js_library("display_manager_types") {
}

# This is special file to be used as a substutute of Oobe object for closure
# compilation until we make real one closure compile.
# TODO (https://crbug.com/950198)
js_library("fake_oobe") {
  deps = [
    ":display_manager_types",
    ":oobe_types",
    "//ui/webui/resources/js:util",
  ]
}

# This is special file to be used as a substitute of Authenticaor object for
# closure compilation until OOBE migrates to modules.
js_library("fake_authenticator") {
  deps = [ "//ui/webui/resources/js:cr" ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/webview_tag.js",
  ]
}

js_library("login_screen_behavior") {
  deps = [
    ":display_manager_types",
    ":fake_oobe",
  ]
}

js_library("multi_step_behavior") {
}

js_library("network_select_login") {
  deps = [
    "//ui/webui/resources/cr_components/chromeos/network:network_select",
    "//ui/webui/resources/cr_components/chromeos/network:onc_mojo",
  ]
}

js_library("oobe_buttons") {
  deps = [ "oobe_i18n_behavior:oobe_i18n_behavior" ]
}

js_library("oobe_i18n_dropdown") {
  deps = [
    ":oobe_select",
    ":oobe_types",
  ]
}

js_library("oobe_modal_dialog") {
  deps = [ "oobe_i18n_behavior:oobe_i18n_behavior" ]
}

js_library("oobe_select") {
}

js_library("html-echo") {
}

#### Polymer3 / JS Modules

js_library("display_manager_types.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/components/display_manager_types.m.js" ]
  deps = []
  extra_deps = [ ":modulize" ]
}

js_library("login_screen_behavior.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/components/login_screen_behavior.m.js" ]
  deps = [
    ":display_manager_types.m"
  ]
  extra_deps = [ ":modulize" ]
}

js_library("multi_step_behavior.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/components/multi_step_behavior.m.js" ]
  deps = []
  extra_deps = [ ":modulize" ]
}

js_modulizer("modulize") {
  input_files = [
    "display_manager_types.js",
    "login_screen_behavior.js",
    "multi_step_behavior.js",
  ]
  namespace_rewrites = []
}