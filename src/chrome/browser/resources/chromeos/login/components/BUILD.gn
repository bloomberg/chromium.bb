# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

group("closure_compile") {
  deps = [
    ":closure_compile_local",
    "buttons:closure_compile",
    "dialogs:closure_compile",
    "oobe_carousel:closure_compile",
    "behaviors:closure_compile",
    "oobe_slide:closure_compile",
    "progress_list_item:closure_compile",
    "throbber_notice:closure_compile",
  ]
}

js_type_check("closure_compile_local") {
  uses_legacy_modules = true
  deps = [
    ":fake_oobe",
    ":hd_iron_icon",
    ":html-echo",
    ":network_select_login",
    ":oobe_i18n_dropdown",
    ":oobe_select",
    ":oobe_types",
  ]
}

group("closure_compile_module") {
  deps = [
    "behaviors:closure_compile_module",
    "buttons:closure_compile_module",
    "common_styles:closure_compile_module",
    "dialogs:closure_compile_module",
    "oobe_vars:closure_compile_module",

    # Local targets
    ":hd_iron_icon.m",
    ":oobe_icons.m",
  ]
}

group("polymer3_elements") {
  public_deps = [
    "behaviors:modulize",
    "buttons:polymer3_elements",
    "common_styles:polymer3_elements",
    "dialogs:polymer3_elements",
    "oobe_vars:polymer3_elements",

    # Local targets
    ":hd_iron_icon_module",
    ":oobe_icons_module"
  ]
}


###############################
# Closure compiler libraries below

js_library("oobe_types") {
}

js_library("display_manager_types") {
}

# This is special file to be used as a substutute of Oobe object for closure
# compilation until we make real one closure compile.
# TODO (https://crbug.com/950198)
js_library("fake_oobe") {
  deps = [
    ":display_manager_types",
    ":oobe_types",
    "//ui/webui/resources/js:util",
  ]
}

# This is special file to be used as a substitute of Authenticaor object for
# closure compilation until OOBE migrates to modules.
js_library("fake_authenticator") {
  deps = [ "//ui/webui/resources/js:cr" ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/webview_tag.js",
  ]
}

js_library("network_select_login") {
  deps = [
    "//ui/webui/resources/cr_components/chromeos/network:network_select",
    "//ui/webui/resources/cr_components/chromeos/network:onc_mojo",
  ]
}

js_library("oobe_i18n_dropdown") {
  deps = [
    ":oobe_select",
    ":oobe_types",
  ]
}

js_library("oobe_select") {
}

js_library("html-echo") {
}

js_library("hd_iron_icon") {
}

#### Polymer3 / JS Modules

js_library("hd_iron_icon.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/components/hd_iron_icon.m.js" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  extra_deps = [ ":hd_iron_icon_module" ]
}

js_library("oobe_icons.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/chromeos/login/components/oobe_icons.m.js" ]
  deps = []
  extra_deps = [ ":oobe_icons_module" ]
}

polymer_modulizer("hd_iron_icon") {
  js_file = "hd_iron_icon.js"
  html_file = "hd_iron_icon.html"
  html_type = "dom-module"
}

polymer_modulizer("oobe_icons") {
  js_file = "oobe_icons.m.js"
  html_file = "oobe_icons.html"
  html_type = "iron-iconset"
}
