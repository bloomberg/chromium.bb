# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(enable_extensions, "enable extensions check failed")

preprocess_manifest = "preprocessed_manifest.json"

grit("resources") {
  defines = chrome_grit_defines

  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/sync_file_system_internals_resources.h",
    "grit/sync_file_system_internals_resources_map.h",
    "grit/sync_file_system_internals_resources_map.cc",
    "sync_file_system_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

generate_grd("build_grd") {
  grd_prefix = "sync_file_system_internals"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [ "main.html" ]
  input_files_base_dir = rebase_path(".", "//")
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/tsconfig.manifest",
  ]
  deps = [
    ":build_ts",
    ":preprocess",
  ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/preprocessed"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [ "main.css" ]
}

ts_library("build_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"
  in_files = [
    "dump_database.ts",
    "extension_statuses.ts",
    "file_metadata.ts",
    "sync_service.ts",
    "task_log.ts",
    "utils.ts",
  ]
  deps = [ "//ui/webui/resources:library" ]
  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
}
