# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

js_type_check("closure_compile") {
  is_polymer3 = true
  closure_flags = default_closure_args + mojom_js_args
  deps = [ ":memories" ]
}

js_library("app") {
  deps = [
    ":browser_proxy",
    ":memory_card",
    ":mojom_conversion_mixin",
    ":page_thumbnail",
    ":router",
    "//chrome/browser/ui/webui/memories:mojo_bindings_webui_js",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_toolbar:cr_toolbar",
    "//ui/webui/resources/cr_elements/cr_toolbar:cr_toolbar_search_field",
    "//url/mojom:url_mojom_origin_webui_js",
  ]
}

js_library("browser_proxy") {
  deps = [
    "//chrome/browser/ui/webui/memories:mojo_bindings_webui_js",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("memories") {
  deps = [ ":app" ]
}

js_library("memory_card") {
  deps = [
    ":memory_tile",
    ":mojom_conversion_mixin",
    ":page_favicon",
    ":page_thumbnail",
    ":search_query",
    ":top_visit",
    ":utils",
    "//components/history_clusters/core:mojo_bindings_webui_js",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//url/mojom:url_mojom_origin_webui_js",
  ]
}

js_library("memory_tile") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//url/mojom:url_mojom_origin_webui_js",
  ]
}

js_library("mojom_conversion_mixin") {
  deps = [
    "//mojo/public/mojom/base:base_webui_js",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("page_favicon") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:icon.m",
    "//url/mojom:url_mojom_origin_webui_js",
  ]
}

js_library("page_thumbnail") {
  deps = [
    ":mojom_conversion_mixin",
    "//components/history_clusters/core:mojo_bindings_webui_js",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//url/mojom:url_mojom_origin_webui_js",
  ]
}

js_library("router") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("search_query") {
  deps = [
    ":mojom_conversion_mixin",
    "//components/history_clusters/core:mojo_bindings_webui_js",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("top_visit") {
  deps = [
    ":visit_row",
    "//components/history_clusters/core:mojo_bindings_webui_js",
    "//third_party/polymer/v3_0/components-chromium/iron-collapse:iron-collapse",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_expand_button:cr_expand_button.m",
  ]
}

js_library("utils") {
  deps = [ "//url/mojom:url_mojom_origin_webui_js" ]
}

js_library("visit_row") {
  deps = [
    ":mojom_conversion_mixin",
    ":page_favicon",
    ":utils",
    "//components/history_clusters/core:mojo_bindings_webui_js",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//url/mojom:url_mojom_origin_webui_js",
  ]
}

html_to_js("web_components_local") {
  js_files = [
    "app.js",
    "memory_card.js",
    "memory_tile.js",
    "page_favicon.js",
    "page_thumbnail.js",
    "router.js",
    "search_query.js",
    "shared_vars.js",
    "top_visit.js",
    "visit_row.js",
  ]
}

group("web_components") {
  public_deps = [ ":web_components_local" ]
}

grd_prefix = "memories"
preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

preprocess_if_expr("preprocess") {
  defines = chrome_grit_defines + [ "is_official_build=$is_official_build" ]
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [
    "browser_proxy.js",
    "memories.js",
    "mojom_conversion_mixin.js",
    "utils.js",
  ]
}

preprocess_if_expr("preprocess_gen") {
  defines = chrome_grit_defines + [ "is_official_build=$is_official_build" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [
    "app.js",
    "memory_card.js",
    "memory_tile.js",
    "page_favicon.js",
    "page_thumbnail.js",
    "router.js",
    "search_query.js",
    "shared_vars.js",
    "top_visit.js",
    "visit_row.js",
  ]
  deps = [ ":web_components" ]
}

generate_grd("build_memories_api_mojo_grdp") {
  grd_prefix = "$grd_prefix" + "_API"
  out_grd = "$target_gen_dir/memories_api_mojo_resources.grdp"
  input_files = [ "memories.mojom-webui.js" ]
  resource_path_rewrites = [ "memories.mojom-webui.js|chrome/browser/ui/webui/memories/memories.mojom-webui.js" ]
  input_files_base_dir =
      rebase_path("$root_gen_dir/mojom-webui/chrome/browser/ui/webui/memories",
                  root_build_dir)
}

generate_grd("build_memories_definition_mojo_grdp") {
  grd_prefix = "$grd_prefix" + "_DEFINITION"
  out_grd = "$target_gen_dir/memories_definition_mojo_resources.grdp"
  input_files = [ "memories.mojom-webui.js" ]
  resource_path_rewrites = [ "memories.mojom-webui.js|components/history_clusters/core/memories.mojom-webui.js" ]
  input_files_base_dir =
      rebase_path("$root_gen_dir/mojom-webui/components/history_clusters/core",
                  root_build_dir)
}

generate_grd("build_grd") {
  grd_prefix = "$grd_prefix"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [ "memories.html" ]
  input_files_base_dir = rebase_path(".", "//")
  grdp_files = [
    "$target_gen_dir/memories_api_mojo_resources.grdp",
    "$target_gen_dir/memories_definition_mojo_resources.grdp",
  ]
  deps = [
    ":build_memories_api_mojo_grdp",
    ":build_memories_definition_mojo_grdp",
    ":preprocess",
    ":preprocess_gen",
  ]
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/$preprocess_gen_manifest",
  ]
}

grit("resources") {
  defines = chrome_grit_defines + [ "is_official_build=$is_official_build" ]
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [
    ":build_grd",
    "//chrome/browser/ui/webui/memories:mojo_bindings_webui_js",
    "//components/history_clusters/core:mojo_bindings_webui_js",
  ]
  outputs = [
    "grit/memories_resources.h",
    "grit/memories_resources_map.cc",
    "grit/memories_resources_map.h",
    "memories_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}
