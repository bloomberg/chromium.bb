# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder = "$target_gen_dir/preprocessed"

ts_library("build_ts") {
  root_dir = preprocess_folder
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "app.ts",
    "browser_proxy.ts",
    "chrome/browser/ui/webui/history_clusters/history_clusters.mojom-webui.js",
    "cluster.ts",
    "components/history_clusters/core/history_clusters.mojom-webui.js",
    "history_clusters.ts",
    "page_favicon.ts",
    "router.ts",
    "search_query.ts",
    "shared_style.ts",
    "shared_vars.ts",
    "top_visit.ts",
    "visit_row.ts",
  ]
  path_mappings = [
    "/chrome/browser/ui/webui/history_clusters/*|" +
        rebase_path(
            "$preprocess_folder/chrome/browser/ui/webui/history_clusters/*",
            target_gen_dir),
    "/components/history_clusters/core/*|" +
        rebase_path("$preprocess_folder/components/history_clusters/core/*",
                    target_gen_dir),
  ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
    "//ui/webui/resources/mojo:library",
  ]
  extra_deps = [
    ":copy_history_clusters_api_mojom",
    ":copy_history_clusters_definitions_mojom",
    ":preprocess",
    ":preprocess_generated",
  ]
}

copy("copy_history_clusters_api_mojom") {
  sources = [ "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/history_clusters/history_clusters.mojom-webui.js" ]
  outputs = [ "$preprocess_folder/chrome/browser/ui/webui/history_clusters/{{source_file_part}}" ]
  deps = [ "//chrome/browser/ui/webui/history_clusters:mojo_bindings_webui_js" ]
}

copy("copy_history_clusters_definitions_mojom") {
  sources = [ "$root_gen_dir/mojom-webui/components/history_clusters/core/history_clusters.mojom-webui.js" ]
  outputs = [
    "$preprocess_folder/components/history_clusters/core/{{source_file_part}}",
  ]
  deps = [ "//components/history_clusters/core:mojo_bindings_webui_js" ]
}

html_to_js("web_components_local") {
  js_files = [
    "app.ts",
    "cluster.ts",
    "page_favicon.ts",
    "router.ts",
    "search_query.ts",
    "shared_style.ts",
    "shared_vars.ts",
    "top_visit.ts",
    "visit_row.ts",
  ]
}

group("web_components") {
  public_deps = [ ":web_components_local" ]
}

preprocess_if_expr("preprocess") {
  defines = chrome_grit_defines + [ "is_official_build=$is_official_build" ]
  in_folder = "./"
  out_folder = preprocess_folder
  in_files = [
    "browser_proxy.ts",
    "history_clusters.ts",
  ]
}

preprocess_if_expr("preprocess_generated") {
  defines = chrome_grit_defines + [ "is_official_build=$is_official_build" ]
  in_folder = target_gen_dir
  out_folder = preprocess_folder
  in_files = [
    "app.ts",
    "cluster.ts",
    "page_favicon.ts",
    "router.ts",
    "search_query.ts",
    "shared_style.ts",
    "shared_vars.ts",
    "top_visit.ts",
    "visit_row.ts",
  ]
  deps = [ ":web_components" ]
}

generate_grd("build_grd") {
  grd_prefix = "history_clusters"
  resource_path_prefix = "history_clusters"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [ "history_clusters.html" ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
}

grit("resources") {
  defines = chrome_grit_defines + [ "is_official_build=$is_official_build" ]
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]
  outputs = [
    "grit/history_clusters_resources.h",
    "grit/history_clusters_resources_map.cc",
    "grit/history_clusters_resources_map.h",
    "history_clusters_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}
