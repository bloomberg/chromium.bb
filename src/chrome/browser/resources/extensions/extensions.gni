# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

non_web_component_files = [
  "drag_and_drop_handler.ts",
  "extensions.ts",
  "item_mixin.ts",
  "item_util.ts",
  "keyboard_shortcut_delegate.ts",
  "navigation_helper.ts",
  "service.ts",
  "shortcut_util.ts",
  "site_settings_mixin.ts",
  "url_util.ts",
]

if (is_chromeos_ash) {
  non_web_component_files += [ "kiosk_browser_proxy.ts" ]
}

# Files holding a Polymer element definition and have an equivalent .html file.
web_component_files = [
  "activity_log/activity_log.ts",
  "activity_log/activity_log_history.ts",
  "activity_log/activity_log_history_item.ts",
  "activity_log/activity_log_stream.ts",
  "activity_log/activity_log_stream_item.ts",
  "code_section.ts",
  "detail_view.ts",
  "drop_overlay.ts",
  "error_page.ts",
  "host_permissions_toggle_list.ts",
  "install_warnings_dialog.ts",
  "item.ts",
  "item_list.ts",
  "keyboard_shortcuts.ts",
  "load_error.ts",
  "manager.ts",
  "options_dialog.ts",
  "pack_dialog.ts",
  "pack_dialog_alert.ts",
  "restricted_sites_dialog.ts",
  "runtime_host_permissions.ts",
  "runtime_hosts_dialog.ts",
  "shortcut_input.ts",
  "sidebar.ts",
  "site_permissions.ts",
  "site_permissions_by_site.ts",
  "site_permissions_edit_permissions_dialog.ts",
  "site_permissions_edit_url_dialog.ts",
  "site_permissions_list.ts",
  "toggle_row.ts",
  "toolbar.ts",
]

if (is_chromeos_ash) {
  web_component_files += [ "kiosk_dialog.ts" ]
}

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".ts", ".html") ]
}

icons_html_files = [ "icons.html" ]

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files + icons_html_files) {
  html_wrapper_files += [ f + ".ts" ]
}

# Files that are passed as input to css_to_wrapper().
css_files = [
  "shared_style.css",
  "shared_vars.css",
]

# Files that are generated by css_to_wrapper().
css_wrapper_files = []
foreach(f, css_files) {
  css_wrapper_files += [ f + ".ts" ]
}

ts_files = web_component_files + non_web_component_files
