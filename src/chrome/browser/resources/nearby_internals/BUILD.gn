# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

generate_grd("build_grd") {
  grd_prefix = "nearby_internals"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [ "index.html" ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [
    ":preprocess",
    ":preprocess_generated",
  ]
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/$preprocess_gen_manifest",
  ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [
    "nearby_contact_browser_proxy.js",
    "nearby_http_browser_proxy.js",
    "nearby_logs_browser_proxy.js",
    "nearby_ui_trigger_browser_proxy.js",
    "types.js",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [
    "contact_object.js",
    "contact_tab.js",
    "http_message_object.js",
    "http_tab.js",
    "log_object.js",
    "logging_tab.js",
    "nearby_internals.js",
    "shared_style.js",
    "ui_trigger_list_object.js",
    "ui_trigger_tab.js",
  ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/nearby_internals_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/nearby_internals_resources.h",
    "grit/nearby_internals_resources_map.cc",
    "grit/nearby_internals_resources_map.h",
    "nearby_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

js_type_check("closure_compile") {
  is_polymer3 = true
  deps = [
    ":contact_object",
    ":contact_tab",
    ":http_message_object",
    ":http_tab",
    ":log_object",
    ":logging_tab",
    ":nearby_contact_browser_proxy",
    ":nearby_http_browser_proxy",
    ":nearby_internals",
    ":nearby_logs_browser_proxy",
    ":nearby_ui_trigger_browser_proxy",
    ":types",
    ":ui_trigger_list_object",
    ":ui_trigger_tab",
  ]
}

js_library("nearby_internals") {
  deps = [
    ":contact_object",
    ":contact_tab",
    ":http_message_object",
    ":http_tab",
    ":log_object",
    ":logging_tab",
    ":nearby_contact_browser_proxy",
    ":nearby_http_browser_proxy",
    ":nearby_logs_browser_proxy",
    ":nearby_ui_trigger_browser_proxy",
    ":types",
    ":ui_trigger_list_object",
    ":ui_trigger_tab",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("logging_tab") {
  deps = [
    ":log_object",
    ":nearby_logs_browser_proxy",
    ":types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
}

js_library("log_object") {
  deps = [
    ":nearby_logs_browser_proxy",
    ":types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("nearby_logs_browser_proxy") {
  deps = [
    ":types",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("nearby_http_browser_proxy") {
  deps = [
    ":types",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("http_tab") {
  deps = [
    ":http_message_object",
    ":nearby_http_browser_proxy",
    ":types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("http_message_object") {
  deps = [
    ":nearby_http_browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("nearby_ui_trigger_browser_proxy") {
  deps = [
    ":types",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("ui_trigger_tab") {
  deps = [
    ":nearby_ui_trigger_browser_proxy",
    ":types",
    ":ui_trigger_list_object",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("ui_trigger_list_object") {
  deps = [
    ":nearby_ui_trigger_browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("types") {
}

js_library("shared_style") {
}

js_library("contact_tab") {
  deps = [
    ":contact_object",
    ":nearby_contact_browser_proxy",
    ":types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("contact_object") {
  deps = [
    ":types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("nearby_contact_browser_proxy") {
  deps = [
    ":types",
    "//ui/webui/resources/js:cr.m",
  ]
}

html_to_js("web_components") {
  js_files = [
    "contact_object.js",
    "contact_tab.js",
    "http_message_object.js",
    "http_tab.js",
    "log_object.js",
    "logging_tab.js",
    "nearby_internals.js",
    "shared_style.js",
    "ui_trigger_list_object.js",
    "ui_trigger_tab.js",
  ]
}
