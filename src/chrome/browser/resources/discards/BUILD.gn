# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")

js_type_check("closure_compile") {
  deps = [
    ":database_tab",
    ":discards",
    ":discards_tab",
    ":graph_doc",
    ":graph_tab",
  ]
}

js_library("sorted_table_behavior") {
  deps = [
    "//ui/webui/resources/js:assert",
  ]
}

js_library("discards") {
  deps = [
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:cr",
  ]
}

js_library("database_tab") {
  deps = [
    ":discards",
    ":sorted_table_behavior",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:cr",
  ]
  extra_deps = [ "//chrome/browser/ui/webui/discards:mojo_bindings_js" ]
  externs_list = [
    "$root_gen_dir/chrome/browser/ui/webui/discards/discards.mojom.externs.js",
    "$root_gen_dir/chrome/browser/resource_coordinator/lifecycle_unit_state.mojom.externs.js",
    "$externs_path/mojo.js",
  ]
}

js_library("discards_tab") {
  deps = [
    ":discards",
    ":sorted_table_behavior",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:icon",
    "//ui/webui/resources/js:util",
  ]
  extra_deps = [ "//chrome/browser/ui/webui/discards:mojo_bindings_js" ]
  externs_list = [
    "$externs_path/mojo.js",
    "$root_gen_dir/chrome/browser/resource_coordinator/lifecycle_unit_state.mojom.externs.js",
    "$root_gen_dir/chrome/browser/ui/webui/discards/discards.mojom.externs.js",
  ]
}

js_library("graph_tab") {
  deps = [
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:cr",
  ]
  extra_deps = [
    "//chrome/browser/ui/webui/discards:mojo_bindings_js",
    "//mojo/public/mojom/base:base_js",
    "//services/resource_coordinator/public/mojom:mojom_js",
  ]
  externs_list = [
    "$externs_path/mojo.js",
    "$root_gen_dir/chrome/browser/resource_coordinator/lifecycle_unit_state.mojom.externs.js",
    "$root_gen_dir/chrome/browser/ui/webui/discards/discards.mojom.externs.js",
    "$root_gen_dir/mojo/public/mojom/base/process_id.mojom.externs.js",
    "$root_gen_dir/mojo/public/mojom/base/time.mojom.externs.js",
    "$root_gen_dir/services/resource_coordinator/public/mojom/webui_graph_dump.mojom.externs.js",
  ]
}

js_library("graph_doc") {
  extra_deps = [ "//services/resource_coordinator/public/mojom:mojom_js" ]

  externs_list = [
    "../../../../third_party/d3/src/externs.js",
    "$root_gen_dir/mojo/public/mojom/base/process_id.mojom.externs.js",
    "$root_gen_dir/mojo/public/mojom/base/time.mojom.externs.js",
    "$root_gen_dir/services/resource_coordinator/public/mojom/webui_graph_dump.mojom.externs.js",
  ]
}

# This action merges the graph tab script into the graph_doc HTML template.
# It then base64 encodes the combination, and merges into the graph_tab HTML
# template to complete the data: URL for the webview therein.
action("generate_graph_tab") {
  script = "generate_graph_tab.py"
  sources = [
    "graph_doc.js",
    "graph_doc_template.html",
    "graph_tab_template.html",
  ]
  outputs = [
    "$target_gen_dir/graph_tab.html",
  ]

  args = rebase_path(outputs) + rebase_path([
                                              "graph_doc_template.html",
                                              "graph_doc.js",
                                              "graph_tab_template.html",
                                            ])
}

# Action with a transparent name.
group("discards_resources_gen") {
  deps = [
    ":generate_graph_tab",
  ]
}
