# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("../tools/optimize_webui.gni")

preprocess_folder = "preprocessed"

if (optimize_webui) {
  build_manifest = "build_manifest.json"

  optimize_webui("build") {
    host = "history"
    js_module_in_files = [
      "history.js",
      "lazy_load.js",
    ]
    input = rebase_path("$target_gen_dir/tsc", root_build_dir)
    js_out_files = [
      "history.rollup.js",
      "lazy_load.rollup.js",
      "shared.rollup.js",
    ]
    out_manifest = "$target_gen_dir/$build_manifest"

    deps = [
      ":build_ts",
      "../../../../ui/webui/resources:preprocess",
    ]
    excludes = [ "chrome://resources/js/cr.m.js" ]
  }
}

generate_grd("build_grd") {
  grd_prefix = "history"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [
    "history.html",
    "images/sign_in_promo_dark.svg",
    "images/sign_in_promo.svg",
  ]
  input_files_base_dir = rebase_path(".", "//")

  if (optimize_webui) {
    deps = [ ":build" ]
    manifest_files = [ "$target_gen_dir/$build_manifest" ]
    resource_path_rewrites = [
      "history.rollup.js|history.js",
      "lazy_load.rollup.js|lazy_load.js",
    ]
  } else {
    deps = [ ":build_ts" ]
    manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  }
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "browser_service.ts",
    "constants.ts",
    "externs.ts",
    "history.ts",
    "lazy_load.ts",
    "query_manager.ts",
    "searched_label.ts",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "app.ts",
    "history_item.ts",
    "history_list.ts",
    "history_toolbar.ts",
    "router.ts",
    "shared_style.ts",
    "shared_vars.ts",
    "side_bar.ts",
    "synced_device_card.ts",
    "synced_device_manager.ts",
  ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/history_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/history_resources.h",
    "grit/history_resources_map.cc",
    "grit/history_resources_map.h",
    "history_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

html_to_js("web_components") {
  js_files = [
    "app.ts",
    "history_item.ts",
    "history_list.ts",
    "history_toolbar.ts",
    "router.ts",
    "shared_style.ts",
    "shared_vars.ts",
    "side_bar.ts",
    "synced_device_card.ts",
    "synced_device_manager.ts",
  ]
}

ts_library("build_ts") {
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "app.ts",
    "browser_service.ts",
    "constants.ts",
    "externs.ts",
    "history_item.ts",
    "history.ts",
    "history_list.ts",
    "history_toolbar.ts",
    "lazy_load.ts",
    "query_manager.ts",
    "router.ts",
    "searched_label.ts",
    "shared_style.ts",
    "shared_vars.ts",
    "side_bar.ts",
    "synced_device_card.ts",
    "synced_device_manager.ts",
  ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
  ]
  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  extra_deps = [
    ":preprocess",
    ":preprocess_generated",
  ]
}
