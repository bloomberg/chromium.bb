# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/grit/grit_rule.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("webui_gallery.gni")

assert(!is_android)

grit("resources") {
  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/webui_gallery_resources.h",
    "grit/webui_gallery_resources_map.cc",
    "grit/webui_gallery_resources_map.h",
    "webui_gallery_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

generate_grd("build_grd") {
  grd_prefix = "webui_gallery"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [
    "demos/cr_button_demo.html",
    "demos/cr_checkbox_demo.html",
    "webui_gallery.html",
  ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
}

html_to_wrapper("html_wrapper_files") {
  in_files = html_files
}

copy("copy_ts_files") {
  sources = ts_files
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

ts_library("build_ts") {
  root_dir = target_gen_dir
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = ts_files + html_wrapper_files
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
  ]
  extra_deps = [
    ":copy_ts_files",
    ":html_wrapper_files",
  ]
}
