# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_android)

# Move everything to one folder using preprocess_if_expr.
preprocess_folder = "preprocessed"

generate_grd("build_grd") {
  grd_prefix = "profile_internals"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [ "profile_internals.html" ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
}

preprocess_if_expr("preprocess") {
  in_folder = "."
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "profile_internals_app.ts",
    "profile_internals_browser_proxy.ts",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":html_wrapper_files" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [ "profile_internals_app.html.ts" ]
}

grit("resources") {
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]
  outputs = [
    "grit/profile_internals_resources.h",
    "grit/profile_internals_resources_map.cc",
    "grit/profile_internals_resources_map.h",
    "profile_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

html_to_wrapper("html_wrapper_files") {
  in_files = [ "profile_internals_app.html" ]
}

ts_library("build_ts") {
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "profile_internals_app.ts",
    "profile_internals_app.html.ts",
    "profile_internals_browser_proxy.ts",
  ]
  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
  ]
  extra_deps = [
    ":preprocess",
    ":preprocess_generated",
  ]
}
