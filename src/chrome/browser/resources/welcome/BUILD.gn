# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_chromeos_ash && !is_android)

preprocess_folder = "preprocessed"

generate_grd("build_grd") {
  grd_prefix = "welcome"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [
    "images/background_svgs/bookmarks_background.svg",
    "images/background_svgs/bookmarks_foreground.svg",
    "images/background_svgs/devices_check.svg",
    "images/background_svgs/devices.svg",
    "images/background_svgs/hexagon.svg",
    "images/background_svgs/lozenge.svg",
    "images/background_svgs/password_field.svg",
    "images/background_svgs/password.svg",
    "images/background_svgs/square.svg",
    "images/background_svgs/streamer_circle.svg",
    "images/background_svgs/streamer_line.svg",
    "images/background_svgs/triangle.svg",
    "welcome.html",
    "welcome.css",
  ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [ ":build" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]

  if (is_chrome_branded) {
    deps += [ ":build_icons_grdp" ]
    grdp_files = [ "$target_gen_dir/icon_resources.grdp" ]
  }
}

if (is_chrome_branded) {
  generate_grd("build_icons_grdp") {
    grd_prefix = "welcome_images"
    out_grd = "$target_gen_dir/icon_resources.grdp"
    input_files = [
      "module_icons/add_bookmarks.svg",
      "module_icons/pick_a_background.svg",
      "module_icons/set_default_dark.svg",
      "module_icons/set_default_light.svg",
      "ntp_thumbnails/art.jpg",
      "ntp_thumbnails/cityscape.jpg",
      "ntp_thumbnails/earth.jpg",
      "ntp_thumbnails/geometric_shapes.jpg",
      "ntp_thumbnails/landscape.jpg",
      "set_default_dark.svg",
      "set_default_light.svg",
    ]
    input_files_base_dir =
        rebase_path("//chrome/app/theme/google_chrome/welcome/", "//")
    resource_path_prefix = "images"
  }
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "google_apps/google_app_proxy.ts",
    "google_apps/google_apps_metrics_proxy.ts",
    "landing_view_proxy.ts",
    "navigation_mixin.ts",
    "ntp_background/ntp_background_metrics_proxy.ts",
    "ntp_background/ntp_background_proxy.ts",
    "set_as_default/nux_set_as_default_proxy.ts",
    "shared/bookmark_proxy.ts",
    "shared/module_metrics_proxy.ts",
    "shared/nux_types.ts",
    "signin_view_proxy.ts",
    "welcome_browser_proxy.ts",
    "welcome.ts",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "google_apps/nux_google_apps.ts",
    "landing_view.ts",
    "ntp_background/nux_ntp_background.ts",
    "set_as_default/nux_set_as_default.ts",
    "shared/action_link_style_css.ts",
    "shared/animations_css.ts",
    "shared/chooser_shared_css.ts",
    "shared/navi_colors_css.ts",
    "shared/onboarding_background.ts",
    "shared/splash_pages_shared_css.ts",
    "shared/step_indicator.ts",
    "signin_view.ts",
    "welcome_app.ts",
  ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/welcome_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/welcome_resources.h",
    "grit/welcome_resources_map.cc",
    "grit/welcome_resources_map.h",
    "welcome_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

group("web_components") {
  public_deps = [
    ":web_components_local",
    "./google_apps:web_components",
    "./ntp_background:web_components",
    "./set_as_default:web_components",
    "./shared:web_components",
  ]
}

html_to_js("web_components_local") {
  js_files = [
    "landing_view.ts",
    "signin_view.ts",
    "welcome_app.ts",
  ]
}

ts_library("build") {
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/$preprocess_folder"
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "google_apps/google_app_proxy.ts",
    "google_apps/google_apps_metrics_proxy.ts",
    "google_apps/nux_google_apps.ts",
    "landing_view.ts",
    "landing_view_proxy.ts",
    "navigation_mixin.ts",
    "ntp_background/ntp_background_metrics_proxy.ts",
    "ntp_background/ntp_background_proxy.ts",
    "ntp_background/nux_ntp_background.ts",
    "set_as_default/nux_set_as_default.ts",
    "set_as_default/nux_set_as_default_proxy.ts",
    "shared/action_link_style_css.ts",
    "shared/animations_css.ts",
    "shared/bookmark_proxy.ts",
    "shared/chooser_shared_css.ts",
    "shared/module_metrics_proxy.ts",
    "shared/navi_colors_css.ts",
    "shared/nux_types.ts",
    "shared/onboarding_background.ts",
    "shared/splash_pages_shared_css.ts",
    "shared/step_indicator.ts",
    "signin_view.ts",
    "signin_view_proxy.ts",
    "welcome_app.ts",
    "welcome_browser_proxy.ts",
    "welcome.ts",
  ]
  definitions = [
    "//tools/typescript/definitions/bookmarks.d.ts",
    "//tools/typescript/definitions/chrome_event.d.ts",
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
  ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
  ]
  extra_deps = [
    ":preprocess",
    ":preprocess_generated",
  ]
}
