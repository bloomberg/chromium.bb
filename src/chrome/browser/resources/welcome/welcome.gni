# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Files holding a Polymer element definition AND have an equivalent .html file.
web_component_files = [
  "google_apps/nux_google_apps.ts",
  "landing_view.ts",
  "ntp_background/nux_ntp_background.ts",
  "set_as_default/nux_set_as_default.ts",
  "shared/onboarding_background.ts",
  "shared/step_indicator.ts",
  "signin_view.ts",
  "welcome_app.ts",
]

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".ts", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".ts" ]
}

ts_files = [
             "google_apps/google_app_proxy.ts",
             "google_apps/google_apps_metrics_proxy.ts",
             "landing_view_proxy.ts",
             "navigation_mixin.ts",
             "ntp_background/ntp_background_metrics_proxy.ts",
             "ntp_background/ntp_background_proxy.ts",
             "set_as_default/nux_set_as_default_proxy.ts",
             "shared/bookmark_proxy.ts",
             "shared/module_metrics_proxy.ts",
             "shared/nux_types.ts",
             "signin_view_proxy.ts",
             "welcome_browser_proxy.ts",
           ] + web_component_files

css_wrapper_files = [
  "shared/action_link_style_css.ts",
  "shared/animations_css.ts",
  "shared/chooser_shared_css.ts",
  "shared/navi_colors_css.ts",
  "shared/splash_pages_shared_css.ts",
]
