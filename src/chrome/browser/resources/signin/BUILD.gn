# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/common/features.gni")
import("//components/signin/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_android)

preprocess_folder = "preprocessed"

group("web_components") {
  public_deps = [
    ":web_components_local",
    "sync_confirmation:web_components",
  ]
  if (!is_chromeos_ash) {
    public_deps += [
      "enterprise_profile_welcome:web_components",
      "profile_customization:web_components",
      "signin_email_confirmation:web_components",
      "signin_error:web_components",
    ]
  }
  if (enable_dice_support) {
    public_deps += [
      "dice_web_signin_intercept:web_components",
      "signin_reauth:web_components",
    ]
  }
}

html_to_js("web_components_local") {
  js_files = [
    "signin_shared_css.ts",
    "signin_vars_css.ts",
  ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/signin_resources.h",
    "grit/signin_resources_map.cc",
    "grit/signin_resources_map.h",
    "signin_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

generate_grd("build_grd") {
  grd_prefix = "signin"
  out_grd = "$target_gen_dir/resources.grd"

  input_files = [
    "sync_confirmation/sync_confirmation.html",
    "sync_confirmation/sync_disabled_confirmation.html",
    "sync_confirmation/sync_loading_confirmation.html",
    "sync_confirmation/images/sync_confirmation_refreshed_illustration_dark.svg",
    "sync_confirmation/images/sync_confirmation_illustration.svg",
    "sync_confirmation/images/sync_confirmation_illustration_dark.svg",
    "sync_confirmation/images/sync_confirmation_refreshed_illustration.svg",
  ]
  if (!is_chromeos_ash) {
    input_files += [
      "enterprise_profile_welcome/enterprise_profile_welcome.html",
      "enterprise_profile_welcome/images/enterprise_profile_welcome_illustration.svg",
      "profile_customization/profile_customization.html",
      "signin_email_confirmation/signin_email_confirmation.html",
      "signin_error/signin_error.html",
    ]
  }
  if (enable_dice_support) {
    input_files += [
      "dice_web_signin_intercept/dice_web_signin_intercept.html",
      "dice_web_signin_intercept/images/split_header.svg",
      "signin_reauth/images/account_passwords_reauth_illustration_dark.svg",
      "signin_reauth/images/account_passwords_reauth_illustration.svg",
      "signin_reauth/signin_reauth.html",
    ]
  }
  input_files_base_dir = rebase_path(".", "//")

  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "sync_confirmation/sync_confirmation_browser_proxy.ts",
    "sync_confirmation/sync_confirmation.ts",
  ]

  if (!is_chromeos_ash) {
    in_files += [
      "enterprise_profile_welcome/enterprise_profile_welcome_browser_proxy.ts",
      "profile_customization/profile_customization_browser_proxy.ts",
      "signin_error/signin_error.ts",
    ]
  }
  if (enable_dice_support) {
    in_files += [
      "dice_web_signin_intercept/dice_web_signin_intercept_browser_proxy.ts",
      "signin_reauth/signin_reauth_browser_proxy.ts",
    ]
  }
}

preprocess_if_expr("preprocess_gen") {
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "signin_shared_css.ts",
    "signin_vars_css.ts",
    "sync_confirmation/sync_confirmation_app.ts",
    "sync_confirmation/sync_disabled_confirmation_app.ts",
  ]

  if (!is_chromeos_ash) {
    in_files += [
      "enterprise_profile_welcome/enterprise_profile_welcome_app.ts",
      "profile_customization/profile_customization_app.ts",
      "signin_email_confirmation/signin_email_confirmation_app.ts",
      "signin_error/signin_error_app.ts",
    ]
  }
  if (enable_dice_support) {
    in_files += [
      "dice_web_signin_intercept/dice_web_signin_intercept_app.ts",
      "signin_reauth/signin_reauth_app.ts",
    ]
  }

  deps = [ ":web_components" ]
}

ts_library("build_ts") {
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  composite = true
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "signin_shared_css.ts",
    "signin_vars_css.ts",
    "sync_confirmation/sync_confirmation_app.ts",
    "sync_confirmation/sync_confirmation_browser_proxy.ts",
    "sync_confirmation/sync_confirmation.ts",
    "sync_confirmation/sync_disabled_confirmation_app.ts",
  ]

  if (!is_chromeos_ash) {
    in_files += [
      "enterprise_profile_welcome/enterprise_profile_welcome_app.ts",
      "enterprise_profile_welcome/enterprise_profile_welcome_browser_proxy.ts",
      "profile_customization/profile_customization_app.ts",
      "profile_customization/profile_customization_browser_proxy.ts",
      "signin_email_confirmation/signin_email_confirmation_app.ts",
      "signin_error/signin_error_app.ts",
      "signin_error/signin_error.ts",
    ]
  }
  if (enable_dice_support) {
    in_files += [
      "dice_web_signin_intercept/dice_web_signin_intercept_app.ts",
      "dice_web_signin_intercept/dice_web_signin_intercept_browser_proxy.ts",
      "signin_reauth/signin_reauth_app.ts",
      "signin_reauth/signin_reauth_browser_proxy.ts",
    ]
  }

  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]

  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
    "//ui/webui/resources/cr_components/customize_themes:build_ts",
  ]
  extra_deps = [
    ":preprocess",
    ":preprocess_gen",
  ]
}
