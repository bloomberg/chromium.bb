# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder = "preprocessed"

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/read_later_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/read_later_resources.h",
    "grit/read_later_resources_map.cc",
    "grit/read_later_resources_map.h",
    "read_later_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

generate_grd("build_grd") {
  input_files = [
    "images/read_later_empty_dark.svg",
    "images/read_later_empty.svg",
    "read_later.html",
    "side_panel/bookmarks.html",
    "side_panel/reader_mode/reader_mode.html",
    "side_panel/side_panel.html",
  ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  grd_prefix = "read_later"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [ "read_later_api_proxy.ts" ]
}

preprocess_if_expr("preprocess_mojo") {
  deps = [ "//chrome/browser/ui/webui/read_later:mojo_bindings_webui_js" ]
  in_folder = "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/read_later/"
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [ "read_later.mojom-webui.js" ]
}

preprocess_if_expr("preprocess_web_components") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "app.ts",
    "icons.ts",
    "read_later_item.ts",
  ]
}

html_to_js("web_components") {
  js_files = [
    "app.ts",
    "icons.ts",
    "read_later_item.ts",
  ]
}

ts_library("build_ts") {
  tsconfig_base = "tsconfig_base.json"
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  composite = true
  in_files = [
    "app.ts",
    "icons.ts",
    "read_later_api_proxy.ts",
    "read_later_item.ts",
    "read_later.mojom-webui.js",

    # SidePanel resources
    "side_panel/app.ts",
    "side_panel/bookmark_folder.ts",
    "side_panel/bookmarks_list.ts",
    "side_panel/bookmarks_api_proxy.ts",
    "side_panel/bookmarks_drag_manager.ts",
    "side_panel/bookmarks.mojom-webui.js",
    "side_panel/reader_mode/app.ts",
    "side_panel/reader_mode/reader_mode_api_proxy.ts",
    "side_panel/reader_mode/reader_mode.mojom-webui.js",
  ]
  definitions = [
    "//tools/typescript/definitions/bookmark_manager_private.d.ts",
    "//tools/typescript/definitions/bookmarks.d.ts",
    "//tools/typescript/definitions/chrome_event.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
  ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
    "//ui/webui/resources/mojo:library",
  ]
  extra_deps = [
    ":preprocess",
    ":preprocess_mojo",
    ":preprocess_web_components",
    "side_panel:preprocess",
    "side_panel:preprocess_generated",
    "side_panel:preprocess_mojo",
  ]
}
