# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")

# TODO(dpapad): Add compile targets for all files, crbug.com/721073.
group("closure_compile") {
  deps = [
    ":pdf_resources",
    "elements/viewer-bookmark:closure_compile",
    "elements/viewer-error-screen:closure_compile",
    "elements/viewer-form-warning:closure_compile",
    "elements/viewer-page-indicator:closure_compile",
    "elements/viewer-page-selector:closure_compile",
    "elements/viewer-password-screen:closure_compile",
  ]
  if (is_chromeos) {
    deps += [
      "elements/viewer-ink-host:closure_compile",
      "ink:closure_compile",
    ]
  }
}

js_library("browser_api") {
  deps = [
    "//ui/webui/resources/js:assert",
  ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/mime_handler_private.js",
  ]
}

js_library("pdf_fitting_type") {
}

js_library("gesture_detector") {
}

js_library("open_pdf_params_parser") {
  deps = [
    ":pdf_fitting_type",
  ]
}

js_library("pdf_scripting_api") {
  deps = [
    "//ui/webui/resources/js:assert",
  ]
}

js_library("viewport_scroller") {
}

js_library("viewport_interface") {
  deps = [
    ":pdf_fitting_type",
  ]
}

js_library("viewport") {
  deps = [
    ":gesture_detector",
    ":viewport_interface",
    ":zoom_manager",
    "//ui/webui/resources/js:util",
  ]
  externs_list = [ "$externs_path/pending.js" ]
}

js_library("zoom_manager") {
  deps = [
    ":browser_api",
    ":viewport_interface",
  ]
}

js_library("metrics") {
  externs_list = [ "$externs_path/metrics_private.js" ]
}

js_type_check("pdf_resources") {
  deps = [
    ":browser_api",
    ":gesture_detector",
    ":metrics",
    ":open_pdf_params_parser",
    ":pdf_fitting_type",
    ":pdf_scripting_api",
    ":viewport",
    ":viewport_interface",
    ":viewport_scroller",
    ":zoom_manager",
  ]
}
