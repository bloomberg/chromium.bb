# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/html_to_js.gni")

js_type_check("closure_compile") {
  is_polymer3 = true
  deps = [
    ":viewer-bookmark",
    ":viewer-error-screen",
    ":viewer-page-indicator",
    ":viewer-page-selector",
    ":viewer-password-screen",
    ":viewer-pdf-toolbar",
    ":viewer-toolbar-dropdown",
    ":viewer-zoom-button",
    ":viewer-zoom-toolbar",
  ]
  if (is_chromeos) {
    deps += [
      ":viewer-form-warning",
      ":viewer-ink-host",
      ":viewer-pen-options",
    ]
  }
}

js_library("viewer-bookmark") {
  deps = [
    "..:bookmark_type",
    "//third_party/polymer/v3_0/components-chromium/iron-a11y-keys-behavior",
  ]
}

js_library("viewer-error-screen") {
  deps = [ "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m" ]
}

if (is_chromeos) {
  js_library("viewer-form-warning") {
    deps = [
      "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
      "//ui/webui/resources/js:promise_resolver.m",
    ]
  }

  js_library("viewer-ink-host") {
    deps = [
      "//chrome/browser/resources/pdf:metrics",
      "//chrome/browser/resources/pdf:viewport",
      "//chrome/browser/resources/pdf/ink:ink_api",
    ]
  }
}

js_library("viewer-page-indicator") {
  deps = [
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("viewer-page-selector") {
  deps = [ "//ui/webui/resources/cr_elements/cr_input:cr_input.m" ]
}

js_library("viewer-password-screen") {
  deps = [ "//ui/webui/resources/cr_elements/cr_input:cr_input.m" ]
}

js_library("viewer-pdf-toolbar") {
  deps = [
    ":viewer-bookmark",
    ":viewer-page-selector",
    ":viewer-toolbar-dropdown",
    "..:annotation_tool",
  ]
  externs_list = [ "$externs_path/pending.js" ]
}

js_library("viewer-pen-options") {
}

js_library("viewer-toolbar-dropdown") {
  deps = []
}

js_library("viewer-zoom-toolbar") {
  deps = [
    ":viewer-zoom-button",
    "..:constants",
    "//ui/webui/resources/js:assert.m",
  ]
}

js_library("viewer-zoom-button") {
  deps = []
}

html_to_js("web_components") {
  js_files = [
    "icons.js",
    "shared-vars.js",
    "viewer-bookmark.js",
    "viewer-error-screen.js",
    "viewer-page-indicator.js",
    "viewer-page-selector.js",
    "viewer-password-screen.js",
    "viewer-pdf-toolbar.js",
    "viewer-toolbar-dropdown.js",
    "viewer-zoom-button.js",
    "viewer-zoom-toolbar.js",
  ]
  if (is_chromeos) {
    js_files += [
      "viewer-form-warning.js",
      "viewer-ink-host.js",
      "viewer-pen-options.js",
    ]
  }
}
