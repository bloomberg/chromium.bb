# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/tools/optimize_webui.gni")
import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

preprocess_folder = "preprocessed"

if (optimize_webui) {
  build_manifest = "build_manifest.json"

  optimize_webui("build") {
    host = "download-shelf.top-chrome"
    input = rebase_path("$target_gen_dir/tsc", root_build_dir)
    js_out_files = [ "download_shelf.rollup.js" ]
    js_module_in_files = [ "download_shelf.js" ]
    out_manifest = "$target_gen_dir/$build_manifest"

    excludes = [
      "chrome://resources/js/cr.m.js",
      "chrome://resources/mojo/url/mojom/url.mojom-webui.js",
      "chrome://resources/mojo/mojo/public/js/bindings.js",
      "chrome://resources/mojo/mojo/public/mojom/base/file_path.mojom-webui.js",
      "chrome://resources/mojo/mojo/public/mojom/base/time.mojom-webui.js",
    ]

    deps = [
      ":build_ts",
      "../../../../ui/webui/resources:preprocess",
    ]
  }
}

generate_grd("build_grd") {
  grd_prefix = "download_shelf"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [ "download_shelf.html" ]
  input_files_base_dir = rebase_path(".", "//")

  if (optimize_webui) {
    deps = [ ":build" ]
    manifest_files = [ "$target_gen_dir/$build_manifest" ]
    resource_path_rewrites = [ "download_shelf.rollup.js|download_shelf.js" ]
  } else {
    deps = [ ":build_ts" ]
    manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  }
}

copy("copy_mojo") {
  deps = [ "//chrome/browser/ui/webui/download_shelf:mojo_bindings_webui_js" ]
  sources = [ "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/download_shelf/download_shelf.mojom-webui.js" ]
  outputs = [ "$target_gen_dir/$preprocess_folder/{{source_file_part}}" ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "download_shelf_api_proxy.ts",
    "download_shelf.ts",
  ]
}

preprocess_if_expr("preprocess_web_components") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "app.ts",
    "download_item.ts",
    "download_list.ts",
  ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/download_shelf_resources.h",
    "grit/download_shelf_resources_map.cc",
    "grit/download_shelf_resources_map.h",
    "download_shelf_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

html_to_js("web_components") {
  js_files = [
    "app.ts",
    "download_item.ts",
    "download_list.ts",
  ]
}

ts_library("build_ts") {
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "app.ts",
    "download_item.ts",
    "download_list.ts",
    "download_shelf.mojom-webui.js",
    "download_shelf.ts",
    "download_shelf_api_proxy.ts",
  ]
  deps = [ "//ui/webui/resources:library" ]
  definitions = [
    "//tools/typescript/definitions/chrome_downloads.d.ts",
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
  ]
  extra_deps = [
    ":copy_mojo",
    ":preprocess",
    ":preprocess_web_components",
  ]
}
