# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_web_components_manifest = "preprocessed_gen_manifest.json"
preprocess_mojo_manifest = "preprocessed_mojo_manifest.json"

generate_grd("build_grd") {
  grd_prefix = "download_shelf"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [ "download_shelf.html" ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [
    ":preprocess",
    ":preprocess_mojo",
    ":preprocess_web_components",
  ]
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/$preprocess_mojo_manifest",
    "$target_gen_dir/$preprocess_web_components_manifest",
  ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [ "download_shelf_api_proxy.js" ]
}

preprocess_if_expr("preprocess_mojo") {
  deps = [ "//chrome/browser/ui/webui/download_shelf:mojo_bindings_webui_js" ]
  in_folder =
      "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/download_shelf/"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_mojo_manifest"
  in_files = [ "download_shelf.mojom-webui.js" ]
}

preprocess_if_expr("preprocess_web_components") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_web_components_manifest"
  in_files = [
    "app.js",
    "download_item.js",
    "download_list.js",
  ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/download_shelf_resources.h",
    "grit/download_shelf_resources_map.cc",
    "grit/download_shelf_resources_map.h",
    "download_shelf_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

js_type_check("closure_compile") {
  closure_flags = default_closure_args + mojom_js_args + [
                    "js_module_root=" + rebase_path(".", root_build_dir),
                    "js_module_root=" + rebase_path(
                            "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/download_shelf",
                            root_build_dir),
                  ]
  deps = [
    ":app",
    ":download_item",
    ":download_list",
    ":download_shelf_api_proxy",
  ]
}

js_library("download_shelf_api_proxy") {
  deps = [
    "//chrome/browser/ui/webui/download_shelf:mojo_bindings_webui_js",
    "//ui/webui/resources/js:cr.m",
  ]
  externs_list = [ "$externs_path/chrome_extensions.js" ]
}

js_library("download_item") {
  deps = [
    ":download_shelf_api_proxy",
    "//ui/webui/resources/js:custom_element",
  ]
  externs_list = [ "$externs_path/chrome_extensions.js" ]
}

js_library("download_list") {
  deps = [
    ":download_item",
    ":download_shelf_api_proxy",
    "//ui/webui/resources/js:custom_element",
  ]
}

js_library("app") {
  deps = [
    ":download_item",
    ":download_list",
    "//ui/webui/resources/js:custom_element",
  ]
}

html_to_js("web_components") {
  js_files = [
    "app.js",
    "download_item.js",
    "download_list.js",
  ]
}
