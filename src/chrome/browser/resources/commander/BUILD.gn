# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder = "preprocessed"

generate_grd("build_grd") {
  grd_prefix = "commander"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [ "commander.html" ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [ ":build" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "browser_proxy.ts",
    "types.ts",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = [
    "app.ts",
    "icons.ts",
    "option.ts",
  ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/commander_resources.grd"
  deps = [ ":build_grd" ]
  outputs = [
    "grit/commander_resources.h",
    "grit/commander_resources_map.cc",
    "grit/commander_resources_map.h",
    "commander_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

html_to_js("web_components") {
  js_files = [
    "app.ts",
    "option.ts",
    "icons.ts",
  ]
}

ts_library("build") {
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "app.ts",
    "browser_proxy.ts",
    "icons.ts",
    "option.ts",
    "types.ts",
  ]
  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
  ]
  extra_deps = [
    ":preprocess",
    ":preprocess_generated",
  ]
}
