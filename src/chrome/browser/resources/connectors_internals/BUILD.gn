# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/grit/grit_rule.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

resources_grd_file = "$target_gen_dir/resources.grd"

web_component_files = [
  "app.ts",
  "connectors_tabs.ts",
  "zero_trust_connector.ts",
]

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".ts", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".ts" ]
}

html_to_wrapper("html_wrapper_files") {
  in_files = html_files
  template = "native"
}

copy("copy_mojo") {
  deps = [
    "//chrome/browser/ui/webui/connectors_internals:mojo_bindings_webui_js",
  ]
  mojom_folder =
      "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/connectors_internals/"
  sources = [ "$mojom_folder/connectors_internals.mojom-webui.js" ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

copy("copy_src") {
  sources = web_component_files
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

ts_library("build_ts") {
  root_dir = target_gen_dir
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = web_component_files + html_wrapper_files +
             [ "connectors_internals.mojom-webui.js" ]
  deps = [ "//ui/webui/resources:library" ]
  extra_deps = [
    ":copy_mojo",
    ":copy_src",
    ":html_wrapper_files",
  ]
}

generate_grd("build_grd") {
  deps = [ ":build_ts" ]
  input_files = [ "index.html" ]
  input_files_base_dir = rebase_path(".", "//")
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]

  grd_prefix = "connectors_internals"
  out_grd = resources_grd_file
}

grit("resources") {
  enable_input_discovery_for_gn_analyze = false
  source = resources_grd_file
  deps = [ ":build_grd" ]

  outputs = [
    "grit/connectors_internals_resources.h",
    "grit/connectors_internals_resources_map.cc",
    "grit/connectors_internals_resources_map.h",
    "connectors_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}
