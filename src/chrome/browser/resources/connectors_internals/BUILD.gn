# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder = "preprocessed"
resources_grd_file = "$target_gen_dir/resources.grd"

ts_files = [
  "app.ts",
  "connectors_tabs.ts",
  "zero_trust_connector.ts",
]

html_to_js("web_components") {
  js_files = ts_files
}

copy("copy_mojo") {
  deps = [
    "//chrome/browser/ui/webui/connectors_internals:mojo_bindings_webui_js",
  ]
  mojom_folder =
      "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/connectors_internals/"
  sources = [ "$mojom_folder/connectors_internals.mojom-webui.js" ]
  outputs = [ "$target_gen_dir/$preprocess_folder/{{source_file_part}}" ]
}

preprocess_if_expr("preprocess_web_components") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = ts_files
}

ts_library("build_ts") {
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = ts_files + [ "connectors_internals.mojom-webui.js" ]
  deps = [ "//ui/webui/resources:library" ]
  extra_deps = [
    ":copy_mojo",
    ":preprocess_web_components",
  ]
}

generate_grd("build_grd") {
  deps = [ ":build_ts" ]
  input_files = [ "index.html" ]
  input_files_base_dir = rebase_path(".", "//")
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]

  grd_prefix = "connectors_internals"
  out_grd = resources_grd_file
}

grit("resources") {
  enable_input_discovery_for_gn_analyze = false
  source = resources_grd_file
  deps = [ ":build_grd" ]

  outputs = [
    "grit/connectors_internals_resources.h",
    "grit/connectors_internals_resources_map.cc",
    "grit/connectors_internals_resources_map.h",
    "connectors_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}
