# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//extensions/buildflags/buildflags.gni")
import("//third_party/protobuf/proto_library.gni")

proto_library("discovery_resources_proto") {
  sources = [ "discovery_resources.proto" ]
  cc_generator_options = "lite=true:"
}

static_library("access_code_cast_feature") {
  sources = [
    "access_code_cast_feature.cc",
    "access_code_cast_feature.h",
  ]
  public_deps = [ "//base" ]
  deps = [
    "//components/pref_registry:pref_registry",
    "//components/prefs:prefs",
    "//components/user_prefs:user_prefs",
  ]
}

if (!is_android) {
  static_library("access_code_sink_service") {
    sources = [
      "access_code_cast_sink_service.cc",
      "access_code_cast_sink_service.h",
    ]
    public_deps = [
      "//base",
      "//chrome/browser/profiles:profile",
      "//components/media_router/browser:browser",
      "//components/media_router/common",
      "//components/media_router/common/mojom:logger",
      "//components/media_router/common/mojom:media_router",
    ]
    deps = [
      ":access_code_cast_feature",
      "//chrome/browser/media/router:router",
      "//chrome/browser/media/router/discovery:discovery",
    ]
  }

  static_library("access_code_sink_service_factory") {
    sources = [
      "access_code_cast_sink_service_factory.cc",
      "access_code_cast_sink_service_factory.h",
    ]
    public_deps = [
      "//base",
      "//chrome/browser/profiles:profile",
      "//components/keyed_service/content:content",
      "//components/media_router/browser",
      "//components/media_router/common",
    ]
    deps = [
      ":access_code_cast_feature",
      ":access_code_sink_service",
      "//chrome/browser/media/router:router",
    ]
  }
}
