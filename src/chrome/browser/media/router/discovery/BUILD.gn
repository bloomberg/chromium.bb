# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(!is_android)

static_library("discovery") {
  inputs = [ "$root_gen_dir/chrome/grit/generated_resources.h" ]
  deps = [
    "//base",
    "//base:i18n",
    "//chrome/app:generated_resources",
    "//chrome/common:constants",
    "//chrome/common/media_router:router",
    "//components/cast_channel",
    "//components/version_info",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/cpp",
  ]
  sources = [
    "dial/device_description_fetcher.cc",
    "dial/device_description_fetcher.h",
    "dial/device_description_service.cc",
    "dial/device_description_service.h",
    "dial/dial_app_discovery_service.cc",
    "dial/dial_app_discovery_service.h",
    "dial/dial_device_data.cc",
    "dial/dial_device_data.h",
    "dial/dial_media_sink_service.cc",
    "dial/dial_media_sink_service.h",
    "dial/dial_media_sink_service_impl.cc",
    "dial/dial_media_sink_service_impl.h",
    "dial/dial_registry.cc",
    "dial/dial_registry.h",
    "dial/dial_service.cc",
    "dial/dial_service.h",
    "dial/dial_url_fetcher.cc",
    "dial/dial_url_fetcher.h",
    "dial/parsed_dial_app_info.cc",
    "dial/parsed_dial_app_info.h",
    "dial/parsed_dial_device_description.cc",
    "dial/parsed_dial_device_description.h",
    "dial/safe_dial_app_info_parser.cc",
    "dial/safe_dial_app_info_parser.h",
    "dial/safe_dial_device_description_parser.cc",
    "dial/safe_dial_device_description_parser.h",
    "discovery_network_info.cc",
    "discovery_network_info.h",
    "discovery_network_list.h",
    "discovery_network_list_wifi.h",
    "discovery_network_monitor.cc",
    "discovery_network_monitor.h",
    "discovery_network_monitor_metric_observer.cc",
    "discovery_network_monitor_metric_observer.h",
    "discovery_network_monitor_metrics.cc",
    "discovery_network_monitor_metrics.h",
    "mdns/cast_media_sink_service.cc",
    "mdns/cast_media_sink_service.h",
    "mdns/cast_media_sink_service_impl.cc",
    "mdns/cast_media_sink_service_impl.h",
    "mdns/dns_sd_delegate.cc",
    "mdns/dns_sd_delegate.h",
    "mdns/dns_sd_device_lister.cc",
    "mdns/dns_sd_device_lister.h",
    "mdns/dns_sd_registry.cc",
    "mdns/dns_sd_registry.h",
    "mdns/media_sink_util.cc",
    "mdns/media_sink_util.h",
    "media_sink_discovery_metrics.cc",
    "media_sink_discovery_metrics.h",
  ]

  if (is_linux) {
    sources += [ "discovery_network_list_wifi_linux.cc" ]
  }

  if (is_posix) {
    sources += [ "discovery_network_list_posix.cc" ]
  }

  if (is_mac) {
    sources += [ "discovery_network_list_wifi_mac.mm" ]
    libs = [ "CoreWLAN.framework" ]
  }

  if (is_win) {
    sources += [ "discovery_network_list_win.cc" ]
    libs = [ "iphlpapi.lib" ]
  }
}
