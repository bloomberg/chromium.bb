# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//build/config/locales.gni")
import("//chrome/common/features.gni")

android_library("java") {
  deps = [
    ":java_resources",
    "//base:base_java",
    "//base:jni_java",
    "//chrome/android:chrome_java",
    "//chrome/browser/flags:java",
    "//chrome/browser/profiles/android:java",
    "//chrome/browser/touch_to_fill/android:public_java",
    "//chrome/browser/ui/android/favicon:java",
    "//chrome/browser/util:java",
    "//components/embedder_support/android:util_java",
    "//components/url_formatter/android:url_formatter_java",
    "//third_party/android_deps:androidx_recyclerview_recyclerview_java",
    "//ui/android:ui_java",
  ]

  sources = [
    "java/src/org/chromium/chrome/browser/touch_to_fill/TouchToFillBridge.java",
    "java/src/org/chromium/chrome/browser/touch_to_fill/TouchToFillCoordinator.java",
    "java/src/org/chromium/chrome/browser/touch_to_fill/TouchToFillMediator.java",
    "java/src/org/chromium/chrome/browser/touch_to_fill/TouchToFillProperties.java",
    "java/src/org/chromium/chrome/browser/touch_to_fill/TouchToFillView.java",
    "java/src/org/chromium/chrome/browser/touch_to_fill/TouchToFillViewBinder.java",
    "java/src/org/chromium/chrome/browser/touch_to_fill/TouchToFillViewHolder.java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

android_resources("java_resources") {
  deps = [
    ":java_strings_grd",
    "//third_party/android_deps:android_support_v7_appcompat_java",
    "//ui/android:ui_java_resources",
  ]
  sources = [
    "java/res/drawable-night/touch_to_fill_header_image.xml",
    "java/res/drawable/touch_to_fill_credential_background.xml",
    "java/res/drawable/touch_to_fill_header_image.xml",
    "java/res/layout/touch_to_fill_credential_item.xml",
    "java/res/layout/touch_to_fill_fill_button.xml",
    "java/res/layout/touch_to_fill_footer.xml",
    "java/res/layout/touch_to_fill_header_item.xml",
    "java/res/layout/touch_to_fill_sheet.xml",
    "java/res/values/dimens.xml",
  ]
  custom_package = "org.chromium.chrome.browser.touch_to_fill"
}

java_strings_grd("java_strings_grd") {
  defines = chrome_grit_defines
  grd_file = "java/strings/android_touch_to_fill_strings.grd"
  outputs =
      [ "values/android_touch_to_fill_strings.xml" ] +
      process_file_template(
          android_bundle_locales_as_resources,
          [ "values-{{source_name_part}}/android_touch_to_fill_strings.xml" ])
}
