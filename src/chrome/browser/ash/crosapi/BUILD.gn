# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")

assert(is_chromeos_ash)
assert(use_ozone)

source_set("browser_util") {
  sources = [
    "browser_util.cc",
    "browser_util.h",
  ]

  deps = [
    "//ash/constants",
    "//base",
    "//chrome/browser:browser_process",
    "//chrome/common",
    "//chromeos/crosapi/cpp",
    "//chromeos/crosapi/mojom",
    "//components/exo",
    "//components/prefs",
    "//components/user_manager",
    "//components/version_info",
    "//google_apis",
  ]
}

source_set("crosapi") {
  sources = [
    "arc_ash.cc",
    "arc_ash.h",
    "authentication_ash.cc",
    "authentication_ash.h",
    "automation_ash.cc",
    "automation_ash.h",
    "browser_data_migrator.cc",
    "browser_data_migrator.h",
    "browser_data_migrator_util.cc",
    "browser_data_migrator_util.h",
    "browser_loader.cc",
    "browser_loader.h",
    "browser_manager.cc",
    "browser_manager.h",
    "browser_manager_observer.h",
    "browser_service_host_ash.cc",
    "browser_service_host_ash.h",
    "browser_service_host_observer.h",
    "browser_version_service_ash.cc",
    "browser_version_service_ash.h",
    "cert_database_ash.cc",
    "cert_database_ash.h",
    "chrome_app_window_tracker_ash.cc",
    "chrome_app_window_tracker_ash.h",
    "clipboard_ash.cc",
    "clipboard_ash.h",
    "clipboard_history_ash.cc",
    "clipboard_history_ash.h",
    "content_protection_ash.cc",
    "content_protection_ash.h",
    "copy_migrator.cc",
    "copy_migrator.h",
    "crosapi_ash.cc",
    "crosapi_ash.h",
    "crosapi_id.h",
    "crosapi_manager.cc",
    "crosapi_manager.h",
    "crosapi_util.cc",
    "crosapi_util.h",
    "desk_template_ash.cc",
    "desk_template_ash.h",
    "device_attributes_ash.cc",
    "device_attributes_ash.h",
    "device_settings_ash.cc",
    "device_settings_ash.h",
    "dlp_ash.cc",
    "dlp_ash.h",
    "download_controller_ash.cc",
    "download_controller_ash.h",
    "drive_integration_service_ash.cc",
    "drive_integration_service_ash.h",
    "environment_provider.cc",
    "environment_provider.h",
    "feedback_ash.cc",
    "feedback_ash.h",
    "field_trial_service_ash.cc",
    "field_trial_service_ash.h",
    "file_manager_ash.cc",
    "file_manager_ash.h",
    "force_installed_tracker_ash.cc",
    "force_installed_tracker_ash.h",
    "geolocation_service_ash.cc",
    "geolocation_service_ash.h",
    "identity_manager_ash.cc",
    "identity_manager_ash.h",
    "idle_service_ash.cc",
    "idle_service_ash.h",
    "image_writer_ash.cc",
    "image_writer_ash.h",
    "keystore_service_ash.cc",
    "keystore_service_ash.h",
    "keystore_service_factory_ash.cc",
    "keystore_service_factory_ash.h",
    "kiosk_session_service_ash.cc",
    "kiosk_session_service_ash.h",
    "lacros_availability_policy_observer.cc",
    "lacros_availability_policy_observer.h",
    "local_printer_ash.cc",
    "local_printer_ash.h",
    "login_ash.cc",
    "login_ash.h",
    "login_screen_storage_ash.cc",
    "login_screen_storage_ash.h",
    "login_state_ash.cc",
    "login_state_ash.h",
    "message_center_ash.cc",
    "message_center_ash.h",
    "metrics_reporting_ash.cc",
    "metrics_reporting_ash.h",
    "migration_progress_tracker.cc",
    "migration_progress_tracker.h",
    "move_migrator.cc",
    "move_migrator.h",
    "native_theme_service_ash.cc",
    "native_theme_service_ash.h",
    "network_settings_service_ash.cc",
    "network_settings_service_ash.h",
    "network_settings_translation.h",
    "networking_attributes_ash.cc",
    "networking_attributes_ash.h",
    "policy_service_ash.cc",
    "policy_service_ash.h",
    "power_ash.cc",
    "power_ash.h",
    "prefs_ash.cc",
    "prefs_ash.h",
    "remoting_ash.cc",
    "remoting_ash.h",
    "resource_manager_ash.cc",
    "resource_manager_ash.h",
    "screen_manager_ash.cc",
    "screen_manager_ash.h",
    "search_provider_ash.cc",
    "search_provider_ash.h",
    "select_file_ash.cc",
    "select_file_ash.h",
    "sharesheet_ash.cc",
    "sharesheet_ash.h",
    "structured_metrics_service_ash.cc",
    "structured_metrics_service_ash.h",
    "system_display_ash.cc",
    "system_display_ash.h",
    "task_manager_ash.cc",
    "task_manager_ash.h",
    "test_mojo_connection_manager.cc",
    "test_mojo_connection_manager.h",
    "time_zone_service_ash.cc",
    "time_zone_service_ash.h",
    "translate_crosapi_to_proxy_config.cc",
    "translate_proxy_config_to_crosapi.cc",
    "url_handler_ash.cc",
    "url_handler_ash.h",
    "video_capture_device_ash.cc",
    "video_capture_device_ash.h",
    "video_capture_device_factory_ash.cc",
    "video_capture_device_factory_ash.h",
    "video_frame_handler_ash.cc",
    "video_frame_handler_ash.h",
    "web_app_service_ash.cc",
    "web_app_service_ash.h",
    "web_page_info_ash.cc",
    "web_page_info_ash.h",
    "window_util.cc",
    "window_util.h",
  ]

  deps = [
    "//ash",
    "//ash/components/account_manager",
    "//ash/components/arc/mojom",
    "//ash/components/arc/session",
    "//ash/components/cryptohome",
    "//ash/components/login/auth",
    "//ash/components/settings:settings",
    "//ash/constants",
    "//base",
    "//chrome/browser:browser_process",
    "//chrome/browser/chromeos:print_job_info_proto",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/webui/settings/chromeos/constants:mojom",
    "//chrome/browser/web_applications",
    "//chrome/common",
    "//chrome/common:channel_info",
    "//chrome/common:constants",
    "//chromeos/components/cdm_factory_daemon:cdm_factory_daemon_browser",
    "//chromeos/components/sensors",
    "//chromeos/crosapi/cpp",
    "//chromeos/crosapi/mojom",
    "//chromeos/dbus",
    "//chromeos/dbus/cros_disks",
    "//chromeos/dbus/power",
    "//chromeos/dbus/resourced",
    "//chromeos/dbus/session_manager",
    "//chromeos/dbus/update_engine",
    "//chromeos/dbus/upstart",
    "//chromeos/dbus/userdataauth:userdataauth_proto",
    "//chromeos/login/login_state",
    "//chromeos/network",
    "//chromeos/printing",
    "//chromeos/services/machine_learning/public/cpp",
    "//chromeos/startup:constants",
    "//chromeos/system",
    "//chromeos/tpm",
    "//components/arc",
    "//components/crash/core/app",
    "//components/exo",
    "//components/flags_ui",
    "//components/keyed_service/content",
    "//components/metrics",
    "//components/metrics/structured",
    "//components/policy/core/common:common_constants",
    "//components/prefs",
    "//components/printing/browser",
    "//components/session_manager/core",
    "//components/version_info:channel",
    "//content/public/common",
    "//extensions/browser/api",
    "//extensions/browser/api/automation_internal",
    "//extensions/common",
    "//media/mojo/services:services",
    "//printing",
    "//printing/backend",
    "//remoting/host/chromeos:remoting_service",
    "//services/device/wake_lock/power_save_blocker",
    "//services/video_capture/public/cpp:cpp",
    "//services/video_capture/public/mojom",
    "//ui/message_center",
    "//ui/message_center/public/cpp",
    "//ui/shell_dialogs",
    "//ui/snapshot",
    "//ui/views",
  ]

  # local_printer_ash.cc includes
  # chrome/browser/ash/printing/print_management/printing_manager.h, which in
  # turn requires //ash/webui/print_management/mojom to generate the Mojo
  # headers referenced by printing_manager.h.
  # Ideally, //chrome/browser/chromeos can just make
  # //ash/webui/print_management/mojom a public_deps entry, and this target can
  # depend on //chrome/browser/chromeos. However, that creates a circular
  # dependency, so add this indirect dependency to make it work as intended.
  deps += [ "//ash/webui/print_management/mojom" ]

  public_deps = [ ":browser_util" ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "fake_browser_manager.cc",
    "fake_browser_manager.h",
    "test_controller_ash.cc",
    "test_controller_ash.h",
  ]

  deps = [
    ":crosapi",
    "//ash",
    "//chrome/test:test_support",
    "//chromeos/crosapi/mojom",
    "//testing/gmock",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "browser_data_migrator_unittest.cc",
    "browser_data_migrator_util_unittest.cc",
    "browser_loader_unittest.cc",
    "browser_manager_unittest.cc",
    "browser_util_unittest.cc",
    "browser_version_service_ash_unittest.cc",
    "chrome_app_window_tracker_ash_unittest.cc",
    "copy_migrator_unittest.cc",
    "crosapi_util_unittest.cc",
    "dlp_ash_unittest.cc",
    "download_controller_ash_unittest.cc",
    "fake_migration_progress_tracker.h",
    "field_trial_service_ash_unittest.cc",
    "geolocation_service_ash_unittest.cc",
    "keystore_service_ash_unittest.cc",
    "lacros_availability_policy_observer_unittest.cc",
    "local_printer_ash_unittest.cc",
    "login_screen_storage_ash_unittest.cc",
    "login_state_ash_unittest.cc",
    "message_center_ash_unittest.cc",
    "metrics_reporting_ash_unittest.cc",
    "move_migrator_unittest.cc",
    "network_settings_translation_unittest.cc",
    "networking_attributes_ash_unittest.cc",
    "prefs_ash_unittest.cc",
    "test_local_printer_ash.cc",
    "test_local_printer_ash.h",
    "test_mojo_connection_manager_unittest.cc",
  ]

  deps = [
    ":crosapi",
    ":test_support",
    "//ash",
    "//ash/components/cryptohome",
    "//base",
    "//base/test:test_config",
    "//base/test:test_support",
    "//chrome/browser/chromeos:test_support",
    "//chrome/common/printing",
    "//chrome/test:test_support",
    "//chromeos/crosapi/cpp:cpp",
    "//chromeos/dbus/shill",
    "//chromeos/dbus/upstart",
    "//chromeos/login/login_state",
    "//chromeos/printing",
    "//chromeos/startup:constants",
    "//components/component_updater:test_support",
    "//components/session_manager/core",
    "//printing/backend",
    "//printing/backend:test_support",
    "//testing/gtest:gtest",
  ]
}
