# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/grit/grit_rule.gni")

grit("supervised_user_unscaled_resources") {
  source = "supervised_user_unscaled_resources.grd"
  outputs = [
    "grit/supervised_user_unscaled_resources.h",
    "supervised_user_unscaled_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome/browser/supervised_user"
}

static_library("test_support") {
  testonly = true
  sources = [
    "supervised_user_test_util.cc",
    "supervised_user_test_util.h",
  ]
  deps = [
    "//chrome/browser",
    "//chrome/browser/profiles:profile",
    "//chrome/common:constants",
    "//components/prefs",
  ]
}

if (is_android) {
  android_library("website_parent_approval_java") {
    deps = [
      "//base:base_java",
      "//base:jni_java",
      "//build/android:build_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//ui/android:ui_no_recycler_view_java",
      "//url:gurl_java",
    ]
    annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
    sources = [
      "android/java/src/org/chromium/chrome/browser/supervised_user/WebsiteParentApproval.java",
      "android/java/src/org/chromium/chrome/browser/supervised_user/WebsiteParentApprovalDelegate.java",
      "android/java/src/org/chromium/chrome/browser/supervised_user/WebsiteParentApprovalDelegateImpl.java",
    ]

    # Add the actual implementation where necessary so that downstream targets
    # can provide their own implementations.
    jar_excluded_patterns = [ "*/WebsiteParentApprovalDelegateImpl.class" ]
  }

  generate_jni("jni_headers") {
    sources = [ "android/java/src/org/chromium/chrome/browser/supervised_user/WebsiteParentApproval.java" ]
  }
}
