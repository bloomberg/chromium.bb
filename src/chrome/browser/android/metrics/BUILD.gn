# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")

  _jni_sources = [ "javatests/src/org/chromium/chrome/browser/metrics/util/UkmUtilsForTest.java" ]

  static_library("ukm_utils_for_test") {
    testonly = true
    sources = [ "ukm_utils_for_test.cc" ]
    deps = [
      "//chrome/browser",
      "//chrome/browser/android/metrics:jni_headers",
      "//components/metrics_services_manager:metrics_services_manager",
      "//components/ukm",
    ]
  }

  generate_jni("jni_headers") {
    testonly = true
    sources = _jni_sources
  }

  android_library("ukm_java_test_support") {
    testonly = true
    sources = _jni_sources
    deps = [ "//base:base_java" ]
  }

  android_library("ukm_javatests") {
    testonly = true
    sources =
        [ "javatests/src/org/chromium/chrome/browser/metrics/UkmTest.java" ]

    deps = [
      ":ukm_java_test_support",
      "//base:base_java_test_support",
      "//chrome/android:chrome_java",
      "//chrome/browser/tab:java",
      "//chrome/test/android:chrome_java_test_support",
      "//components/metrics:metrics_java",
      "//content/public/test/android:content_java_test_support",
      "//third_party/android_support_test_runner:rules_java",
      "//third_party/android_support_test_runner:runner_java",
      "//third_party/junit",
    ]
  }
}
