# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//build/config/allocator.gni")
import("//build/config/clang/clang.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/dcheck_always_on.gni")
import("//build/nocompile.gni")
import("//build/toolchain/win/midl.gni")
import("//chrome/process_version_rc_template.gni")
import("//tools/grit/grit_rule.gni")
import("//testing/libfuzzer/fuzzer_test.gni")
import("//testing/test.gni")

# This static library is shared with the setup program.

source_set("common") {
  sources = [
    "gcp_strings.cc",
    "gcp_strings.h",
    "gcp_utils.cc",
    "gcp_utils.h",
    "logging.cc",
    "logging.h",
  ]
  public_configs = [ ":common_config" ]
  deps = [
    "//base:base",
    "//components/version_info",
    "//google_apis:google_apis",
  ]
}

config("common_config") {
  defines = [
    # Needed in order to include the win32 header security.h.
    "SECURITY_WIN32",
  ]
  libs = [
    "secur32.lib",  # For LsaXXX functions
  ]
}

# This static library is shared with the test code.

source_set("gaiacp_lib") {
  sources = [
    "gaia_credential.cc",
    "gaia_credential.h",
    "gaia_credential_base.cc",
    "gaia_credential_base.h",
    "gaia_credential_provider.cc",
    "gaia_credential_provider.h",
    "gaia_credential_provider_module.cc",
    "gaia_credential_provider_module.h",
    "initguid.cc",
    "os_process_manager.cc",
    "os_process_manager.h",
    "os_user_manager.cc",
    "os_user_manager.h",
    "reauth_credential.cc",
    "reauth_credential.h",
    "reg_utils.cc",
    "reg_utils.h",
    "scoped_handle.h",
    "scoped_lsa_policy.cc",
    "scoped_lsa_policy.h",
    "scoped_user_profile.cc",
    "scoped_user_profile.h",
    "stdafx.h",
    "win_http_url_fetcher.cc",
    "win_http_url_fetcher.h",
  ]
  public_configs = [ ":gaiacp_config" ]
  public_deps = [ ":common" ]
  deps = [
    ":gaia_credential_provider_idl",
    ":resources",
    "//chrome/common:version_header",
  ]
}

config("gaiacp_config") {
  defines = [
    # Needed to build COM objects that will be hosted in a DLL.
    "_WINDLL",
  ]
  cflags = [
    # C4245: 'conversion' : conversion from 'type1' to 'type2',
    #                       signed/unsigned mismatch
    # Disabling because this Level 4 warning is causing ATL code to not compile,
    # warnings are errors:
    #     c:\src\depot_tools\win_toolchain\vs_files\
    #         d3cb0e37bdd120ad0ac4650b674b09e81be45616\vc\atlmfc\include\
    #         atlexcept.h(102): warning C4245: '=':
    #         conversion from 'NTSTATUS' to 'DWORD', signed/unsigned mismatch
    "/wd4245",
  ]
  libs = [
    "advapi32.lib",  # For LookupAccountSid()
    "credui.lib",  # For CredPackAuthenticationBufferW()
    "crypt32.lib",  # For CryptProtectData()
    "netapi32.lib",  # For NetUserXXX()
    "userenv.lib",  # For GetUserProfileDirectoryW()
    "winhttp.lib",  # For WinHttpXXX()
  ]
}

# Targets for the credential provider dll.

process_version_rc_template("version") {
  template_file = "gaia_credential_provider.rc.version"
  output = "$root_out_dir/gaia_credential_provider.rc"
}

midl("gaia_credential_provider_idl") {
  sources = [
    "gaia_credential_provider.idl",
  ]
  header_file = "gaia_credential_provider_i.h"
}

grit("resources") {
  source = "gaia_resources.grd"
  inputs = [
    "gaia_credential_provider.rgs",
    "google_logo_small.bmp",
  ]
  outputs = [
    "grit/gaia_resources.h",
    "grit/gaia_resources.rc",
  ]
  resource_ids = ""
  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]
  deps = [
    ":gaia_credential_provider_idl",
  ]
}

shared_library("gaia1_0") {
  sources = [
    "dllmain.cc",
    "dllmain.h",
    "gaia_credential_provider.def",
    "gaia_credential_provider.rgs",
  ]
  deps = [
    ":common",
    ":gaiacp_lib",
    ":version",
    "//chrome/common:version_header",
  ]
  configs += [
    "//build/config/win:windowed",
  ]
}
