# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_android)
assert(is_chrome_branded)

generate_grd("build_grdp") {
  grd_prefix = "media_router"
  out_grd = "$target_gen_dir/resources.grdp"

  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  resource_path_prefix = "media_router"
}

ts_library("build_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [
    "chrome://cast-feedback/*|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/media_router/cast_feedback/tsc/*",
            target_gen_dir),
    "chrome://webui-test/*|" +
        rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                    target_gen_dir),
  ]
  in_files = [ "cast_feedback_ui_test.ts" ]

  definitions = [ "//tools/typescript/definitions/feedback_private.d.ts" ]

  deps = [ "//chrome/browser/resources/media_router/cast_feedback:build_ts" ]
  extra_deps = [ "..:generate_definitions" ]
}
