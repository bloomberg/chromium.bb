# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_win || is_mac || is_linux || is_fuchsia)

generate_grd("build_grdp") {
  grd_prefix = "webui_app_settings"
  out_grd = "$target_gen_dir/resources.grdp"

  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  resource_path_prefix = "app_settings"
}

ts_library("build_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [
    "chrome://app-settings/*|" +
        rebase_path("$root_gen_dir/chrome/browser/resources/app_settings/tsc/*",
                    target_gen_dir),
    "chrome://webui-test/*|" +
        rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                    target_gen_dir),
  ]
  in_files = [
    "app_test.ts",
    "test_app_management_browser_proxy.ts",
  ]
  definitions = [ "//tools/typescript/definitions/metrics_private.d.ts" ]
  deps = [ "//chrome/browser/resources/app_settings:build_ts" ]
  extra_deps = [ "..:generate_definitions" ]
}
