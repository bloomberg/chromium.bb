# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//crypto/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

js_type_check("closure_compile") {
  is_polymer3 = true
  closure_flags = default_closure_args + mojom_js_args + [
                    "browser_resolver_prefix_replacements=\"chrome://settings/=../../chrome/browser/resources/settings/\"",
                    "browser_resolver_prefix_replacements=\"chrome://new-tab-page/=../../chrome/browser/resources/new_tab_page//\"",
                    "js_module_root=./gen/ui/webui/resources/preprocessed/",
                    "js_module_root=../../chrome/test/data/webui/",
                    "js_module_root=./gen/chrome/test/data/webui/",
                  ]

  deps = [ ":managed_dialog_test" ]
}

js_library("managed_dialog_test") {
  deps = [
    "..:chai_assert",
    "//ui/webui/resources/cr_components/managed_dialog",
    "//ui/webui/resources/js:load_time_data.m",
  ]
  externs_list = [ "$externs_path/mocha-2.5.js" ]
}

generate_grd("build_grdp") {
  grd_prefix = "webui_cr_components"
  out_grd = "$target_gen_dir/resources.grdp"

  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  resource_path_prefix = "cr_components"
}

ts_library("build_ts") {
  root_dir = "./"
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [ "chrome://webui-test/*|" +
                    rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                                target_gen_dir) ]
  in_files = [
    "most_visited_focus_test.ts",
    "most_visited_test.ts",
    "most_visited_test_support.ts",
  ]
  deps = [ "//ui/webui/resources/cr_components/most_visited:build_ts" ]
  extra_deps = [ "..:generate_definitions" ]
}
