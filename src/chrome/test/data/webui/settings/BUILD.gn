# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

# Test files that contain // <if expr> and therefore require preprocessing.
preprocessed_files = [
  "about_page_tests.ts",
  "appearance_page_test.ts",
  "clear_browsing_data_test.ts",
  "cookies_page_test.ts",
  "downloads_page_test.ts",
  "password_check_test.ts",
  "password_edit_dialog_test.ts",
  "password_view_test.ts",
  "people_page_test.ts",
  "people_page_sync_page_test.ts",
  "personalization_options_test.ts",
  "privacy_page_test.ts",
  "route_tests.ts",
  "security_page_test.ts",
  "settings_menu_test.ts",
  "settings_toggle_button_tests.ts",
  "site_list_entry_tests.ts",
  "sync_account_control_test.ts",
  "test_about_page_browser_proxy.ts",
  "test_lifetime_browser_proxy.ts",
  "test_sync_browser_proxy.ts",
]

if (!is_chromeos_ash) {
  preprocessed_files += [
    "test_languages_browser_proxy.ts",
    "languages_page_tests.ts",
    "languages_page_metrics_test_browser.ts",
  ]
}

# Test files that do not require preprocessing. If adding // <if expr> to any
# file below, move it to the list above.
non_preprocessed_files = [
  "a11y/about_a11y_test.ts",
  "a11y/accessibility_a11y_test.ts",
  "a11y/basic_a11y_test.ts",
  "a11y/manage_profile_a11y_test.ts",
  "a11y/passwords_a11y_test.ts",
  "a11y/sign_out_a11y_test.ts",
  "advanced_page_test.ts",
  "all_sites_tests.ts",
  "appearance_fonts_page_test.ts",
  "autofill_page_test.ts",
  "autofill_section_test.ts",
  "avatar_icon_test.ts",
  "basic_page_test.ts",
  "category_default_setting_tests.ts",
  "category_setting_exceptions_tests.ts",
  "checkbox_tests.ts",
  "chooser_exception_list_entry_tests.ts",
  "chooser_exception_list_tests.ts",
  "collapse_radio_button_tests.ts",
  "controlled_button_tests.ts",
  "controlled_radio_button_tests.ts",
  "do_not_track_toggle_test.ts",
  "dropdown_menu_tests.ts",
  "extension_controlled_indicator_tests.ts",
  "fake_settings_private.ts",
  "help_page_test.ts",
  "idle_load_tests.ts",
  "multi_store_exception_entry_test.ts",
  "multi_store_password_ui_entry_test.ts",
  "on_startup_page_tests.ts",
  "passwords_and_autofill_fake_data.ts",
  "passwords_device_section_test.ts",
  "passwords_export_test.ts",
  "passwords_section_test.ts",
  "payments_section_interactive_test.ts",
  "payments_section_test.ts",
  "people_page_sync_controls_test.ts",
  "people_page_sync_page_interactive_test.ts",
  "prefs_test_cases.ts",
  "prefs_tests.ts",
  "pref_util_tests.ts",
  "privacy_guide_page_test.ts",
  "privacy_sandbox_test.ts",
  "protocol_handlers_tests.ts",
  "recent_site_permissions_test.ts",
  "reset_page_test.ts",
  "reset_profile_banner_test.ts",
  "safety_check_page_test.ts",
  "search_engines_page_test.ts",
  "search_page_test.ts",
  "search_settings_test.ts",
  "secure_dns_interactive_test.ts",
  "secure_dns_test.ts",
  "security_keys_subpage_test.ts",
  "security_keys_phones_subpage_test.ts",
  "settings_animated_pages_test.ts",
  "settings_category_default_radio_group_tests.ts",
  "settings_main_test.ts",
  "settings_menu_interactive_ui_test.ts",
  "settings_page_test_util.ts",
  "settings_slider_tests.ts",
  "settings_subpage_test.ts",
  "settings_textarea_tests.ts",
  "settings_ui_tests.ts",
  "site_data_details_subpage_tests.ts",
  "site_data_test.ts",
  "site_details_permission_tests.ts",
  "site_details_tests.ts",
  "site_entry_tests.ts",
  "site_favicon_test.ts",
  "site_list_tests.ts",
  "site_settings_page_test.ts",
  "startup_urls_page_test.ts",
  "sync_test_util.ts",
  "test_clear_browsing_data_browser_proxy.ts",
  "test_extension_control_browser_proxy.ts",
  "test_hats_browser_proxy.ts",
  "test_local_data_browser_proxy.ts",
  "test_metrics_browser_proxy.ts",
  "test_open_window_proxy.ts",
  "test_password_manager_proxy.ts",
  "test_privacy_page_browser_proxy.ts",
  "test_profile_info_browser_proxy.ts",
  "test_reset_browser_proxy.ts",
  "test_search_engines_browser_proxy.ts",
  "test_site_settings_prefs_browser_proxy.ts",
  "test_util.ts",
  "zoom_levels_tests.ts",
]

if (!is_chromeos) {
  non_preprocessed_files += [
    "default_browser_test.ts",
    "import_data_dialog_test.ts",
    "system_page_tests.ts",
  ]
} else {
  non_preprocessed_files += [ "passwords_section_test_cros.ts" ]
}

if (!is_mac && !is_chromeos_ash) {
  non_preprocessed_files += [ "edit_dictionary_page_test.ts" ]
}

if (is_chromeos_ash) {
  non_preprocessed_files += [
    "people_page_test_cros.ts",
    "site_list_tests_cros.ts",
    "test_android_info_browser_proxy.ts",
  ]
} else {
  non_preprocessed_files += [
    "fake_language_settings_private.ts",
    "languages_subpage_details_tests.ts",
    "languages_subpage_tests.ts",
    "languages_tests.ts",
    "metrics_reporting_tests.ts",
    "people_page_manage_profile_test.ts",
    "relaunch_confirmation_dialog_test.ts",
  ]
}

if (is_win && is_chrome_branded) {
  non_preprocessed_files += [
    "chrome_cleanup_page_test.ts",
    "incompatible_applications_page_test.ts",
    "safety_check_chrome_cleaner_test.ts",
    "test_chrome_cleanup_proxy.ts",
  ]
}

preprocessed_folder = "$target_gen_dir/preprocessed"

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = preprocessed_folder
  in_files = preprocessed_files
}

copy("copy") {
  sources = non_preprocessed_files
  outputs = [ "$preprocessed_folder/{{source_target_relative}}" ]
}

generate_grd("build_grdp") {
  grd_prefix = "webui_settings"
  out_grd = "$target_gen_dir/resources.grdp"
  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  resource_path_prefix = "settings"
}

ts_library("build_ts") {
  root_dir = preprocessed_folder
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [
    "chrome://settings/*|" +
        rebase_path("$root_gen_dir/chrome/browser/resources/settings/tsc/*",
                    target_gen_dir),
    "chrome://webui-test/*|" +
        rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                    target_gen_dir),
  ]
  in_files = preprocessed_files + non_preprocessed_files
  definitions = [
    "//tools/typescript/definitions/autofill_private.d.ts",
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/language_settings_private.d.ts",
    "//tools/typescript/definitions/management.d.ts",
    "//tools/typescript/definitions/passwords_private.d.ts",
    "//tools/typescript/definitions/pending.d.ts",
    "//tools/typescript/definitions/settings_private.d.ts",
  ]
  deps = [ "//chrome/browser/resources/settings:build_ts" ]
  extra_deps = [
    ":copy",
    ":preprocess",
    "..:generate_definitions",
  ]
}
