# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

# Print Preview test files that contain // <if expr> and therefore require
# preprocessing.
preprocessed_tests = [
  "destination_settings_test.js",
  "destination_store_test.js",
  "invalid_settings_browsertest.js",
  "key_event_test.js",
  "native_layer_stub.ts",
  "policy_test.js",
  "preview_generation_test.js",
  "print_button_test.js",
  "print_preview_app_test.js",
  "print_preview_sidebar_test.js",
  "restore_state_test.js",
  "user_manager_test.js",
]

# Print Preview test files that do not require preprocessing. If adding
# // <if expr to any file below, move it to the list above.
non_preprocessed_tests = [
  "advanced_dialog_test.js",
  "advanced_item_test.js",
  "button_strip_interactive_test.js",
  "button_strip_test.js",
  "cloud_print_interface_stub.ts",
  "color_settings_test.js",
  "copies_settings_test.js",
  "custom_margins_test.js",
  "destination_dialog_cros_interactive_test.js",
  "destination_dialog_cros_test.js",
  "destination_dialog_interactive_test.js",
  "destination_dialog_test.js",
  "destination_item_test.js",
  "destination_list_test.js",
  "destination_search_test.js",
  "destination_select_test.js",
  "dpi_settings_test.js",
  "duplex_settings_test.js",
  "header_test.js",
  "layout_settings_test.js",
  "link_container_test.js",
  "margins_settings_test.js",
  "media_size_settings_test.js",
  "model_settings_availability_test.js",
  "model_settings_policy_test.js",
  "model_test.js",
  "number_settings_section_interactive_test.js",
  "number_settings_section_test.js",
  "other_options_settings_test.js",
  "pages_per_sheet_settings_test.js",
  "pages_settings_test.js",

  # TODO(rbpotter): Figure out how to enable these while PDF viewer is still
  # using JS.
  # "pdf_toolbar_manager_test.js",
  # "pdf_viewer_test.js",
  # "pdf_zoom_toolbar_test.js",
  "preview_area_test.js",
  "print_preview_test_utils.js",
  "scaling_settings_interactive_test.js",
  "scaling_settings_test.js",
  "select_behavior_test.js",
  "settings_select_test.js",
  "system_dialog_browsertest.js",
  "test_plugin_proxy.ts",
]

if (is_chromeos) {
  non_preprocessed_tests += [
    "destination_dropdown_cros_test.js",
    "destination_item_test_cros.js",
    "destination_search_test_chromeos.js",
    "destination_select_test_cros.js",
    "destination_settings_test_cros.js",
    "native_layer_cros_stub.ts",
    "pin_settings_test.js",
    "print_server_store_test.js",
  ]
}

generate_grd("build_grdp") {
  grd_prefix = "webui_print_preview"
  out_grd = "$target_gen_dir/resources.grdp"
  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  resource_path_prefix = "print_preview"
}

preprocessed_folder = "$target_gen_dir/preprocessed"

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = preprocessed_folder
  in_files = preprocessed_tests
}

copy("copy") {
  sources = non_preprocessed_tests
  outputs = [ "$preprocessed_folder/{{source_target_relative}}" ]
}

# Move pdf_scripting_api.d.ts to the expected location. The autogenerated .js
# file doesn't contain the types.
copy("copy_pdf_definitions") {
  sources = [ "//chrome/browser/resources/pdf/pdf_scripting_api.d.ts" ]
  outputs = [ "$root_gen_dir/chrome/browser/resources/print_preview/tsc/pdf/{{source_file_part}}" ]
  deps = [ "//chrome/browser/resources/print_preview:build_ts" ]
}

ts_library("build_ts") {
  root_dir = preprocessed_folder
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [
    "chrome://print/*|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/print_preview/tsc/*",
            target_gen_dir),
    "chrome://webui-test/*|" +
        rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                    target_gen_dir),
  ]
  in_files = preprocessed_tests + non_preprocessed_tests
  deps = [ "//chrome/browser/resources/print_preview:build_ts" ]
  extra_deps = [
    ":copy",
    ":copy_pdf_definitions",
    ":preprocess",
    "..:generate_definitions",
  ]
}
