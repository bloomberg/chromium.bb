# Copyright 2021 Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//build/util/process_version.gni")
import("//chrome/updater/branding.gni")
import("//testing/test.gni")

source_set("policy") {
  sources = [
    "dm_policy_manager.cc",
    "dm_policy_manager.h",
    "manager.cc",
    "manager.h",
    "service.cc",
    "service.h",
  ]

  deps = [
    "//base",
    "//chrome/updater:base",
    "//chrome/updater:branding_header",
    "//chrome/updater:version_header",
    "//chrome/updater/protos:omaha_proto",
  ]
  public_deps = [ "//components/policy/proto" ]

  if (is_mac) {
    sources += [
      "mac/managed_preference_policy_manager.h",
      "mac/managed_preference_policy_manager.mm",
      "mac/managed_preference_policy_manager_impl.h",
      "mac/managed_preference_policy_manager_impl.mm",
    ]
  }
  if (is_win) {
    sources += [
      "win/group_policy_manager.cc",
      "win/group_policy_manager.h",
    ]
    deps += [ "//chrome/updater/win:constants" ]
  }
}

source_set("unittest") {
  testonly = true

  sources = [
    "dm_policy_manager_unittest.cc",
    "manager_unittest.cc",
    "service_unittest.cc",
  ]

  deps = [
    ":policy",
    "//base",
    "//base/test:test_support",
    "//chrome/updater:base",
    "//chrome/updater:updater_tests_support",
    "//chrome/updater/protos:omaha_proto",
    "//net:test_support",
    "//testing/gtest",
  ]

  if (is_mac) {
    sources += [
      "mac/managed_preference_policy_manager_impl_unittest.mm",
      "mac/managed_preference_policy_manager_unittest.cc",
    ]
  }

  if (is_win) {
    sources += [ "win/group_policy_manager_unittest.cc" ]
    deps += [ "//chrome/updater/win:constants" ]
  }
}
