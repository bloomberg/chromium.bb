# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/util/process_version.gni")
import("//chrome/process_version_rc_template.gni")
import("//chrome/updater/branding.gni")
import("//testing/test.gni")

# This target builds the updater executable, its installer, and unittests.
group("win") {
  deps = [
    ":updater",
    "//chrome/updater/win/installer:installer",
  ]
}

executable("updater") {
  sources = [
    "main.cc",
    "updater.rc",
  ]

  configs += [ "//build/config/win:windowed" ]

  deps = [
    ":install_app",
    ":lib",
    ":version_resources",
    "//build/win:default_exe_manifest",
    "//chrome/updater:lib",
    "//chrome/updater/win/ui:ui_resources",
  ]

  data_deps = [ ":uninstall.cmd" ]
}

process_version("uninstall.cmd") {
  template_file = "setup/uninstall.cmd"
  output = "$target_gen_dir/uninstall.cmd"
  process_only = true
  extra_args = [
    "-e",
    "PRODUCT_FULLNAME=\"$updater_product_full_name\"",
    "-e",
    "COMPANY_SHORTNAME=\"$updater_company_short_name\"",
  ]
}

process_version_rc_template("version_resources") {
  sources = [ "updater.ver" ]
  output = "$target_gen_dir/updater_exe.rc"
}

source_set("base") {
  sources = [
    "scoped_handle.h",
    "task_scheduler.cc",
    "task_scheduler.h",
    "user_info.cc",
    "user_info.h",
    "util.cc",
    "util.h",
  ]

  defines = [ "SECURITY_WIN32" ]

  deps = [
    ":constants",
    "//base",
    "//chrome/updater:base",
    "//chrome/updater:version_header",
  ]

  libs = [
    "secur32.lib",
    "taskschd.lib",
  ]
}

source_set("constants") {
  sources = [
    "constants.cc",
    "constants.h",
  ]

  deps = [
    "//base",
    "//chrome/updater:version_header",
  ]
}

source_set("lib") {
  sources = [
    "action_handler.cc",
    "net/net_util.cc",
    "net/net_util.h",
    "net/network.h",
    "net/network_fetcher.cc",
    "net/network_fetcher.h",
    "net/network_winhttp.cc",
    "net/network_winhttp.h",
    "net/scoped_hinternet.h",
    "setup/setup.cc",
    "setup/setup.h",
    "setup/setup_util.cc",
    "setup/setup_util.h",
    "setup/uninstall.cc",
    "setup/uninstall.h",
    "update_service_out_of_process.cc",
    "update_service_out_of_process.h",
  ]

  libs = [ "winhttp.lib" ]

  configs -= [ "//build/config/win:winver" ]
  configs += [ "//chrome/updater/server/win:winver" ]

  deps = [
    ":base",
    ":constants",
    "//base",
    "//chrome/installer/util:with_no_strings",
    "//chrome/updater:base",
    "//chrome/updater/server/win:updater_idl_idl",
    "//components/update_client",
    "//url:url",
  ]
}

source_set("install_progress_observer") {
  sources = [
    "install_progress_observer.cc",
    "install_progress_observer.h",
  ]

  deps = [ "//base" ]
}

# This build target is defined to minimize the impact of -Wno-missing-braces
# compiler switch. In the future it might be possible to isolate the
# dependency of ATL in the UI so ATL headers are not visible in the
# compilation units outside the UI itself.
# TODO(sorin): https://crbug.com/1014311
source_set("install_app") {
  sources = [
    "install_app.cc",
    "install_app.h",
  ]

  deps = [
    ":install_progress_observer",
    ":lib",
    "//base",
    "//base:i18n",
    "//chrome/updater:base",
    "//chrome/updater:lib",
    "//chrome/updater:version_header",
    "//chrome/updater/win/ui",
    "//components/prefs",
    "//components/update_client",
  ]
  allow_circular_includes_from = [ "//chrome/updater:lib" ]

  cflags_cc = [ "-Wno-missing-braces" ]
}

# Tests built into Chrome's unit_tests.exe.
source_set("updater_tests") {
  testonly = true

  sources = [
    "net/network_unittest.cc",
    "util_unittest.cc",
  ]

  deps = [
    ":base",
    ":install_app",
    ":lib",
    "//base/test:test_support",
    "//testing/gtest",
  ]

  data_deps = [
    ":updater_unittests",
    "//chrome/updater/win/installer:installer_unittest",
  ]
}

# Specific tests which must run in their own process due to COM, security, or
# test isolation requirements.
test("updater_unittests") {
  testonly = true

  sources = [
    "//chrome/updater/win/test/test_main.cc",
    "setup/setup_util_unittest.cc",
    "task_scheduler_unittest.cc",
  ]

  deps = [
    ":base",
    ":install_app",
    ":lib",
    "//base",
    "//base/test:test_support",
    "//chrome/updater:version_header",
    "//chrome/updater/win/test:test_executables",
    "//chrome/updater/win/test:test_strings",
    "//testing/gtest",
  ]

  data = [ "//chrome/updater/test/data/updater.runtime_deps" ]
}
