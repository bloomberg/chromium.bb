# Copyright 2020 Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//build/util/process_version.gni")
import("//chrome/updater/branding.gni")
import("//testing/test.gni")

source_set("device_management") {
  sources = [
    "dm_cached_policy_info.cc",
    "dm_cached_policy_info.h",
    "dm_client.cc",
    "dm_client.h",
    "dm_message.cc",
    "dm_message.h",
    "dm_policy_manager.cc",
    "dm_policy_manager.h",
    "dm_storage.cc",
    "dm_storage.h",
  ]

  deps = [
    "//base",
    "//chrome/updater:base",
    "//chrome/updater:version_header",
    "//chrome/updater/protos:omaha_proto",
    "//components/update_client",
    "//crypto",
    "//third_party/boringssl",
    "//url",
  ]
  public_deps = [
    "//components/policy/core/common:common",
    "//components/policy/proto",
  ]

  if (is_mac) {
    sources += [ "dm_storage_mac.mm" ]
    deps += [ "//chrome/updater/mac:network_fetcher_sources" ]
  }
  if (is_win) {
    sources += [ "dm_storage_win.cc" ]
    deps += [
      "//chrome/updater/win:constants",
      "//chrome/updater/win:lib",
    ]
  }
}

source_set("unittest") {
  testonly = true

  sources = [
    "dm_client_unittest.cc",
    "dm_message_unittest.cc",
    "dm_policy_builder_for_testing.cc",
    "dm_policy_builder_for_testing.h",
    "dm_policy_manager_unittest.cc",
    "dm_storage_unittest.cc",
  ]

  deps = [
    ":device_management",
    "//base",
    "//base/test:test_support",
    "//chrome/updater:base",
    "//chrome/updater:updater_tests_support",
    "//chrome/updater/protos:omaha_proto",
    "//components/update_client:update_client",
    "//net:test_support",
    "//testing/gtest",
    "//url",
  ]

  if (is_mac) {
    deps += [ "//chrome/updater/mac:network_fetcher_sources" ]
  }

  if (is_win) {
    deps += [ "//chrome/updater/win:lib" ]
  }
}
