# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

android_library("java") {
  sources = [
    "java/src/org/chromium/chrome/browser/webauth/authenticator/BLEAdvert.java",
    "java/src/org/chromium/chrome/browser/webauth/authenticator/CableAuthenticator.java",
    "java/src/org/chromium/chrome/browser/webauth/authenticator/CableAuthenticatorUI.java",
    "java/src/org/chromium/chrome/browser/webauth/authenticator/CameraView.java",
    "java/src/org/chromium/chrome/browser/webauth/authenticator/QRScanDialog.java",
    "java/src/org/chromium/chrome/browser/webauth/authenticator/USBHandler.java",
  ]

  deps = [
    ":java_resources",
    "$google_play_services_package:google_play_services_base_java",
    "$google_play_services_package:google_play_services_fido_java",
    "$google_play_services_package:google_play_services_tasks_java",
    "$google_play_services_package:google_play_services_vision_common_java",
    "$google_play_services_package:google_play_services_vision_java",
    "//base:base_java",
    "//content/public/android:content_java",
    "//third_party/android_deps:android_support_v7_appcompat_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//ui/android:ui_full_java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  resources_package = "org.chromium.chrome.browser.webauth.authenticator"
}

generate_jni("jni_headers") {
  sources = [
    "java/src/org/chromium/chrome/browser/webauth/authenticator/BLEAdvert.java",
    "java/src/org/chromium/chrome/browser/webauth/authenticator/CableAuthenticator.java",
    "java/src/org/chromium/chrome/browser/webauth/authenticator/USBHandler.java",
  ]
  split_name = "cablev2_authenticator"
}

android_resources("java_resources") {
  sources = [
    "java/res/anim/circle_loader_animation_interpolator_0.xml",
    "java/res/anim/circle_loader_animation_interpolator_1.xml",
    "java/res/anim/circle_loader_animation_interpolator_2.xml",
    "java/res/anim/circle_loader_animation_interpolator_3.xml",
    "java/res/anim/circle_loader_animation_interpolator_4.xml",
    "java/res/anim/circle_loader_animation_interpolator_5.xml",
    "java/res/anim/circle_loader_animation_interpolator_6.xml",
    "java/res/anim/circle_loader_animation_interpolator_7.xml",
    "java/res/anim/circle_loader_animation_interpolator_8.xml",
    "java/res/anim/circle_loader_circleblue_animation.xml",
    "java/res/anim/circle_loader_circlegreen_animation.xml",
    "java/res/anim/circle_loader_circlered_animation.xml",
    "java/res/anim/circle_loader_circleyellow_animation.xml",
    "java/res/anim/circle_loader_ellipse_path_2_animation.xml",
    "java/res/anim/circle_loader_ellipse_path_3_animation.xml",
    "java/res/anim/circle_loader_ellipse_path_4_animation.xml",
    "java/res/anim/circle_loader_ellipse_path_5_animation.xml",
    "java/res/drawable-night/ble.xml",
    "java/res/drawable-night/error_icon.xml",
    "java/res/drawable/ble.xml",
    "java/res/drawable/circle_loader.xml",
    "java/res/drawable/circle_loader_animation.xml",
    "java/res/drawable/error_icon.xml",
    "java/res/drawable/header.xml",
    "java/res/drawable/ic_lock_googblue_48dp.xml",
    "java/res/drawable/usb_conn_disconnect.xml",
    "java/res/layout-sw600dp/cablev2_ble_enable.xml",
    "java/res/layout-sw600dp/cablev2_error.xml",
    "java/res/layout-sw600dp/cablev2_serverlink.xml",
    "java/res/layout/cablev2_ble_enable.xml",
    "java/res/layout/cablev2_error.xml",
    "java/res/layout/cablev2_qr_dialog.xml",
    "java/res/layout/cablev2_qr_scan.xml",
    "java/res/layout/cablev2_serverlink.xml",
    "java/res/layout/cablev2_usb_attached.xml",
    "java/res/values/attrs.xml",
    "java/res/values/styles.xml",
  ]
  deps = [ "//chrome/android:chrome_app_java_resources" ]
}

source_set("native") {
  sources = [ "native/cablev2_authenticator_android.cc" ]

  deps = [
    ":jni_headers",
    "//base",
    "//components/cbor",
    "//components/device_event_log",
    "//content/public/browser",
    "//crypto",
    "//device/fido",
    "//device/fido:cablev2_authenticator",
    "//device/fido:cablev2_registration",
    "//third_party/boringssl",
  ]
}
