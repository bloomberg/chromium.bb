# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")
import("//ppapi/buildflags/buildflags.gni")

# See //content/BUILD.gn for how this works.
group("child") {
  if (is_component_build) {
    public_deps = [ "//content" ]
  } else {
    public_deps = [ ":child_sources" ]
    if (!is_android) {
      public_deps += [ "//content/gpu" ]
    }
    if (enable_plugins) {
      public_deps += [ "//content/ppapi_plugin" ]
    }
  }
}

jumbo_source_set("child_sources") {
  # External code should depend in via ":child" above.
  visibility = [ "//content/*" ]

  sources = [
    "child_thread.h",
    "dwrite_font_proxy_init_win.h",
    "image_decoder_utils.h",
  ]

  configs += [
    "//build/config/compiler:wexit_time_destructors",
    "//content:content_implementation",
  ]

  deps = [
    "//content/child",  # Must not be public_deps!
    "//content/public/common:common_sources",
    "//gin",
    "//ppapi/c",
  ]

  allow_circular_includes_from = [
    # This target is a pair with content/child. They always go together and
    # include headers from each other.
    "//content/child",
  ]
}
