# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//device/vr/buildflags/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")

mojom_component("mojom") {
  output_prefix = "device_vr_mojo_bindings"
  macro_prefix = "DEVICE_VR_MOJO_BINDINGS"

  sources = [
    "isolated_xr_service.mojom",
    "vr_service.mojom",
  ]

  public_deps = [
    ":test_mojom",
    "//device/gamepad/public/mojom",
    "//gpu/ipc/common:interfaces",
    "//mojo/public/mojom/base",
    "//ui/display/mojom:mojom",
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
  ]

  enabled_features = []
  if (enable_oculus_vr) {
    enabled_features += [ "enable_oculus_vr" ]
  }
  if (enable_openvr) {
    enabled_features += [ "enable_openvr" ]
  }
  if (enable_windows_mr) {
    enabled_features += [ "enable_windows_mr" ]
  }
  if (enable_openxr) {
    enabled_features += [ "enable_openxr" ]
  }

  shared_cpp_typemap = {
    types = [
      {
        mojom = "device.mojom.RgbaTupleF16"
        cpp = "::device::RgbaTupleF16"
      },
      {
        mojom = "device.mojom.RgbTupleF32"
        cpp = "::device::RgbTupleF32"
      },
    ]
    traits_headers = [ "//device/vr/public/mojom/vr_service_mojom_traits.h" ]
  }

  cpp_typemaps = [ shared_cpp_typemap ]
  blink_cpp_typemaps = [ shared_cpp_typemap ]
}

mojom_component("test_mojom") {
  output_prefix = "device_vr_test_mojo_bindings"
  macro_prefix = "DEVICE_VR_TEST_MOJO_BINDINGS"

  sources = [ "browser_test_interfaces.mojom" ]

  public_deps = [
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
  ]
}
