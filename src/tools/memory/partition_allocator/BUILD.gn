# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//base/allocator/allocator.gni")

# Only support x86_64 Linux as the system calls used are Linux-specific. No
# point in building this if PartitionAlloc is not the allocator used.
_tcache_tool_supported =
    target_cpu == "x64" && target_os == "linux" && use_allocator == "partition"

if (_tcache_tool_supported) {
  source_set("pa_tool_utils") {
    sources = [
      "inspect_utils.cc",
      "inspect_utils.h",
    ]
    deps = [ "//base" ]
  }
  executable("pa_tcache_inspect") {
    sources = [ "pa_tcache_inspect.cc" ]
    deps = [
      ":pa_tool_utils",
      "//base",
    ]
  }
  executable("pa_buckets_inspect") {
    sources = [ "pa_buckets_inspect.cc" ]
    deps = [
      ":pa_tool_utils",
      "//base",
    ]
  }
  executable("pa_dump_heap") {
    sources = [ "pa_dump_heap.cc" ]
    deps = [
      ":pa_tool_utils",
      "//base",
      "//third_party/snappy:snappy",
    ]
  }
}

group("all") {
  if (_tcache_tool_supported) {
    deps = [
      ":pa_buckets_inspect",
      ":pa_tcache_inspect",
    ]
  }
}
