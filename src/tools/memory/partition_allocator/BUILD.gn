# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  has_local_elfutils = false
}

_tcache_tool_supported = target_cpu == "x64" && target_os == "linux"

if (_tcache_tool_supported) {
  executable("pa_tcache_inspect") {
    sources = [ "pa_tcache_inspect.cc" ]
    deps = [ "//base" ]

    if (has_local_elfutils) {
      configs += [ ":elfutils" ]
      sources += [
        "lookup_symbol.cc",
        "lookup_symbol.h",
      ]
    }
  }

  if (has_local_elfutils) {
    config("elfutils") {
      cflags = []
      defines = []
      ldflags = []

      cflags += [ "-I/usr/include/elfutils" ]
      defines += [ "HAS_LOCAL_ELFUTILS" ]
      ldflags += [
        "-L/usr/lib/x86_64-linux-gnu/",
        "-ldw",
      ]
    }
  }
}

group("all") {
  if (_tcache_tool_supported) {
    deps = [ ":pa_tcache_inspect" ]
  }
}
