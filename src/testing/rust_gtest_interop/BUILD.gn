# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/mixed_static_library.gni")
import("//build/rust/rust_macro.gni")
import("//testing/test.gni")

if (enable_rust) {
  mixed_static_library("rust_gtest_interop") {
    testonly = true
    sources = [
      "rust_gtest_interop.cc",
      "rust_gtest_interop.h",
    ]
    if (enable_rust) {
      rs_crate_root = "rust_gtest_interop.rs"
      rs_sources = [
        "expect_macros.rs",
        "rust_gtest_interop.rs",
      ]
      rs_cxx_bindings = [ "rust_gtest_interop.rs" ]
      rs_deps = [
        ":gtest_attribute($host_toolchain)",

        # These are re-exported for the gtest_attribute macros.
        # TODO(crbug.com/1299883): Make using proc-macros easier in GN.
        "//third_party/rust/small_ctor/v0_1:test_support($host_toolchain)",
      ]
      deps = [
        "//build/rust:cxx_cppdeps",
        "//testing/gtest",
      ]
    }
  }

  rust_macro("gtest_attribute") {
    testonly = true

    crate_root = "gtest_attribute.rs"
    sources = [ "gtest_attribute.rs" ]
    deps = [
      "//third_party/rust/proc_macro2/v1:lib",
      "//third_party/rust/quote/v1:lib",
      "//third_party/rust/syn/v1:lib",
    ]

    # This target's contents are exposed as part of :rust_gtest_interop.
    visibility = [ ":*" ]
  }

  test("rust_gtest_interop_unittests") {
    sources = [ "rust_gtest_interop_unittest_main.cc" ]
    deps = [
      "//base",
      "//base/test:test_support",
      "//testing/gtest",
    ]

    rs_sources = [ "rust_gtest_interop_unittest.rs" ]
  }
}
