# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_fuchsia)

import("//testing/test.gni")

source_set("cast_streaming_core") {
  deps = [
    "//base",
    "//media",
  ]
  public_deps = [
    "//components/cast/message_port",
    "//third_party/openscreen/src/cast/common:public",
    "//third_party/openscreen/src/cast/streaming:common",
  ]
  visibility = [ ":*" ]
  sources = [
    "cast_message_port_impl.cc",
    "cast_message_port_impl.h",
    "config_conversions.cc",
    "config_conversions.h",
    "message_serialization.cc",
    "message_serialization.h",
  ]
}

source_set("cast_streaming_session") {
  deps = [
    ":cast_streaming_core",
    "//base",
    "//components/openscreen_platform",
    "//media",
    "//media/mojo/common",
    "//media/mojo/mojom",
    "//mojo/public/cpp/system",
    "//third_party/openscreen/src/cast/common:public",
    "//third_party/openscreen/src/cast/streaming:receiver",
    "//third_party/openscreen/src/util",
  ]
  public_deps = [ "//components/cast/message_port" ]
  visibility = [ ":*" ]
  public = [ "public/cast_streaming_session.h" ]
  sources = [
    "cast_streaming_session.cc",
    "stream_consumer.cc",
    "stream_consumer.h",
  ]
}

source_set("cast_streaming_network_context") {
  public = [ "public/network_context_getter.h" ]
  deps = [
    "//base",
    "//components/openscreen_platform:openscreen_platform_network_service",
  ]
  visibility = [ ":*" ]
  sources = [ "network_context_getter.cc" ]
}

source_set("cast_streaming") {
  public_deps = [
    ":cast_streaming_network_context",
    ":cast_streaming_session",
  ]
  visibility = [ "//fuchsia/engine:*" ]
}

source_set("cast_streaming_test_sender") {
  testonly = true
  deps = [
    ":cast_streaming_core",
    "//media/mojo/common",
    "//media/mojo/mojom",
    "//mojo/public/cpp/system",
    "//third_party/openscreen/src/platform:api",
    "//third_party/openscreen/src/util",
  ]
  public_deps = [
    "//base",
    "//components/openscreen_platform",
    "//media",
    "//third_party/openscreen/src/cast/common:public",
    "//third_party/openscreen/src/cast/streaming:sender",
  ]
  sources = [
    "test/cast_message_port_sender_impl.cc",
    "test/cast_message_port_sender_impl.h",
    "test/cast_streaming_test_sender.cc",
    "test/cast_streaming_test_sender.h",
  ]
}

source_set("cast_streaming_test_receiver") {
  testonly = true
  deps = [
    ":cast_streaming_session",
    "//base",
    "//components/openscreen_platform",
    "//media",
    "//media/mojo/common",
    "//mojo/public/cpp/system",
    "//third_party/openscreen/src/cast/common:public",
    "//third_party/openscreen/src/cast/streaming:receiver",
    "//third_party/openscreen/src/platform:api",
    "//third_party/openscreen/src/util",
  ]
  visibility = [ ":*" ]
  sources = [
    "test/cast_streaming_test_receiver.cc",
    "test/cast_streaming_test_receiver.h",
  ]
}

test("cast_streaming_unittests") {
  deps = [
    ":cast_streaming_core",
    ":cast_streaming_session",
    ":cast_streaming_test_receiver",
    ":cast_streaming_test_sender",
    "//base/test:test_support",
    "//components/cast/message_port:test_message_port_receiver",
    "//components/openscreen_platform:openscreen_platform_net",
    "//media",
    "//mojo/core/embedder",
    "//testing/gtest",
    "//third_party/openscreen/src/platform:api",
  ]
  sources = [
    "cast_message_port_impl_unittest.cc",
    "cast_streaming_session_unittest.cc",
    "test/run_all_unittests.cc",
  ]
}
