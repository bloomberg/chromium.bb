# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_fuchsia)

import("//build/config/fuchsia/generate_runner_scripts.gni")
import("//testing/test.gni")

# Integration helpers for commonly used fuchsia.* APIs.
source_set("base") {
  # Only for use by Fuchsia Components.
  visibility = [
    "./test/*",
    "//chromecast/internal/*",
    "//fuchsia/engine/*",
    "//fuchsia/runners/*",
  ]
  sources = [
    "config_reader.cc",
    "feedback_registration.cc",
    "fuchsia_dir_scheme.cc",
    "init_logging.cc",
    "inspect.cc",
    "string_util.cc",
  ]
  public = [
    "config_reader.h",
    "feedback_registration.h",
    "fuchsia_dir_scheme.h",
    "init_logging.h",
    "inspect.h",
    "string_util.h",
  ]
  deps = [
    "//base",
    "//components/version_info",
    "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.feedback",
    "//third_party/fuchsia-sdk/sdk/pkg/fdio",
    "//third_party/fuchsia-sdk/sdk/pkg/sys_cpp",
    "//third_party/fuchsia-sdk/sdk/pkg/sys_inspect_cpp",
    "//url",
  ]
}

# Integration helpers for fuchsia.modular.
source_set("modular") {
  # Only for use by Fuchsia Components.
  visibility = [
    ":cr_fuchsia_base_unittests__exec",
    "./test/*",
    "//chromecast/internal/*",
    "//fuchsia/engine/*",
    "//fuchsia/runners/*",
  ]
  sources = [
    "agent_impl.cc",
    "agent_impl.h",
    "agent_manager.cc",
    "agent_manager.h",
    "lifecycle_impl.cc",
    "lifecycle_impl.h",
  ]
  deps = [ "//base" ]
  public_deps = [
    "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.modular",
    "//third_party/fuchsia-sdk/sdk/pkg/sys_cpp",
  ]
}

# FIDL/Mojo adapters for MessagePorts.
source_set("message_port") {
  visibility = [ "//fuchsia/engine/*" ]
  sources = [ "message_port.cc" ]
  public = [ "message_port.h" ]
  public_deps = [
    "//third_party/abseil-cpp:absl",
    "//third_party/blink/public/common:headers",
    "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.web",
  ]
  deps = [
    "//base",
    "//mojo/public/cpp/bindings",
    "//third_party/blink/public/common",
    "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.mem",
    "//third_party/fuchsia-sdk/sdk/pkg/fit-promise",
  ]
}

source_set("legacymetrics") {
  visibility = [
    ":cr_fuchsia_base_unittests__exec",
    "//chromecast/internal/*",
    "//fuchsia/engine/*",
  ]
  sources = [
    "legacymetrics_client.cc",
    "legacymetrics_client.h",
    "legacymetrics_histogram_flattener.cc",
    "legacymetrics_histogram_flattener.h",
    "legacymetrics_user_event_recorder.cc",
    "legacymetrics_user_event_recorder.h",
  ]
  public = [ "legacymetrics_client.h" ]
  deps = [ "//base" ]
  public_deps = [ "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.legacymetrics" ]
  friend = [ ":*" ]
}

static_library("run_all_integration_tests") {
  testonly = true
  visibility = [
    "//fuchsia/engine/*",
    "//fuchsia/runners/*",
  ]
  sources = [ "run_all_integration_tests.cc" ]
  deps = [ "//base/test:test_support" ]
}

# Unit-tests for all //fuchsia/base utilities.
test("cr_fuchsia_base_unittests") {
  sources = [
    "agent_impl_unittest.cc",
    "config_reader_unittest.cc",
    "inspect_unittest.cc",
    "legacymetrics_client_unittest.cc",
    "legacymetrics_histogram_flattener_unittest.cc",
    "legacymetrics_user_event_recorder_unittest.cc",
  ]
  deps = [
    ":legacymetrics",
    ":modular",
    "//base",
    "//base:testfidl",
    "//base/test:run_all_unittests",
    "//base/test:test_support",
    "//components/version_info",
    "//fuchsia/base/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/fuchsia-sdk/sdk/pkg/sys_cpp",
    "//third_party/fuchsia-sdk/sdk/pkg/sys_inspect_cpp",
  ]
}
