# Copyright (c) 2015 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/toolchain/gcc_toolchain.gni")

# This toolchain's sole purpose is to compile nacl_helper_bootstrap.c.
# That requires a separate toolchain because that's the most reasonable way
# to instantiate the standard configs in a context where use_gold=false
# and use_debug_fission=false when the main build is using either
# use_gold=true or use_debug_fission=true. See ../BUILD.gn for details.

template("nacl_bootstrap_toolchain") {
  clang_toolchain(target_name) {
    toolchain_args = {
      current_cpu = invoker.toolchain_cpu
      current_os = target_os
      use_debug_fission = false
      use_gold = false
    }
  }
}

nacl_bootstrap_toolchain("nacl_bootstrap_x64") {
  toolchain_cpu = "x64"
}

nacl_bootstrap_toolchain("nacl_bootstrap_x86") {
  toolchain_cpu = "x86"
}

nacl_bootstrap_toolchain("nacl_bootstrap_arm") {
  toolchain_cpu = "arm"
}

nacl_bootstrap_toolchain("nacl_bootstrap_mipsel") {
  toolchain_cpu = "mipsel"
}
