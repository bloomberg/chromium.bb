# -*- python -*-
# Copyright 2008, Google Inc.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
#     * Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above
# copyright notice, this list of conditions and the following disclaimer
# in the documentation and/or other materials provided with the
# distribution.
#     * Neither the name of Google Inc. nor the names of its
# contributors may be used to endorse or promote products derived from
# this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
# "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
# LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
# A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
# OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
# LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.


import os
import sys
Import('env')

# TODO(robertm): consider adding this to the top level scons files
env.Append(CPPPATH=['${TARGET_ROOT}'])

# TODO(bradchen): eliminate need for the following line
env.FilterOut(CCFLAGS=['-Wextra', '-Wswitch-enum', '-Wsign-compare'])

# Start by building libary utilities that may be used by other architectures.
env.ComponentLibrary('nchelper', ['ncfileutil.c'])

# Now build validator for x86
if not env.Bit('target_x86'):
  Return()

# Create environment for command-line tools and testing, rather than
# part of the TCB. Then define compile-time flag that communicates
# that we are compiling in the test environment (rather than for the TCB).
test_env = env.Clone();
test_env.Append(CCFLAGS=['-DNACL_TRUSTED_BUT_NOT_TCB'])

# Define where generated code should be put.
ncv_gen_basedir = '${TARGET_ROOT}'
ncv_gen_dir = ncv_gen_basedir +'/gen/native_client/src/trusted/validator_x86'

#------------------------------------------------------
# Generate the needed header files for enumerated types.
# Note that we use the same directory for all platforms.

# Define enumerate type files, and the options to process.
ncv_enum_pairs = {
    'nacl_disallows': '--name=NaClDisallowsFlag --add_size=1',
    'ncopcode_prefix': '--name=NaClInstPrefix --add_size=1',
    'ncopcode_insts':
      '--name=NaClMnemonic --add_size=1 --sort=1 --name_prefix=Inst',
    'ncopcode_opcode_flags': '--name=NaClIFlag --add_size=1',
    'ncopcode_operand_kind': '--name=NaClOpKind --add_size=1',
    'ncopcode_operand_flag': '--name=NaClOpFlag --add_size=1',
    'ncop_expr_node_flag': '--name=NaClExpFlag --add_size=1',
    'ncop_expr_node_kind': '--name=NaClExpKind --add_size=1',
}

# Now code generate the enumerated types.
for ncv_enum in ncv_enum_pairs:
  ncv_options = ncv_enum_pairs[ncv_enum]
  ncv_base = ncv_gen_dir + '/' + ncv_enum
  ncv_enum_file = env.File(ncv_enum + '.enum')
  ncv_header_1 = env.File(ncv_base + '.h')
  ncv_header_2 = env.File(ncv_base + '_impl.h')
  ncv_headers = [ ncv_header_1 , ncv_header_2 ]
  ncv_command = (
      '${PYTHON} %s --header="%s" --source="%s" --path_prefix="%s" %s %s' %
      (env.File('enum_gen.py'), ncv_header_1, ncv_header_2, ncv_gen_basedir,
       ncv_options, ncv_enum_file))
  env.Command(ncv_headers,
              [ncv_enum_file, env.File('enum_gen.py')],
              [ncv_command])

ncopcode_desc_o = env.ComponentObject('ncopcode_desc.c')
env.ComponentLibrary('ncopcode_utils', ncopcode_desc_o)

# Make a copy of debug CRT for now.
# TODO(bradnelson): there should be a better way to generalize this requirement.
# NOTE: debug builds on windows break without this
crt = []
if env.AllBits('windows', 'debug'):
  for i in ['.', '$STAGING_DIR']:
    crt += env.Replicate(i, '$VC80_DIR/vc/redist/Debug_NonRedist/'
                         'x86/Microsoft.VC80.DebugCRT')
    crt += env.Replicate(i, '$VC80_DIR/vc/redist/x86/Microsoft.VC80.CRT')

# Isolate the environment for ncdecode_table to prevent a cycle.
env_decode_table = test_env.Clone()
# TODO: This should be handled more cleanly, by just building
#   Testing type programs for coverage. But for the moment, we need sel_ldr
#   and others to have coverage, so it's easier to gate it off here.
env_decode_table['COVERAGE_LINKCOM_EXTRAS'] = None

ncdecode_table = env_decode_table.ComponentProgram('ncdecode_table',
                                                   ['ncdecode_table.c'],)
env.Requires(ncdecode_table, crt)

force_cpp = env.ComponentObject('force_cpp.cc')

# Add new x86 table generator.
ncdecode_tablegen = env_decode_table.ComponentProgram(
    'ncdecode_tablegen',
    ['ncdecode_tablegen.c',
     'ncdecode_forms.c',
     'zero_extends.c',
     'long_mode.c',
     'nc_rep_prefix.c',
     'defsize64.c',
     'nacl_illegal.c',
     'lock_insts.c',
     'ncdecode_st.c',
     'ncdecode_onebyte.c',
     'ncdecode_OF.c',
     'ncdecode_sse.c',
     'ncdecodeX87.c',
     force_cpp,
    ],
    EXTRA_LIBS=['utils',
                'gio',
                'platform',
                'ncopcode_utils'])

env.Requires(ncdecode_tablegen, crt)

# Segment register validator library
NCVALIDATE_SOURCES = ['nacl_cpuid.c',
                                    'ncdecode.c',
                                    'nc_segment.c',
                                    'nc_inst_iter.c',
                                    'nc_inst_state.c',
                                    'nc_inst_trans.c',
                                    'ncop_exps.c',
                                    'ncvalidate.c',
                                    'nccopycode.c',
                                    'nccopycode_stores.S'
                                    ]
if env['TARGET_SUBARCH']=='64':
  NCVALIDATE_SOURCES = NCVALIDATE_SOURCES + ncopcode_desc_o

env.ComponentLibrary('ncvalidate', NCVALIDATE_SOURCES)

# SFI validator library
env.ComponentLibrary('ncvalidate_sfi',
                     ['ncvalidate_iter.c',
                      'ncvalidator_registry.c',
                      'nc_opcode_histogram.c',
                      'nc_cpu_checks.c',
                      'nc_illegal.c',
                      'nc_protect_base.c',
                      'nc_memory_protect.c',
                      'ncvalidate_utils.c',
                      'nc_jumps.c'])

test_env.ComponentLibrary('ncdis_util',
                          ['ncdis_util.c',
                           'ncdis_segments.c',
                           'ncval_driver.c',
                           'nc_read_segment.c',
                           'ncenuminsts.c'])

#------------------------------------------------------------------
# Generate the header files containing the modeled x86 instructions.

ncv_decodetab_h = env.File(ncv_gen_dir + '/ncdecodetab.h')
ncv_disasmtab_h = env.File(ncv_gen_dir + '/ncdisasmtab.h')

env.Command(
    [ncv_decodetab_h, ncv_disasmtab_h],
    [ncdecode_table, '${STAGING_DIR}/${PROGPREFIX}ncdecode_table${PROGSUFFIX}'],
    ['${STAGING_DIR}/${PROGPREFIX}ncdecode_table${PROGSUFFIX} -m%s %s %s' %
     (env['TARGET_SUBARCH'], ncv_decodetab_h, ncv_disasmtab_h)])

# TODO(gregoryd): use nc_opcode_table64.h on all 64-bit platforms.
if env['TARGET_SUBARCH'] == '64' and env.Bit('windows'):
  ncv_opcode_table_h = env.File(ncv_gen_dir + '/nc_opcode_table64.h')
else:
  ncv_opcode_table_h = env.File(ncv_gen_dir + '/nc_opcode_table.h')

env.Command(
    [ncv_opcode_table_h],
    [ncdecode_tablegen,
     '${STAGING_DIR}/${PROGPREFIX}ncdecode_tablegen${PROGSUFFIX}'],
    ['${STAGING_DIR}/${PROGPREFIX}ncdecode_tablegen${PROGSUFFIX} -m%s %s' %
     (env['TARGET_SUBARCH'], ncv_opcode_table_h)])

# ======================================================================
# Decoder unittests
if test_env['TARGET_SUBARCH'] == '32':
  # TODO(kschimpf): remove this when the validator builds as 64-bits.
  if test_env['BUILD_SUBARCH'] == '32':
    ncdecode_tests = test_env.ComponentProgram('ncdecode_tests',
                                               ['ncdecode_tests.c'],
                                               EXTRA_LIBS=['ncvalidate',
                                                           'ncdis_util'])

    node = test_env.CommandTest(
        'ncdecode_tests.out',
        [ncdecode_tests],
        )

    test_env.AddNodeToTestSuite(node, ['small_tests'])

# ======================================================================
ncdis = test_env.ComponentProgram(
  'ncdis', [
    'ncdis.c',
    force_cpp,
  ],
  EXTRA_LIBS=[
    'ncdis_util',
    'utils',
    'ncvalidate',
    'ncopcode_utils',
    'nchelper',
    'ncdis_util',
    'platform',
    'gio'
  ])

env.Requires(ncdis, crt)

# ======================================================================
# Valiator unittests
if test_env['TARGET_SUBARCH'] == '32':
  ncval_tests = test_env.ComponentProgram('ncval_tests',
                                          ['ncval_tests.c'],
                                          EXTRA_LIBS=['ncvalidate', 'nchelper'])
  test_env.Requires(ncval_tests, crt)

  node = test_env.CommandTest(
      'ncval_tests.out',
      [ncval_tests],
      )

  test_env.AddNodeToTestSuite(node, ['small_tests', 'validator_tests'])

# ======================================================================
if test_env['TARGET_SUBARCH'] == '32':
  nacl_cpuid = test_env.ComponentProgram('nacl_cpuid',
                                         ['nacl_cpuid_test.c'],
                                         EXTRA_LIBS=['ncvalidate'])
  test_env.Requires(nacl_cpuid, crt)


  node = test_env.CommandTest(
      'nacl_cpuid_test.out',
      [nacl_cpuid])

  test_env.AddNodeToTestSuite(node, ['large_tests'])

# ======================================================================
# Helper functions for getting automated tests from the corresponding
# test directory.

TESTDATA_DIR = 'testdata/' + env['TARGET_SUBARCH'] + '/'

# Generates the set of test files with the given extension.
def __GoldenFiles(ext):
  return Glob(TESTDATA_DIR + '*.' + ext)

# Generates base names (i.e. minus path and extention (ext) suffix) of
# all test data input files.
def __FilterOutTestFileBaseGen(files, ext):
  for f in files:
    yield os.path.basename(f.path).replace('.' + ext, "")

# Generates the set of golden bases for the given extension.
def __GoldenBases(ext):
  return __FilterOutTestFileBaseGen(__GoldenFiles(ext), ext)

# Generates the corresponding test file from the given base.
def __BaseTestFile(base, ext):
  return test_env.File(TESTDATA_DIR + base + '.' + ext)

# Generates the corresponding test name from the given base.
def __BaseTestName(test, base, ext):
  return test + '_' + base + '_' + ext

# Generates the corresponding output file from the given base.
def __OutTestFile(test, base, ext):
  return test + "_" + base + '.' + ext + '.out'

def __AddTest(test, base, ext, command, groups):
  test_env.AddNodeToTestSuite(command, groups, __BaseTestName(test, base, ext))


# ======================================================================
# Decoder vs objdump tests
# TODO: add tests using the toolchain binaries in the sdk
if test_env.Bit('linux') and test_env['TARGET_SUBARCH'] == '32':
  test = 'discmp'
  ext = 'nexe'
  for base in __GoldenBases(ext):
    node = test_env.CommandTest(
        __OutTestFile(test, base, ext),
        ["${PYTHON}", test_env.File('discmp.py'), test_env.File('ncdis'),
         __BaseTestFile(base, ext)],
        )
    __AddTest(test, base, ext, node, ['large_tests'])

# ======================================================================
# Decoder on sample instruction sequences.
TARGET_ARCH_NAME =  env['TARGET_ARCHITECTURE'] + '_' + env['TARGET_SUBARCH']

NCDIS_TEST_NAME = 'ncdis_' + TARGET_ARCH_NAME + '_test'

ncdis_input_golden = test_env.File(TESTDATA_DIR + 'ncdis_test.input')

ncdis_test = test_env.CommandTest(
    NCDIS_TEST_NAME + '.out',
    [ncdis , '--use_iter=false', '--self_document', '--commands=-'],
    stdin=ncdis_input_golden,
    stdout_golden=ncdis_input_golden)

test_env.AddNodeToTestSuite(ncdis_test, ['small_tests'], NCDIS_TEST_NAME)

# ======================================================================
# Decoder on sample instruction sequences using new iterator model.

NCDIS_ITER_TEST_NAME = 'ncdis_iter_' + TARGET_ARCH_NAME + '_test'

ncdis_iter_input_golden = test_env.File(TESTDATA_DIR + 'ncdis_iter_test.input')

ncdis_iter_test = test_env.CommandTest(
    NCDIS_ITER_TEST_NAME + '.out',
    [ncdis , '--use_iter', '--self_document', '--commands=-'],
    stdin=ncdis_iter_input_golden,
    stdout_golden=ncdis_iter_input_golden)

test_env.AddNodeToTestSuite(ncdis_iter_test,
                            ['small_tests'],
                            NCDIS_ITER_TEST_NAME)

# Same as above, but comparing internal representation against golde files.

ncdis_iter_internal_golden = test_env.File(TESTDATA_DIR +
                                           'ncdis_iter_test.internal')

ncdis_internal_test = test_env.CommandTest(
    NCDIS_ITER_TEST_NAME + "_int.out",
    [ncdis, "--use_iter", "--internal", "--commands=-"],
    stdin=ncdis_iter_input_golden,
    stdout_golden=ncdis_iter_internal_golden)

test_env.AddNodeToTestSuite(ncdis_internal_test,
                            ['small_tests'],
                            NCDIS_ITER_TEST_NAME)

# ======================================================================
# Segment register validator.
ncval = test_env.ComponentProgram(
  'ncval', [
    'ncval.c',
    force_cpp,
  ],
  EXTRA_LIBS=[
    'ncdis_util',
    'ncvalidate_sfi',
    'ncvalidate',
    'ncopcode_utils',
    'nchelper',
    'platform',
    'gio',
    'utils'
  ])

test_env.Requires(ncval, crt)

# ======================================================================
# Validator tests against real nacl images
# TODO: add death tests
# TODO: add tests using binaries produced by nacl.scons files
# TODO: consider comparing the actual output (not just exit codes)
# Validate tests
if env['TARGET_SUBARCH'] == '32':
  test = 'validate'
  ext = 'nexe'
  for base in __GoldenBases(ext):
    node = test_env.CommandTest(
        __OutTestFile(test, base, ext),
        [ncval, '-t', __BaseTestFile(base, ext)],
        )
    __AddTest(test, base, ext, node, ['large_tests'])

# Run tests using ncdis on test files. Assumes that extension
# dis implies to use the iterator model, while ndis does not.
def __AddNcDisTests(use_iter):
  test = 'ncdis_hex'
  if use_iter:
    ext = "dis"
  else:
    ext = "ndis"
  options = ['--hex_text=-']
  if use_iter:
    options.append('--use_iter');
  else:
    options.append('--use_iter=false');
  for base in __GoldenBases(ext):
    node_dis = test_env.CommandTest(
        __OutTestFile(test, base, ext),
        [ncdis] + options,
        stdin = __BaseTestFile(base, 'hex'),
        stdout_golden = __BaseTestFile(base, ext))
    __AddTest(test, base, ext, node_dis,
              ['small_tests', 'validator_tests'])

__AddNcDisTests(True)
__AddNcDisTests(False)

# Run tests using ncval --use_iter, on test files. Assume that write sandbox tests
# use extension 'val' while read/write sandbox tests use extention 'rval'.
def __AddNcvalSfiTests(test_readwrite):
  if env['TARGET_SUBARCH'] == '64':
    test = 'ncval64_hex'
    if test_readwrite:
      ext = 'rval'
    else:
      ext = 'val'
    for base in __GoldenBases(ext):
      command = [ncval, '--use_iter', '--hex_text=-', '--max_errors=1000']
      if test_readwrite:
        command.append('--readwrite_sfi')
      else:
        command.append("--write_sfi")
      node_val = test_env.CommandTest(
          __OutTestFile(test, base, ext),
          command,
          stdin = __BaseTestFile(base, 'hex'),
          stdout_golden = __BaseTestFile(base, ext))
      __AddTest(test, base, ext, node_val, ['small_tests', 'validator_tests'])

__AddNcvalSfiTests(True)
__AddNcvalSfiTests(False)

# Run tests using ncval --use_iter=false on test files.
def __AddNcvalSegmentTests():
  if env['TARGET_SUBARCH'] == '32':
    test = 'ncval32_hex'
    ext = 'nval'
    for base in __GoldenBases(ext):
      command = [ncval,
                 '--use_iter=false',
                 '--hex_text=-',
                 '--max_errors=1000']
      node_val = test_env.CommandTest(
          __OutTestFile(test, base, ext),
          command,
          stdin = __BaseTestFile(base, 'hex'),
          stdout_golden = __BaseTestFile(base, ext))
      __AddTest(test, base, ext, node_val,
                ['small_tests', 'validator_tests'])

__AddNcvalSegmentTests()

# Run tests for stubout on test files. Assumes that the extension
# sval implies to use the SFI model, while snval implies use of the
# (32 bit) segment model. The input file uses extension hex.
def __AddNcValStuboutTests(use_sfi):
  if (env['TARGET_SUBARCH'] == '32') or use_sfi:
    test = 'ncval_stubout'
    options = ['--hex_text=-', '--stubout']
    if use_sfi:
      ext = 'sval'
      options.append('--use_iter')
    else:
      ext = 'snval'
      options.append('--use_iter=false')
    for base in __GoldenBases(ext):
      command = [ncval] + options
      node_stubout = test_env.CommandTest(
          __OutTestFile(test, base, ext),
          command,
          stdin = __BaseTestFile(base, 'hex'),
          stdout_golden = __BaseTestFile(base, ext))
      __AddTest(test, base, ext, node_stubout,
                ['small_tests', 'validator_tests'])

__AddNcValStuboutTests(True)
__AddNcValStuboutTests(False)

# Run test to see if we have changed the set of modeled instructions.
def __AddModeledInstsTest():
  test = 'test'
  base = 'modeled_insts'
  ext = 'txt'
  command = [ncdecode_tablegen,
             '-m%s' % (env['TARGET_SUBARCH']),
             '-documentation']
  model = test_env.CommandTest(
      __OutTestFile(test, base, ext),
      command,
      stdout_golden = __BaseTestFile(base, ext))
  __AddTest(test, base, ext, model, ['small_tests',
                                     'validator_tests',
                                     'validator_modeling'])

__AddModeledInstsTest()


# Run test to show that --max_errors=-1 works in 32 bit mode.
def __AddNegMaxErrors():
  if env['TARGET_SUBARCH'] == '32':
    test = 'neg_max_errs'
    base = 'ret'
    ext = 'nval'
    node_val = test_env.CommandTest(
        __OutTestFile(test, base, ext),
        [ncval, '--use_iter=false', '--max_errors=-1', '--hex_text=-'],
        stdin = __BaseTestFile(base, 'hex'),
        stdout_golden = __BaseTestFile(base, ext))
    __AddTest(test, base, ext, node_val,
              ['small_tests', 'validator_tests'])

__AddNegMaxErrors()
