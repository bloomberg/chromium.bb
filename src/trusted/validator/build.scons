# -*- python -*-
# Copyright (c) 2012 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.


import os
import sys
Import('env')


env.ComponentLibrary(env.NaClTargetArchSuffix('ncfileutils'), ['ncfileutil.c'])

val_lib_env = env.Clone()
if env.Bit('validator_ragel'):
  val_lib_env.Append(CPPDEFINES=[['NACL_VALIDATOR_RAGEL', '1']])

val_lib_env.ComponentLibrary('validation_cache', ['validation_cache.c'])

val_lib_env.ComponentLibrary('validators', ['validator_init.c'])

if env.Bit('target_x86') and not env.Bit('validator_ragel'):
  # The R-DFA validator ignores requests to cache validation results, so the
  # test fails. Validation caching will probably never be supported in R-DFA
  # because the R-DFA pass is faster than computing a hash for the executable
  # segment.
  #
  # TODO(ncbray) support ARM.  This will require making validation caching safe
  # for ARM and embedding ARM binary chunks in the test that can trigger various
  # validation scenarios.
  gtest_env = env.MakeGTestEnv()

  validation_cache_test_exe = gtest_env.ComponentProgram(
      'validation_cache_test',
      ['validation_cache_test.cc'],
      EXTRA_LIBS=['validators'])

  node = gtest_env.CommandTest(
      'validation_cache_test.out',
      command=[validation_cache_test_exe])

  env.AddNodeToTestSuite(node, ['small_tests', 'validator_tests'],
                         'run_validation_cache_test')
