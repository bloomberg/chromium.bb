# -*- python -*-
# Copyright (c) 2011 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import os
Import('env')

if env.Bit('linux'):

  env.Append(
    # TODO(cbiffle): give everyone else these warnings too.
    CXXFLAGS='-Weffc++ -Woverloaded-virtual -fno-rtti -fomit-frame-pointer',
  )

  # NOTE: we cannot use TARGET_ROOT here because we want to share this file
  #       between different scons invocations
  gen_dec=('${DESTINATION_ROOT}/gen/native_client/src/trusted/validator_arm/'
           'decode.cc')

  env.Command(target=gen_dec,
              source=['armv7-opt.table',
                      'generate_decoder.py',
                      'dgen_core.py',
                      'dgen_input.py',
                      'dgen_opt.py',
                      'dgen_output.py'],
             action=['${SOURCES[1].abspath} ${SOURCES[0].abspath} '
                         '${TARGET.abspath}'])

  env.ComponentLibrary('arm_validator_core',
                       ['address_set.cc',
                        'inst_classes.cc',
                        'validator.cc',
                        gen_dec])

  env.ComponentLibrary('ncvalidate_arm_v2',
                       ['ncvalidate.cc'],
                       LIBS=['arm_validator_core',
                             '${OPTIONAL_COVERAGE_LIBS}'])

  ncval = env.ComponentProgram(
      'arm-ncval-core',
      ['ncval.cc'],
      LIBS=['arm_validator_core',
            env.NaClTargetArchSuffix('ncfileutils'),
            '${OPTIONAL_COVERAGE_LIBS}'])

  env.SDKInstallTrusted('ncval', ncval, target='arm')

  env.ComponentProgram('address_set_test_binary',
                       ['address_set_test.cc'],
                       LIBS=['arm_validator_core',
                             '${OPTIONAL_COVERAGE_LIBS}'])

  address_set_test = env.Command(target='address_set_test.out',
                                 source=['address_set_test_binary'],
                                 action=['${SOURCES[0].abspath}'])

  # TODO(cbiffle): get this wrapped in QEMU.
  #env.AddNodeToTestSuite(address_set_test, ['small_tests'], 'address_set_test')

  validator_tests = {
    'test_external_jumps': 1,
    'test_forbidden_instructions': 1,
    'test_internal_jumps': 1,
    'test_sp_updates': 1,
    'test_stores': 1,
    'test_vector_stores': 1,
    'test_loads': 1,
    'test_vector_loads': 1,
  }

  for test, exit_status in validator_tests.iteritems():
    node = env.CommandTest(
        test + '_actual.out',
        [ncval, env.File('testdata/' + test + '.nexe')],
        exit_status = str(exit_status),
        filter_regex = "'^ncval'",
        # NOTE: all stdout_golden are currently empty
        stdout_golden = env.File('testdata/' + test + '.out'),
        stderr_golden = env.File('testdata/' + test + '.err'))

    env.AddNodeToTestSuite(node, ['small_tests', 'validator_tests'],
                           "run_arm_" + test)

  gtest_env = env.Clone()
  # gtest does not compile with our stringent settings.
  gtest_env.FilterOut(
      CCFLAGS=['-pedantic'],
      CXXFLAGS=['-fno-rtti', '-Weffc++'])
  gtest_env.Prepend(CPPPATH=['${SOURCE_ROOT}/testing/gtest/include'])

  # gtest is incompatible with static linking due to obscure libstdc++
  # linking interactions.
  # See http://code.google.com/p/nativeclient/issues/detail?id=1987
  gtest_env.FilterOut(LINKFLAGS=['-static'])

  # Do NOT name this program 'validator_tests' because this is the same name as
  # a test suite, and scons will run that test suite if it ever builds
  # a program of the same name.
  validator_tests_exe = gtest_env.ComponentProgram('arm_validator_tests',
                                 ['validator_tests.cc'],
                                 EXTRA_LIBS=['gtest', 'arm_validator_core'])

  test_node = gtest_env.CommandTest(
      'validator_tests.out',
      command=[validator_tests_exe])
  gtest_env.AddNodeToTestSuite(test_node, ['small_tests'],
      'run_arm_validator_tests')
