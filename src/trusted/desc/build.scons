# -*- python -*-
# Copyright 2009, Google Inc.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
#     * Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above
# copyright notice, this list of conditions and the following disclaimer
# in the documentation and/or other materials provided with the
# distribution.
#     * Neither the name of Google Inc. nor the names of its
# contributors may be used to endorse or promote products derived from
# this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
# "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
# LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
# A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
# OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
# LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.


Import('env')

env_no_strict_aliasing = env.Clone()
if env.Bit('linux'):
   env_no_strict_aliasing.Append(CFLAGS = ['-fno-strict-aliasing'])

nrd_xfer_obj = env_no_strict_aliasing.ComponentObject('nrd_xfer.c')

nrd_lib_inputs = [
    'nacl_desc_base.c',
    'nacl_desc_cond.c',
    'nacl_desc_conn_cap.c',
    'nacl_desc_dir.c',
    'nacl_desc_imc.c',
    'nacl_desc_imc_bound_desc.c',
    'nacl_desc_imc_shm.c',
    'nacl_desc_io.c',
    'nacl_desc_mutex.c',
    'nrd_all_modules.c',
    nrd_xfer_obj,
    'nrd_xfer_effector.c',
    'nacl_desc_effector_cleanup.c',
    'nacl_desc_effector_ldr.c',
    'nacl_desc_semaphore.c',
]

if env.Bit('windows'):
  nrd_lib_inputs += [
      'win/nacl_desc.c',
  ]
elif env.Bit('mac'):
  nrd_lib_inputs += [
      'linux/nacl_desc.c',
  ]
else:
  nrd_lib_inputs += [
      'linux/nacl_desc.c',
  ]

env.ComponentLibrary('nrd_xfer', nrd_lib_inputs,
                     EXTRA_LIBS=['platform'])

# TODO(robertm): is this still needed?
# Make a copy of debug CRT for now.
# TODO(bradnelson): this really should be avoided if possible.
crt = env.Replicate('.', '$VC80_DIR/vc/redist/Debug_NonRedist/'
                    'x86/Microsoft.VC80.DebugCRT')
crt += env.Replicate('.', '$VC80_DIR/vc/redist/x86/Microsoft.VC80.CRT')

# TODO: add comment
if env['TARGET_PLATFORM'] == 'WINDOWS':
  env.Append(LIBS = [ 'ws2_32', 'advapi32' ])

nrd_tester = env.ComponentProgram('nrd_xfer_test', 'nrd_xfer_test.c',
                                  EXTRA_LIBS=['sel',
                                              'nrd_xfer',
                                              'nonnacl_srpc',
                                              'ncvalidate',
                                              'google_nacl_imc_c',
                                              'platform',
                                              'gio'])

if env.Bit('linux'):
   node = env.CommandTestAgainstGoldenOutput(
    'nrd_xfer_test.out',
    [env.File('nrd_xfer_test.sh'), nrd_tester[0]])
   env.AddNodeToTestSuite(node, ['medium_tests'], 'run_nrd_xfer_test')
