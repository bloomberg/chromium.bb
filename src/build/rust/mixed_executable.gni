# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/rust.gni")
import("//build/rust/mixed_target.gni")

# Defines an executable containing both Rust and C++ code.
# See mixed_target.gni for documentation.

template("mixed_executable") {
  mixed_target(target_name) {
    target_type = "executable"
    forward_variables_from(invoker,
                           "*",
                           TESTONLY_AND_VISIBILITY + [ "rs_visibility" ])
    forward_variables_from(invoker, TESTONLY_AND_VISIBILITY)
  }
}

set_defaults("mixed_executable") {
  configs = default_executable_configs

  # lld currently eats a section which Rust executables need to
  # determine their command line arguments. These lines work around this
  # by retaining all sections.
  # TODO(crbug/1281664) - remove these lines when this is fixed.
  if (toolchain_has_rust) {
    configs -= [ "//build/config/compiler:default_optimization" ]
    configs += [
      "//build/config/compiler:no_optimize",
      "//build/rust:keep_sections",
    ]
  }
}
