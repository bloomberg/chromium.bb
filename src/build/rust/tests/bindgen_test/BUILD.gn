# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rust_bindgen.gni")
import("//build/rust/rust_executable.gni")
import("//build/rust/rust_static_library.gni")

component("c_lib") {
  sources = [
    "lib.c",
    "lib.h",
  ]

  defines = [ "COMPONENT_IMPLEMENTATION" ]
}

rust_bindgen("c_lib_bindgen") {
  header = "lib.h"
  configs = default_compiler_configs
}

rust_static_library("bindgen_test_lib") {
  deps = [
    ":c_lib",
    ":c_lib_bindgen",
  ]
  sources = [ "src/lib.rs" ]
  build_native_rust_unit_tests = true
  crate_root = "src/lib.rs"

  bindgen_output = get_target_outputs(":c_lib_bindgen")
  rustenv = [ "BINDGEN_RS_FILE=" + rebase_path(bindgen_output[0]) ]
}

rust_executable("bindgen_test") {
  deps = [ ":bindgen_test_lib" ]
  sources = [ "main.rs" ]
  crate_root = "main.rs"
}
