# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/rust.gni")
import("//build/rust/rust_unit_tests_group.gni")

group("tests") {
  # Build some minimal binaries to exercise the Rust toolchain
  # only if that toolchain is enabled in gn args.
  testonly = true

  deps = [ ":deps" ]
  if (build_rust_unit_tests) {
    deps += [ ":build_rust_tests" ]
  }
}

group("deps") {
  testonly = true

  # These should build with or without Rust, in different modes
  deps = [
    "test_mixed_component:test_mixed_component_demo",
    "test_mixed_executable",
    "test_mixed_testonly_executable",
    "test_variable_source_set:test_variable_source_set_demo",
  ]

  # All the rest require Rust.
  if (toolchain_has_rust) {
    deps += [
      "test_cpp_including_rust",
      "test_mixed_source_set",
      "test_rlib_crate",
      "test_rust_source_set",
      "test_serde_jsonrc",
    ]
    if (build_rust_unit_tests) {
      deps += [
        "test_cpp_including_rust:test_cpp_including_rust_unittests",
        "test_mixed_component:test_mixed_component_rs_unittests",
        "test_mixed_source_set:test_mixed_source_set_rs_unittests",
        "test_rlib_crate:test_rlib_crate_unittests",
        "test_rust_exe:test_rust_exe_unittests",
        "test_rust_source_set:test_rust_source_set_unittests",
        "test_rust_source_set_non_standard_arrangement:foo_tests",
        "test_rust_unittests",
        "test_variable_source_set:test_variable_source_set_rs_unittests",
      ]
    }
    if (rustc_can_link) {
      deps += [
        "test_bin_crate",
        "test_rlib_crate:test_rlib_crate_associated_bin",
        "test_rust_exe",
        "test_rust_multiple_dep_versions_exe",
        "test_simple_rust_exe",
        "//third_party/rust/bindgen/v0_59:bindgen",
      ]
    }
  }
}

if (build_rust_unit_tests) {
  # A group covering all native Rust unit tests under //build/rust directory.
  rust_unit_tests_group("build_rust_tests") {
    deps = [ ":deps" ]
  }
}
