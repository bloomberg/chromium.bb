# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Projector is ChromeOS only")

js_type_check("closure_compile") {
  is_polymer3 = true
  deps = [
    ":message_types",
    ":projector_browser_proxy",
    ":trusted_annotator_closure_compile",
    ":trusted_app_closure_compile",
    ":untrusted_annotator_closure_compile",
    ":untrusted_app_closure_compile",
  ]
}

js_library("projector_browser_proxy") {
  sources = [ "communication/projector_browser_proxy.js" ]
}

js_library("message_types") {
  sources = [ "communication/message_types.js" ]
}

js_library("trusted_annotator_closure_compile") {
  sources = [
    "annotator/annotator_embedder_impl.js",
    "annotator/trusted/trusted_annotator_comm_factory.js",
  ]
  deps = [
    ":projector_browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:post_message_api_client.m",
    "//ui/webui/resources/js:post_message_api_request_handler.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  externs_list = [ "communication/projector_app.externs.js" ]
}

js_library("trusted_app_closure_compile") {
  sources = [
    "app/embedder.js",
    "app/trusted/trusted_app_comm_factory.js",
  ]
  deps = [
    ":projector_browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:post_message_api_client.m",
    "//ui/webui/resources/js:post_message_api_request_handler.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
}

js_library("untrusted_annotator_closure_compile") {
  sources = [ "annotator/untrusted/untrusted_annotator_comm_factory.js" ]
  deps = [
    "//ui/webui/resources/js:post_message_api_client.m",
    "//ui/webui/resources/js:post_message_api_request_handler.m",
  ]
  externs_list = [ "communication/projector_app.externs.js" ]
}

js_library("untrusted_app_closure_compile") {
  sources = [ "app/untrusted/untrusted_app_comm_factory.js" ]
  deps = [
    ":message_types",
    "//ui/webui/resources/js:post_message_api_client.m",
    "//ui/webui/resources/js:post_message_api_request_handler.m",
  ]
}

generate_grd("build_untrusted_grd") {
  input_files = [
    "annotator/annotator.html",
    "annotator/untrusted/untrusted_annotator_comm_factory.js",
    "app/index.html",
    "app/sandboxed_load_time_data.js",
    "app/untrusted/untrusted_app_comm_factory.js",
    "communication/message_types.js",
  ]

  input_files_base_dir = rebase_path(".", "//")
  grd_prefix = "ash_projector_app_untrusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}

generate_grd("build_trusted_grd") {
  input_files = [
    "annotator/annotator_embedder.html",
    "annotator/annotator_embedder.css",
    "annotator/annotator_embedder_impl.js",
    "annotator/trusted/trusted_annotator_comm_factory.js",
    "app/trusted/trusted_app_comm_factory.js",
    "communication/projector_browser_proxy.js",
    "app/embedder.html",
    "app/embedder.css",
    "app/embedder.js",
    "assets/icon_16.png",
    "assets/icon_32.png",
    "assets/icon_48.png",
    "assets/icon_64.png",
    "assets/icon_96.png",
    "assets/icon_128.png",
    "assets/icon_192.png",
    "assets/icon_256.png",
  ]
  input_files_base_dir = rebase_path(".", "//")
  grd_prefix = "ash_projector_app_trusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}
