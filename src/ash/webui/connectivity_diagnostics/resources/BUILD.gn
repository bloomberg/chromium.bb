# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash)

preprocess_folder = "preprocessed"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

js_type_check("closure_compile_module") {
  is_polymer3 = true
  closure_flags = default_closure_args + mojom_js_args
  deps = [ ":connectivity_diagnostics" ]
}

js_library("connectivity_diagnostics") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_components/chromeos/network_health:network_diagnostics",
    "//ui/webui/resources/cr_components/chromeos/network_health:network_health_summary",
    "//ui/webui/resources/cr_elements:cr_container_shadow_behavior.m",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]

  externs_list = [ "$externs_path/chrome_send.js" ]
}

generate_grd("build_grd") {
  deps = [ ":preprocess_generated" ]
  manifest_files = [ "$target_gen_dir/$preprocess_gen_manifest" ]
  input_files_base_dir = rebase_path(".", "//")
  input_files = [
    "index.html",
    "app_icon_192.png",
    "app_icon_256.png",
  ]
  grd_prefix = "connectivity_diagnostics"
  out_grd = "$target_gen_dir/connectivity_diagnostics_resources.grd"
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = [ "connectivity_diagnostics.js" ]
}

html_to_js("web_components") {
  js_files = [ "connectivity_diagnostics.js" ]
}
