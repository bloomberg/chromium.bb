# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [ "shimless_rma.mojom" ]

  public_deps = [ "//mojo/public/mojom/base" ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "ash.shimless_rma.mojom.CalibrationComponent"
          cpp = "::rmad::CheckCalibrationState_CalibrationStatus_Component"
        },
        {
          mojom = "ash.shimless_rma.mojom.ComponentRepairStatus"
          cpp =
              "::rmad::ComponentsRepairState_ComponentRepairStatus_RepairStatus"
        },
        {
          mojom = "ash.shimless_rma.mojom.ComponentType"
          cpp = "::rmad::RmadComponent"
        },
        {
          mojom = "ash.shimless_rma.mojom.Component"
          cpp = "::rmad::ComponentsRepairState_ComponentRepairStatus"
        },
        {
          mojom = "ash.shimless_rma.mojom.ProvisioningStep"
          cpp = "::rmad::ProvisionDeviceState_ProvisioningStep"
        },
        {
          mojom = "ash.shimless_rma.mojom.RmadErrorCode"
          cpp = "::rmad::RmadErrorCode"
        },
        {
          mojom = "media.mojom.CdmKeyInformation"
          cpp = "::std::unique_ptr<::media::CdmKeyInformation>"
          move_only = true
        },
        {
          mojom = "media.mojom.CdmKeyStatus"
          cpp = "::media::CdmKeyInformation::KeyStatus"
        },
      ]
      traits_headers = [
        "shimless_rma_mojom_traits.h",
        "//chromeos/dbus/rmad/rmad.pb.h",
      ]
      traits_sources = [ "shimless_rma_mojom_traits.cc" ]
      traits_public_deps = [ "//chromeos/dbus/rmad:rmad_proto" ]
    },
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "shimless_rma_mojom_traits_unittest.cc" ]

  deps = [
    ":mojom",
    "//base",
    "//base/test:test_support",
    "//chromeos/dbus/rmad",
    "//chromeos/dbus/rmad:rmad_proto",
    "//content/test:test_support",
    "//services/data_decoder/public/cpp:test_support",
    "//services/device/public/cpp:test_support",
    "//testing/gtest",
    "//ui/gfx",
    "//ui/shell_dialogs",
    "//ui/webui",
  ]
}
