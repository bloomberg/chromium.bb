# Copyright 2021 The Chromium Authors.All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Non-ChromeOS builds cannot depend on //ash")

preprocessed_dir = "preprocessed"
preprocessed_gen_manifest = "preprocessed_gen_manifest.json"

polymer_element_files = [
  "confirmation_page.js",
  "help_resources_icons.js",
  "os_feedback_shared_css.js",
  "search_page.js",
]

generate_grd("build_grd") {
  input_files = [
    "app_icon_48.png",
    "app_icon_192.png",
    "app_icon_256.png",
    "feedback_types.js",
    "index.html",
    "mojo_interface_provider.js",
  ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [ ":preprocess_generated" ]
  manifest_files = [ "$target_gen_dir/$preprocessed_gen_manifest" ]
  grd_prefix = "ash_os_feedback"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  closure_flags = default_closure_args
  deps = [
    ":confirmation_page",
    ":feedback_types",
    ":mojo_interface_provider",
    ":search_page",
  ]
}

js_library("confirmation_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}
js_library("feedback_types") {
}

js_library("help_resources_icons") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("mojo_interface_provider") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("search_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocessed_dir"
  out_manifest = "$target_gen_dir/$preprocessed_gen_manifest"
  in_files = polymer_element_files
}

html_to_js("web_components") {
  js_files = polymer_element_files
}
