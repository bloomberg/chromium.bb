# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos, "Sample System Web App is Chrome OS only")
assert(!is_official_build,
       "Sample System Web App is only built for unofficial builds")

static_library("sample_system_web_app_ui") {
  sources = [
    "sample_page_handler.cc",
    "sample_page_handler.h",
    "sample_system_web_app_ui.cc",
    "sample_system_web_app_ui.h",
    "untrusted_sample_system_web_app_ui.cc",
    "untrusted_sample_system_web_app_ui.h",
    "url_constants.cc",
    "url_constants.h",
  ]

  deps = [
    "//ash/webui/resources:sample_system_web_app_resources",
    "//ash/webui/resources:sample_system_web_app_untrusted_resources",
    "//ash/webui/sample_system_web_app_ui/mojom",
    "//content/public/browser",
    "//ui/webui",
  ]
}

js_type_check("closure_compile") {
  deps = [
    ":trusted",
    ":untrusted",
  ]
  closure_flags = default_closure_args + mojom_js_args
}

js_library("untrusted") {
  sources = [ "resources/untrusted.js" ]
}

js_library("trusted") {
  sources = [ "resources/page_handler.js" ]
  deps = [ "//ash/webui/sample_system_web_app_ui/mojom:mojom_webui_js" ]
}

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [ "test/sample_system_web_app_ui_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

# Prefix for the generated variables, in this case
# `IDR_ASH_SAMPLE_SYSTEM_WEB_APP_`.
grd_prefix = "ash_sample_system_web_app"
mojo_grdp_file = "$target_gen_dir/sample_system_web_app_mojo_resources.grdp"

# Rule to generate a grdp with all the generated mojo bindings.
generate_grd("build_mojo_grdp") {
  out_grd = mojo_grdp_file
  grd_prefix = grd_prefix

  deps = [ "//ash/webui/sample_system_web_app_ui/mojom:mojom_webui_js" ]

  # Flatten out the dependency tree of your mojom and add any files not included
  # in `chrome://resources` here.
  input_files = [ "ash/webui/sample_system_web_app_ui/mojom/sample_system_web_app_ui.mojom-webui.js" ]

  input_files_base_dir =
      rebase_path("$root_gen_dir/mojom-webui", "$root_build_dir")
}

generate_grd("build_untrusted_grd") {
  input_files_base_dir = rebase_path("resources", "//")
  input_files = [
    "untrusted.html",
    "untrusted.js",
  ]
  grd_prefix = "ash_sample_system_web_app_untrusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}

generate_grd("build_trusted_grd") {
  input_files_base_dir = rebase_path("resources", "//")
  input_files = [
    "app_icon_192.png",
    "component_playground.html",
    "index.html",
    "sandbox.html",
    "main.js",
    "worker.js",
    "page_handler.js",
    "timer.html",
    "timer.js",
  ]

  # Manifest for generated files generated by resources:component_playground.
  manifest_files = [ "$target_gen_dir/resources/build_manifest.json" ]
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  deps = [
    ":build_mojo_grdp",
    "resources:component_playground",
  ]
  grdp_files = [ mojo_grdp_file ]
}
