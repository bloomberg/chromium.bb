# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/test/base/js2gtest.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Sample System Web App is ash-chrome only")
assert(!is_official_build,
       "Sample System Web App is only built for unofficial builds")

static_library("sample_system_web_app_ui") {
  sources = [
    "sample_page_handler.cc",
    "sample_page_handler.h",
    "sample_system_web_app_ui.cc",
    "sample_system_web_app_ui.h",
    "untrusted_sample_system_web_app_ui.cc",
    "untrusted_sample_system_web_app_ui.h",
    "url_constants.cc",
    "url_constants.h",
  ]

  deps = [
    "//ash/webui/resources:sample_system_web_app_resources",
    "//ash/webui/resources:sample_system_web_app_untrusted_resources",
    "//ash/webui/sample_system_web_app_ui/mojom:trusted",
    "//ash/webui/sample_system_web_app_ui/mojom:untrusted",
    "//content/public/browser",
    "//ui/webui",
  ]
}

js_type_check("closure_compile") {
  deps = [
    ":trusted",
    ":untrusted",
  ]
  closure_flags = default_closure_args + mojom_js_args
}

js_library("untrusted") {
  sources = [ "resources/untrusted.js" ]
}

js_library("trusted") {
  sources = [ "resources/page_handler.js" ]
  deps = [ "//ash/webui/sample_system_web_app_ui/mojom:trusted_webui_js" ]
}

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [ "test/sample_system_web_app_ui_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

# Rules to generate grdp files for the generated mojo bindings.
grd_prefix = "ash_sample_system_web_app"

trusted_mojo_grdp =
    "$target_gen_dir/sample_system_web_app_trusted_mojo_resources.grdp"
generate_grd("build_trusted_mojo_grdp") {
  out_grd = trusted_mojo_grdp
  grd_prefix = grd_prefix

  deps = [ "//ash/webui/sample_system_web_app_ui/mojom:trusted_webui_js" ]

  # Flatten out the dependency tree of your mojom and add generated bindings
  # file here.
  input_files = [ "ash/webui/sample_system_web_app_ui/mojom/sample_system_web_app_ui.mojom-webui.js" ]

  input_files_base_dir =
      rebase_path("$root_gen_dir/mojom-webui", "$root_build_dir")
}

shared_mojo_grdp =
    "$target_gen_dir/sample_system_web_app_shared_mojo_resources.grdp"
generate_grd("build_shared_mojo_grdp") {
  grd_prefix = grd_prefix
  out_grd = shared_mojo_grdp

  deps = [ "//ash/webui/sample_system_web_app_ui/mojom:shared_webui_js" ]

  # Flatten out the dependency tree of your mojom and add generated bindings
  # file here.
  input_files = [ "ash/webui/sample_system_web_app_ui/mojom/sample_system_web_app_shared_ui.mojom-webui.js" ]

  input_files_base_dir =
      rebase_path("$root_gen_dir/mojom-webui", "$root_build_dir")
}

untrusted_mojo_grdp =
    "$target_gen_dir/sample_system_web_app_untrusted_mojo_resources.grdp"
generate_grd("build_untrusted_mojo_grdp") {
  grd_prefix = grd_prefix
  out_grd = untrusted_mojo_grdp

  deps = [ "//ash/webui/sample_system_web_app_ui/mojom:untrusted_webui_js" ]

  # Flatten out the dependency tree of your mojom and add generated bindings
  # file here.
  input_files = [ "ash/webui/sample_system_web_app_ui/mojom/sample_system_web_app_untrusted_ui.mojom-webui.js" ]

  input_files_base_dir =
      rebase_path("$root_gen_dir/mojom-webui", "$root_build_dir")
}

generate_grd("build_untrusted_grd") {
  input_files_base_dir = rebase_path("resources", "//")
  input_files = [
    "untrusted.html",
    "untrusted.js",
    "untrusted_page_interface.js",
  ]
  grd_prefix = "ash_sample_system_web_app_untrusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"

  deps = [
    ":build_shared_mojo_grdp",
    ":build_untrusted_mojo_grdp",
  ]
  grdp_files = [
    shared_mojo_grdp,
    untrusted_mojo_grdp,
  ]
}

generate_grd("build_trusted_grd") {
  input_files_base_dir = rebase_path("resources", "//")
  input_files = [
    "app_icon_192.png",
    "component_playground.html",
    "index.html",
    "inter_frame_communication.html",
    "inter_frame_communication.js",
    "main.js",
    "worker.js",
    "page_handler.js",
    "timer.html",
    "timer.js",
  ]

  # Manifest for generated files generated by resources:component_playground.
  manifest_files = [ "$target_gen_dir/resources/build_manifest.json" ]
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  deps = [
    ":build_shared_mojo_grdp",
    ":build_trusted_mojo_grdp",
    "resources:component_playground",
  ]
  grdp_files = [
    shared_mojo_grdp,
    trusted_mojo_grdp,
  ]
}
