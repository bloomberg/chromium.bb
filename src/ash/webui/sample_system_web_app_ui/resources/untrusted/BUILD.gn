# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Sample System Web App is ash-chrome only")
assert(!is_official_build,
       "Sample System Web App is only built for unofficial builds")

generate_grd("untrusted_grd") {
  grd_prefix = "ash_sample_system_web_app_untrusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"

  input_files = [ "untrusted.html" ]
  input_files_base_dir = rebase_path(".", "//")

  # tsconfig.manifest is generated by :untrusted_ts and adds all of the
  # in_files to the grd.
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]

  # Flatten out the dependency tree of your mojom and add generated bindings
  # grdp files here.
  # TODO(crbug.com/1002798): We could add a mojo_grdp_deps variable to
  # generate_grd that automatically adds these grdp files when you add a
  # X_webui_grdp target to it.
  grdp_files = [
    "$target_gen_dir/../../mojom/shared_webui_resources.grdp",
    "$target_gen_dir/../../mojom/untrusted_webui_resources.grdp",
  ]

  deps = [
    ":untrusted_ts",
    "../../mojom:shared_webui_grdp",
    "../../mojom:untrusted_webui_grdp",
  ]
}

ts_library("untrusted_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"

  in_files = [
    "untrusted.ts",
    "untrusted_page_interface.ts",
  ]

  composite = true
  tsconfig_base = "../../tsconfig_base.json"

  # Allows TSC to check the generated mojom-webui/ directory when it encounters
  # imports.
  path_mappings =
      [ "/*|" + rebase_path("$root_gen_dir/mojom-webui/*", target_gen_dir) ]

  deps = [ "../../mojom:mojom_ts" ]
}
