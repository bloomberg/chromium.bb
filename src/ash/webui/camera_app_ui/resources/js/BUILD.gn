# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ash/webui/camera_app_ui/resources/images/images.gni")
import("//ash/webui/camera_app_ui/resources/js/js.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/closure_args.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/typescript/ts_definitions.gni")
import("//tools/typescript/ts_library.gni")

assert(is_chromeos_ash)

action("gen_preload_images_js") {
  script = "../utils/gen_preload_images_js.py"
  inputs = []
  foreach(image, in_app_images) {
    inputs += [ "../images/$image" ]
  }

  outputs = [ "$target_gen_dir/preload_images.js" ]
  response_file_contents = inputs
  args = [
    "--output_file",
    rebase_path(outputs[0], root_build_dir),
    "--images_list_file={{response_file_name}}",
  ]
}

js_library("preload_images") {
  extra_deps = [ ":gen_preload_images_js" ]
  sources = [ "$target_gen_dir/preload_images.js" ]
  deps = []
}

copy("copy_sources") {
  sources = compile_js_files
  outputs = [ "{{source_gen_dir}}/{{source_file_part}}" ]
}

template("copy_generated_mojo") {
  forward_variables_from(invoker, [ "deps" ])

  copy(target_name) {
    dir = invoker.dir
    sources = []
    foreach(file, invoker.mojo_files) {
      sources += [ "$root_gen_dir/mojom-webui/$dir/$file" ]
    }
    outputs = [ "$target_gen_dir/mojom-webui/$dir/{{source_file_part}}" ]
  }
}

copy_generated_mojo("copy_mojo_camera_app_ui") {
  deps = [ "//ash/webui/camera_app_ui:mojo_bindings_js" ]
  dir = "ash/webui/camera_app_ui"
  mojo_files = [ "camera_app_helper.mojom-webui.js" ]
}

copy_generated_mojo("copy_mojo_arc") {
  deps = [ "//ash/components/arc/mojom:camera_intent_js" ]
  dir = "ash/components/arc/mojom"
  mojo_files = [ "camera_intent.mojom-webui.js" ]
}

copy_generated_mojo("copy_mojo_image_caputre") {
  deps = [ "//media/capture/mojom:image_capture_js" ]
  dir = "media/capture/mojom"
  mojo_files = [ "/image_capture.mojom-webui.js" ]
}

copy_generated_mojo("copy_mojo_camera_common") {
  deps = [ "//media/capture/video/chromeos/mojom:cros_camera_common_js" ]
  dir = "media/capture/video/chromeos/mojom"
  mojo_files = [
    "camera3.mojom-webui.js",
    "camera_app.mojom-webui.js",
    "camera_common.mojom-webui.js",
    "camera_metadata.mojom-webui.js",
    "camera_metadata_tags.mojom-webui.js",
  ]
}

copy_generated_mojo("copy_mojo_document_scanner") {
  deps = [ "//chromeos/services/machine_learning/public/mojom:document_scanner_param_types_js" ]
  dir = "chromeos/services/machine_learning/public/mojom"
  mojo_files = [ "document_scanner_param_types.mojom-webui.js" ]
}

ts_definitions("build_mojo_dts") {
  extra_deps = [
    ":copy_mojo_arc",
    ":copy_mojo_camera_app_ui",
    ":copy_mojo_camera_common",
    ":copy_mojo_document_scanner",
    ":copy_mojo_image_caputre",
  ]

  root_dir = target_gen_dir
  out_dir = root_dir

  js_files = []
  foreach(dep, extra_deps) {
    js_files += rebase_path(get_target_outputs(dep), root_dir, root_build_dir)
  }

  extra_deps += [ "//ui/webui/resources/mojo:library" ]
}

ts_library("build_ts") {
  deps = [ "//ui/webui/resources/mojo:library" ]

  tsconfig_base = "../tsconfig_base.json"

  root_dir = "$target_gen_dir/.."
  out_dir = "$target_gen_dir/tsc"

  extra_deps = [
    ":build_mojo_dts",
    ":copy_sources",
    ":preload_images",
  ]

  definitions = [ "externs/types.d.ts" ]
  foreach(file, get_target_outputs(":build_mojo_dts")) {
    # Only take the .d.ts files, and ignoring the manifest.json that is also
    # listed as output file.
    if (get_path_info(file, "extension") == "ts") {
      definitions += [ rebase_path(file) ]
    }
  }

  in_files =
      rebase_path(get_target_outputs(":copy_sources"), root_dir, root_build_dir)

  path_mappings = [ "/mojom-webui/*|mojom-webui/*" ]
}
