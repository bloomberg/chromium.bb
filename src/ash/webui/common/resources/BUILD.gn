# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/grit/repack.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash)

preprocessed_dir = "preprocessed"
preprocessed_gen_manifest = "preprocessed_gen_manifest.json"

polymer_element_files = [
  "keyboard_diagram.js",
  "keyboard_icons.js",
  "keyboard_key.js",
  "navigation_icons.js",
  "navigation_selector.js",
  "navigation_shared_vars.js",
  "navigation_view_panel.js",
  "page_toolbar.js",
]

generate_grd("build_grdp") {
  input_files = [
    "fake_method_resolver.js",
    "fake_observables.js",
    "keyboard_layouts.js",
    "mojo_utils.js",
  ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [ ":preprocess_generated" ]
  manifest_files = [ "$target_gen_dir/$preprocessed_gen_manifest" ]
  grd_prefix = "ash_common"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grdp"
  resource_path_prefix = "ash/common"
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":fake_method_resolver",
    ":fake_observables",
    ":keyboard_diagram",
    ":keyboard_icons",
    ":keyboard_key",
    ":keyboard_layouts",
    ":mojo_utils",
    ":navigation_selector",
    ":navigation_view_panel",
    ":page_toolbar",
  ]
}

js_library("fake_observables") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("fake_method_resolver") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("keyboard_diagram") {
  deps = [
    ":keyboard_key",
    ":keyboard_layouts",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
}

js_library("keyboard_icons") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("keyboard_key") {
  deps = [
    ":keyboard_icons",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
}

js_library("keyboard_layouts") {
  sources = [ "keyboard_layouts.js" ]
}

js_library("mojo_utils") {
  deps = [ "//ash/webui/common/mojom:mojom_js_library_for_compile" ]
}

js_library("navigation_selector") {
  deps = [
    ":navigation_icons",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("navigation_shared_vars") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("navigation_view_panel") {
  deps = [
    ":navigation_selector",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("navigation_icons") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("page_toolbar") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocessed_dir"
  out_manifest = "$target_gen_dir/$preprocessed_gen_manifest"
  in_files = polymer_element_files
}

html_to_js("web_components") {
  js_files = polymer_element_files
}
