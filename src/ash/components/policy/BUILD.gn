# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash, "Non-Chrome-OS builds must not depend on //ash")

component("policy") {
  output_name = "ash_policy"  # Avoid conflict with //sandbox/policy
  defines = [ "IS_ASH_POLICY_IMPL" ]

  public_deps = [
    "//components/policy/proto",
    "//third_party/abseil-cpp:absl",
  ]
  deps = [
    "//base",
    "//base:i18n",
    "//third_party/icu",
  ]
  sources = [
    "weekly_time/time_utils.cc",
    "weekly_time/time_utils.h",
    "weekly_time/weekly_time.cc",
    "weekly_time/weekly_time.h",
    "weekly_time/weekly_time_interval.cc",
    "weekly_time/weekly_time_interval.h",
  ]
}

source_set("unit_tests") {
  testonly = true
  deps = [
    ":policy",
    "//base",
    "//base:i18n",
    "//base/test:test_support",
    "//components/policy/proto",
    "//testing/gtest",
    "//third_party/icu",
  ]
  sources = [
    "weekly_time/time_utils_unittest.cc",
    "weekly_time/weekly_time_interval_unittest.cc",
    "weekly_time/weekly_time_unittest.cc",
  ]
}
