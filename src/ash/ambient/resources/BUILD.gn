# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cipd/cipd.gni")
import("//build/config/chrome_build.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//tools/grit/grit_rule.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

# See DEPS file. Both of these are prerequisites for downloading Lottie
# ambient mode resources from CIPD.
assert(is_chromeos_ash && is_chrome_branded)

lottie_resources_grd_file = "$target_gen_dir/lottie_resources.grd"

generate_grd("build_lottie_grd") {
  out_grd = lottie_resources_grd_file
  grd_prefix = "ash_ambient_lottie"
  input_files = [
    "lottie/feel_the_breeze/animation.json",
    "lottie/feel_the_breeze/bg.png",
    "lottie/feel_the_breeze/clips_bottom.png",
    "lottie/feel_the_breeze/clips_top.png",
    "lottie/feel_the_breeze/shadow_1.png",
    "lottie/feel_the_breeze/shadow_2.png",
    "lottie/feel_the_breeze/shadow_3.png",
    "lottie/feel_the_breeze/shadow_4.png",
    "lottie/feel_the_breeze/shadow_5.png",
    "lottie/feel_the_breeze/shadow_6.png",
    "lottie/feel_the_breeze/time_weather.png",
    "lottie/feel_the_breeze/tree_shadow.png",
  ]
  input_files_base_dir = rebase_path(".", "//")
}

grit("lottie_resources") {
  # Required since the .grd file is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = lottie_resources_grd_file
  deps = [ ":build_lottie_grd" ]
  outputs = [
    "grit/ash_ambient_lottie_resources.h",
    "grit/ash_ambient_lottie_resources_map.cc",
    "grit/ash_ambient_lottie_resources_map.h",
    "ash_ambient_lottie_resources.pak",
  ]
}
