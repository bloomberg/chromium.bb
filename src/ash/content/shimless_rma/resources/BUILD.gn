# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocessed_dir = "preprocessed"
preprocessed_gen_manifest = "preprocessed_gen_manifest.json"

polymer_element_files = [
  "base_page.js",
  "onboarding_landing_page.js",
  "onboarding_update_page.js",
  "shimless_rma.js",
  "shimless_rma_shared_css.js",
]

generate_grd("build_grd") {
  input_files = [
    "app_icon_192.png",
    "fake_shimless_rma_service.js",
    "index.html",
    "mojo_interface_provider.js",
    "shimless_rma_types.js",
  ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [ ":preprocess_generated" ]
  manifest_files = [ "$target_gen_dir/$preprocessed_gen_manifest" ]
  grd_prefix = "ash_shimless_rma"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  closure_flags = default_closure_args
  deps = [
    ":base_page",
    ":fake_shimless_rma_service",
    ":mojo_interface_provider",
    ":onboarding_landing_page",
    ":shimless_rma",
    ":shimless_rma_types",
  ]
}

js_library("shimless_rma") {
  deps = [
    ":mojo_interface_provider",
    ":onboarding_landing_page",
    ":onboarding_update_page",
    ":shimless_rma_types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
  ]
}

js_library("base_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("fake_shimless_rma_service") {
  deps = [
    ":shimless_rma_types",
    "//ash/content/common/resources:fake_method_resolver",
    "//ash/content/common/resources:fake_observables",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("mojo_interface_provider") {
  deps = [
    ":fake_shimless_rma_service",
    ":shimless_rma_types",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("onboarding_landing_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("onboarding_update_page") {
  deps = [
    ":mojo_interface_provider",
    ":shimless_rma_types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("shimless_rma_types") {
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocessed_dir"
  out_manifest = "$target_gen_dir/$preprocessed_gen_manifest"
  in_files = polymer_element_files
}

html_to_js("web_components") {
  js_files = polymer_element_files
}
