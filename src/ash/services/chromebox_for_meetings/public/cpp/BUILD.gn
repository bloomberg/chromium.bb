# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chromeos/components/chromebox_for_meetings/buildflags/buildflags.gni")

assert(is_chromeos_ash, "Non-ChromeOS builds cannot depend on //ash")

source_set("cpp") {
  sources = [
    "appid_util.cc",
    "appid_util.h",
  ]
  deps = [
    "//ash/services/chromebox_for_meetings/public/mojom",
    "//base",
  ]
  if (is_cfm) {
    sources += [
      "service_connection.cc",
      "service_connection.h",
    ]
    deps += [ "//chromeos/ash/components/dbus/chromebox_for_meetings" ]
    public_deps = [
      "//chromeos/components/chromebox_for_meetings/buildflags",
      "//chromeos/components/chromebox_for_meetings/features",
    ]
  }
}

if (is_cfm) {
  source_set("test_support") {
    testonly = true
    sources = [
      "fake_service_connection.cc",
      "fake_service_connection.h",
      "fake_service_context.cc",
      "fake_service_context.h",
    ]
    deps = [
      ":cpp",
      "//ash/services/chromebox_for_meetings/public/mojom",
      "//base",
      "//chromeos/ash/components/dbus/chromebox_for_meetings",
      "//mojo/public/cpp/bindings",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "appid_util_unittest.cc" ]
  deps = [
    ":cpp",
    "//ash/services/chromebox_for_meetings/public/mojom",
    "//base/test:test_support",
    "//mojo/core/embedder",
    "//mojo/public/cpp/bindings",
    "//testing/gtest",
  ]
  if (is_cfm) {
    sources += [ "service_connection_unittest.cc" ]
    deps += [
      ":test_support",
      "//chromeos/ash/components/dbus/chromebox_for_meetings",
    ]
  }
}
