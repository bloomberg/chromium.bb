# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//mojo/public/tools/bindings/mojom.gni")

assert(is_chromeos_ash, "Non-ChromeOS builds cannot depend on //ash")

mojom("mojom") {
  sources = [ "device_sync.mojom" ]

  public_deps = [
    "//chromeos/components/multidevice/mojom",
    "//mojo/public/mojom/base",
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "chromeos.device_sync.mojom.ConnectivityStatus"
          cpp = "cryptauthv2::ConnectivityStatus"
        },
        {
          mojom = "chromeos.device_sync.mojom.FeatureStatusChange"
          cpp = "chromeos::device_sync::FeatureStatusChange"
        },
        {
          mojom = "chromeos.device_sync.mojom.CryptAuthService"
          cpp = "cryptauthv2::TargetService"
        },
      ]
      traits_headers = [ "device_sync_mojom_traits.h" ]
      traits_sources = [ "device_sync_mojom_traits.cc" ]
      traits_public_deps = [
        "//ash/services/device_sync:feature_status_change",
        "//ash/services/device_sync/proto",
      ]
    },
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "device_sync_mojom_traits_unittest.cc" ]

  deps = [
    ":mojom",
    "//ash/services/device_sync:feature_status_change",
    "//ash/services/device_sync/proto",
    "//base",
    "//base/test:test_support",
    "//mojo/public/cpp/test_support:test_utils",
    "//testing/gtest",
  ]
}
