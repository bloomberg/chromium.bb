# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromecast_build.gni")
import("//build/config/chromeos/args.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//build/config/features.gni")
import("//build/config/sanitizers/sanitizers.gni")

declare_args() {
  # Enable basic printing support and UI.
  enable_basic_printing = !is_chromecast && !is_ios && !is_fuchsia
}

declare_args() {
  # Enable printing with print preview.
  enable_print_preview = enable_basic_printing && !is_android

  if (use_fuzzing_engine && (is_linux || is_chromeos)) {
    # For fuzzing, just restrict to chromeos and linux.
    use_cups = true
  } else {
    use_cups = (is_chromeos_device || (is_linux || is_chromeos_lacros) ||
                is_mac) && !is_chromecast && !is_fuchsia
  }
}

declare_args() {
  # Enable exporting to tagged PDF.
  enable_tagged_pdf = enable_print_preview
}

declare_args() {
  # Enable the CUPS IPP printing backend.
  # TODO(crbug.com/226176): Remove this after CUPS PPD API calls are removed.
  use_cups_ipp = use_cups && !is_linux
}
