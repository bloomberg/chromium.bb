# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/build/config/remoting_build.gni")

source_set("file_transfer") {
  sources = [
    "ensure_user_mac.cc",
    "ensure_user_win.cc",
    "file_chooser_chromeos.cc",
    "file_chooser_common_win.h",
    "file_chooser_mac.mm",
    "file_chooser_main_win.cc",
    "file_chooser_win.cc",
    "get_desktop_directory.cc",
    "get_desktop_directory.h",
    "get_desktop_directory_win.cc",
  ]

  public_deps = [ ":common" ]

  deps = [
    "//base",
    "//remoting/protocol",
    "//remoting/resources",
  ]

  if (is_mac) {
    libs = [ "AppKit.framework" ]
  }

  if (!is_mac && !is_win) {
    sources += [ "ensure_user_no_op.cc" ]
  }

  if (is_win) {
    sources -= [ "get_desktop_directory.cc" ]
  }

  if (is_desktop_linux) {
    sources += [ "file_chooser_linux.cc" ]
    deps += [ "//build/config/linux/gtk" ]
  }
}

source_set("common") {
  public = [
    "file_operations.h",
    "file_transfer_message_handler.h",
    "ipc_file_operations.h",
    "local_file_operations.h",
    "session_file_operations_handler.h",
  ]

  sources = [
    "buffered_file_writer.cc",
    "buffered_file_writer.h",
    "ensure_user.h",
    "file_chooser.h",
    "file_transfer_message_handler.cc",
    "ipc_file_operations.cc",
    "local_file_operations.cc",
    "session_file_operations_handler.cc",
  ]

  deps = [
    "//base",
    "//remoting/protocol",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "ensure_user_no_op.cc",
    "fake_file_chooser.cc",
    "fake_file_chooser.h",
    "fake_file_operations.cc",
    "fake_file_operations.h",
    "get_desktop_directory.cc",
  ]
  deps = [
    ":common",
    "//remoting/protocol",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "buffered_file_writer_unittest.cc",
    "file_transfer_message_handler_unittest.cc",
    "ipc_file_operations_unittest.cc",
    "local_file_operations_unittest.cc",
  ]

  deps = [
    ":common",
    ":test_support",
    "//remoting/protocol",
    "//testing/gtest",
  ]
}
