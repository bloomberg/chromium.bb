# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromecast/chromecast.gni")

group("cast_core") {
  deps = [
    ":browser",
    ":grpc_webui",
    ":message_port",
    ":metrics_recorder",
    ":renderer",
    ":runtime_service",
    ":streaming_receiver_session_client",
    ":url_rewrite",
  ]

  if (is_linux) {
    deps += [ ":application_dispatch" ]
  }

  if (enable_cast_media_runtime) {
    deps += [ ":cast_runtime_content_client_factories" ]
  }
}

cast_source_set("grpc_support") {
  sources = [
    "grpc_method.cc",
    "grpc_method.h",
    "grpc_server.cc",
    "grpc_server.h",
    "simple_async_grpc.h",
  ]

  public_deps = [
    "//base",
    "//third_party/grpc:grpc++",
  ]
}

cast_source_set("grpc_impl") {
  sources = [
    "runtime_application_service_grpc_impl.cc",
    "runtime_application_service_grpc_impl.h",
    "runtime_message_port_application_service_grpc_impl.cc",
    "runtime_message_port_application_service_grpc_impl.h",
    "runtime_service_grpc_impl.cc",
    "runtime_service_grpc_impl.h",
  ]

  public_deps = [
    ":grpc_support",
    "//base",
    "//third_party/cast_core/public/src/proto/runtime:runtime_service_proto",
    "//third_party/cast_core/public/src/proto/v2:runtime_application_service_proto",
    "//third_party/cast_core/public/src/proto/v2:runtime_message_port_application_service_proto",
    "//third_party/grpc:grpc++",
  ]
}

cast_source_set("url_rewrite") {
  sources = [
    "url_rewrite_rules_adapter.cc",
    "url_rewrite_rules_adapter.h",
  ]

  public_deps = [
    "//base",
    "//chromecast/common/mojom",
    "//mojo/public/cpp/bindings",
    "//third_party/cast_core/public/src/proto/v2:url_rewrite_proto",
  ]

  deps = [ "//url" ]
}

cast_source_set("message_port") {
  sources = [
    "message_port_handler.cc",
    "message_port_handler.h",
    "message_port_service.cc",
    "message_port_service.h",
  ]

  public_deps = [
    ":grpc_support",
    "//base",
    "//components/cast/message_port",
    "//third_party/cast_core/public/src/proto/v2:core_application_service_proto",
    "//third_party/cast_core/public/src/proto/web:message_channel_proto",
  ]
}

cast_source_set("metrics_recorder") {
  sources = [
    "cast_runtime_action_recorder.cc",
    "cast_runtime_action_recorder.h",
    "cast_runtime_histogram_flattener.cc",
    "cast_runtime_histogram_flattener.h",
    "cast_runtime_metrics_recorder.cc",
    "cast_runtime_metrics_recorder.h",
    "cast_runtime_metrics_recorder_service.cc",
    "cast_runtime_metrics_recorder_service.h",
    "metrics_recorder_grpc.cc",
    "metrics_recorder_grpc.h",
  ]

  public_deps = [
    ":grpc_impl",
    "//base",
    "//chromecast/metrics:metrics_recorder",
    "//third_party/cast_core/public/src/proto/metrics:metrics_recorder_proto",
    "//third_party/grpc:grpc++",
  ]

  deps = [
    "//chromecast/base/metrics",
    "//third_party/metrics_proto",
  ]
}

cast_source_set("grpc_webui") {
  sources = [
    "grpc_resource_data_source.cc",
    "grpc_resource_data_source.h",
    "grpc_webui_controller.cc",
    "grpc_webui_controller.h",
    "grpc_webui_controller_factory.cc",
    "grpc_webui_controller_factory.h",
  ]

  deps = [
    "//base",
    "//chromecast:chromecast_buildflags",
    "//chromecast/base",
    "//chromecast/base:chromecast_switches",
    "//chromecast/base:default_create_sys_info",
    "//chromecast/browser:browser_base",
    "//chromecast/browser:public",
    "//third_party/blink/public/common:headers",
    "//third_party/cast_core/public/src/proto/v2:core_application_service_proto",
    "//third_party/grpc:grpc++",
  ]

  if (enable_chromecast_extensions) {
    sources += [
      "grpc_webui_controller_extension.cc",
      "grpc_webui_controller_extension.h",
    ]
    deps += [
      "//components/guest_view/browser",
      "//extensions/browser",
    ]
  }
}

if (is_linux) {
  cast_source_set("core_service_bindings") {
    sources = [
      "bindings_manager_web_runtime.cc",
      "bindings_manager_web_runtime.h",
    ]

    public_deps = [
      ":message_port",
      "//base",
      "//chromecast/bindings/public/mojom",
      "//components/cast/api_bindings:manager",
      "//components/cast/message_port",
      "//components/cast/message_port:message_port_cast",
      "//mojo/public/cpp/bindings",
      "//third_party/cast_core/public/src/proto/v2:core_application_service_proto",
      "//third_party/cast_core/public/src/proto/web:message_channel_proto",
    ]

    deps = [ "//chromecast/bindings:bindings_manager_cast" ]
  }

  cast_source_set("application_dispatch") {
    sources = [
      "runtime_application.cc",
      "runtime_application.h",
      "runtime_application_base.cc",
      "runtime_application_base.h",
      "runtime_application_dispatcher.cc",
      "runtime_application_dispatcher.h",
      "streaming_runtime_application.cc",
      "streaming_runtime_application.h",
      "web_runtime_application.cc",
      "web_runtime_application.h",
    ]

    public_deps = [
      ":grpc_impl",
      ":grpc_support",
      ":grpc_webui",
      ":metrics_recorder",
      "//base",
      "//chromecast/browser:public",
      "//third_party/cast_core/public/src/proto/common:application_config_proto",
      "//third_party/cast_core/public/src/proto/core:cast_core_service_proto",
      "//third_party/cast_core/public/src/proto/metrics:metrics_recorder_proto",
      "//third_party/cast_core/public/src/proto/v2:cast_message_proto",
      "//third_party/cast_core/public/src/proto/v2:core_application_service_proto",
      "//third_party/grpc:grpc++",
    ]

    deps = [
      ":core_service_bindings",
      ":streaming_receiver_session_client",
      ":url_rewrite",
      "//chromecast/browser:browser_base",
      "//components/cast/message_port:message_port_core",
      "//components/cast_streaming/browser",
      "//components/cast_streaming/public",
      "//content/public/browser",
      "//mojo/public/cpp/bindings",
      "//third_party/blink/public/common:headers",
      "//third_party/openscreen/src/cast/common:public",
    ]
  }
}

cast_source_set("renderer") {
  sources = [
    "cast_runtime_content_renderer_client.cc",
    "cast_runtime_content_renderer_client.h",
  ]

  deps = [
    "//base",
    "//chromecast/renderer:renderer_base",
    "//components/cast_streaming/renderer",
    "//content/public/renderer",
    "//media",
  ]
}

cast_source_set("browser") {
  sources = [
    "cast_core_switches.h",
    "cast_runtime_content_browser_client.cc",
    "cast_runtime_content_browser_client.h",
  ]

  deps = [
    ":runtime_service",
    "//base",
    "//chromecast/browser",
    "//chromecast/common:cors_exempt_headers",
    "//media",
  ]

  if (chromecast_branding == "public") {
    sources += [ "cast_runtime_content_browser_client_simple.cc" ]
  }
}

cast_source_set("streaming_receiver_session_client") {
  sources = [
    "streaming_receiver_session_client.cc",
    "streaming_receiver_session_client.h",
  ]

  public_deps = [
    "//base",
    "//chromecast/browser:public",
    "//components/cast/message_port",
    "//components/cast_streaming/browser",
  ]

  deps = [
    "//base",
    "//chromecast/shared:platform_info_serializer",
    "//components/cast/message_port:message_port_core",
    "//components/cast_streaming/public",
    "//components/cast_streaming/public/mojom",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//third_party/blink/public/common:headers",
  ]
}

cast_source_set("runtime_service") {
  sources = [
    "cast_runtime_service.cc",
    "cast_runtime_service.h",
  ]

  deps = [
    "//base",
    "//chromecast:chromecast_buildflags",
    "//chromecast/browser:browser_base",
    "//chromecast/metrics:cast_event_builder_simple",
    "//chromecast/service",
  ]

  public_deps = [ "//chromecast/media/cma/backend/proxy:headers" ]

  if (!enable_cast_media_runtime) {
    sources += [ "cast_runtime_service_simple.cc" ]
  } else {
    sources += [
      "cast_runtime_service_impl.cc",
      "cast_runtime_service_impl.h",
    ]

    public_deps += [
      ":application_dispatch",
      ":metrics_recorder",
    ]

    deps += [ "//chromecast/metrics:cast_event_builder_simple" ]

    if (chromecast_branding == "public") {
      sources += [ "cast_runtime_service_impl_factory.cc" ]
    }
  }
}

if (enable_cast_media_runtime) {
  cast_source_set("cast_runtime_content_client_factories") {
    sources = [
      "cast_content_browser_client_factory_runtime.cc",
      "cast_content_renderer_client_factory_runtime.cc",
    ]

    deps = [
      ":browser",
      ":renderer",
      "//base",
      "//chromecast/browser",
      "//chromecast/common:cors_exempt_headers",
      "//chromecast/renderer:renderer_base",
    ]

    if (chromecast_branding != "public") {
      deps += [ "//chromecast/internal/cast_core/services" ]
    }
  }
}

# When built for use with Cast Core, enble_cast_media_runtime must be true.
cast_executable("core_runtime_simple") {
  sources = [ "cast_runtime_main.cc" ]

  deps = [
    ":cast_core",
    "//chromecast/base:default_create_sys_info",
    "//chromecast/browser:simple_main_parts",
    "//chromecast/utility:simple_client",
    "//content/public/app",
  ]

  if (enable_cast_media_runtime) {
    deps += [ "//chromecast/app:app_core" ]
  } else {
    deps += [
      "//chromecast/app",
      "//chromecast/browser:simple_client",
    ]
  }
}

executable("demo_platform_service") {
  sources = [
    "demo_platform_service.cc",
    "demo_platform_service.h",
  ]

  public_deps = [
    "//base",
    "//third_party/cast_core/public/src/proto/core:cast_core_service_proto",
    "//third_party/cast_core/public/src/proto/platform:platform_service_proto",
  ]

  deps = [ "//third_party/grpc:grpc++" ]
}

cast_source_set("cast_core_test_utils") {
  testonly = true

  sources = [
    "cast_runtime_metrics_test_helpers.cc",
    "cast_runtime_metrics_test_helpers.h",
  ]

  deps = [
    "//third_party/cast_core/public/src/proto/metrics:metrics_recorder_proto",
    "//third_party/metrics_proto",
  ]
}

# TODO(b/194439829): Upstream additional metrics unit tests.
test("cast_cast_core_unittests") {
  sources = [
    "cast_runtime_histogram_flattener_unittest.cc",
    "grpc_resource_data_source_unittest.cc",
    "streaming_receiver_session_client_unittest.cc",
    "url_rewrite_rules_adapter_unittests.cc",
  ]

  deps = [
    ":browser",
    ":cast_core_test_utils",
    ":grpc_webui",
    ":metrics_recorder",
    ":streaming_receiver_session_client",
    ":url_rewrite",
    "//base",
    "//base/test:test_support",
    "//chromecast/base/metrics",
    "//chromecast/browser:simple_client",
    "//chromecast/browser:simple_main_parts",
    "//chromecast/browser:test_support",
    "//chromecast/shared:platform_info_serializer",
    "//components/cast_streaming/browser",
    "//components/cast_streaming/public/mojom",
    "//mojo/core/test:run_all_unittests",
    "//mojo/public/cpp/bindings",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/cast_core/public/src/proto/v2:core_application_service_proto",
    "//third_party/grpc:grpc++",
    "//third_party/inspector_protocol:crdtp",
    "//third_party/metrics_proto",
  ]
}
