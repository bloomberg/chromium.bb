import("//build/config/chromeos/ui_mode.gni")
import("//mojo/public/tools/bindings/mojom.gni")

assert(is_chromeos_ash)

mojom("nearby_share_target_types") {
  sources = [ "nearby_share_target_types.mojom" ]
}

mojom("mojom") {
  sources = [
    "nearby_connections.mojom",
    "nearby_connections_types.mojom",
    "nearby_decoder.mojom",
    "nearby_decoder_types.mojom",
    "sharing.mojom",
    "webrtc.mojom",
    "webrtc_signaling_messenger.mojom",
  ]

  public_deps = [
    ":nearby_share_target_types",
    "//device/bluetooth/public/mojom:deprecated_experimental_interfaces",
    "//mojo/public/mojom/base",
    "//services/network/public/mojom",
    "//url/mojom:url_mojom_gurl",
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "location.nearby.connections.mojom.LogSeverity"
          cpp = "api::LogMessage::Severity"
        },
      ]
      traits_headers = [ "nearby_connections_mojom_traits.h" ]
      traits_sources = [ "nearby_connections_mojom_traits.cc" ]
      traits_deps = [ "//third_party/nearby:platform_api_types" ]
    },
  ]
}
