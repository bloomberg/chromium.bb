# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromeos/components/chromebox_for_meetings/buildflags/buildflags.gni")

assert(is_chromeos, "Non-ChromeOS builds cannot depend on //chromeos")

source_set("cpp") {
  sources = [
    "appid_util.cc",
    "appid_util.h",
  ]
  deps = [
    "//base",
    "//chromeos/services/chromebox_for_meetings/public/mojom",
  ]
  if (is_cfm) {
    sources += [
      "service_connection.cc",
      "service_connection.h",
    ]
    deps += [ "//chromeos/dbus/chromebox_for_meetings" ]
    public_deps = [
      "//chromeos/components/chromebox_for_meetings/buildflags",
      "//chromeos/components/chromebox_for_meetings/features",
    ]
  }
}

if (is_cfm) {
  source_set("test_support") {
    testonly = true
    sources = [
      "fake_service_connection.cc",
      "fake_service_connection.h",
      "fake_service_context.cc",
      "fake_service_context.h",
    ]
    deps = [
      ":cpp",
      "//base",
      "//chromeos/dbus/chromebox_for_meetings",
      "//chromeos/services/chromebox_for_meetings/public/mojom",
      "//mojo/public/cpp/bindings",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "appid_util_unittest.cc" ]
  deps = [
    ":cpp",
    "//base/test:test_support",
    "//chromeos/services/chromebox_for_meetings/public/mojom",
    "//mojo/core/embedder",
    "//mojo/public/cpp/bindings",
    "//testing/gtest",
  ]
  if (is_cfm) {
    sources += [ "service_connection_unittest.cc" ]
    deps += [
      ":test_support",
      "//chromeos/dbus/chromebox_for_meetings",
    ]
  }
}
