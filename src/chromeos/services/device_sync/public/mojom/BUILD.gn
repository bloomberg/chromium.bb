# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [ "device_sync.mojom" ]

  public_deps = [
    "//chromeos/components/multidevice/mojom",
    "//mojo/public/mojom/base",
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "chromeos.device_sync.mojom.ConnectivityStatus"
          cpp = "cryptauthv2::ConnectivityStatus"
        },
        {
          mojom = "chromeos.device_sync.mojom.FeatureStatusChange"
          cpp = "chromeos::device_sync::FeatureStatusChange"
        },
        {
          mojom = "chromeos.device_sync.mojom.CryptAuthService"
          cpp = "cryptauthv2::TargetService"
        },
      ]
      traits_headers = [ "device_sync_mojom_traits.h" ]
      traits_sources = [ "device_sync_mojom_traits.cc" ]
      traits_public_deps = [
        "//chromeos/services/device_sync:feature_status_change",
        "//chromeos/services/device_sync/proto",
      ]
    },
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "device_sync_mojom_traits_unittest.cc" ]

  deps = [
    ":mojom",
    "//base",
    "//base/test:test_support",
    "//chromeos/services/device_sync:feature_status_change",
    "//chromeos/services/device_sync/proto",
    "//mojo/public/cpp/test_support:test_utils",
    "//testing/gtest",
  ]
}
