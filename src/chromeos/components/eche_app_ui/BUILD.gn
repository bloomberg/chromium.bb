# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos, "Eche App is Chrome OS only")

preprocess_mojo_manifest = "preprocessed_mojo_manifest.json"
preprocess_folder = "preprocessed"

static_library("eche_app_ui") {
  sources = [
    "eche_app_manager.cc",
    "eche_app_manager.h",
    "eche_app_ui.cc",
    "eche_app_ui.h",
    "eche_connector.cc",
    "eche_connector.h",
    "eche_feature_status_provider.cc",
    "eche_feature_status_provider.h",
    "eche_notification_click_handler.cc",
    "eche_notification_click_handler.h",
    "eche_signaler.cc",
    "eche_signaler.h",
    "feature_status.cc",
    "feature_status.h",
    "feature_status_provider.cc",
    "feature_status_provider.h",
    "url_constants.cc",
    "url_constants.h",
  ]

  deps = [
    "//ash/constants",
    "//chromeos/components/eche_app_ui/mojom",
    "//chromeos/components/eche_app_ui/proto",
    "//chromeos/components/multidevice:multidevice",
    "//chromeos/components/multidevice/logging",
    "//chromeos/components/phonehub:phonehub",
    "//chromeos/resources:eche_app_resources",
    "//chromeos/resources:eche_bundle_resources",
    "//chromeos/services/device_sync/public/cpp:cpp",
    "//chromeos/services/multidevice_setup/public/cpp:cpp",
    "//chromeos/services/secure_channel/public/cpp/client:client",
    "//content/public/browser",
    "//ui/webui",
  ]
}

static_library("test_support") {
  testonly = true

  sources = [
    "fake_feature_status_provider.cc",
    "fake_feature_status_provider.h",
  ]

  public_deps = [ ":eche_app_ui" ]

  deps = [ "//base" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "eche_feature_status_provider_unittest.cc",
    "eche_notification_click_handler_unittest.cc",
  ]
  deps = [
    ":eche_app_ui",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//chromeos/components/multidevice",
    "//chromeos/components/multidevice:test_support",
    "//chromeos/components/phonehub",
    "//chromeos/components/phonehub:debug",
    "//chromeos/services/device_sync/public/cpp",
    "//chromeos/services/device_sync/public/cpp:test_support",
    "//chromeos/services/multidevice_setup/public/cpp",
    "//chromeos/services/multidevice_setup/public/cpp:test_support",
    "//chromeos/services/secure_channel/public/cpp/client:test_support",
    "//components/prefs:test_support",
    "//testing/gtest",
  ]
}

js_type_check("closure_compile") {
  deps = [ ":app" ]
}

js_library("app") {
  deps =
      [ "//chromeos/components/eche_app_ui/mojom:mojom_js_library_for_compile" ]
  sources = [
    "resources/browser_proxy.js",
    "resources/mock/js/app_bundle.js",
  ]
}

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [ "test/eche_app_ui_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

generate_grd("build_grd") {
  input_files_base_dir = rebase_path("resources", "//")
  input_files = [ "browser_proxy.js" ]
  manifest_files = []
  grd_prefix = "chromeos_eche_app"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  deps = [ ":build_mojo_grdp" ]
  grdp_files = [ "$target_gen_dir/chromeos_eche_app_mojo_resources.grdp" ]
  resource_path_rewrites = [ "chromeos/components/eche_app_ui/mojom/eche_app.mojom-lite.js|mojo/eche_app.mojom-lite.js" ]
}

generate_grd("build_mojo_grdp") {
  grd_prefix = "chromeos_eche_app"
  out_grd = "$target_gen_dir/${grd_prefix}_mojo_resources.grdp"
  deps = [ ":preprocess_mojo" ]
  manifest_files = [ "$target_gen_dir/$preprocess_mojo_manifest" ]
  resource_path_rewrites = [ "chromeos/components/eche_app_ui/mojom/eche_app.mojom-lite.js|mojo/eche_app.mojom-lite.js" ]
}

# Mojo files, for generating grdp
preprocess_if_expr("preprocess_mojo") {
  deps =
      [ "//chromeos/components/eche_app_ui/mojom:mojom_js_library_for_compile" ]
  in_folder = "$root_gen_dir"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_mojo_manifest"
  in_files = [ "chromeos/components/eche_app_ui/mojom/eche_app.mojom-lite.js" ]
}
