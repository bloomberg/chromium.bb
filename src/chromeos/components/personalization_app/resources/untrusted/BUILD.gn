# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

js_library("collections_grid") {
  deps = [
    ":setup",
    "../../mojom:mojom_js_library_for_compile",
    "../common:constants",
    "../common:iframe_api",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("images_grid") {
  deps = [
    ":setup",
    "../../mojom:mojom_js_library_for_compile",
    "../common:constants",
    "../common:iframe_api",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("setup") {
  deps = [
    "../common:icons",
    "../common:styles",
    "//third_party/polymer/v3_0/components-chromium/iron-icon:iron-icon",
    "//third_party/polymer/v3_0/components-chromium/iron-iconset-svg:iron-iconset-svg",
    "//third_party/polymer/v3_0/components-chromium/paper-styles:color",
    "//ui/webui/resources/js:load_time_data",
  ]
}

js_library("styles") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

# TODO(cowmoo) remove this hack when there is a better way to access //resources
# from untrusted. This is necessary for now to block unresolvable imports inside
# the generated shared_vars_css.m.js file. The output
# untrusted_shared_vars_css.js must be imported after polymer. For the full
# range of color declarations, also manually import paper-styles/color.js.
polymer_modulizer("shared_vars_css") {
  js_file = "untrusted_shared_vars_css.js"
  html_file =
      "../../../../../ui/webui/resources/cr_elements/shared_vars_css.html"
  html_type = "custom-style"
  ignore_imports = [
    "ui/webui/resources/html/polymer.html",
    "third_party/polymer/v1_0/components-chromium/paper-styles/color.html",
  ]
}

js_type_check("closure_compile") {
  is_polymer3 = true
  closure_flags = default_closure_args + [
                    "language_in=ECMASCRIPT_2020",
                    "browser_resolver_prefix_replacements=\"chrome-untrusted://personalization/polymer/v3_0/=../../third_party/polymer/v3_0/components-chromium/\"",
                    "browser_resolver_prefix_replacements=\"chrome-untrusted://personalization/polymer/v3_0/polymer/polymer_bundled.min.js=../../third_party/polymer/v3_0/components-chromium/polymer/polymer_bundled.js\"",
                  ]
  deps = [
    ":collections_grid",
    ":images_grid",
    ":setup",
    ":styles",
  ]
}

html_to_js("web_components") {
  js_files = [
    "collections_grid.js",
    "images_grid.js",
  ]
}

copy("copy_static") {
  sources = [
    "collections.html",
    "images.html",
    "setup.js",
    "styles.js",
  ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

preprocess_if_expr("preprocess") {
  deps = [
    ":copy_static",
    ":shared_vars_css_module",
    ":web_components",
  ]
  in_folder = "$target_gen_dir/.."
  out_folder = "$target_gen_dir/processed"
  out_manifest = "$target_gen_dir/manifest.json"
  in_files = [
    "untrusted/collections.html",
    "untrusted/collections_grid.js",
    "untrusted/images.html",
    "untrusted/images_grid.js",
    "untrusted/setup.js",
    "untrusted/styles.js",
    "untrusted/untrusted_shared_vars_css.js",
  ]
}
