# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")

polymer_element_files = [
  "local_images_element.js",
  "personalization_router_element.js",
  "personalization_toast_element.js",
  "wallpaper_breadcrumb_element.js",
  "wallpaper_collections_element.js",
  "wallpaper_error_element.js",
  "wallpaper_images_element.js",
  "wallpaper_selected_element.js",
]

static_files = [
  "index.html",
  "mojo_interface_provider.js",
  "personalization_actions.js",
  "personalization_app.js",
  "personalization_controller.js",
  "personalization_message_handler.js",
  "personalization_reducers.js",
  "personalization_store.js",
  "styles.js",
]

js_library("local_images_element") {
  deps = [
    ":personalization_controller",
    ":personalization_reducers",
    ":styles",
    "../../mojom:mojom_js_library_for_compile",
    "../common:constants",
    "../common:icons",
    "../common:styles",
    "//third_party/polymer/v3_0/components-chromium/iron-icon:iron-icon",
    "//third_party/polymer/v3_0/components-chromium/iron-iconset-svg:iron-iconset-svg",
    "//third_party/polymer/v3_0/components-chromium/iron-list:iron-list",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("mojo_interface_provider") {
  deps = [
    "../../mojom:mojom_js_library_for_compile",
    "../common:utils",
  ]
}

js_library("personalization_actions") {
  deps = [ "//ui/webui/resources/js/cr/ui:store.m" ]
}

js_library("personalization_app") {
  deps = [
    ":local_images_element",
    ":personalization_message_handler",
    ":personalization_reducers",
    ":personalization_router_element",
    ":personalization_store",
    ":personalization_toast_element",
    ":styles",
    ":wallpaper_breadcrumb_element",
    ":wallpaper_collections_element",
    ":wallpaper_images_element",
    ":wallpaper_selected_element",
  ]
}

js_library("personalization_controller") {
  deps = [
    ":mojo_interface_provider",
    ":personalization_actions",
    ":personalization_store",
    "../common:utils",
  ]
}

js_library("personalization_message_handler") {
  deps = [
    ":personalization_controller",
    ":personalization_router_element",
    ":personalization_store",
    "../common:constants",
    "../common:iframe_api",
  ]
}

js_library("personalization_reducers") {
  deps = [
    ":personalization_actions",
    "//ui/webui/resources/js/cr/ui:store.m",
  ]
}

js_library("personalization_router_element") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/iron-location:iron-location",
    "//third_party/polymer/v3_0/components-chromium/iron-location:iron-query-params",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("personalization_toast_element") {
  deps = [
    ":personalization_actions",
    ":personalization_store",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:load_time_data",
  ]
}

js_library("personalization_store") {
  deps = [
    ":personalization_actions",
    ":personalization_reducers",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:i18n_behavior.m",
    "//ui/webui/resources/js/cr/ui:store.m",
    "//ui/webui/resources/js/cr/ui:store_client.m",
  ]
}

js_library("styles") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("wallpaper_breadcrumb_element") {
  deps = [
    ":personalization_router_element",
    ":personalization_store",
    "../common:styles",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("wallpaper_collections_element") {
  deps = [
    ":personalization_controller",
    ":personalization_store",
    ":styles",
    ":wallpaper_error_element",
    "../common:constants",
    "../common:iframe_api",
    "../common:utils",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("wallpaper_error_element") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
}

js_library("wallpaper_images_element") {
  deps = [
    ":personalization_controller",
    ":personalization_reducers",
    ":styles",
    "../common:constants",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("wallpaper_selected_element") {
  deps = [
    ":personalization_controller",
    ":personalization_reducers",
    ":personalization_router_element",
    ":personalization_store",
    ":styles",
    "../common:icons",
    "../common:styles",
    "../common:utils",
    "//third_party/polymer/v3_0/components-chromium/iron-icon:iron-icon",
    "//third_party/polymer/v3_0/components-chromium/iron-iconset-svg:iron-iconset-svg",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_type_check("closure_compile") {
  is_polymer3 = true
  closure_flags = default_closure_args + [ "language_in=ECMASCRIPT_2020" ]
  deps = [
    ":local_images_element",
    ":mojo_interface_provider",
    ":personalization_actions",
    ":personalization_app",
    ":personalization_reducers",
    ":personalization_router_element",
    ":personalization_store",
    ":personalization_toast_element",
    ":styles",
    ":wallpaper_breadcrumb_element",
    ":wallpaper_collections_element",
    ":wallpaper_images_element",
    ":wallpaper_selected_element",
  ]
}

html_to_js("web_components") {
  js_files = polymer_element_files
}

copy("copy_static") {
  sources = static_files
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

copy("copy_mojo_to_trusted") {
  deps = [
    "../../mojom:mojom_js",
    "//mojo/public/mojom/base",
  ]
  sources = [
    "$root_gen_dir/mojo/public/mojom/base/file_path.mojom-lite.js",
    "$target_gen_dir/../../mojom/personalization_app.mojom-lite.js",
  ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

preprocess_if_expr("preprocess") {
  deps = [
    ":copy_mojo_to_trusted",
    ":copy_static",
    ":web_components",
  ]
  in_folder = "$target_gen_dir/.."
  out_folder = "$target_gen_dir/processed"
  out_manifest = "$target_gen_dir/manifest.json"
  in_files = []
  foreach(file, polymer_element_files) {
    in_files += [ "trusted/$file" ]
  }
  foreach(file, static_files) {
    in_files += [ "trusted/$file" ]
  }
  in_files += [
    "trusted/personalization_app.mojom-lite.js",
    "trusted/file_path.mojom-lite.js",
  ]
}
