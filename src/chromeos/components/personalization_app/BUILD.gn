# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")

assert(is_chromeos, "Personalization App is Chrome OS only")

static_library("personalization_app") {
  sources = [
    "personalization_app_ui.cc",
    "personalization_app_ui.h",
    "personalization_app_ui_delegate.h",
    "personalization_app_url_constants.cc",
    "personalization_app_url_constants.h",
    "untrusted_personalization_app_ui_config.cc",
    "untrusted_personalization_app_ui_config.h",
  ]

  deps = [
    "//ash/constants:constants",
    "//chromeos/components/personalization_app/mojom",
    "//chromeos/resources:personalization_app_resources",
    "//chromeos/strings",
    "//content/public/browser",
    "//ui/resources:webui_generated_resources_grd_grit",
    "//ui/webui",
  ]
}

group("closure_compile") {
  deps = [ "resources:closure_compile" ]
}

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [ "test/personalization_app_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [ ":browser_test_support" ]
}

source_set("browser_test_support") {
  testonly = true
  sources = [
    "test/fake_personalization_app_ui_delegate.cc",
    "test/fake_personalization_app_ui_delegate.h",
    "test/personalization_app_browsertest_fixture.cc",
    "test/personalization_app_browsertest_fixture.h",
  ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    ":personalization_app",
    "//chrome/test:test_support_ui",
    "//chromeos/components/personalization_app/mojom",
    "//chromeos/components/web_applications/test:test_support",
  ]

  data = []
}
