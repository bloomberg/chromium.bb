# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/tools/optimize_webui.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")

preprocess_folder = "$target_gen_dir/preprocessed"
mojom_webui_folder =
    "$root_gen_dir/mojom-webui/chromeos/components/file_manager/mojom"

js_library("crt0") {
}

js_library("browser_proxy") {
}

js_library("script_loader") {
}

js_library("main") {
}

js_library("main_js") {
  sources = [
    "browser_proxy.js",
    "main.js",
    "script_loader.js",
  ]
  deps = [ "//chromeos/components/file_manager/mojom:mojom_webui_js" ]
}

js_type_check("closure_compile") {
  deps = [
    ":main_js",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:load_time_data",
  ]
  closure_flags =
      default_closure_args + mojom_js_args + [
        "js_module_root=" + rebase_path(".", root_build_dir),
        "js_module_root=" + rebase_path(mojom_webui_folder, root_build_dir),
      ]
}

action("gen_main_html") {
  inputs = [ "//ui/file_manager/file_manager/main.html" ]

  script = "//chromeos/components/file_manager/resources/gen_main_html.py"

  args = [
    "--source",
    rebase_path("//ui/file_manager/file_manager/main.html", root_build_dir),
    "--target",
    rebase_path("$target_gen_dir/main.html", root_build_dir),
  ]

  outputs = [
    "$target_gen_dir/main.html",
    "$target_gen_dir/main.html.stamp",
  ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"  #target_gen_dir
  out_folder = preprocess_folder

  in_files = [
    "browser_proxy.js",
    "crt0.js",
    "main.js",
    "script_loader.js",
  ]

  deps = [
    ":browser_proxy",
    ":crt0",
    ":main",
    ":script_loader",
  ]
}

preprocess_if_expr("preprocess_mojo") {
  in_folder = mojom_webui_folder
  in_files = [ "file_manager.mojom-webui.js" ]
  out_folder = preprocess_folder
  deps = [ "//chromeos/components/file_manager/mojom:mojom_webui_js" ]
}

optimize_webui("build") {
  host = "file_manager"
  input = rebase_path(preprocess_folder, root_build_dir)

  js_module_in_files = [ "main.js" ]

  js_out_files = [ "main.rollup.js" ]

  excludes = [ "chrome://resources/mojo/mojo/public/js/bindings.js" ]

  deps = [
    ":preprocess",
    ":preprocess_mojo",
    "//ui/file_manager:preprocess_generated",
    "//ui/file_manager:preprocess_static",
    "//ui/webui/resources:preprocess",
  ]
}
