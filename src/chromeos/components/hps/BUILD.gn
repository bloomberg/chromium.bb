# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//chrome/test/base/js2gtest.gni")
import("//tools/typescript/ts_library.gni")

assert(is_chromeos_ash)

static_library("hps") {
  sources = [
    "hps_configuration.cc",
    "hps_configuration.h",
    "hps_internals.cc",
    "hps_internals.h",
  ]

  deps = [ "//ash/constants:constants" ]

  public_deps = [
    "//base",
    "//chromeos/dbus/hps",
    "//chromeos/dbus/hps:hps_proto",
  ]
}

ts_library("hps_internals_ts") {
  root_dir = "."
  in_files = [ "resources/hps_internals.ts" ]
  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "hps_configuration_unittest.cc" ]
  deps = [
    ":hps",
    "//ash/constants:constants",
    "//base/test:test_support",
    "//testing/gtest",
  ]
}

js2gtest("browser_tests_js") {
  test_type = "webui"

  sources = [ "test/hps_internals_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}
