# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos, "Sample System Web App is Chrome OS only")
assert(!is_official_build,
       "Sample System Web App is only built for unofficial builds")

static_library("sample_system_web_app_ui") {
  sources = [
    "sample_system_web_app_ui.cc",
    "sample_system_web_app_ui.h",
    "untrusted_sample_system_web_app_ui.cc",
    "untrusted_sample_system_web_app_ui.h",
    "url_constants.cc",
    "url_constants.h",
  ]

  deps = [
    "//chromeos/resources:sample_system_web_app_resources",
    "//chromeos/resources:sample_system_web_app_untrusted_resources",
    "//content/public/browser",
    "//ui/webui",
  ]
}

js_type_check("closure_compile") {
  deps = [ ":untrusted" ]
}

js_library("untrusted") {
  sources = [ "resources/untrusted.js" ]
}

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [ "test/sample_system_web_app_ui_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

generate_grd("build_untrusted_grd") {
  input_files_base_dir = rebase_path("resources", "//")
  input_files = [
    "untrusted.html",
    "untrusted.js",
  ]
  grd_prefix = "chromeos_sample_system_web_app_untrusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}

generate_grd("build_trusted_grd") {
  input_files_base_dir = rebase_path("resources", "//")
  input_files = [
    "app_icon_192.png",
    "component_playground.html",
    "index.html",
    "sandbox.html",
    "main.js",
    "worker.js",
    "timer.html",
    "timer.js",
  ]

  # Manifest for generated files generated by resources:component_playground.
  manifest_files = [ "$target_gen_dir/resources/build_manifest.json" ]
  grd_prefix = "chromeos_sample_system_web_app"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  deps = [ "resources:component_playground" ]
}
