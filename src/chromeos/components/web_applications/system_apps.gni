import("//third_party/closure_compiler/compile_js.gni")

declare_args() {
  # Whether to enable additional linting of closure type annotations.
  # Lint checks are not well documented, but pick up some useful stuff.
  enable_system_app_lint_checks = false
}

system_app_closure_flags =
    default_closure_args + [
      "jscomp_error=strictCheckTypes",
      "language_in=ECMASCRIPT_2018",

      # TODO(crbug/1048973): Remove these when the mojo bindings
      # js is updated to pass a closure compile check.
      "hide_warnings_for=mojo/public/js/",
    ]

if (enable_system_app_lint_checks) {
  system_app_closure_flags += [
    "jscomp_error=lintChecks",
    "hide_warnings_for=mojo/public/interfaces/bindings",
  ]
}

system_app_closure_flags_strict =
    system_app_closure_flags + [
      "conformance_configs " +
          rebase_path("closure_conformance_checks.txt", root_build_dir),
      "jscomp_error=conformanceViolations",
      "jscomp_error=reportUnknownTypes",
    ]

ignore_load_time_data_errors_closure_flags = [
  "hide_warnings_for=ui/webui/resources/js/load_time_data.js",

  # load_time_data.js has dependencies on these files that also throw closure errors.
  "hide_warnings_for=ui/webui/resources/js/assert.js",
  "hide_warnings_for=ui/webui/resources/js/parse_html_subset.js",
  "hide_warnings_for=third_party/jstemplate/",
]
