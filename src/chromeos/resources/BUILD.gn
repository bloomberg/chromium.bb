# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromeos/assistant/assistant.gni")
import("//chromeos/components/help_app_ui/help_app_ui.gni")
import("//chromeos/components/media_app_ui/media_app_ui.gni")
import("//tools/grit/grit_rule.gni")

assert(is_chromeos, "Non-ChromeOS builds cannot depend on //chromeos")

grit("resources") {
  source = "chromeos_resources.grd"

  source_is_generated = true

  outputs = [
    "grit/chromeos_resources.h",
    "grit/chromeos_resources_map.cc",
    "grit/chromeos_resources_map.h",
    "chromeos_resources.pak",
  ]
  output_dir = "$root_gen_dir/chromeos"

  defines = [
    "enable_cros_libassistant=$enable_cros_libassistant",
    "enable_cros_media_app=$enable_cros_media_app",
  ]

  grit_flags = [
    "-E",
    "mojom_root=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  deps = [
    "//chromeos/components/multidevice/mojom:mojom_js",
    "//chromeos/services/cellular_setup/public/mojom:mojom_js",
    "//chromeos/services/device_sync/public/mojom:mojom_js",
    "//chromeos/services/machine_learning/public/mojom:mojom_js",
    "//chromeos/services/multidevice_setup/public/mojom:mojom_js",
    "//chromeos/services/network_config/public/mojom:mojom_js",
  ]
}

# Resources used by chrome://help-app, and parts of the sandboxed app it hosts
# that do not come from the app bundle (below).
grit("help_app_resources") {
  source = "../components/help_app_ui/resources/help_app_resources.grd"

  source_is_generated = true

  outputs = [
    "grit/chromeos_help_app_resources.h",
    "grit/chromeos_help_app_resources_map.cc",
    "grit/chromeos_help_app_resources_map.h",
    "chromeos_help_app_resources.pak",
  ]
  output_dir = "$root_gen_dir/chromeos"
}

# Resources automatically served by the chrome://help-app bundle, obtained via DEPS.
grit("help_app_bundle_resources") {
  if (enable_cros_help_app) {
    # Obtained via src-internal/DEPS.
    source =
        "../components/help_app_ui/resources/app/help_app_bundle_resources.grd"
  } else {
    source = "../components/help_app_ui/resources/mock/help_app_bundle_mock_resources.grd"
  }

  source_is_generated = true

  outputs = [
    "grit/chromeos_help_app_bundle_resources.h",
    "grit/chromeos_help_app_bundle_resources_map.cc",
    "grit/chromeos_help_app_bundle_resources_map.h",
    "chromeos_help_app_bundle_resources.pak",
  ]
  output_dir = "$root_gen_dir/chromeos"
}

# Resources used by chrome://media-app, and parts of the sandboxed app it hosts
# that do not come from the app bundle (below).
grit("media_app_resources") {
  source = "../components/media_app_ui/resources/media_app_resources.grd"

  source_is_generated = true

  outputs = [
    "grit/chromeos_media_app_resources.h",
    "chromeos_media_app_resources.pak",
  ]
  output_dir = "$root_gen_dir/chromeos"
}

# Resources automatically served by the chrome://media-app bundle, obtained via DEPS.
grit("media_app_bundle_resources") {
  if (enable_cros_media_app) {
    # Obtained via src-internal/DEPS.
    source = "../components/media_app_ui/resources/app/app/media_app_bundle_resources.grd"
  } else {
    source = "../components/media_app_ui/resources/mock/media_app_bundle_mock_resources.grd"
  }

  source_is_generated = true

  outputs = [
    "grit/chromeos_media_app_bundle_resources.h",
    "grit/chromeos_media_app_bundle_resources_map.cc",
    "grit/chromeos_media_app_bundle_resources_map.h",
    "chromeos_media_app_bundle_resources.pak",
  ]
  output_dir = "$root_gen_dir/chromeos"
}
