# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/compiled_action.gni")

# Generate C++ include file for the Chrome root store.
compiled_action("gen_root_store_inc") {
  tool = "//net/tools/root_store_tool:root_store_tool"

  inputs = [ "store/root_store.textproto" ]
  outputs = [ "${target_gen_dir}/chrome-root-store-inc.cc" ]
  depfile = "${target_gen_dir}/chrome-root-store-inc.cc.d"
  args = [
    "--root-store-dir=" + rebase_path("store", root_build_dir),
    "--write-cpp=" + rebase_path("${target_gen_dir}/chrome-root-store-inc.cc",
                                 root_build_dir),
    "--cpp-output-format=root",
    "--write-deps=" + rebase_path(depfile, root_build_dir),
  ]
}

compiled_action("gen_root_store_test_inc") {
  tool = "//net/tools/root_store_tool:root_store_tool"

  inputs = [ "testdata/root_store.textproto" ]
  outputs = [ "${target_gen_dir}/chrome-root-store-test-data-inc.cc" ]
  depfile = "${target_gen_dir}/chrome-root-store-test-data-inc.cc.d"
  args = [
    "--root-store-dir=" + rebase_path("testdata", root_build_dir),
    "--write-cpp=" +
        rebase_path("${target_gen_dir}/chrome-root-store-test-data-inc.cc",
                    root_build_dir),
    "--cpp-output-format=root",
    "--write-deps=" + rebase_path(depfile, root_build_dir),
  ]
}
