# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

# X11-specific utility code which does not depend on //ui/events:events is kept
# in this separate component, so that //ui/events:events can depend directly on
# it without causing circular dep issues.
component("x") {
  output_name = "events_x"
  sources = [
    "events_x_export.h",
    "events_x_utils.cc",
    "events_x_utils.h",
    "x11_window_event_manager.cc",
    "x11_window_event_manager.h",
  ]
  defines = [ "EVENTS_X_IMPLEMENTATION" ]
  deps = [
    "//base",
    "//skia",
    "//ui/display",
    "//ui/events:events_base",
    "//ui/events/devices",
    "//ui/events/devices/x11",
    "//ui/events/keycodes:x11",
    "//ui/gfx/x",
  ]
}

# XEvent translation tests are kept in a separate source_set so that both
# ozone_x11_unittests and aura/x11 events_unittests can depend on it.
# TODO(crbug.com/789065): Move this completely into ozone/x11 unit tests once
# X11 migration to Ozone is complete.
source_set("unittests") {
  testonly = true
  visibility = [
    "//ui/events:events_unittests",
    "//ui/ozone/platform/x11:x11_unittests",
  ]
  sources = [
    "events_x_unittest.cc",
    "x11_event_translation_unittest.cc",
  ]
  deps = [
    "//base/test:test_support",
    "//testing/gtest",
    "//ui/events:test_support",
    "//ui/events/devices/x11",
    "//ui/events/x:x",
    "//ui/gfx/x",
  ]
}
