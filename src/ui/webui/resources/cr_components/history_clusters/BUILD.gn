# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/css_to_wrapper.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("history_clusters.gni")

assert(!is_android)

# Output folder used to hold preprocess_if_expr() output.
preprocess_folder_tmp = "$root_gen_dir/ui/webui/resources/preprocessed/cr_components/history_clusters_tmp"

# Output folder used to hold ts_library() output.
preprocess_folder = "$root_gen_dir/ui/webui/resources/preprocessed/cr_components/history_clusters"

mojom("mojo_bindings") {
  sources = [ "history_clusters.mojom" ]
  public_deps = [
    "//mojo/public/mojom/base",
    "//url/mojom:url_mojom_gurl",
  ]
  webui_module_path = "chrome://resources/cr_components/history_clusters/"
}

css_to_wrapper("css_wrapper_files") {
  in_files = css_files
}

html_to_wrapper("html_wrapper_files") {
  in_files = html_files
}

copy("copy_history_clusters_mojom") {
  sources = [ "$root_gen_dir/mojom-webui/ui/webui/resources/cr_components/history_clusters/history_clusters.mojom-webui.js" ]
  outputs = [ "$preprocess_folder_tmp/{{source_file_part}}" ]
  deps = [ ":mojo_bindings_webui_js" ]
}

generate_grd("build_grdp") {
  grd_prefix = "cr_components_history_clusters"
  out_grd = "$target_gen_dir/resources.grdp"
  public_deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  resource_path_prefix = "cr_components/history_clusters"
}

preprocess_if_expr("preprocess") {
  in_folder = "."
  out_folder = preprocess_folder_tmp
  in_files = ts_files
}

preprocess_if_expr("preprocess_generated") {
  deps = [
    ":css_wrapper_files",
    ":html_wrapper_files",
  ]
  in_folder = target_gen_dir
  out_folder = preprocess_folder_tmp
  in_files = html_wrapper_files + css_wrapper_files
}

ts_library("build_ts") {
  root_dir = preprocess_folder_tmp
  out_dir = preprocess_folder
  composite = true
  tsconfig_base = "tsconfig_base.json"
  in_files = ts_files + css_wrapper_files + html_wrapper_files +
             [ "history_clusters.mojom-webui.js" ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
    "//ui/webui/resources/mojo:library",
  ]
  definitions = [ "//tools/typescript/definitions/metrics_private.d.ts" ]
  extra_deps = [
    ":copy_history_clusters_mojom",
    ":preprocess",
    ":preprocess_generated",
  ]
}
