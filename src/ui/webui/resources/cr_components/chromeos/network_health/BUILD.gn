# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

assert(is_chromeos, "Only ChromeOS components belong here.")

# JS type check for Polymer 2 and 3

js_type_check("closure_compile") {
  deps = [
    ":network_diagnostics",
    ":network_health_summary",
  ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":network_diagnostics.m",
    ":network_diagnostics_mojo.m",
    ":network_health_summary.m",
  ]
}

# Sources with Polymer 3 generated modules

js_library("network_health_summary") {
  deps = [
    "//chromeos/services/network_health/public/mojom:mojom_js_library_for_compile",
    "//ui/webui/resources/cr_components/chromeos/network:onc_mojo",
    "//ui/webui/resources/js:i18n_behavior",
  ]
}

js_library("network_diagnostics") {
  deps = [
    "//chromeos/services/network_health/public/mojom:mojom_js_library_for_compile",
    "//ui/webui/resources/js:i18n_behavior",
  ]
}

js_library("network_diagnostics_mojo") {
  deps = [
    "//chromeos/services/network_health/public/mojom:mojom_js_library_for_compile",
    "//ui/webui/resources/js:cr",
  ]
}

js_library("network_health_summary.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_components/chromeos/network_health/network_health_summary.m.js" ]
  deps = [
    "//chromeos/services/network_health/public/mojom:mojom_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_components/chromeos/network:onc_mojo",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
  extra_deps = [ ":network_health_summary_module" ]
}

js_library("network_diagnostics.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_components/chromeos/network_health/network_diagnostics.m.js" ]
  deps = [
    ":network_diagnostics_mojo.m",
    "//chromeos/services/network_health/public/mojom:mojom_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
  extra_deps = [ ":network_diagnostics_module" ]
}

js_library("network_diagnostics_mojo.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_components/chromeos/network_health/network_diagnostics_mojo.m.js" ]
  deps = [ "//chrome/browser/ui/webui/settings/chromeos/search:mojo_bindings_js_library_for_compile" ]
  extra_deps = [ ":modulize" ]
}

# polymer_modulizer for converting Polymer2 to Polymer3

polymer_modulizer("network_health_summary") {
  js_file = "network_health_summary.js"
  html_file = "network_health_summary.html"
  html_type = "dom-module"
}

polymer_modulizer("network_diagnostics") {
  js_file = "network_diagnostics.js"
  html_file = "network_diagnostics.html"
  html_type = "dom-module"
}

js_modulizer("modulize") {
  input_files = [ "network_diagnostics_mojo.js" ]
}

group("polymer3_elements") {
  public_deps = [
    ":modulize",
    ":network_diagnostics_module",
    ":network_health_summary_module",
  ]
}
