# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/html_to_js.gni")

assert(is_chromeos, "Bluetooth dialog is Chrome OS only.")

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":bluetooth_dialog",
    ":bluetooth_metrics_utils",
    ":bluetooth_utils",
    ":cros_bluetooth_config",
  ]
}

js_library("bluetooth_metrics_utils") {
  externs_list = chrome_extension_public_externs + [
                   "$externs_path/metrics_private.js",
                   "$externs_path/bluetooth.js",
                   "$externs_path/bluetooth_private.js",
                 ]
}

js_library("bluetooth_utils") {
}

js_library("bluetooth_dialog") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
  externs_list = chrome_extension_public_externs + [
                   "$externs_path/bluetooth.js",
                   "$externs_path/bluetooth_private.js",
                 ]
  extra_sources = [
    "$interfaces_path/bluetooth_interface.js",
    "$interfaces_path/bluetooth_private_interface.js",
  ]
}

js_library("cros_bluetooth_config") {
  deps = [ "//chromeos/services/bluetooth_config/public/mojom:mojom_js_library_for_compile" ]
}

html_to_js("polymer3_elements") {
  js_files = [ "bluetooth_dialog.js" ]
}
