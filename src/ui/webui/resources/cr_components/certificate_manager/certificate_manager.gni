# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

# Files holding a Polymer element definition AND have an equivalent .html file.
_web_component_files = [
  "ca_trust_edit_dialog.ts",
  "certificate_delete_confirmation_dialog.ts",
  "certificate_entry.ts",
  "certificate_list.ts",
  "certificate_manager.ts",
  "certificate_password_decryption_dialog.ts",
  "certificate_password_encryption_dialog.ts",
  "certificate_subentry.ts",
  "certificates_error_dialog.ts",
]

if (is_chromeos_ash) {
  _web_component_files += [
    "certificate_provisioning_details_dialog.ts",
    "certificate_provisioning_entry.ts",
    "certificate_provisioning_list.ts",
  ]
}

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, _web_component_files) {
  html_files += [ string_replace(f, ".ts", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".ts" ]
}

_non_web_component_files = [
  "certificate_manager_types.ts",
  "certificates_browser_proxy.ts",
]

if (is_chromeos_ash) {
  _non_web_component_files += [ "certificate_provisioning_browser_proxy.ts" ]
}

ts_files = _web_component_files + _non_web_component_files

css_wrapper_files = [ "certificate_shared_css.ts" ]
