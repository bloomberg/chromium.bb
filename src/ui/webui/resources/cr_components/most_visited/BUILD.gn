# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_android && !is_ios)

preprocess_folder =
    "$root_gen_dir/ui/webui/resources/preprocessed/cr_components/most_visited"

mojom("mojom") {
  sources = [ "most_visited.mojom" ]
  public_deps = [
    "//mojo/public/mojom/base",
    "//skia/public/mojom",
    "//url/mojom:url_mojom_gurl",
  ]
  webui_module_path = "chrome://resources/cr_components/most_visited/"
}

html_to_js("web_components_local") {
  js_files = [ "most_visited.ts" ]
}

group("web_components") {
  public_deps = [
    ":mojom_webui_js",
    ":web_components_local",
  ]
}

copy("copy_mojom") {
  deps = [ ":mojom_webui_js" ]
  sources = [ "$root_gen_dir/mojom-webui/ui/webui/resources/cr_components/most_visited/most_visited.mojom-webui.js" ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

copy("copy_src") {
  sources = [
    "browser_proxy.ts",
    "window_proxy.ts",
  ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

generate_grd("build_grdp") {
  grd_prefix = "cr_components_most_visited"
  out_grd = "$target_gen_dir/resources.grdp"

  deps = [
    ":build_ts",
    ":mojom_webui_js",
  ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  resource_path_prefix = "cr_components/most_visited"
}

ts_library("build_ts") {
  root_dir = target_gen_dir
  out_dir = preprocess_folder
  composite = true
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "browser_proxy.ts",
    "most_visited.ts",
    "most_visited.mojom-webui.js",
    "window_proxy.ts",
  ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
    "//ui/webui/resources/mojo:library",
  ]
  extra_deps = [
    ":copy_mojom",
    ":copy_src",
    ":web_components_local",
  ]
}
