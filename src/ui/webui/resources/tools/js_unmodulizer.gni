# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

template("js_unmodulizer") {
  action(target_name) {
    script = "//ui/webui/resources/tools/js_unmodulizer.py"

    inputs = invoker.input_files

    outputs = []
    foreach(_output, invoker.output_files) {
      outputs += [ "$target_gen_dir/" + _output ]
    }

    data = outputs

    args = [
             "--in_folder",
             rebase_path(".", root_build_dir),
             "--out_folder",
             rebase_path(target_gen_dir, root_build_dir),
             "--input_files",
           ] + invoker.input_files + [ "--output_files" ] + invoker.output_files
  }
}
