# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

common_namespace_rewrites = [
  "cr.addSingletonGetter|addSingletonGetter",
  "cr.addWebUIListener|addWebUIListener",
  "cr.dispatchSimpleEvent|dispatchSimpleEvent",
  "cr.EventTracker|EventTracker",
  "cr.icon.getFavicon|getFavicon",
  "cr.icon.getImage|getImage",
  "cr.isAndroid|isAndroid",
  "cr.isChromeOS|isChromeOS",
  "cr.isIOS|isIOS",
  "cr.isLacros|isLacros",
  "cr.isLinux|isLinux",
  "cr.isMac|isMac",
  "cr.isWindows|isWindows",
  "cr.png.convertImageSequenceToPng|convertImageSequenceToPng",
  "cr.removeWebUIListener|removeWebUIListener",
  "cr.sendWithPromise|sendWithPromise",
  "cr_slider.SliderTick|SliderTick",
  "cr.toastManager.getToastManager|getToastManager",
  "cr.ui.FocusOutlineManager|FocusOutlineManager",
  "cr.ui.FocusRowBehavior|FocusRowBehavior",
  "cr.ui.focusWithoutInk|focusWithoutInk",
  "cr.ui.KeyboardShortcutList|KeyboardShortcutList",
  "cr.ui.Store|Store",
  "Polymer.dom.|",
  "Polymer.dom(|dom(",
  "Polymer.IronA11yAnnouncer|IronA11yAnnouncer",
  "Polymer.PaperRippleBehavior|PaperRippleBehavior",
  "Polymer.RenderStatus.afterNextRender|afterNextRender",
  "Polymer.RenderStatus.beforeNextRender|beforeNextRender",

  # TODO(dpapad): Add more such rewrites as they get discovered.
]

template("js_modulizer") {
  _outputs = []
  foreach(_input, invoker.input_files) {
    _outputs += [ "$target_gen_dir/" + get_path_info(_input, "name") + ".m.js" ]
  }

  action(target_name) {
    script = "//ui/webui/resources/tools/js_modulizer.py"

    inputs = invoker.input_files
    outputs = _outputs

    args = [
             "--in_folder",
             rebase_path(".", root_build_dir),
             "--out_folder",
             rebase_path(target_gen_dir, root_build_dir),
             "--input_files",
           ] + invoker.input_files

    args += [ "--namespace_rewrites" ] + common_namespace_rewrites
    if (defined(invoker.namespace_rewrites)) {
      args += invoker.namespace_rewrites
    }

    # TODO(crbug.com/1030998) Remove when File Manager uses JS modules only.
    if (defined(invoker.preserve_offsets)) {
      args += [ "--preserve_offsets" ] + invoker.preserve_offsets
    }
  }

  # Appends the compiled outputs of |target_name| to the dependent's
  # runtime_data list.
  source_set("${target_name}_runtime_data") {
    testonly = true

    deps = [ ":${invoker.target_name}" ]
    data = _outputs
  }
}
