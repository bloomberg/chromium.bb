# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//tools/typescript/ts_library.gni")

preprocessed_folder = "$root_gen_dir/ui/webui/resources/preprocessed/mojo"

ts_library("library") {
  root_dir = "$root_gen_dir/mojom-webui"
  out_dir = preprocessed_folder
  tsconfig_base = "tsconfig_base.json"
  composite = true

  in_files = [
    "mojo/public/mojom/base/big_buffer.mojom-webui.js",
    "mojo/public/mojom/base/file_path.mojom-webui.js",
    "mojo/public/mojom/base/safe_base_name.mojom-webui.js",
    "mojo/public/mojom/base/string16.mojom-webui.js",
    "mojo/public/mojom/base/text_direction.mojom-webui.js",
    "mojo/public/mojom/base/time.mojom-webui.js",
    "mojo/public/mojom/base/token.mojom-webui.js",
    "mojo/public/mojom/base/unguessable_token.mojom-webui.js",
    "skia/public/mojom/skcolor.mojom-webui.js",
    "skia/public/mojom/bitmap.mojom-webui.js",
    "skia/public/mojom/image_info.mojom-webui.js",
    "ui/base/mojom/window_open_disposition.mojom-webui.js",
    "ui/gfx/geometry/mojom/geometry.mojom-webui.js",
    "ui/gfx/image/mojom/image.mojom-webui.js",
    "url/mojom/url.mojom-webui.js",
    "url/mojom/origin.mojom-webui.js",
  ]

  definitions = [ "$preprocessed_folder/mojo/public/js/bindings.d.ts" ]

  extra_deps = [
    ":copy_bindings_dts",
    "//mojo/public/mojom/base:base_js__generator",
    "//skia/public/mojom:mojom_js__generator",
    "//ui/base/mojom:mojom_js__generator",
    "//ui/gfx/geometry/mojom:mojom_js__generator",
    "//ui/gfx/image/mojom:mojom_js__generator",
    "//url/mojom:url_mojom_gurl_js__generator",
    "//url/mojom:url_mojom_origin_js__generator",
  ]

  if (is_chromeos_ash) {
    in_files += [ "ui/gfx/range/mojom/range.mojom-webui.js" ]
    extra_deps += [ "//ui/gfx/range/mojom:mojom_js__generator" ]
  }
}

# Copy bindings.d.ts file under the same location where other shared Mojo JS
# generated files reside, so that it can be located by the TS compiler when
# resolving chrome://resources/mojo/mojo/public/js/bindings.js.
copy("copy_bindings_dts") {
  sources = [ "bindings.d.ts" ]
  outputs = [ "$preprocessed_folder/mojo/public/js/{{source_file_part}}" ]
}
