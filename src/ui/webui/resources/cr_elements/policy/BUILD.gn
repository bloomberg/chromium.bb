# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("../../tools/js_modulizer.gni")

js_type_check("closure_compile") {
  deps = [
    ":cr_policy_indicator",
    ":cr_policy_indicator_behavior",
    ":cr_policy_pref_behavior",
    ":cr_policy_pref_indicator",
    ":cr_tooltip_icon",
  ]
  if (is_chromeos) {
    deps += [
      ":cr_policy_network_behavior_mojo",
      ":cr_policy_network_indicator_mojo",
    ]
  }
}

js_library("cr_policy_indicator") {
  deps = [
    ":cr_policy_indicator_behavior",
    "//ui/webui/resources/js:assert",
  ]
}

js_library("cr_policy_indicator_behavior") {
  deps = [ "//ui/webui/resources/js:assert" ]
}

js_library("cr_policy_pref_behavior") {
  externs_list = [ "$externs_path/settings_private.js" ]
}

js_library("cr_policy_pref_indicator") {
  deps = [ ":cr_policy_indicator_behavior" ]
  externs_list = [ "$externs_path/settings_private.js" ]
}

if (is_chromeos) {
  # TODO(jonmann): These cr_policy_network_* libs should be moved to cr_components.
  js_library("cr_policy_network_behavior_mojo") {
    deps = [
      ":cr_policy_indicator_behavior",
      "//chromeos/services/network_config/public/mojom:mojom_js_library_for_compile",
      "//ui/webui/resources/cr_components/chromeos/network:mojo_interface_provider",
      "//ui/webui/resources/cr_components/chromeos/network:onc_mojo",
    ]
  }

  js_library("cr_policy_network_indicator_mojo") {
    deps = [
      ":cr_policy_indicator_behavior",
      ":cr_policy_network_behavior_mojo",
      ":cr_tooltip_icon",
    ]
  }
}

js_library("cr_tooltip_icon") {
}

# Polymer 3 autogenerated version

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":cr_policy_indicator.m",
    ":cr_policy_indicator_behavior.m",
    ":cr_policy_pref_behavior.m",
    ":cr_policy_pref_indicator.m",
    ":cr_tooltip_icon.m",
  ]
}

js_library("cr_policy_indicator.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_elements/policy/cr_policy_indicator.m.js" ]
  deps = [
    ":cr_policy_indicator_behavior.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  extra_deps = [ ":cr_policy_indicator_module" ]
}

js_library("cr_policy_indicator_behavior.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_elements/policy/cr_policy_indicator_behavior.m.js" ]
  deps = [ "//ui/webui/resources/js:assert.m" ]
  extra_deps = [ ":modulize" ]
}

js_library("cr_policy_pref_behavior.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_elements/policy/cr_policy_pref_behavior.m.js" ]
  externs_list = [ "$externs_path/settings_private.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("cr_policy_pref_indicator.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_elements/policy/cr_policy_pref_indicator.m.js" ]
  deps = [
    ":cr_policy_indicator_behavior.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  externs_list = [ "$externs_path/settings_private.js" ]
  extra_deps = [ ":cr_policy_pref_indicator_module" ]
}

js_library("cr_tooltip_icon.m") {
  sources = [
    "$root_gen_dir/ui/webui/resources/cr_elements/policy/cr_tooltip_icon.m.js",
  ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  extra_deps = [ ":cr_tooltip_icon_module" ]
}

group("polymer3_elements") {
  public_deps = [
    ":cr_policy_indicator_module",
    ":cr_policy_pref_indicator_module",
    ":cr_tooltip_icon_module",
    ":modulize",
  ]
}

polymer_modulizer("cr_policy_indicator") {
  js_file = "cr_policy_indicator.js"
  html_file = "cr_policy_indicator.html"
  html_type = "dom-module"
  auto_imports = [ "ui/webui/resources/cr_elements/policy/cr_policy_indicator_behavior.html|CrPolicyIndicatorBehavior,CrPolicyIndicatorType" ]
}

polymer_modulizer("cr_policy_pref_indicator") {
  js_file = "cr_policy_pref_indicator.js"
  html_file = "cr_policy_pref_indicator.html"
  html_type = "dom-module"
  auto_imports = [ "ui/webui/resources/cr_elements/policy/cr_policy_indicator_behavior.html|CrPolicyIndicatorBehavior,CrPolicyIndicatorType" ]
}

polymer_modulizer("cr_tooltip_icon") {
  js_file = "cr_tooltip_icon.js"
  html_file = "cr_tooltip_icon.html"
  html_type = "dom-module"
}

js_modulizer("modulize") {
  input_files = [
    "cr_policy_indicator_behavior.js",
    "cr_policy_pref_behavior.js",
  ]
}
