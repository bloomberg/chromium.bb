# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/linux/gtk/gtk.gni")
import("//gpu/vulkan/features.gni")
import("//ui/base/ui_features.gni")

visibility = [ "//ui/ozone/*" ]

source_set("x11") {
  sources = [
    "client_native_pixmap_factory_x11.cc",
    "client_native_pixmap_factory_x11.h",
    "gl_egl_utility_x11.cc",
    "gl_egl_utility_x11.h",
    "gl_ozone_glx.cc",
    "gl_ozone_glx.h",
    "gl_surface_egl_readback_x11.cc",
    "gl_surface_egl_readback_x11.h",
    "ozone_platform_x11.cc",
    "ozone_platform_x11.h",
    "x11_canvas_surface.cc",
    "x11_canvas_surface.h",
    "x11_clipboard_ozone.cc",
    "x11_clipboard_ozone.h",
    "x11_menu_utils.cc",
    "x11_menu_utils.h",
    "x11_screen_ozone.cc",
    "x11_screen_ozone.h",
    "x11_surface_factory.cc",
    "x11_surface_factory.h",
    "x11_user_input_monitor.cc",
    "x11_user_input_monitor.h",
  ]

  deps = [
    "//base",
    "//build:chromecast_buildflags",
    "//build:chromeos_buildflags",
    "//gpu/vulkan:buildflags",
    "//skia",
    "//ui/base:base",
    "//ui/base:buildflags",
    "//ui/base:data_exchange",
    "//ui/base/clipboard:clipboard_types",
    "//ui/base/cursor:cursor_base",
    "//ui/base/ime",
    "//ui/base/x",
    "//ui/base/x:gl",
    "//ui/display/fake",
    "//ui/events",
    "//ui/events/devices",
    "//ui/events/ozone",
    "//ui/events/ozone/layout",
    "//ui/events/platform",
    "//ui/events/platform/x11",
    "//ui/events/x",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gfx/linux:gbm",
    "//ui/gfx/linux:gpu_memory_buffer_support_x11",
    "//ui/gfx/x",
    "//ui/gl",
    "//ui/ozone:ozone_base",
    "//ui/ozone/common",
    "//ui/platform_window",
    "//ui/platform_window/wm",
    "//ui/platform_window/x11",
  ]

  if (is_chromeos_ash) {
    deps += [ "//ui/base/ime/chromeos" ]
  } else {
    sources += [
      "x11_os_exchange_data_provider_ozone.cc",
      "x11_os_exchange_data_provider_ozone.h",
    ]
    deps += [ "//ui/base/ime/linux" ]
  }

  if (enable_vulkan) {
    deps += [ "//gpu/vulkan/x" ]
  }

  if (use_gtk) {
    deps += [ "//ui/gtk/x" ]
  }

  if (use_xkbcommon) {
    configs += [ "//ui/events/ozone/layout:xkbcommon" ]
  }

  public_configs = [ "//third_party/khronos:khronos_headers" ]
}

source_set("x11_unittests") {
  testonly = true
  sources = [
    "x11_screen_ozone_unittest.cc",
    "x11_window_ozone_unittest.cc",
  ]

  deps = [
    ":x11",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/base/x",
    "//ui/events:test_support",
    "//ui/events/devices/x11",
    "//ui/events/platform/x11",
    "//ui/ozone:platform",
    "//ui/ozone:test_support",
    "//ui/ozone/common",
    "//ui/platform_window/x11:x11",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "x11_ozone_ui_controls_test_helper.cc",
    "x11_ozone_ui_controls_test_helper.h",
  ]

  deps = [
    "//ui/base/x",
    "//ui/base/x:test_support",
  ]
}
