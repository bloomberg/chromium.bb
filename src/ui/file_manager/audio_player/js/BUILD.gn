# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/optimize_webui.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

group("closure_compile") {
  testonly = true
  deps = [
    ":closure_compile_jsmodules",
    ":closure_compile_legacy",
  ]
}

js_type_check("closure_compile_legacy") {
  deps = [
    ":audio_player",
    ":background",
    ":closure_compile_externs",
    ":error_util",
    ":metadata_worker",
  ]
}

js_type_check("closure_compile_jsmodules") {
  uses_js_modules = true
  deps = [
    ":background.m",
    ":error_util.m",
    ":main.m",
  ]
}

js_library("closure_compile_externs") {
  sources = []
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/mediasession.js",
    "//ui/file_manager/externs/audio_player_foreground.js",
    "//ui/file_manager/externs/platform.js",
  ]
}

js_library("audio_player") {
  deps = [
    "//ui/file_manager/audio_player/elements:audio_player",
    "//ui/file_manager/audio_player/elements:track_list",
    "//ui/file_manager/base/js:app_util",
    "//ui/file_manager/base/js:filtered_volume_manager",
    "//ui/file_manager/base/js:mediasession_types",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/foreground/js/metadata:content_metadata_provider",
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_model",
  ]
}

js_library("background") {
  deps = [
    "//ui/file_manager/file_manager/background/js:app_window_wrapper",
    "//ui/file_manager/file_manager/background/js:background_base",
    "//ui/file_manager/file_manager/common/js:file_type",
    "//ui/file_manager/file_manager/common/js:util",
  ]
}

js_library("background.m") {
  sources = [ "$root_gen_dir/ui/file_manager/audio_player/js/background.m.js" ]
  deps = [
    "//ui/file_manager/file_manager/background/js:app_window_wrapper.m",
    "//ui/file_manager/file_manager/background/js:background_base.m",
    "//ui/file_manager/file_manager/common/js:file_type.m",
    "//ui/file_manager/file_manager/common/js:util.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("error_util") {
}

js_library("error_util.m") {
  sources = [ "$root_gen_dir/ui/file_manager/audio_player/js/error_util.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("metadata_worker") {
  deps = [
    "//ui/file_manager/file_manager/foreground/js/metadata:metadata_dispatcher",
  ]
}

js_library("test_util") {
}

js_library("test_util.m") {
  sources = [ "$root_gen_dir/ui/file_manager/audio_player/js/test_util.m.js" ]
  extra_deps = [ ":modulize" ]
}

js_library("main.m") {
  deps = [
    "//ui/file_manager/base/js:app_util.m",
    "//ui/file_manager/base/js:volume_manager_types.m",
  ]
}

js_library("main_background.m") {
  deps = [
    ":background.m",
    ":error_util.m",
    "//ui/file_manager/file_manager/background/js:test_util_base.m",
  ]
}

js_modulizer("modulize") {
  input_files = [
    "background.js",
    "error_util.js",
    "test_util.js",
  ]
}

preprocess_folder =
    rebase_path("$target_gen_dir/../../preprocessed/audio_player/js",
                root_build_dir)

optimize_webui("build") {
  host = "audio_player"

  input = preprocess_folder
  js_module_in_files = [ "main.m.js" ]

  js_out_files = [ "main.m.rollup.js" ]

  deps = [
    ":main.m",
    "//ui/file_manager:preprocess_generated",
    "//ui/file_manager:preprocess_static",
    "//ui/webui/resources:preprocess",
  ]
}
