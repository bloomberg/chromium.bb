# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/tools/optimize_webui.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//ui/file_manager/base/gn/js_test_gen_html.gni")

group("closure_compile") {
  testonly = true
  deps = [
    ":background",
    ":closure_compile_jsmodules",
    ":error_util",
    ":js_test_gen_html_modules_type_check_auto",
    ":test_util",
    ":video_player_metrics",
    ":video_player_native_controls",
  ]
}

group("js_modules") {
  deps = [
    ":background",
    ":error_util",
    ":main.m",
    ":main_background.m",
    ":test_util",
    ":video_player",
    ":video_player",
    ":video_player_metrics",
    ":video_player_native_controls",
  ]
}

js_type_check("closure_compile_jsmodules") {
  is_polymer3 = true
  closure_flags = default_closure_args + [
                    "js_module_root=gen/ui/file_manager/",
                    "js_module_root=../../ui/file_manager/",
                    "browser_resolver_prefix_replacements=\"chrome-extension://hhaomjibdihmijegdhdafkllkbggdgoj/=./file_manager/\"",
                    "browser_resolver_prefix_replacements=\"chrome-extension://pmfjbimdmchhbnneeidfognadeopoehp/=./image_loader/\"",
                  ]
  deps = [
    ":background",
    ":error_util",
    ":main.m",
    ":test_util",
    ":video_player",
    ":video_player_metrics",
    ":video_player_native_controls",
  ]
}

js_library("background") {
  deps = [
    "//ui/file_manager/file_manager/background/js:app_window_wrapper",
    "//ui/file_manager/file_manager/background/js:background_base",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/externs/background:background_base",
    "//ui/webui/resources/js:assert.m",
  ]
}

js_library("error_util") {
}

js_library("test_util") {
  deps = [
    ":background",
    "//ui/file_manager/file_manager/background/js:test_util_base",
  ]
}

js_library("video_player_native_controls") {
  deps = [
    "//ui/file_manager/file_manager/common/js:app_util",
    "//ui/webui/resources/cr_elements/cr_toast:cr_toast",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
  ]

  externs_list = [ "//ui/file_manager/file_manager/externs/platform.js" ]
}

js_unittest("video_player_native_controls_unittest.m") {
  deps = [
    ":video_player_native_controls",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/file_manager/common/js:mock_chrome",
  ]
}

js_library("video_player") {
  deps = [
    ":error_util",
    ":video_player_metrics",
    ":video_player_native_controls",
    "//ui/file_manager/file_manager/common/js:app_util",
    "//ui/file_manager/file_manager/common/js:filtered_volume_manager",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("video_player_metrics") {
  deps = [ "//ui/file_manager/file_manager/common/js:metrics_base" ]
}

js_test_gen_html("js_test_gen_html_modules") {
  deps = [ ":video_player_native_controls_unittest.m" ]
  is_polymer3 = true

  closure_flags = strict_error_checking_closure_args + [
                    "generate_exports=false",
                    "js_module_root=gen/ui/file_manager/",
                    "js_module_root=./gen/ui",
                    "js_module_root=../../ui/file_manager/",
                    "browser_resolver_prefix_replacements=\"chrome://test/=./\"",
                    "browser_resolver_prefix_replacements=\"chrome-extension://hhaomjibdihmijegdhdafkllkbggdgoj/=./file_manager/\"",
                    "browser_resolver_prefix_replacements=\"chrome-extension://pmfjbimdmchhbnneeidfognadeopoehp/=./image_loader/\"",
                    "hide_warnings_for=third_party/",
                  ]
}

js_library("main.m") {
  deps = [ ":video_player" ]
}

js_library("main_background.m") {
  deps = [
    ":background",
    ":error_util",
    ":test_util",
    ":video_player_metrics",
  ]
}

preprocess_folder =
    rebase_path("$target_gen_dir/../../preprocessed/video_player/js",
                root_build_dir)

files_app_path =
    rebase_path("$root_gen_dir/ui/file_manager/preprocessed/file_manager",
                root_build_dir)
image_loader_path =
    rebase_path("$root_gen_dir/ui/file_manager/preprocessed/image_loader",
                root_build_dir)

optimize_webui("build") {
  host = "video_player"

  input = preprocess_folder
  js_module_in_files = [ "main.m.js" ]

  js_out_files = [ "main.m.rollup.js" ]

  external_paths = [
    "chrome-extension://hhaomjibdihmijegdhdafkllkbggdgoj|$files_app_path",
    "chrome-extension://pmfjbimdmchhbnneeidfognadeopoehp|$image_loader_path",
  ]

  deps = [
    ":main.m",
    "//ui/file_manager:preprocess_generated",
    "//ui/file_manager:preprocess_static",
    "//ui/webui/resources:preprocess",
  ]
}

optimize_webui("build_background") {
  host = "video_player"

  input = preprocess_folder
  js_module_in_files = [
    "main_background.m.js",
    "../../file_manager/background/js/runtime_loaded_test_util.js",
  ]

  js_out_files = [
    "main_background.m.rollup.js",
    "runtime_loaded_test_util.rollup.js",
    "shared.m.rollup.js",
  ]

  external_paths = [
    "chrome-extension://hhaomjibdihmijegdhdafkllkbggdgoj|$files_app_path",
    "chrome-extension://pmfjbimdmchhbnneeidfognadeopoehp|$image_loader_path",
  ]

  deps = [
    ":main_background.m",
    "//ui/file_manager:preprocess_generated",
    "//ui/file_manager:preprocess_static",
    "//ui/webui/resources:preprocess",
  ]
}
