# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//ui/file_manager/base/gn/js_test_gen_html.gni")
import("//ui/webui/resources/js/cr.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

visibility = [ "//ui/file_manager/*" ]

group("closure_compile") {
  testonly = true
  deps = [
    ":closure_compile_jsmodules",
    ":closure_compile_module",
    ":js_test_gen_html_type_check_auto",
    ":test_support_type_check",
    ":test_support_type_check_jsmodules",
  ]
}

js_type_check("closure_compile_jsmodules") {
  uses_js_modules = true
  deps = [
    ":app_util.m",
    ":filtered_volume_manager.m",
    ":volume_manager_types.m",
  ]
}

js_type_check("closure_compile_module") {
  deps = [
    ":app_util",
    ":error_counter",
    ":filtered_volume_manager",
  ]
}

js_type_check("test_support_type_check") {
  testonly = true
  deps = [
    ":mock_chrome",
    ":test_error_reporting",
  ]
}

js_type_check("test_support_type_check_jsmodules") {
  uses_js_modules = true
  testonly = true
  deps = [
    ":mock_chrome.m",
    ":test_error_reporting.m",
  ]
}

js_library("app_util") {
  externs_list = [ "//ui/file_manager/externs/app_window_common.js" ]
}

js_library("app_util.m") {
  sources = [ "$root_gen_dir/ui/file_manager/base/js/app_util.m.js" ]
  deps = [ "//ui/file_manager/externs:file_manager_private" ]
  externs_list = [ "//ui/file_manager/externs/app_window_common.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("error_counter") {
}

js_library("filtered_volume_manager") {
  deps = [
    "//ui/file_manager/base/js:volume_manager_types",
    "//ui/file_manager/externs:file_manager_private",
    "//ui/file_manager/externs:volume_manager",
    "//ui/file_manager/file_manager/common/js:async_util",
    "//ui/file_manager/file_manager/common/js:files_app_entry_types",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js/cr/ui:array_data_model",
  ]
  externs_list =
      [ "//ui/file_manager/externs/background/volume_manager_factory.js" ]
}

js_library("filtered_volume_manager.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/base/js/filtered_volume_manager.m.js" ]
  deps = [
    "//ui/file_manager/base/js:volume_manager_types.m",
    "//ui/file_manager/externs:entry_location.m",
    "//ui/file_manager/externs:files_app_entry_interfaces.m",
    "//ui/file_manager/externs:volume_info.m",
    "//ui/file_manager/externs:volume_info_list.m",
    "//ui/file_manager/externs:volume_manager.m",
    "//ui/file_manager/file_manager/common/js:files_app_entry_types.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js/cr:event_target.m",
    "//ui/webui/resources/js/cr/ui:array_data_model.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("mock_chrome") {
  testonly = true
}

js_library("mock_chrome.m") {
  testonly = true
  sources = [ "$root_gen_dir/ui/file_manager/base/js/mock_chrome.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("test_error_reporting") {
  testonly = true
  deps = [
    # Note we allow targets depending on test_error_reporting to access
    # webui_resource_test transitively.
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("test_error_reporting.m") {
  testonly = true
  sources =
      [ "$root_gen_dir/ui/file_manager/base/js/test_error_reporting.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("volume_manager_types") {
  deps = [
    "//ui/file_manager/externs:file_manager_private",
    "//ui/webui/resources/js:assert",
  ]
}

js_unittest("volume_manager_types_unittest.m") {
  deps = [ ":volume_manager_types.m" ]
}

js_library("volume_manager_types.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/base/js/volume_manager_types.m.js" ]

  deps = [
    "//ui/file_manager/externs:file_manager_private",
    "//ui/webui/resources/js:assert.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_test_gen_html("js_test_gen_html") {
  deps = [ ":volume_manager_types_unittest.m" ]
  js_module = true

  closure_flags =
      strict_error_checking_closure_args + [
        "js_module_root=./gen/ui/file_manager/base/js",
        "js_module_root=../../ui/file_manager/base/js",
        "browser_resolver_prefix_replacements=\"chrome://test/=./\"",
      ]
}

js_library("mediasession_types") {
}

js_library("mediasession_types.m") {
  sources = [ "$root_gen_dir/ui/file_manager/base/js/mediasession_types.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_modulizer("modulize") {
  input_files = [
    "app_util.js",
    "filtered_volume_manager.js",
    "mediasession_types.js",
    "mock_chrome.js",
    "test_error_reporting.js",
    "volume_manager_types.js",
  ]

  namespace_rewrites = cr_namespace_rewrites
}
