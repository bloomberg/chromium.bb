# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//ui/file_manager/base/gn/js_test_gen_html.gni")
import("//tools/polymer/polymer.gni")

visibility = [ "//ui/file_manager/file_manager/foreground/*" ]

group("closure_compile") {
  testonly = true
  visibility += [ "//ui/file_manager:closure_compile" ]
  deps = [
    ":closure_compile_internal",
    ":closure_compile_jsmodules",
    ":js_test_gen_html_type_check_auto",
  ]
}

js_type_check("closure_compile_internal") {
  deps = [
    ":files_format_dialog",
    ":files_icon_button",
    ":files_message",
    ":files_metadata_box",
    ":files_metadata_entry",
    ":files_password_dialog",
    ":files_quick_view",
    ":files_ripple",
    ":files_safe_media",
    ":files_safe_media_webview_content",
    ":files_spinner",
    ":files_toast",
    ":files_toggle_ripple",
    ":files_tooltip",
    ":xf_button",
    ":xf_circular_progress",
    ":xf_display_panel",
    ":xf_panel_item",
  ]
}

js_type_check("closure_compile_jsmodules") {
  is_polymer3 = true
  deps = [
    ":files_icon_button.m",
    ":files_toggle_ripple.m",
    ":files_ripple.m",
  ]
}

js_library("files_format_dialog") {
  deps = [
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/webui/resources/cr_elements/cr_button:cr_button",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog",
    "//ui/webui/resources/cr_elements/cr_input:cr_input",
    "//ui/webui/resources/js:i18n_behavior",
  ]
}

js_library("files_icon_button") {
  deps = [
    "//third_party/polymer/v1_0/components-chromium/iron-behaviors:iron-button-state-extracted",
    "//third_party/polymer/v1_0/components-chromium/iron-behaviors:iron-control-state-extracted",
  ]
}

polymer_modulizer("files_icon_button") {
  js_file = "files_icon_button.js"
  html_file = "files_icon_button.html"
  html_type = "dom-module"
  preserve_url_scheme = true

  namespace_rewrites = [
    "Polymer.IronButtonState|IronButtonState",
    "Polymer.IronControlState|IronControlState",
  ]

  auto_imports = [
    "third_party/polymer/v1_0/components-chromium/iron-behaviors/iron-button-state.html|IronButtonState",
    "third_party/polymer/v1_0/components-chromium/iron-behaviors/iron-control-state.html|IronControlState",
  ]
}

js_library("files_icon_button.m") {
  visibility += [ "//ui/file_manager/audio_player/*" ]
  sources = [
    "$root_gen_dir/ui/file_manager/file_manager/foreground/elements/files_icon_button.m.js",
  ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/iron-behaviors:iron-button-state",
    "//third_party/polymer/v3_0/components-chromium/iron-behaviors:iron-control-state",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    ":files_toggle_ripple.m",
  ]
  extra_deps = [ ":files_icon_button_module" ]
}

js_library("files_message") {
  deps = [
    "//ui/webui/resources/cr_elements/cr_button:cr_button",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button",
    "//ui/webui/resources/js:assert",
  ]
}

js_unittest("files_message_unittest") {
  deps = [
    ":files_message",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("files_metadata_box") {
  externs_list = [ "$externs_path/pending_polymer.js" ]
}

js_library("files_metadata_entry") {
}

js_library("files_password_dialog") {
  deps = [
    "//ui/file_manager/file_manager/common/js:async_util",
    "//ui/webui/resources/cr_elements/cr_button:cr_button",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog",
    "//ui/webui/resources/cr_elements/cr_input:cr_input",
    "//ui/webui/resources/js:load_time_data",
  ]
}

js_unittest("files_password_dialog_unittest") {
  deps = [
    ":files_password_dialog",
    "//ui/file_manager/base/js:test_error_reporting",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("files_quick_view") {
  deps = [ ":files_metadata_box" ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/webview_tag.js",
    "$externs_path/pending_polymer.js",
  ]
}

js_library("files_ripple") {
  deps = [ "//ui/webui/resources/js:assert" ]
}

polymer_modulizer("files_ripple") {
  js_file = "files_ripple.js"
  html_file = "files_ripple.html"
  html_type = "dom-module"
  preserve_url_scheme = true

  auto_imports = [ "ui/webui/resources/html/assert.html|assert,assertInstanceof" ]
}

js_library("files_ripple.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/file_manager/foreground/elements/files_ripple.m.js",
  ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
  ]
  extra_deps = [ ":files_ripple_module" ]
}

js_library("files_safe_media") {
}

js_library("files_safe_media_webview_content") {
}

js_library("files_spinner") {
}

# TODO(tapted): Move this to //ui/file_manager/base.
js_library("files_toast") {
  visibility += [ "//ui/file_manager/gallery/*" ]
  deps = [
    "//ui/webui/resources/cr_elements/cr_button:cr_button",
    "//ui/webui/resources/cr_elements/cr_toast:cr_toast",
  ]
}

# TODO(tapted): Move this to //ui/file_manager/base.
js_library("files_toggle_ripple") {
  visibility += [ "//ui/file_manager/gallery/*" ]
}

polymer_modulizer("files_toggle_ripple") {
  js_file = "files_toggle_ripple.js"
  html_file = "files_toggle_ripple.html"
  html_type = "dom-module"
  preserve_url_scheme = true
}

js_library("files_toggle_ripple.m") {
  visibility += [ "//ui/file_manager/audio_player/*" ]
  sources = [
    "$root_gen_dir/ui/file_manager/file_manager/foreground/elements/files_toggle_ripple.m.js",
  ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  extra_deps = [ ":files_toggle_ripple_module" ]
}

# TODO(tapted): Move this to //ui/file_manager/base.
js_library("files_tooltip") {
  visibility += [ "//ui/file_manager/gallery/*" ]
  deps = [ "//ui/file_manager/file_manager/common/js:util" ]
}

js_unittest("files_tooltip_unittest") {
  deps = [
    ":files_tooltip",
    "//ui/file_manager/base/js:test_error_reporting",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_unittest("files_toast_unittest") {
  deps = [
    ":files_toast",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_unittest("files_xf_elements_unittest") {
  deps = [
    ":xf_display_panel",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_test_gen_html("js_test_gen_html") {
  deps = [
    ":files_message_unittest",
    ":files_password_dialog_unittest",
    ":files_toast_unittest",
    ":files_tooltip_unittest",
    ":files_xf_elements_unittest",
  ]
  html_import = true
}

js_library("xf_button") {
  deps = [
    "//ui/webui/resources/cr_elements/cr_button:cr_button",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button",
  ]
}

js_library("xf_circular_progress") {
}

js_library("xf_display_panel") {
  deps = [
    ":xf_panel_item",
    "//ui/file_manager/file_manager/common/js:util",
  ]
}

js_library("xf_panel_item") {
  deps = [
    ":xf_button",
    ":xf_circular_progress",
  ]
}
