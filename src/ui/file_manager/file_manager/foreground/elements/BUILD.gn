# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/file_manager/base/gn/js_test_gen_html.gni")

visibility = [ "//ui/file_manager/file_manager/foreground/*" ]

group("elements") {
  visibility += [ "//ui/file_manager/*" ]
  public_deps =
      [ "//ui/file_manager/file_manager/foreground/elements:files_spinner" ]
}

group("closure_compile") {
  testonly = true
  visibility += [ "//ui/file_manager:closure_compile" ]
  deps = [
    ":closure_compile_jsmodules",
    ":js_test_gen_html_modules_type_check_auto",
  ]
}

js_type_check("closure_compile_jsmodules") {
  is_polymer3 = true
  closure_flags = default_closure_args + [
                    "js_module_root=gen/ui/file_manager/",
                    "js_module_root=../../ui/file_manager/",
                  ]
  deps = [
    ":files_format_dialog",
    ":files_icon_button",
    ":files_metadata_box",
    ":files_metadata_entry",
    ":files_password_dialog",
    ":files_quick_view",
    ":files_ripple",
    ":files_safe_media",
    ":files_spinner",
    ":files_toast",
    ":files_toggle_ripple",
    ":files_tooltip",
    ":xf_button",
    ":xf_circular_progress",
    ":xf_display_panel",
    ":xf_panel_item",
  ]
}

html_to_js("web_components") {
  visibility += [ "//ui/file_manager/*" ]
  js_files = [
    "files_format_dialog.js",
    "files_icon_button.js",
    "files_metadata_box.js",
    "files_metadata_entry.js",
    "files_password_dialog.js",
    "files_quick_view.js",
    "files_ripple.js",
    "files_safe_media.js",
    "files_spinner.js",
    "files_toast.js",
    "files_toggle_ripple.js",
    "files_tooltip.js",
    "icons.js",
    "xf_button.js",
    "xf_circular_progress.js",
    "xf_display_panel.js",
    "xf_panel_item.js",
  ]
}

js_library("files_format_dialog") {
  visibility += [ "//ui/file_manager/audio_player/*" ]
  deps = [
    "//third_party/polymer/v3_0/components-chromium/iron-icon:iron-icon",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/file_manager/file_manager/common/js:files_app_entry_types",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/file_manager/file_manager/externs:volume_info",
    "//ui/file_manager/file_manager/foreground/js:file_rename",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
}

js_library("files_icon_button") {
  visibility += [ "//ui/file_manager/audio_player/*" ]
  deps = [
    ":files_toggle_ripple",
    "//third_party/polymer/v3_0/components-chromium/iron-behaviors:iron-button-state",
    "//third_party/polymer/v3_0/components-chromium/iron-behaviors:iron-control-state",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("files_metadata_box") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("files_metadata_entry") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_unittest("files_password_dialog_unittest.m") {
  deps = [
    ":files_password_dialog",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/file_manager/common/js:test_error_reporting",
    "//ui/webui/resources/js:assert.m",
  ]
}

js_library("files_password_dialog") {
  deps = [
    "//ui/file_manager/file_manager/common/js:async_util",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("files_quick_view") {
  deps = [
    ":files_icon_button",
    ":files_metadata_box",
    ":files_safe_media",
    ":files_tooltip",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
  ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/webview_tag.js",
  ]
}

js_library("files_ripple") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
  ]
}

js_library("files_safe_media") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
  ]
  externs_list = [ "//ui/file_manager/file_manager/externs/quick_view.js" ]
}

js_library("files_spinner") {
}

# TODO(tapted): Move this to //ui/file_manager/base.
js_library("files_toast") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
  ]
  externs_list =
      [ "//ui/webui/resources/cr_elements/cr_toast/cr_toast_externs.js" ]
}

# TODO(tapted): Move this to //ui/file_manager/base.
js_library("files_toggle_ripple") {
  visibility += [ "//ui/file_manager/audio_player/*" ]

  #visibility += [ "//ui/file_manager/gallery/*" ]

  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

# TODO(tapted): Move this to //ui/file_manager/base.
js_unittest("files_tooltip_unittest.m") {
  deps = [
    ":files_tooltip",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/file_manager/common/js:test_error_reporting",
  ]
}

js_library("files_tooltip") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_unittest("files_toast_unittest.m") {
  deps = [
    ":files_toast",
    "//chrome/test/data/webui:chai_assert",
  ]
}

js_unittest("files_xf_elements_unittest.m") {
  deps = [
    ":xf_display_panel",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]

  externs_list = [ "$externs_path/file_manager_private.js" ]
}

js_library("xf_button") {
  deps = [
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button.m",
  ]
}

js_library("xf_circular_progress") {
  deps = [ "//ui/webui/resources/js:assert.m" ]
}

js_library("xf_display_panel") {
  deps = [
    ":xf_panel_item",
    "//ui/file_manager/file_manager/common/js:util",
  ]
}

js_test_gen_html("js_test_gen_html_modules") {
  is_polymer3 = true
  deps = [
    ":files_password_dialog_unittest.m",
    ":files_toast_unittest.m",
    ":files_tooltip_unittest.m",
    ":files_xf_elements_unittest.m",
  ]

  closure_flags =
      strict_error_checking_closure_args + [
        "generate_exports=false",
        "js_module_root=./gen/ui",
        "js_module_root=../../ui",
        "browser_resolver_prefix_replacements=\"chrome://test/=./\"",
        "hide_warnings_for=third_party/",
      ]
}

js_library("xf_panel_item") {
  deps = [
    ":xf_button",
    ":xf_circular_progress",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/webui/resources/js:assert.m",
  ]
}
