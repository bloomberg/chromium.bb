# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//ui/file_manager/base/gn/js_test_gen_html.gni")
import("//ui/webui/resources/js/cr.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

visibility = [
  "//chromeos/components/file_manager/resources/*",
  "//ui/file_manager/*",
]

group("closure_compile") {
  testonly = true
  deps = [
    ":closure_compile_jsmodules",
    ":closure_compile_module",
    ":js_test_gen_html_modules_type_check_auto",
    ":test_support_modules_type_check",
    ":test_support_type_check",
  ]
}

js_type_check("closure_compile_module") {
  uses_legacy_modules = true
  deps = [
    ":app_util",
    ":async_util",
    ":error_counter",
    ":file_operation_common",
    ":file_type",
    ":files_app_entry_types",
    ":filtered_volume_manager",
    ":importer_common",
    ":lru_cache",
    ":metrics",
    ":metrics_base",
    ":mock_entry",
    ":power",
    ":progress_center_common",
    ":storage_adapter",
    ":trash",
    ":util",
    ":volume_manager_types",
    ":xfm",
  ]
}

js_type_check("closure_compile_jsmodules") {
  deps = [
    ":app_util.m",
    ":async_util.m",
    ":error_counter.m",
    ":file_operation_common.m",
    ":file_type.m",
    ":files_app_entry_types.m",
    ":filtered_volume_manager.m",
    ":importer_common.m",
    ":lru_cache.m",
    ":metrics.m",
    ":metrics_base.m",
    ":mock_entry.m",
    ":power.m",
    ":progress_center_common.m",
    ":storage_adapter.m",
    ":trash.m",
    ":util.m",
    ":volume_manager_types.m",
    ":xfm.m",
  ]

  closure_flags = strict_error_checking_closure_args + [
                    "js_module_root=./gen/ui",
                    "js_module_root=../../ui",
                    "hide_warnings_for=third_party/",
                  ]
}

js_type_check("test_support_type_check") {
  uses_legacy_modules = true
  testonly = true
  deps = [
    ":mock_chrome",
    ":test_error_reporting",
    ":test_importer_common",
    ":unittest_util",
  ]
}

js_type_check("test_support_modules_type_check") {
  testonly = true
  deps = [
    ":mock_chrome.m",
    ":test_error_reporting.m",
    ":test_importer_common.m",
    ":unittest_util.m",
  ]

  closure_flags =
      strict_error_checking_closure_args +
      [ "browser_resolver_prefix_replacements=\"chrome://test/=./\"" ]
}

js_library("app_util") {
  deps = [
    ":volume_manager_types",
    ":xfm",
    "//ui/file_manager/file_manager/externs:volume_manager",
  ]
  externs_list = [
    "//ui/file_manager/file_manager/externs/app_window_common.js",
    "//ui/file_manager/file_manager/externs/background/background_base.js",
  ]
}

js_library("app_util.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/file_manager/common/js/app_util.m.js" ]
  deps = [
    "//ui/file_manager/file_manager/externs:file_manager_private",
    "//ui/file_manager/file_manager/externs:volume_manager.m",
    "//ui/file_manager/file_manager/externs/background:background_base.m",
  ]
  externs_list =
      [ "//ui/file_manager/file_manager/externs/app_window_common.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("async_util") {
}

js_library("async_util.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/file_manager/common/js/async_util.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("error_counter") {
}

js_library("error_counter.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/file_manager/common/js/error_counter.m.js",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("files_app_entry_types") {
  deps = [ "//ui/file_manager/file_manager/externs:file_manager_private" ]
  externs_list = [
    "//ui/file_manager/file_manager/externs/files_app_entry_interfaces.js",
    "//ui/file_manager/file_manager/externs/volume_info.js",
  ]
}

js_unittest("files_app_entry_types_unittest.m") {
  deps = [
    ":files_app_entry_types.m",
    ":mock_entry.m",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/file_manager/common/js:test_error_reporting.m",
    "//ui/file_manager/file_manager/externs:volume_info.m",
    "//ui/webui/resources/js:assert.m",
  ]
}

js_library("files_app_entry_types.m") {
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/files_app_entry_types.m.js" ]
  deps = [
    "//ui/file_manager/file_manager/common/js:volume_manager_types.m",
    "//ui/file_manager/file_manager/externs:volume_info.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("file_type") {
  deps = [
    ":files_app_entry_types",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
  ]
}

js_library("file_type.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/file_manager/common/js/file_type.m.js" ]
  deps = [
    ":files_app_entry_types.m",
    "//ui/file_manager/file_manager/common/js:volume_manager_types.m",
    "//ui/file_manager/file_manager/externs:files_app_entry_interfaces.m",
    "//ui/webui/resources/js:assert.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_unittest("file_type_unittest.m") {
  deps = [
    ":file_type.m",
    ":mock_entry.m",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/file_manager/common/js:volume_manager_types.m",
  ]
}

js_library("file_operation_common") {
  deps = [ ":util" ]
  externs_list =
      [ "//ui/file_manager/file_manager/externs/files_app_entry_interfaces.js" ]
}

js_library("file_operation_common.m") {
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/file_operation_common.m.js" ]
  deps = [
    ":util.m",
    "//ui/file_manager/file_manager/externs:files_app_entry_interfaces.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("filtered_volume_manager") {
  deps = [
    "//ui/file_manager/file_manager/common/js:async_util",
    "//ui/file_manager/file_manager/common/js:files_app_entry_types",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/file_manager/file_manager/externs:file_manager_private",
    "//ui/file_manager/file_manager/externs:volume_manager",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js/cr/ui:array_data_model",
  ]
  externs_list = [ "//ui/file_manager/file_manager/externs/background/volume_manager_factory.js" ]
}

js_library("filtered_volume_manager.m") {
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/filtered_volume_manager.m.js" ]
  deps = [
    "//ui/file_manager/file_manager/common/js:files_app_entry_types.m",
    "//ui/file_manager/file_manager/common/js:volume_manager_types.m",
    "//ui/file_manager/file_manager/externs:entry_location.m",
    "//ui/file_manager/file_manager/externs:files_app_entry_interfaces.m",
    "//ui/file_manager/file_manager/externs:volume_info.m",
    "//ui/file_manager/file_manager/externs:volume_info_list.m",
    "//ui/file_manager/file_manager/externs:volume_manager.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js/cr:event_target.m",
    "//ui/webui/resources/js/cr/ui:array_data_model.m",
  ]

  extra_deps = [ ":modulize" ]
}

# These importer files actually belong here. Nothing outside the Files app uses
# them, so restrict visibility. TODO(tapted): Simplify visibility when
# everything else moves to //ui/file_manager/base.
js_library("importer_common") {
  visibility = []
  visibility = [ "//ui/file_manager/file_manager/*" ]
  deps = [
    ":file_type",
    ":xfm",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/file_manager/file_manager/externs:volume_manager",
  ]
}

js_library("importer_common.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/file_manager/common/js/importer_common.m.js",
  ]
  deps = [
    ":file_type.m",
    "//ui/file_manager/file_manager/common/js:volume_manager_types.m",
    "//ui/file_manager/file_manager/externs:files_app_entry_interfaces.m",
    "//ui/file_manager/file_manager/externs:volume_info.m",
    "//ui/file_manager/file_manager/externs:volume_manager.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("test_importer_common") {
  testonly = true
  deps = [
    ":importer_common",
    ":unittest_util",
    "//ui/file_manager/file_manager/common/js:mock_chrome",
    "//ui/file_manager/file_manager/common/js:test_error_reporting",
  ]
  visibility = []
  visibility = [ "//ui/file_manager/file_manager/*" ]
}

js_library("test_importer_common.m") {
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/test_importer_common.m.js" ]
  deps = [
    ":importer_common.m",
    ":unittest_util.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_unittest("importer_common_unittest.m") {
  deps = [
    ":importer_common.m",
    ":mock_entry.m",
    ":test_importer_common.m",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/file_manager/background/js:mock_volume_manager.m",
    "//ui/file_manager/file_manager/common/js:mock_chrome.m",
    "//ui/file_manager/file_manager/common/js:test_error_reporting.m",
    "//ui/file_manager/file_manager/common/js:volume_manager_types.m",
    "//ui/file_manager/file_manager/externs:volume_info.m",
  ]
}

js_library("lru_cache") {
}

js_library("lru_cache.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/file_manager/common/js/lru_cache.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_unittest("lru_cache_unittest.m") {
  deps = [
    ":lru_cache.m",
    "//chrome/test/data/webui:chai_assert",
  ]
}

js_library("mediasession_types") {
}

js_library("mediasession_types.m") {
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/mediasession_types.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("metrics") {
  visibility = []
  visibility = [ "//ui/file_manager/file_manager/*" ]
  deps = [
    ":metrics_base",
    "//ui/file_manager/file_manager/externs:file_manager_private",
    "//ui/webui/resources/js:assert",
  ]
}

js_library("metrics.m") {
  visibility = []
  visibility = [ "//ui/file_manager/file_manager/*" ]
  sources =
      [ "$root_gen_dir/ui/file_manager/file_manager/common/js/metrics.m.js" ]
  deps = [ ":metrics_base.m" ]

  extra_deps = [ ":modulize" ]
}

js_library("metrics_base") {
  externs_list = [ "$externs_path/metrics_private.js" ]
}

js_library("metrics_base.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/file_manager/common/js/metrics_base.m.js",
  ]
  externs_list = [ "$externs_path/metrics_private.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("mock_chrome") {
  testonly = true
}

js_library("mock_chrome.m") {
  testonly = true
  sources = [
    "$root_gen_dir/ui/file_manager/file_manager/common/js/mock_chrome.m.js",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("mock_entry") {
  deps = [
    ":util",
    "//ui/webui/resources/js:assert",
  ]
}

js_library("mock_entry.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/file_manager/common/js/mock_entry.m.js" ]
  deps = [ ":util.m" ]

  extra_deps = [ ":modulize" ]
}

js_library("progress_center_common") {
}

js_library("progress_center_common.m") {
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/progress_center_common.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("power") {
}

js_library("power.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/file_manager/common/js/power.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("storage_adapter") {
}

js_library("storage_adapter.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/file_manager/common/js/storage_adapter.m.js",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("test_error_reporting") {
  testonly = true
  deps = [
    # Note we allow targets depending on test_error_reporting to access
    # webui_resource_test transitively.
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("test_error_reporting.m") {
  testonly = true
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/test_error_reporting.m.js" ]

  extra_deps = [ ":modulize" ]
}

js_library("xfm") {
  deps = [
    ":power",
    ":storage_adapter",
  ]
}

js_library("xfm.m") {
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/xfm.m.js" ]
  deps = [
    ":power.m",
    ":storage_adapter.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("trash") {
  deps = [
    "//ui/file_manager/file_manager/common/js:files_app_entry_types",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
  ]
  externs_list = [
    "//ui/file_manager/file_manager/externs/files_app_entry_interfaces.js",
    "//ui/file_manager/file_manager/externs/volume_info.js",
    "//ui/file_manager/file_manager/externs/volume_manager.js",
  ]
}

js_library("trash.m") {
  sources =
      [ "$root_gen_dir/ui/file_manager/file_manager/common/js/trash.m.js" ]
  deps = [
    ":files_app_entry_types.m",
    ":util.m",
    "//ui/file_manager/file_manager/common/js:volume_manager_types.m",
    "//ui/file_manager/file_manager/externs:files_app_entry_interfaces.m",
    "//ui/file_manager/file_manager/externs:volume_manager.m",
    "//ui/webui/resources/js:assert.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_library("unittest_util") {
  testonly = true

  # Only files app tests use this util file.
  visibility = []
  visibility = [ "//ui/file_manager/file_manager/*" ]
  deps = [ "//ui/webui/resources/js:webui_resource_test" ]
  externs_list = [ "$externs_path/command_line_private.js" ]
}

js_library("unittest_util.m") {
  sources = [
    "$root_gen_dir/ui/file_manager/file_manager/common/js/unittest_util.m.js",
  ]
  deps = [ "//chrome/test/data/webui:chai_assert" ]

  extra_deps = [ ":modulize" ]
}

js_library("util") {
  deps = [
    ":files_app_entry_types",
    "//ui/file_manager/file_manager/externs:file_manager_private",

    # TODO(tapted): Remove these base util dependencies, which exist temporarily
    # to allow targets to depend only on util, if they depend on one of these.
    "//ui/file_manager/file_manager/common/js:app_util",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js/cr:event_target",
    "//ui/webui/resources/js/cr:ui",
  ]
  externs_list = [
    "$externs_path/command_line_private.js",
    "//ui/file_manager/file_manager/externs/entry_location.js",
    "//ui/file_manager/file_manager/externs/platform.js",
    "//ui/file_manager/file_manager/externs/volume_info.js",
    "//ui/file_manager/file_manager/externs/volume_info_list.js",
    "//ui/file_manager/file_manager/externs/volume_manager.js",
  ]
}

js_library("util.m") {
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/util.m.js" ]

  deps = [
    ":files_app_entry_types.m",
    "//ui/file_manager/file_manager/common/js:files_app_entry_types.m",
    "//ui/file_manager/file_manager/common/js:volume_manager_types.m",
    "//ui/file_manager/file_manager/externs:entry_location.m",
    "//ui/file_manager/file_manager/externs:file_manager_private",
    "//ui/file_manager/file_manager/externs:files_app_entry_interfaces.m",
    "//ui/file_manager/file_manager/externs:volume_info.m",
    "//ui/file_manager/file_manager/externs:volume_manager.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
    "//ui/webui/resources/js/cr:ui.m",
  ]

  externs_list = [
    "//ui/file_manager/file_manager/externs/platform.js",
    "//ui/file_manager/file_manager/externs/app_window_common.js",
  ]

  extra_deps = [ ":modulize" ]
}

js_unittest("util_unittest.m") {
  deps = [
    ":files_app_entry_types.m",
    ":mock_entry.m",
    ":util.m",
    "//chrome/test/data/webui:chai_assert",
    "//ui/file_manager/file_manager/background/js:mock_volume_manager.m",
    "//ui/file_manager/file_manager/common/js:volume_manager_types.m",
  ]
}

js_library("volume_manager_types") {
  deps = [
    "//ui/file_manager/file_manager/externs:file_manager_private",
    "//ui/webui/resources/js:assert",
  ]
}

js_unittest("volume_manager_types_unittest.m") {
  deps = [ ":volume_manager_types.m" ]
}

js_library("volume_manager_types.m") {
  sources = [ "$root_gen_dir/ui/file_manager/file_manager/common/js/volume_manager_types.m.js" ]

  deps = [
    "//ui/file_manager/file_manager/externs:file_manager_private",
    "//ui/webui/resources/js:assert.m",
  ]

  extra_deps = [ ":modulize" ]
}

js_test_gen_html("js_test_gen_html_modules") {
  js_module = true
  deps = [
    ":file_type_unittest.m",
    ":files_app_entry_types_unittest.m",
    ":importer_common_unittest.m",
    ":lru_cache_unittest.m",
    ":util_unittest.m",
    ":volume_manager_types_unittest.m",
  ]

  closure_flags =
      strict_error_checking_closure_args + [
        "js_module_root=./gen/ui",
        "js_module_root=../../ui",
        "browser_resolver_prefix_replacements=\"chrome://test/=./\"",
        "hide_warnings_for=third_party/",
      ]
}

js_modulizer("modulize") {
  input_files = [
    "app_util.js",
    "async_util.js",
    "error_counter.js",
    "file_operation_common.js",
    "file_type.js",
    "files_app_entry_types.js",
    "filtered_volume_manager.js",
    "importer_common.js",
    "lru_cache.js",
    "mediasession_types.js",
    "metrics.js",
    "metrics_base.js",
    "mock_chrome.js",
    "mock_entry.js",
    "power.js",
    "progress_center_common.js",
    "storage_adapter.js",
    "test_error_reporting.js",
    "test_importer_common.js",
    "trash.js",
    "unittest_util.js",
    "util.js",
    "volume_manager_types.js",
    "xfm.js",
  ]

  namespace_rewrites = cr_namespace_rewrites
}
