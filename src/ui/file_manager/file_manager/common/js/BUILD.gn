# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//ui/file_manager/base/gn/js_test_gen_html.gni")

# TODO(tapted): This entire folder should move to //ui/file_manager/base.
visibility = [ "//ui/file_manager/*" ]

group("closure_compile") {
  testonly = true
  deps = [
    ":closure_compile_module",
    ":js_test_gen_html_type_check_auto",
    ":test_support_type_check",
  ]
}

js_type_check("closure_compile_module") {
  deps = [
    ":async_util",
    ":file_type",
    ":files_app_entry_types",
    ":importer_common",
    ":lru_cache",
    ":metrics",
    ":metrics_base",
    ":mock_entry",
    ":progress_center_common",
    ":util",
    "//ui/file_manager/base/js:volume_manager_types",
  ]
}

js_type_check("test_support_type_check") {
  testonly = true
  deps = [
    ":test_importer_common",
    ":unittest_util",
  ]
}

js_library("async_util") {
}

js_library("files_app_entry_types") {
  deps = [ "//ui/file_manager/externs:file_manager_private" ]
  externs_list = [ "//ui/file_manager/externs/volume_info.js" ]
}

js_unittest("files_app_entry_types_unittest") {
  deps = [
    ":files_app_entry_types",
    ":mock_entry",
    "//ui/file_manager/base/js:test_error_reporting",
    "//ui/file_manager/base/js:volume_manager_types",
  ]
}

js_library("file_type") {
  deps = [
    ":files_app_entry_types",
    "//ui/file_manager/base/js:volume_manager_types",
  ]
}

js_unittest("file_type_unittest") {
  deps = [
    ":file_type",
    ":mock_entry",
    "//ui/file_manager/base/js:test_error_reporting",
  ]
}

# These importer files actually belong here. Nothing outside the Files app uses
# them, so restrict visibility. TODO(tapted): Simplify visibility when
# everything else moves to //ui/file_manager/base.
js_library("importer_common") {
  visibility = []
  visibility = [ "//ui/file_manager/file_manager/*" ]
  deps = [
    ":file_type",
    "//ui/file_manager/base/js:volume_manager_types",
    "//ui/file_manager/externs:volume_manager",
  ]
}

js_library("test_importer_common") {
  testonly = true
  deps = [
    ":importer_common",
    ":unittest_util",
    "//ui/file_manager/base/js:mock_chrome",
    "//ui/file_manager/base/js:test_error_reporting",
  ]
  visibility = []
  visibility = [ "//ui/file_manager/file_manager/*" ]
}

js_unittest("importer_common_unittest") {
  deps = [
    ":mock_entry",
    ":test_importer_common",
    ":util",
    "//ui/file_manager/base/js:mock_chrome",
    "//ui/file_manager/file_manager/background/js:mock_volume_manager",
  ]
}

js_library("lru_cache") {
}

js_unittest("lru_cache_unittest") {
  deps = [
    ":lru_cache",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("metrics") {
  visibility = []
  visibility = [ "//ui/file_manager/file_manager/*" ]
  deps = [
    ":metrics_base",
    "//ui/file_manager/externs:file_manager_private",
    "//ui/webui/resources/js:assert",
  ]
}

js_library("metrics_base") {
  externs_list = [ "$externs_path/metrics_private.js" ]
}

js_library("mock_entry") {
  deps = [
    ":util",
    "//ui/webui/resources/js:assert",
  ]
}

js_library("progress_center_common") {
}

js_library("unittest_util") {
  testonly = true

  # Only files app tests use this util file.
  visibility = []
  visibility = [ "//ui/file_manager/file_manager/*" ]
  deps = [ "//ui/webui/resources/js:webui_resource_test" ]
  externs_list = [ "$externs_path/command_line_private.js" ]
}

js_library("util") {
  deps = [
    ":files_app_entry_types",
    "//ui/file_manager/externs:file_manager_private",

    # TODO(tapted): Remove these base util dependencies, which exist temporarily
    # to allow targets to depend only on util, if they depend on one of these.
    "//ui/file_manager/base/js:app_util",
    "//ui/file_manager/base/js:volume_manager_types",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js/cr:event_target",
    "//ui/webui/resources/js/cr:ui",
  ]
  externs_list = [
    "$externs_path/command_line_private.js",
    "//ui/file_manager/externs/entry_location.js",
    "//ui/file_manager/externs/platform.js",
    "//ui/file_manager/externs/volume_info.js",
    "//ui/file_manager/externs/volume_info_list.js",
    "//ui/file_manager/externs/volume_manager.js",
  ]
}

js_unittest("util_unittest") {
  deps = [
    ":files_app_entry_types",
    ":mock_entry",
    ":unittest_util",
    ":util",
    "//ui/file_manager/file_manager/background/js:mock_volume_manager",
  ]
}

js_test_gen_html("js_test_gen_html") {
  deps = [
    ":file_type_unittest",
    ":files_app_entry_types_unittest",
    ":importer_common_unittest",
    ":lru_cache_unittest",
    ":util_unittest",
  ]
}
