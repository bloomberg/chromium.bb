# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  generate_java = true

  sources = [
    "display.mojom",
    "display_constants.mojom",
    "display_layout.mojom",
    "display_mode.mojom",
    "display_snapshot.mojom",
    "gamma_ramp_rgb_entry.mojom",
  ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
  ]

  shared_cpp_typemaps = [
    {
      types = [
        {
          mojom = "display.mojom.Rotation"
          cpp = "::display::Display::Rotation"
        },
      ]
      traits_headers = [ "display_mojom_traits.h" ]
      traits_public_deps = [ ":shared_mojom_traits" ]
    },
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "display.mojom.Display"
          cpp = "::display::Display"
        },
        {
          mojom = "display.mojom.TouchSupport"
          cpp = "::display::Display::TouchSupport"
        },
        {
          mojom = "display.mojom.AccelerometerSupport"
          cpp = "::display::Display::AccelerometerSupport"
        },
      ]
      traits_headers = [ "display_mojom_traits.h" ]
      traits_public_deps = [ ":shared_mojom_traits" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.DisplayConnectionType"
          cpp = "::display::DisplayConnectionType"
        },
        {
          mojom = "display.mojom.HDCPState"
          cpp = "::display::HDCPState"
        },
        {
          mojom = "display.mojom.PrivacyScreenState"
          cpp = "::display::PrivacyScreenState"
        },
      ]
      traits_sources = [ "display_constants_mojom_traits.cc" ]
      traits_headers = [ "display_constants_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.Position"
          cpp = "::display::DisplayPlacement::Position"
        },
        {
          mojom = "display.mojom.OffsetReference"
          cpp = "::display::DisplayPlacement::OffsetReference"
        },
        {
          mojom = "display.mojom.DisplayPlacement"
          cpp = "::display::DisplayPlacement"
        },
        {
          mojom = "display.mojom.DisplayLayout"
          cpp = "::std::unique_ptr<::display::DisplayLayout>"
          move_only = true
        },
      ]
      traits_sources = [ "display_layout_mojom_traits.cc" ]
      traits_headers = [ "display_layout_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.DisplayMode"
          cpp = "::std::unique_ptr<::display::DisplayMode>"
          move_only = true
        },
      ]
      traits_sources = [ "display_mode_mojom_traits.cc" ]
      traits_headers = [ "display_mode_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
      traits_deps = [ "//ui/gfx/geometry" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.DisplaySnapshot"
          cpp = "::std::unique_ptr<::display::DisplaySnapshot>"
          move_only = true
        },
      ]
      traits_sources = [ "display_snapshot_mojom_traits.cc" ]
      traits_headers = [ "display_snapshot_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
      traits_deps = [ "//ui/gfx/geometry" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.GammaRampRGBEntry"
          cpp = "::display::GammaRampRGBEntry"
        },
      ]
      traits_sources = [ "gamma_ramp_rgb_entry_mojom_traits.cc" ]
      traits_headers = [ "gamma_ramp_rgb_entry_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
    },
  ]

  cpp_typemaps += shared_cpp_typemaps
  blink_cpp_typemaps = shared_cpp_typemaps
}

component("shared_mojom_traits") {
  output_name = "display_shared_mojom_traits"
  defines = [ "IS_DISPLAY_SHARED_MOJOM_TRAITS_IMPL" ]
  sources = [
    "display_mojom_traits.cc",
    "display_mojom_traits.h",
  ]
  public_deps = [
    ":mojom_shared",
    "//base",
    "//ui/display",
    "//ui/gfx/mojom",
  ]
}
