# -*- python -*-
# Copyright (c) 2011 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

stub_sources = ['plugin_main_irt.c', 'thread_creator.c']

irt_stub = env.ComponentObject(
    'irt_stub', '${SCONSTRUCT_DIR}/src/untrusted/irt/irt_elf_utils.c')

env.ComponentLibrary('libppapi_stub', stub_sources + irt_stub)
env.AddLibraryToSdk(['libppapi_stub'])
env.AddObjectToSdk(['libppapi.a'])

if not env.Bit('nacl_disable_shared'):
  env.AddObjectToSdk(['libppapi.so'])

gap_env = env.Clone()
gap_env.Append(CPPDEFINES=[
    ['IRT_DATA_REGION_START', '${IRT_DATA_REGION_START}']
    ])
if gap_env.Bit('bitcode'):
  # This is not actually a platform-specific, it's generic to any assembler
  # for an ELF target.  But it can't be compiled to LLVM bitcode.
  gap_env.Replace(OBJSUFFIX='.o')
  gap_env.Append(ASFLAGS=['-arch', '${TARGET_FULLARCH}'])

link_segment_gap = gap_env.ComponentObject(
    'link_segment_gap',
    '${SCONSTRUCT_DIR}/src/untrusted/irt/link_segment_gap.S'
    )
gap_env.AddObjectToSdk(link_segment_gap, is_platform=True)
