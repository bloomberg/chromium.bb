# -*- python -*-
# Copyright (c) 2008 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

# To get generated include files.
env.Append(CPPPATH = [ '${TARGET_ROOT}' ])

# TODO(gregoryd): eliminate the need for the next line
#env.FilterOut(CCFLAGS=['-Wextra', '-Wswitch-enum'])

if env.Bit('linux'):
    env.Append(CPPDEFINES=['XP_UNIX'])


if env.Bit('mac'):
    env.Append(
        CPPDEFINES = [
            'XP_MACOSX',
            'XP_UNIX',
            ['TARGET_API_MAC_CARBON', '1'],
            'NO_X11',
            'USE_SYSTEM_CONSOLE',
        ],
    )


if env.Bit('windows'):
    env.Append(
        CCFLAGS = [
            '/EHsc',
        ],
        CPPDEFINES = [
            'XP_WIN', 'WIN32', '_WINDOWS'
        ],
    )

env_no_strict_aliasing = env.Clone()
if env.Bit('linux'):
   env_no_strict_aliasing.Append(CCFLAGS = ['-fno-strict-aliasing'])

# The RPCs performed by the trusted portion of the NPAPI layer are specified
# abstractly, and proxies and stubs are generated automatically.
SRPCGEN = '${SOURCE_ROOT}/native_client/tools/srpcgen.py'
GENERATED = '${TARGET_ROOT}/gen/native_client/src/shared/npruntime'

npmodule_specinputs = ['npmodule.srpc',
                       'npobjectstub.srpc',
                       'device2d.srpc',
                       'device3d.srpc',
                       'audio.srpc', ]

npmodule_rpc_h = GENERATED + '/npmodule_rpc.h'
npmodule_rpc_cc = GENERATED + '/npmodule_rpc_server.cc'
npmodule_rpc_files = [ npmodule_rpc_h, npmodule_rpc_cc ]

env.Command(target=npmodule_rpc_files,
            source=[SRPCGEN, npmodule_specinputs],
            action=[Action('${PYTHON} ${SOURCES[0]} '
                           '-s '
                           'NPModuleRpcs '
                           'GEN_NPRUNTIME_NPMODULE_RPC_H_ '
                           + npmodule_rpc_h + ' '
                           + npmodule_rpc_cc + ' '
                           '${SOURCES[1:]}'),
                   ],
           )

npnavigator_specinputs = ['npnavigator.srpc',
                          'npobjectstub.srpc', ]

npnavigator_rpc_h = GENERATED + '/npnavigator_rpc.h'
npnavigator_rpc_cc = GENERATED + '/npnavigator_rpc_client.cc'
npnavigator_rpc_files = [ npnavigator_rpc_h, npnavigator_rpc_cc ]

env.Command(target=npnavigator_rpc_files,
            source=[SRPCGEN, npnavigator_specinputs],
            action=[Action('${PYTHON} ${SOURCES[0]} '
                           '-c '
                           'NPNavigatorRpcs '
                           'GEN_NPRUNTIME_NPNAVIGATOR_RPC_H_ '
                           + npnavigator_rpc_h + ' '
                           + npnavigator_rpc_cc + ' '
                           '${SOURCES[1:]}'),
                   ],
           )

npupcall_specinputs = ['npupcall.srpc']

npupcall_rpc_h = GENERATED + '/npupcall_rpc.h'
npupcall_rpc_cc = GENERATED + '/npupcall_rpc_server.cc'
npupcall_rpc_files = [ npupcall_rpc_h, npupcall_rpc_cc ]

env.Command(target=npupcall_rpc_files,
            source=[SRPCGEN, npupcall_specinputs],
            action=[Action('${PYTHON} ${SOURCES[0]} '
                           '-s '
                           'NPUpcallRpcs '
                           'GEN_NPRUNTIME_NPUPCALL_RPC_H_ '
                           + npupcall_rpc_h + ' '
                           + npupcall_rpc_cc + ' '
                           '${SOURCES[1:]}'),
                   ],
           )

NACL_NPRUNTIME_INPUTS = [
    'naclnp_util.cc',
    'npbridge.cc',
    'npmodule.cc',
    'npobject_proxy.cc',
    'npobject_stub.cc',
    env_no_strict_aliasing.DualObject('structure_translations.cc'),
    npmodule_rpc_cc,
    npnavigator_rpc_cc,
    'npmodule_rpc_impl.cc',
    'npstub_rpc_impl.cc',
    'npupcall_server.cc',
    npupcall_rpc_cc,
    'pointer_translations.cc',
]
env.DualLibrary('google_nacl_npruntime', NACL_NPRUNTIME_INPUTS)
