# Copyright (c) 2008 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

# TODO(robertm,cbiffle): fix header conflicts to allow this to build.
if env['TARGET_ARCHITECTURE'] == 'arm':
  Return()

# To enable getting the generated include files and sources.
env.Append(CPPPATH=['${TARGET_ROOT}'])

env.Append(CPPDEFINES=['XP_UNIX'])
env.FilterOut(CCFLAGS=['-Wswitch-enum'])
env.Append(CCFLAGS=['-Wno-long-long'])

# Some ugly type punning in NPP_StreamAsFile
env_no_strict_aliasing = env.Clone()
env_no_strict_aliasing.Append(CCFLAGS = ['-fno-strict-aliasing'])

# The RPCs performed by the untrusted portion of the NPAPI layer are specified
# abstractly, and proxies and stubs are generated automatically.
SRPCGEN = '${SOURCE_ROOT}/native_client/tools/srpcgen.py'
GENERATED = '${TARGET_ROOT}/gen/native_client/src/shared/npruntime'

npmodule_specinputs = ['npmodule.srpc',
                       'npobjectstub.srpc',
                       'device2d.srpc',
                       'device3d.srpc',
                       'audio.srpc', ]

npmodule_rpc_h = GENERATED + '/npmodule_rpc.h'
npmodule_rpc_cc = GENERATED + '/npmodule_rpc_client.cc'
npmodule_rpc_files = [ npmodule_rpc_h, npmodule_rpc_cc ]
env.Command(target=npmodule_rpc_files,
            source=[SRPCGEN, npmodule_specinputs],
            action=[Action('${PYTHON} ${SOURCES[0]} '
                           '-c '
                           'NPModuleRpcs '
                           'GEN_NPRUNTIME_NPMODULE_RPC_H_ '
                           + npmodule_rpc_h + ' '
                           + npmodule_rpc_cc + ' '
                           ' ${SOURCES[1:]}'),
                   ],
           )

npnavigator_specinputs = ['npnavigator.srpc',
                          'npobjectstub.srpc', ]

npnavigator_rpc_h = GENERATED + '/npnavigator_rpc.h'
npnavigator_rpc_cc = GENERATED + '/npnavigator_rpc_server.cc'
npnavigator_rpc_files = [ npnavigator_rpc_h, npnavigator_rpc_cc ]
env.Command(target=npnavigator_rpc_files,
            source=[SRPCGEN, npnavigator_specinputs],
            action=[Action('${PYTHON} ${SOURCES[0]} '
                           '-s '
                           'NPNavigatorRpcs '
                           'GEN_NPRUNTIME_NPNAVIGATOR_RPC_H_ '
                           + npnavigator_rpc_h + ' '
                           + npnavigator_rpc_cc + ' '
                           ' ${SOURCES[1:]}'),
                   ],
           )

npupcall_specinputs = ['npupcall.srpc']

npupcall_rpc_h = GENERATED + '/npupcall_rpc.h'
npupcall_rpc_cc = GENERATED + '/npupcall_rpc_client.cc'
npupcall_rpc_files = [ npupcall_rpc_h, npupcall_rpc_cc ]
env.Command(target=npupcall_rpc_files,
            source=[SRPCGEN, npupcall_specinputs],
            action=[Action('${PYTHON} ${SOURCES[0]} '
                           '-c '
                           'NPNavigatorRpcs '
                           'GEN_NPRUNTIME_NPUPCALL_RPC_H_ '
                           + npupcall_rpc_h + ' '
                           + npupcall_rpc_cc + ' '
                           ' ${SOURCES[1:]}'),
                   ],
           )

env.ComponentLibrary('google_nacl_npruntime',
                     ['audio.cc',
                      'device2d.cc',
                      'device3d.cc',
                      'main.cc',
                      'naclnp_util.cc',
                      'npbridge.cc',
                      'npclosure.cc',
                      'npextensions.cc',
                      npmodule_rpc_cc,
                      'npn_gate.cc',
                      env_no_strict_aliasing.ComponentObject('npnavigator.cc'),
                      npnavigator_rpc_cc,
                      'npnavigator_rpc_impl.cc',
                      'npobject_proxy.cc',
                      'npobject_stub.cc',
                      'npstub_rpc_impl.cc',
                      npupcall_rpc_cc,
                      'pointer_translations.cc',
                      'structure_translations.cc',
                      ])

headers = [
  'nacl_npapi.h',
  '$SOURCE_ROOT/third_party/npapi/bindings/npapi.h',
  '$SOURCE_ROOT/third_party/npapi/bindings/npruntime.h',
  '$SOURCE_ROOT/third_party/npapi/bindings/npapi_extensions.h',
  '$SOURCE_ROOT/native_client/src/third_party/npapi/files/include/npupp.h',
]

header_install = env.AddHeaderToSdk(headers)
env.AddLibraryToSdk(['google_nacl_npruntime'])

# because nacl_npapi.h imports headers from the sdk
env.Requires('google_nacl_npruntime', header_install)
