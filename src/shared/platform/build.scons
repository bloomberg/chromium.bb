# -*- python -*-
#
# Copyright (c) 2011 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.


import atexit
import os
import tempfile
import shutil
import sys

Import('env')

platform_inputs = [];

if env.Bit('windows'):
  platform_inputs += [
    'win/condition_variable.cc',
    'win/lock.cc',
    'win/nacl_exit.c',
    'win/nacl_find_addrsp.c',
    'win/nacl_host_desc.c',
    'win/nacl_host_dir.c',
    'win/lock_impl_win.cc',
    'win/nacl_secure_random.c',
    'win/nacl_semaphore.c',
    'win/nacl_sync_win.cc',
    'win/nacl_threads.c',
    'win/nacl_time.c',
    'win/nacl_timestamp.c',
    'win/port_win.c',
    'win/time.cc',
    'win/time_win.cc',
    'win/xlate_system_error.c',
  ]
  # Needed for rand_s().
  env.Append(
        CCFLAGS = ['/D_CRT_RAND_S',
                   '/D_UNICODE',
                   '/DUNICODE'],
  )
elif env.Bit('linux'):
  platform_inputs += [
    'linux/nacl_exit.c',
    'linux/nacl_find_addrsp.c',
    'linux/nacl_host_desc.c',
    'linux/nacl_host_dir.c',
    'linux/nacl_secure_random.c',
    'linux/nacl_semaphore.c',
    'linux/nacl_thread_id.c',
    'linux/nacl_threads.c',
    'linux/nacl_time.c',
    'linux/nacl_timestamp.c',
    'linux/condition_variable.c',
    'linux/lock.c',
    ]
elif env.Bit('mac'):
  platform_inputs += [
    'linux/nacl_exit.c',
    'linux/nacl_find_addrsp.c',
    'linux/nacl_host_desc.c',
    'linux/nacl_secure_random.c',
    'linux/nacl_thread_id.c',
    'linux/nacl_threads.c',
    'linux/nacl_time.c',
    'linux/nacl_timestamp.c',
    'osx/nacl_host_dir.c',
    'osx/nacl_semaphore.c',
    'osx/strnlen_osx.c',
    'linux/condition_variable.c',
    'linux/lock.c',
    ]

platform_inputs += [
    'nacl_check.c',
    'nacl_global_secure_random.c',
    'nacl_host_desc_common.c',
    'nacl_interruptible_condvar.c',
    'nacl_interruptible_mutex.c',
    'nacl_log.c',
    'nacl_secure_random_common.c',
    'nacl_sync_checked.c',
    'nacl_time_common.c',
    'platform_init.c',
    'refcount_base.cc',
    ]

env.DualLibrary('platform', platform_inputs)


PORT_TEST_SOURCES = ['platform_tests.cc']

if env.Bit('windows'):
  PORT_TEST_SOURCES = [PORT_TEST_SOURCES,
                       'win/port_win_test.c']
  if env.Bit('build_x86_64'):
    PORT_TEST_SOURCES = [PORT_TEST_SOURCES,
                         'win/test_tls.S']

port_test_exe = env.ComponentProgram('port_test',
                                     PORT_TEST_SOURCES,
                                     EXTRA_LIBS=['platform','gio'])

node = env.CommandTest('port_test.out',
                       [port_test_exe],
                       size='huge')

env.AddNodeToTestSuite(node,
                       ['large_tests'],
                       'run_platform_tests')


nacl_semaphore_test_exe = env.ComponentProgram('nacl_semaphore_test',
                                               ['nacl_semaphore_test.c'],
                                               EXTRA_LIBS=['platform',
                                                           'gio'])
node = env.CommandTest('nacl_semaphore_test.out',
                       command=[nacl_semaphore_test_exe])

# BUG: http://code.google.com/p/nativeclient/issues/detail?id=2142
env.AddNodeToTestSuite(node,
                       ['small_tests'],
                       'run_nacl_semaphore_test',
                       is_broken=env.Bit('target_arm') and env.UsingEmulator())


nacl_host_dir_test_exe = env.ComponentProgram('nacl_host_dir_test',
                                              ['nacl_host_dir_test.c'],
                                              EXTRA_LIBS=['platform',
                                                          'gio',
                                                          'nrd_xfer',
                                                          ])

# For -d, env.Dir does not work.  bradnelson@ says SCons's underlay
# works for Files, but possibly not for Dir -- and apparently it does
# not, since the path that results is OBJ_DIR relative.

# We need a temporary directory, since it is impossible to check a
# file the name of which is exactly 255 characters into subversion.
# (Actually, it has to be 255-9=246 characters or less, since svn
# wants to create a file the name of which is the original with
# '.svn-base' appended.

tmpdir = tempfile.mkdtemp()

def CleanupBinder(dirname):
  def Cleanup():
    shutil.rmtree(dirname, True)
  return Cleanup

atexit.register(CleanupBinder(tmpdir))

node = env.CommandTest('nacl_host_dir_test.out',
                       [nacl_host_dir_test_exe,
                        '-f',
                        env.File('testdata/sample_dir1_expected.txt'),
                        '-d',
                        tmpdir])
env.AddNodeToTestSuite(node, ['small_tests'], 'run_nacl_host_dir_test')

env.EnsureRequiredBuildWarnings()
