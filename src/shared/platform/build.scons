# -*- python -*-
#
# Copyright (c) 2012 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.


import atexit
import os
import tempfile
import shutil
import sys

Import('env')

platform_inputs = []

# On OSX and Windows, we have observed sleep returning early.  The
# time_slop_args is used in nacl_clock_test to permit some extra slop
# in the elapsed time (in ms).
time_slop_args = []

if env.Bit('windows'):
  platform_inputs += [
    'win/aligned_malloc.c',
    'win/condition_variable.cc',
    'win/lock.cc',
    'win/nacl_clock.c',
    'win/nacl_exit.c',
    'win/nacl_fast_mutex.c',
    'win/nacl_find_addrsp.c',
    'win/nacl_host_desc.c',
    'win/nacl_host_dir.c',
    'win/lock_impl_win.cc',
    'win/nacl_secure_random.c',
    'win/nacl_semaphore.c',
    'win/nacl_sync_win.cc',
    'win/nacl_threads.c',
    'win/nacl_time.c',
    'win/nacl_timestamp.c',
    'win/port_win.c',
    'win/time.cc',
    'win/time_win.cc',
    'win/xlate_system_error.c',
  ]
  time_slop_args += [ '-s', '10' ]
  # Needed for rand_s().
  env.Append(
        CCFLAGS = ['/D_CRT_RAND_S',
                   '/D_UNICODE',
                   '/DUNICODE'],
  )
elif env.Bit('linux'):
  platform_inputs += [
    'linux/nacl_clock.c',
    'linux/nacl_host_dir.c',
    'linux/nacl_semaphore.c',
    ]
elif env.Bit('mac'):
  platform_inputs += [
    'osx/nacl_clock.c',
    'osx/nacl_host_dir.c',
    'osx/nacl_semaphore.c',
    'osx/strnlen_osx.c',
    ]
  time_slop_args += [ '-s', '10' ]

if env.Bit('posix'):
  platform_inputs += [
    'posix/aligned_malloc.c',
    'posix/condition_variable.c',
    'posix/lock.c',
    'posix/nacl_exit.c',
    'posix/nacl_fast_mutex.c',
    'posix/nacl_find_addrsp.c',
    'posix/nacl_host_desc.c',
    'posix/nacl_secure_random.c',
    'posix/nacl_thread_id.c',
    'posix/nacl_threads.c',
    'posix/nacl_time.c',
    'posix/nacl_timestamp.c',
    ]

platform_inputs += [
    'nacl_check.c',
    'nacl_global_secure_random.c',
    'nacl_host_desc_common.c',
    'nacl_interruptible_condvar.c',
    'nacl_interruptible_mutex.c',
    'nacl_log.c',
    'nacl_secure_random_common.c',
    'nacl_sync_checked.c',
    'nacl_time_common.c',
    'platform_init.c',
    'refcount_base.cc',
    ]

env.DualLibrary('platform', platform_inputs)


PORT_TEST_SOURCES = ['platform_tests.cc']

if env.Bit('windows'):
  PORT_TEST_SOURCES = [PORT_TEST_SOURCES,
                       'win/port_win_test.c']
  if env.Bit('build_x86_64'):
    PORT_TEST_SOURCES = [PORT_TEST_SOURCES,
                         'win/test_tls.S']

port_test_exe = env.ComponentProgram('port_test',
                                     PORT_TEST_SOURCES,
                                     EXTRA_LIBS=['platform','gio'])

node = env.CommandTest('port_test.out',
                       [port_test_exe],
                       size='huge')

env.AddNodeToTestSuite(node,
                       ['large_tests'],
                       'run_platform_tests')


nacl_semaphore_test_exe = env.ComponentProgram('nacl_semaphore_test',
                                               ['nacl_semaphore_test.c'],
                                               EXTRA_LIBS=['platform',
                                                           'gio'])
node = env.CommandTest('nacl_semaphore_test.out',
                       command=[nacl_semaphore_test_exe])

env.AddNodeToTestSuite(node, ['small_tests'], 'run_nacl_semaphore_test')


nacl_host_dir_test_exe = env.ComponentProgram('nacl_host_dir_test',
                                              ['nacl_host_dir_test.c'],
                                              EXTRA_LIBS=['platform',
                                                          'gio',
                                                          'nrd_xfer',
                                                          ])

# For -d, env.Dir does not work.  bradnelson@ says SCons's underlay
# works for Files, but possibly not for Dir -- and apparently it does
# not, since the path that results is OBJ_DIR relative.

# We need a temporary directory, since it is impossible to check a
# file the name of which is exactly 255 characters into subversion.
# (Actually, it has to be 255-9=246 characters or less, since svn
# wants to create a file the name of which is the original with
# '.svn-base' appended.

tmpdir = tempfile.mkdtemp()

def CleanupDirBinder(dirname):
  def Cleanup():
    shutil.rmtree(dirname, True)
  return Cleanup

atexit.register(CleanupDirBinder(tmpdir))

node = env.CommandTest('nacl_host_dir_test.out',
                       [nacl_host_dir_test_exe,
                        '-f',
                        env.File('testdata/sample_dir1_expected.txt'),
                        '-d',
                        tmpdir])
env.AddNodeToTestSuite(node, ['small_tests'], 'run_nacl_host_dir_test')


nacl_clock_test_exe = env.ComponentProgram('nacl_clock_test',
                                           ['nacl_clock_test.c'],
                                           EXTRA_LIBS=['platform'])

node = env.CommandTest('nacl_clock_test.out',
                       [nacl_clock_test_exe] + time_slop_args)

env.AddNodeToTestSuite(node, ['small_tests'], 'run_nacl_clock_test')


nacl_sync_test_exe = env.ComponentProgram('nacl_sync_test',
                                          ['nacl_sync_test.c'],
                                          EXTRA_LIBS=['platform'])

for flavor in ['lock_lock', 'lock_trylock',
               'trylock_lock', 'trylock_trylock']:
  name = 'nacl_sync_' + flavor + '_test'
  node = env.CommandTest(name + '.out',
                         [nacl_sync_test_exe, '-T', flavor])

  env.AddNodeToTestSuite(node, ['small_tests'], 'run_' + name)

nacl_host_desc_mmap_test_exe = env.ComponentProgram(
    'nacl_host_desc_mmap_test',
    ['nacl_host_desc_mmap_test.c'],
    EXTRA_LIBS=['platform',
                'nrd_xfer'])  # nrd_xfer for NaClDescEffectorTrustedMem

temporary_file = tempfile.mktemp()

def CleanupFileBinder(fn):
  def Cleanup():
    try:
      os.unlink(fn)
    except OSError:
      pass
  return Cleanup

atexit.register(CleanupFileBinder(temporary_file))

node = env.CommandTest('nacl_host_desc_mmap_test.out',
                       [nacl_host_desc_mmap_test_exe, '-f', temporary_file])

env.AddNodeToTestSuite(node, ['small_tests'], 'run_nacl_host_desc_mmap_test',
                       is_broken=env.UsingEmulator())

env.EnsureRequiredBuildWarnings()
