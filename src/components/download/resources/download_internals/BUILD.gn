# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

# preprocess for <if expr>.
preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/preprocess"
  out_manifest = "$target_gen_dir/manifest.json"
  in_files = [ "download_internals.js" ]
}

generate_grd("build_grd") {
  grd_prefix = "download_internals"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [
    "download_internals_browser_proxy.js",
    "download_internals.css",
    "download_internals.html",
    "download_internals_visuals.js",
  ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [ ":preprocess" ]
  manifest_files = [ "$target_gen_dir/manifest.json" ]
}

grit("resources") {
  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/download_internals_resources.h",
    "grit/download_internals_resources_map.cc",
    "grit/download_internals_resources_map.h",
    "download_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/components"
}

js_type_check("closure_compile") {
  deps = [
    ":download_internals",
    ":download_internals_browser_proxy",
    ":download_internals_visuals",
  ]
}

js_library("download_internals") {
  deps = [
    ":download_internals_browser_proxy",
    "//third_party/jstemplate:jstemplate",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("download_internals_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}

js_library("download_internals_visuals") {
  deps = [ ":download_internals_browser_proxy" ]
}
