# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

source_set("public") {
  sources = [
    "client.h",
    "clients.h",
    "download_metadata.cc",
    "download_metadata.h",
    "download_params.cc",
    "download_params.h",
    "download_service.h",
    "download_task_types.h",
    "features.cc",
    "features.h",
    "logger.h",
    "navigation_monitor.h",
    "service_config.h",
    "task_scheduler.h",
  ]

  deps = [
    "//components/keyed_service/core",
  ]

  # TODO(xingliu): Create blob target that doesn't need to depend on blink.
  # Currently gn header check will fail even if the code is not built into any targets.
  if (!is_ios) {
    deps += [ "//storage/browser" ]
  }

  public_deps = [
    "//base",
    "//net",
    "//url",
  ]
}

if (is_android) {
  android_library("public_java") {
    srcjar_deps = [ ":jni_enums" ]

    deps = [
      "//base:base_java",
      "//third_party/android_tools:android_support_annotations_java",
    ]
  }

  java_cpp_enum("jni_enums") {
    visibility = [ "*" ]

    sources = [
      "download_task_types.h",
    ]
  }
}
