# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

static_library("core") {
  sources = [
    "memories_features.cc",
    "memories_features.h",
    "memories_remote_model_helper.cc",
    "memories_remote_model_helper.h",
    "memories_service.cc",
    "memories_service.h",
    "visit_data.h",
  ]
  deps = [
    ":mojo_bindings",
    "//base",
    "//components/history/core/browser",
    "//components/keyed_service/core",
    "//services/data_decoder/public/cpp",
    "//services/network/public/cpp",
    "//url",
  ]
}

mojom("mojo_bindings") {
  sources = [ "memories.mojom" ]
  webui_module_path = "/components/history_clusters/core"
  public_deps = [
    "//mojo/public/mojom/base",
    "//url/mojom:url_mojom_gurl",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "memories_service_unittest.cc" ]
  deps = [
    ":core",
    ":test_support",
    "//base/test:test_support",
    "//components/history/core/browser",
    "//services/data_decoder/public/cpp:test_support",
    "//services/network:test_support",
    "//testing/gtest",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [ "memories_service_test_api.h" ]
  deps = [ ":core" ]
}
