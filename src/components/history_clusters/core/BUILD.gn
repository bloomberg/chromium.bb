# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//build/config/chrome_build.gni")
import("//mojo/public/tools/bindings/mojom.gni")

declare_args() {
  # You can set the variable 'build_with_on_device_clustering_backend' to true
  # to use the on-device clustering backend even in a developer build.  Setting
  # this variable explicitly to true will cause your build to fail if the
  # internal files are missing.
  build_with_on_device_clustering_backend =
      is_chrome_branded && !is_android && !is_ios
}

buildflag_header("history_clusters_buildflags") {
  header = "history_clusters_buildflags.h"
  flags = [ "BUILD_WITH_ON_DEVICE_CLUSTERING_BACKEND=$build_with_on_device_clustering_backend" ]
}

static_library("core") {
  sources = [
    "history_clusters_service.cc",
    "history_clusters_service.h",
    "memories_features.cc",
    "memories_features.h",
    "remote_clustering_backend.cc",
    "remote_clustering_backend.h",
    "visit_data.h",
  ]
  deps = [
    ":clustering_backend",
    ":history_clusters_buildflags",
    ":mojo_bindings",
    "//base",
    "//components/history/core/browser",
    "//components/history_clusters/core/proto",
    "//components/keyed_service/core",
    "//components/query_parser",
    "//net",
    "//services/network/public/cpp",
    "//ui/base",
    "//url",
  ]
  if (build_with_on_device_clustering_backend) {
    deps += [ "//components/history_clusters/internal" ]
  }
}

source_set("clustering_backend") {
  sources = [ "clustering_backend.h" ]
  public_deps = [
    "//base",
    "//components/history/core/browser",
  ]
}

mojom("mojo_bindings") {
  sources = [ "history_clusters.mojom" ]
  webui_module_path = "/components/history_clusters/core"
  public_deps = [
    "//mojo/public/mojom/base",
    "//url/mojom:url_mojom_gurl",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "history_clusters_service_unittest.cc",
    "remote_clustering_backend_unittest.cc",
  ]
  deps = [
    ":clustering_backend",
    ":core",
    ":test_support",
    "//base/test:test_support",
    "//components/history/core/browser",
    "//components/history/core/test",
    "//components/history_clusters/core:mojo_bindings",
    "//components/history_clusters/core/proto",
    "//services/network:test_support",
    "//testing/gtest",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "history_clusters_service_test_api.cc",
    "history_clusters_service_test_api.h",
  ]
  deps = [
    ":clustering_backend",
    ":core",
    "//base/test:test_support",
    "//components/history/core/browser",
    "//components/history/core/test",
  ]
}
