# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("net") {
  sources = [
    "variations_command_line.cc",
    "variations_command_line.h",
    "variations_http_headers.cc",
    "variations_http_headers.h",
  ]

  public_deps = [
    ":omnibox_http_headers",
    "//components/variations",
    "//net",
    "//services/network/public/cpp:cpp",
    "//services/network/public/cpp:cpp_base",
    "//url",
  ]

  deps = [
    "//base",
    "//components/google/core/common",
    "//components/metrics",
    "//components/variations/field_trial_config",
    "//components/variations/proto",
  ]

  # third_party/blink/public/common does not build on iOS
  if (!is_ios) {
    sources += [
      "variations_url_loader_throttle.cc",
      "variations_url_loader_throttle.h",
    ]
    deps += [ "//third_party/blink/public/common" ]
  }
}

component("omnibox_http_headers") {
  output_name = "omnibox_http_headers"

  defines = [ "IS_OMNIBOX_HTTP_HEADERS_IMPL" ]

  sources = [
    "omnibox_http_headers.cc",
    "omnibox_http_headers.h",
  ]

  deps = [
    "//base",
    "//components/google/core/common",
    "//net",
    "//services/network/public/cpp:cpp",
    "//services/network/public/cpp:cpp_base",
    "//url",
  ]

  # third_party/blink/public/common does not build on iOS
  if (!is_ios) {
    sources += [
      "omnibox_url_loader_throttle.cc",
      "omnibox_url_loader_throttle.h",
    ]
    deps += [ "//third_party/blink/public/common" ]
  }
}
