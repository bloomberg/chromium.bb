# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

source_set("content") {
  sources = [
    "page_load_trigger_context.cc",
    "page_load_trigger_context.h",
    "segmentation_platform_tab_helper.cc",
    "segmentation_platform_tab_helper.h",
  ]

  deps = [
    "//base",
    "//components/segmentation_platform/public",
    "//content/public/browser",
  ]

  if (is_android) {
    deps += [ ":jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true

  # IMPORTANT NOTE: When adding new tests, also remember to update the list of
  # tests in //components/segmentation_platform/components_unittests.filter
  sources = []

  deps = [
    ":content",
    "//testing/gtest",
  ]
}

if (is_android) {
  android_library("content_java") {
    visibility = [ "*" ]
    sources = [ "android/java/src/org/chromium/components/segmentation_platform/PageLoadTriggerContext.java" ]

    deps = [
      "//base:base_java",
      "//base:jni_java",
      "//build/android:build_java",
      "//components/segmentation_platform/public:public_java",
      "//content/public/android:content_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//ui/android:ui_no_recycler_view_java",
    ]
    annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  }

  generate_jni("jni_headers") {
    visibility = [ ":*" ]

    sources = [ "android/java/src/org/chromium/components/segmentation_platform/PageLoadTriggerContext.java" ]
  }
}
