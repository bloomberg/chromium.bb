# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/web/js_compile.gni")
import("//testing/test.gni")

source_set("form_util") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "form_activity_observer.h",
    "form_activity_observer_bridge.h",
    "form_activity_observer_bridge.mm",
    "form_activity_tab_helper.h",
    "form_activity_tab_helper.mm",
  ]
  deps = [
    "//base",
    "//ios/web/public",
  ]
}

source_set("test_support") {
  testonly = true
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "test_form_activity_observer.h",
    "test_form_activity_observer.mm",
    "test_form_activity_tab_helper.h",
    "test_form_activity_tab_helper.mm",
  ]
  deps = [
    ":form_util",
    "//base",
    "//ios/web/public",
    "//testing/gtest",
  ]
}

source_set("unit_tests") {
  testonly = true
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "fill_js_unittest.mm",
    "form_activity_observer_bridge_unittest.mm",
    "form_activity_tab_helper_unittest.mm",
    "form_unittest.mm",
  ]
  deps = [
    ":form_js",
    ":form_util",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//ios/chrome/app:tests_fake_hook",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/chrome/browser/tabs:tabs_internal",
    "//ios/chrome/browser/web:test_support",
    "//ios/web/public/test",
    "//ios/web/public/test/fakes",
    "//ios/web/web_state/js",
    "//testing/gtest",
  ]
}

js_compile_checked("form_js") {
  visibility = [ ":unit_tests" ]
  testonly = true
  sources = [
    "resources/form.js",
  ]
}
