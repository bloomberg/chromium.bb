# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

generate_jni("jni_headers") {
  sources = [
    "java/src/org/chromium/components/language/AndroidLanguageMetricsBridge.java",
    "java/src/org/chromium/components/language/GeoLanguageProviderBridge.java",
  ]
}

source_set("language_bridge") {
  sources = [
    "android_language_metrics_bridge.cc",
    "geo_language_provider_bridge.cc",
  ]
  deps = [
    ":jni_headers",
    "//base",
    "//components/language/content/browser:browser",
  ]
}

android_library("java") {
  sources = [
    "java/src/org/chromium/components/language/AndroidLanguageMetricsBridge.java",
    "java/src/org/chromium/components/language/GeoLanguageProviderBridge.java",
    "java/src/org/chromium/components/language/LanguageProfileController.java",
    "java/src/org/chromium/components/language/LanguageProfileMetricsLogger.java",
  ]

  deps = [
    ":ulp_delegate_java",
    "//base:base_java",
    "//base:jni_java",
    "//build/android:build_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

android_library("ulp_delegate_java") {
  sources = [
    "java/src/org/chromium/components/language/LanguageProfileDelegate.java",
    "java/src/org/chromium/components/language/LanguageProfileDelegateImpl.java",
  ]

  # Allow downstream targets to provide their own implementations.
  jar_excluded_patterns = [ "*/LanguageProfileDelegateImpl.class" ]
}

android_library("ulp_delegate_public_java") {
  sources = [ "java/src/org/chromium/components/language/LanguageProfileDelegateImpl.java" ]

  deps = [
    ":ulp_delegate_java",
    "//build/android:build_java",
  ]
}

java_library("junit") {
  # Skip platform checks since Robolectric depends on requires_android targets.
  bypass_platform_checks = true
  testonly = true
  sources = [ "java/src/org/chromium/components/language/LanguageProfileControllerUnitTest.java" ]
  deps = [
    ":java",
    ":ulp_delegate_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//build/android:build_java",
    "//components/browser_ui/widget/android:java",
    "//content/public/android:content_java",
    "//content/public/test/android:content_java_test_support",
    "//third_party/android_deps:robolectric_all_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
    "//ui/android:ui_java",
  ]
}

android_library("javatests") {
  testonly = true

  sources = [ "java/src/org/chromium/components/language/AndroidLanguageMetricsBridgeTest.java" ]
  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//build/android:build_java",
    "//chrome/browser/flags:java",
    "//chrome/test/android:chrome_java_test_support",
    "//content/public/test/android:content_java_test_support",
    "//third_party/android_support_test_runner:rules_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/junit",
  ]
}
