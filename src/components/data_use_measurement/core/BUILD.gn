# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("core") {
  sources = [
    "data_use_user_data.cc",
    "data_use_user_data.h",
  ]
  deps = [
    "//base",
    "//net",
  ]
}

static_library("ascriber") {
  sources = [
    "data_use.cc",
    "data_use.h",
    "data_use_ascriber.cc",
    "data_use_ascriber.h",
    "data_use_measurement.cc",
    "data_use_measurement.h",
    "data_use_network_delegate.cc",
    "data_use_network_delegate.h",
    "data_use_recorder.cc",
    "data_use_recorder.h",
  ]
  deps = [
    ":core",
    "//base",
    "//components/metrics",
    "//google_apis",
    "//net",
  ]

  if (!is_ios) {
    deps += [ "//components/domain_reliability" ]
  }
}

source_set("unit_tests") {
  sources = [
    "data_use_measurement_unittest.cc",
    "data_use_network_delegate_unittest.cc",
  ]
  testonly = true
  deps = [
    ":ascriber",
    ":core",
    "//base/test:test_support",
    "//components/metrics:metrics",
    "//net:test_support",
    "//testing/gtest",
  ]
}
