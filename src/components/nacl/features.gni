# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromecast_build.gni")
import("//build/config/gclient_args.gni")

if (is_mac) {
  import("//build/config/mac/mac_sdk.gni")
}

declare_args() {
  # Enables Native Client support.
  # Temporarily disable nacl on arm64 linux to get rid of compilation errors.
  # TODO: When mipsel-nacl-clang is available, drop the exclusion.
  enable_nacl =
      checkout_nacl && target_os != "ios" && !is_android && !is_fuchsia &&
      !is_chromecast && current_cpu != "mipsel" && current_cpu != "mips64el" &&
      target_cpu != "arm64" && !(is_win && host_os != "win")

  # Non-SFI is not yet supported on mipsel
  enable_nacl_nonsfi = current_cpu != "mipsel" && current_cpu != "mips64el"
}

assert(!(is_win && host_os != "win") || !enable_nacl,
       "NaCl doesn't work in win cross builds, crbug.com/774186")
assert(!enable_nacl || checkout_nacl)

if (is_mac && enable_nacl && machine_os_build_major >= 19) {
  print(
      "********************************************************************************")
  print(
      " WARNING: The NaCL SDK is 32-bit only. macOS 10.15+ removed support for 32-bit")
  print(
      "          executables. To fix, set enable_nacl=false in args.gn, or downgrade to")
  print(
      "          macOS 10.14. For more information, see https://crbug.com/1049832.")
  print(
      "********************************************************************************")
  assert(false, "NaCL SDK is incompatible with host macOS version")
}
