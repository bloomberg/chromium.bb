# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//ppapi/buildflags/buildflags.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "font_service_app.cc",
    "font_service_app.h",
    "fontconfig_matching.cc",
    "fontconfig_matching.h",
  ]

  deps = [
    "//base",
    "//components/services/font/public/interfaces",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//ppapi/buildflags:buildflags",
    "//services/service_manager/public/cpp",
    "//third_party/fontconfig",
    "//ui/gfx",
  ]

  public_deps = [
    "//skia",
  ]

  if (is_linux && enable_plugins) {
    deps += [ ":ppapi_fontconfig_matching" ]
  }

  if (is_linux) {
    deps += [ "//base/test:fontconfig_util_linux" ]
  }
}

if (is_linux && enable_plugins) {
  source_set("ppapi_fontconfig_matching") {
    sources = [
      "ppapi_fontconfig_matching.cc",
      "ppapi_fontconfig_matching.h",
    ]

    deps = [
      "//base:base",
      "//ppapi/buildflags:buildflags",
      "//ppapi/c",
    ]
  }
}

service("font_service") {
  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
    "//base",
    "//mojo/public/c/system",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/mojom",
  ]
}

service_manifest("manifest") {
  name = "font_service"
  source = "manifest.json"
}

test("font_service_unittests") {
  sources = [
    "font_loader_unittest.cc",
  ]

  deps = [
    ":font_service_unittests_catalog_source",
    "//base",
    "//base/test:test_support",
    "//components/services/font/public/cpp",
    "//components/services/font/public/interfaces",
    "//mojo/core/test:run_all_unittests",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//ppapi/buildflags:buildflags",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp/test:test_support",
    "//skia",
    "//testing/gtest",
  ]

  if (enable_plugins) {
    deps += [
      "//ppapi/c",
      "//third_party:freetype_harfbuzz",
    ]
  }

  data_deps = [
    ":font_service",
  ]
}

service_manifest("test_manifest") {
  name = "font_service_unittests"
  source = "test_manifest.json"
}

catalog("font_service_unittests_catalog") {
  embedded_services = [ ":test_manifest" ]
  standalone_services = [ ":manifest" ]
}

catalog_cpp_source("font_service_unittests_catalog_source") {
  testonly = true
  catalog = ":font_service_unittests_catalog"
  generated_function_name = "font_service::test::CreateTestCatalog"
}
