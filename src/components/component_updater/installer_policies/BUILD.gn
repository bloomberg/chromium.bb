# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("installer_policies") {
  public_deps = [
    ":installer_policies_no_content_deps",
    ":installer_policies_with_content_deps",
  ]
}

# Installer policies that can be used in targets that can't depend on
# //content/ such as //android_webview:nonembedded.
# Prefer adding installer policies here.
static_library("installer_policies_no_content_deps") {
  sources = [
    "autofill_states_component_installer.cc",
    "autofill_states_component_installer.h",
    "client_side_phishing_component_installer_policy.cc",
    "client_side_phishing_component_installer_policy.h",
    "origin_trials_component_installer.cc",
    "origin_trials_component_installer.h",
    "safety_tips_component_installer.cc",
    "safety_tips_component_installer.h",
    "trust_token_key_commitments_component_installer_policy.cc",
    "trust_token_key_commitments_component_installer_policy.h",
    "url_param_classification_component_installer.cc",
    "url_param_classification_component_installer.h",
  ]

  deps = [
    "//base",
    "//components/autofill/core/browser",
    "//components/component_updater",
    "//components/prefs:prefs",
    "//components/reputation/core",
    "//components/reputation/core:proto",
    "//components/url_param_filter/core:core",
    "//components/url_param_filter/core:url_param_filter_classification_proto",
  ]

  # Disallow depending on content.
  assert_no_deps = [ "//content/public" ]
}

static_library("installer_policies_with_content_deps") {
  sources = [
    "on_device_head_suggest_component_installer.cc",
    "on_device_head_suggest_component_installer.h",
    "optimization_hints_component_installer.cc",
    "optimization_hints_component_installer.h",
  ]

  deps = [
    "//base",
    "//components/component_updater",
    "//components/omnibox/browser",
    "//components/omnibox/common",
    "//components/optimization_guide/core",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "autofill_states_component_installer_unittest.cc",
    "optimization_hints_component_installer_unittest.cc",
    "trust_token_key_commitments_component_installer_policy_unittest.cc",
    "url_param_classification_component_installer_unittest.cc",
  ]

  deps = [
    ":installer_policies",
    "//base",
    "//base/test:test_support",
    "//components/autofill/core/browser:test_support",
    "//components/component_updater:test_support",
    "//components/optimization_guide/core",
    "//components/url_param_filter/core:core",
    "//components/url_param_filter/core:test_support",
    "//components/url_param_filter/core:url_param_filter_classification_proto",
    "//services/network/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
