# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

source_set("renderer") {
  deps = [
    ":demuxer",
    "//base",
    "//components/cast_streaming/public",
    "//content/public/renderer",
    "//third_party/blink/public:blink_headers",
    "//third_party/blink/public/common",
  ]
  public = [ "public/demuxer_provider.h " ]
  sources = [
    "cast_streaming_render_frame_observer.cc",
    "cast_streaming_render_frame_observer.h",
    "demuxer_provider.cc",
  ]
}

source_set("renderer_controller_proxy") {
  public = [ "public/renderer_controller_proxy.h" ]
  sources = [ "renderer_controller_proxy.cc" ]
  public_deps = [
    "//base",
    "//components/cast_streaming/public/mojom",
    "//mojo/public/cpp/bindings",
  ]
}

source_set("wrapping_renderer_factory_selector") {
  public = [ "public/wrapping_renderer_factory_selector.h" ]
  sources = [ "wrapping_renderer_factory_selector.cc" ]
  public_deps = [ "//media" ]
  deps = [
    ":playback_command_forwarding_renderer",
    ":renderer_controller_proxy",
    "//base",
  ]
}

source_set("renderer_controller_proxy_factory") {
  public = [ "public/renderer_controller_proxy_factory.h" ]
  sources = [
    "renderer_controller_proxy_impl.cc",
    "renderer_controller_proxy_impl.h",
  ]
  public_deps = [
    ":renderer_controller_proxy",
    "//base",
    "//components/cast_streaming/public/mojom",
    "//mojo/public/cpp/bindings",
  ]
}

source_set("demuxer") {
  deps = [
    ":decoder_buffer_reader",
    "//base",
    "//components/cast_streaming/public/mojom",
    "//media",
    "//media/mojo/common",
    "//mojo/public/cpp/bindings",
  ]
  sources = [
    "cast_streaming_demuxer.cc",
    "cast_streaming_demuxer.h",
    "cast_streaming_receiver.cc",
    "cast_streaming_receiver.h",
  ]
  visibility = [ ":*" ]
}

source_set("decoder_buffer_reader") {
  deps = [
    "//base",
    "//media",
    "//media/mojo/common",
    "//mojo/public/cpp/bindings",
  ]
  sources = [
    "decoder_buffer_reader.cc",
    "decoder_buffer_reader.h",
  ]
  visibility = [ ":*" ]
}

source_set("playback_command_forwarding_renderer") {
  sources = [
    "playback_command_forwarding_renderer.cc",
    "playback_command_forwarding_renderer.h",
    "playback_command_forwarding_renderer_factory.cc",
    "playback_command_forwarding_renderer_factory.h",
  ]
  deps = [
    "//base",
    "//media",
    "//media/mojo/mojom",
    "//mojo/public/cpp/bindings",
  ]
  visibility = [ ":*" ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "decoder_buffer_reader_unittest.cc",
    "playback_command_forwarding_renderer_factory_unittest.cc",
    "playback_command_forwarding_renderer_unittest.cc",
  ]

  deps = [
    ":decoder_buffer_reader",
    ":playback_command_forwarding_renderer",
    ":renderer",
    ":renderer_controller_proxy",
    ":renderer_controller_proxy_factory",
    ":wrapping_renderer_factory_selector",
    "//base/test:test_support",
    "//components/cast_streaming/public/mojom",
    "//media:test_support",
    "//media/mojo:test_support",
    "//media/mojo/mojom",
    "//mojo/public/cpp/test_support:test_utils",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
