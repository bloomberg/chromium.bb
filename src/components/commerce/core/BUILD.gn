# Copyright 2021 The Chromium Authors.All rights reserved.
# Use of this source code is governed by a BSD - style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

source_set("public") {
  sources = [
    "discount_consent_handler.h",
    "shopping_prompt.h",
  ]
}

source_set("feature_list") {
  sources = [
    "commerce_feature_list.cc",
    "commerce_feature_list.h",
    "flag_descriptions.cc",
    "flag_descriptions.h",
  ]

  deps = [
    "//base",
    "//components/flags_ui",
    "//components/search",
    "//third_party/re2:re2",
    "//url:url",
  ]

  if (!is_android) {
    deps += [ ":commerce_heuristics_data" ]
  }
}

source_set("feature_list_unittests") {
  testonly = true
  sources = [ "commerce_feature_list_unittest.cc" ]
  deps = [
    ":feature_list",
    "//base",
    "//base/test:test_support",
    "//testing/gtest",
    "//third_party/re2:re2",
  ]

  if (!is_android) {
    deps += [ ":commerce_heuristics_data" ]
  }
}

static_library("metrics") {
  sources = [
    "metrics/metrics_utils.cc",
    "metrics/metrics_utils.h",
  ]

  deps = [
    ":proto",
    "//base",
    "//components/optimization_guide/core",
    "//components/optimization_guide/proto:optimization_guide_proto",
    "//components/prefs",
  ]
}

proto_library("proto") {
  proto_in_dir = "//"
  sources = [ "proto/price_tracking.proto" ]
}

if (is_android) {
  proto_java_library("proto_java") {
    proto_path = "."
    sources = [ "proto/price_tracking.proto" ]
  }
}

if (!is_android) {
  source_set("commerce_heuristics_data") {
    sources = [
      "commerce_heuristics_data.cc",
      "commerce_heuristics_data.h",
      "commerce_heuristics_data_metrics_helper.cc",
      "commerce_heuristics_data_metrics_helper.h",
    ]

    deps = [
      "//base",
      "//third_party/re2:re2",
    ]
  }

  source_set("commerce_heuristics_data_unittests") {
    testonly = true
    sources = [ "commerce_heuristics_data_unittest.cc" ]
    deps = [
      ":commerce_heuristics_data",
      "//base",
      "//base/test:test_support",
      "//testing/gtest",
      "//third_party/re2:re2",
    ]
  }

  source_set("heuristics_provider") {
    sources = [
      "heuristics/commerce_heuristics_provider.cc",
      "heuristics/commerce_heuristics_provider.h",
    ]

    deps = [
      ":commerce_heuristics_data",
      ":feature_list",
      "//base",
      "//components/resources:components_resources_grit",
      "//net",
      "//third_party/re2:re2",
      "//ui/base",
      "//url:url",
    ]
  }
}

static_library("shopping_service") {
  sources = [
    "pref_names.cc",
    "pref_names.h",
    "shopping_bookmark_model_observer.cc",
    "shopping_bookmark_model_observer.h",
    "shopping_service.cc",
    "shopping_service.h",
  ]

  deps = [
    ":feature_list",
    ":proto",
    "//base",
    "//components/bookmarks/browser",
    "//components/keyed_service/core",
    "//components/optimization_guide/core",
    "//components/optimization_guide/proto:optimization_guide_proto",
    "//components/prefs:prefs",
    "//url:url",
  ]

  if (is_android) {
    sources += [
      "android/shopping_service_android.cc",
      "android/shopping_service_android.h",
    ]

    deps += [
      ":shopping_service_jni_headers",
      "//url:gurl_android",
    ]
  }
}

if (is_android) {
  generate_jni("shopping_service_jni_headers") {
    sources = [ "android/java/src/org/chromium/components/commerce/core/ShoppingService.java" ]
  }
}

source_set("shopping_service_unit_tests") {
  testonly = true

  sources = [ "shopping_service_unittest.cc" ]

  deps = [
    ":feature_list",
    ":shopping_service",
    "//base/test:test_support",
    "//components/bookmarks/browser",
    "//components/bookmarks/test",
    "//components/commerce/core:proto",
    "//components/optimization_guide/core",
    "//components/optimization_guide/proto:optimization_guide_proto",
    "//testing/gtest",
    "//url:url",
  ]
}
