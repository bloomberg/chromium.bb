# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

if (!is_android) {
  static_library("live_caption") {
    sources = [
      "caption_bubble_context.h",
      "caption_bubble_controller.h",
      "live_caption_controller.cc",
      "live_caption_controller.h",
    ]

    deps = [
      "//base",
      "//components/keyed_service/core",
      "//components/live_caption:constants",
      "//components/live_caption:utils",
      "//components/pref_registry",
      "//components/prefs",
      "//components/soda",
      "//components/soda:constants",
      "//components/sync_preferences",
      "//media",
      "//media/mojo/mojom",
      "//ui/native_theme",
    ]

    if (toolkit_views) {
      sources += [
        "views/caption_bubble.cc",
        "views/caption_bubble.h",
        "views/caption_bubble_controller_views.cc",
        "views/caption_bubble_controller_views.h",
        "views/caption_bubble_model.cc",
        "views/caption_bubble_model.h",
      ]

      deps += [
        "//components/strings",
        "//components/vector_icons",
        "//third_party/re2",
        "//ui/accessibility",
        "//ui/base",
        "//ui/gfx",
        "//ui/strings:ui_strings_grit",
        "//ui/views",
      ]
    }  # toolkit_views
  }
}  # !is_android

source_set("constants") {
  sources = [
    "pref_names.cc",
    "pref_names.h",
  ]

  deps = [
    "//base",
    "//components/prefs",
    "//media",
  ]

  if (!is_android) {
    deps += [ "//components/soda:constants" ]
  }
}

source_set("utils") {
  sources = [
    "caption_util.cc",
    "caption_util.h",
  ]

  deps = [
    ":constants",
    "//base",
    "//components/prefs",
    "//media",
    "//ui/base",
    "//ui/native_theme",
  ]

  if (is_chromeos_ash) {
    deps += [ "//ash/constants" ]
  }

  if (is_chromeos_lacros) {
    deps += [ "//chromeos/startup" ]
  }
}
