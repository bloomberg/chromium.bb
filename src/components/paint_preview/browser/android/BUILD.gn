# Copyright 2020 The Chromium Authors.All rights reserved.
# Use of this source code is governed by a BSD - style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

assert(is_android, "This directory should only be compiled for Android.")

generate_jni("jni_headers") {
  sources = [ "java/src/org/chromium/components/paintpreview/browser/PaintPreviewUtils.java" ]
}

android_library("java") {
  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]

  sources = [
    "java/src/org/chromium/components/paintpreview/browser/NativePaintPreviewServiceProvider.java",
    "java/src/org/chromium/components/paintpreview/browser/PaintPreviewUtils.java",
  ]

  deps = [
    "//base:base_java",
    "//base:jni_java",
    "//content/public/android:content_java",
  ]
}

source_set("android") {
  sources = [
    "paint_preview_utils.cc",
    "paint_preview_utils.h",
  ]

  deps = [
    ":jni_headers",
    "//base",
    "//components/paint_preview/browser",
    "//components/paint_preview/buildflags",
    "//components/ukm/content",
    "//content/public/browser",
    "//services/metrics/public/cpp:ukm_builders",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "paint_preview_utils_unittest.cc" ]

  deps = [
    ":android",
    "//base",
    "//base/test:test_support",
    "//components/paint_preview/common:test_utils",
    "//components/paint_preview/common/mojom",
    "//components/ukm:test_support",
    "//components/ukm/content",
    "//content/public/browser",
    "//content/test:test_support",
    "//services/metrics/public/cpp:ukm_builders",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/blink/public:blink_headers",
    "//third_party/blink/public/common",
    "//third_party/zlib/google:zip",
  ]
}
