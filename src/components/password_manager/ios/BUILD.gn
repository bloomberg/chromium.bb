# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/web/js_compile.gni")

component("ios") {
  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    ":account_select_fill_data",
    ":password_manager_feature",
    "//base",
    "//components/autofill/core/browser",
    "//components/autofill/core/common",
    "//components/autofill/ios/browser",
    "//components/autofill/ios/browser:util",
    "//components/autofill/ios/form_util",
    "//components/autofill/ios/form_util:form_util_feature",
    "//components/password_manager/core/browser",
    "//components/password_manager/core/browser/form_parsing",
    "//components/password_manager/core/common",
    "//components/security_state/ios",
    "//components/strings:components_strings_grit",
    "//ios/web/common",
    "//ios/web/public",
    "//ios/web/public/js_messaging",
    "//ui/base",
    "//url",
  ]

  sources = [
    "password_form_helper.h",
    "password_form_helper.mm",
    "password_generation_provider.h",
    "password_manager_client_bridge.h",
    "password_manager_driver_bridge.h",
    "password_manager_ios_util.h",
    "password_manager_ios_util.mm",
    "password_suggestion_helper.h",
    "password_suggestion_helper.mm",
    "shared_password_controller.h",
    "shared_password_controller.mm",
  ]
}

source_set("password_manager_feature") {
  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    ":account_select_fill_data",
    ":password_controller_js",
    "//base",
    "//components/autofill/core/common",
    "//components/autofill/ios/browser:util",
    "//components/autofill/ios/form_util:form_util_feature",
    "//ios/web/public/js_messaging",
  ]

  sources = [
    "password_manager_java_script_feature.h",
    "password_manager_java_script_feature.mm",
  ]
}

source_set("account_select_fill_data") {
  deps = [
    "//base",
    "//components/autofill/core/common",
    "//components/autofill/ios/browser:util",
    "//url",
  ]

  sources = [
    "account_select_fill_data.cc",
    "account_select_fill_data.h",
  ]
}

js_compile_bundle("password_controller_js") {
  closure_entry_point = "__crWeb.passwords"
  sources = [ "resources/password_controller.js" ]
}

static_library("test_support") {
  testonly = true
  sources = [
    "test_helpers.cc",
    "test_helpers.h",
  ]

  deps = [
    ":account_select_fill_data",
    ":ios",
    "//base",
    "//components/autofill/core/common",
    "//url",
  ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "account_select_fill_data_unittest.cc",
    "password_form_helper_unittest.mm",
    "shared_password_controller_unittest.mm",
  ]
  deps = [
    ":account_select_fill_data",
    ":ios",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//components/autofill/core/common",
    "//components/autofill/ios/browser",
    "//components/autofill/ios/form_util",
    "//components/autofill/ios/form_util:form_util_feature",
    "//components/autofill/ios/form_util:test_support",
    "//components/password_manager/core/browser",
    "//components/password_manager/core/browser:test_support",
    "//components/password_manager/ios:password_manager_feature",
    "//ios/web/public/js_messaging",
    "//ios/web/public/test",
    "//ios/web/public/test:test_fixture",
    "//ios/web/public/test/fakes",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/ocmock",
  ]
}
