# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

devtools_domains = [
  "accessibility",
  "animation",
  "application_cache",
  "browser",
  "cache_storage",
  "console",
  "css",
  "database",
  "debugger",
  "device_orientation",
  "dom",
  "dom_debugger",
  "dom_snapshot",
  "dom_storage",
  "emulation",
  "headless_experimental",
  "heap_profiler",
  "indexeddb",
  "input",
  "inspector",
  "io",
  "layer_tree",
  "log",
  "memory",
  "network",
  "page",
  "performance",
  "profiler",
  "runtime",
  "security",
  "service_worker",
  "target",
  "tracing",
]

generated_devtools_api = []
foreach(domain, devtools_domains) {
  generated_devtools_api += [
    "$target_gen_dir/devtools/domains/" + domain + ".cc",
    "$target_gen_dir/devtools/domains/" + domain + ".h",
    "$target_gen_dir/devtools/domains/types_" + domain + ".h",
    "$target_gen_dir/devtools/domains/types_" + domain + ".cc",
    "$target_gen_dir/devtools/internal/type_conversions_" + domain + ".h",
    "$target_gen_dir/devtools/internal/" + "types_forward_declarations_" +
        domain + ".h",
  ]
}

action("gen_devtools_client_api") {
  script = "devtools_api/client_api_generator.py"
  deps = [ "//third_party/blink/public/devtools_protocol:protocol_version" ]
  inputs = [
    "$root_gen_dir/third_party/blink/public/devtools_protocol/protocol.json",
  ]

  outputs = generated_devtools_api

  sources = [
    "devtools_api/domain_cc.template",
    "devtools_api/domain_h.template",
    "devtools_api/domain_type_conversions_h.template",
    "devtools_api/domain_types_cc.template",
    "devtools_api/domain_types_forward_declarations_h.template",
    "devtools_api/domain_types_h.template",
  ]

  args = [
    "--protocol",
    rebase_path(inputs[0], root_build_dir),
    "--output_dir",
    rebase_path(target_gen_dir, root_build_dir),
  ]
}

source_set("devtools") {
  sources = [
    "devtools_client.cc",
    "devtools_client.h",
    "error_reporter.cc",
    "error_reporter.h",
    "message_dispatcher.h",
  ]

  sources += generated_devtools_api

  deps = [
    ":gen_devtools_client_api",
    "//base",
    "//content/public/browser",
  ]
}
