# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/optimization_guide/features.gni")
import("//third_party/protobuf/proto_library.gni")

proto_library("model_metadata_proto") {
  sources = [ "model_metadata.proto" ]
}

static_library("renderer") {
  sources = [
    "autofill_assistant_agent.cc",
    "autofill_assistant_agent.h",
  ]

  deps = [
    ":model_metadata_proto",
    "//base",
    "//components/autofill_assistant/content/common:common",
    "//components/autofill_assistant/content/common:mojo_interfaces",
    "//components/autofill_assistant/content/common/proto:proto",
    "//components/optimization_guide:machine_learning_tflite_buildflags",
    "//content/public/common:common",
    "//content/public/renderer:renderer",
    "//mojo/public/cpp/bindings",
    "//third_party/abseil-cpp:absl",
    "//third_party/blink/public:blink",
    "//third_party/blink/public/common",
  ]

  if (build_with_tflite_lib) {
    sources += [
      "autofill_assistant_model_executor.cc",
      "autofill_assistant_model_executor.h",
    ]
    deps += [ "//components/optimization_guide/core:model_executor" ]
    public_deps = [
      "//third_party/tflite",
      "//third_party/tflite:tflite_public_headers",
      "//third_party/tflite_support",
      "//third_party/tflite_support:tflite_support_proto",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = []

  if (build_with_tflite_lib) {
    sources += [ "autofill_assistant_model_executor_unittest.cc" ]
  }

  deps = [
    ":renderer",
    "//base",
    "//base/test:test_support",
    "//third_party/blink/public:blink",
  ]
}

source_set("browser_tests") {
  testonly = true
  sources = []

  if (build_with_tflite_lib) {
    sources += [ "autofill_assistant_agent_browsertest.cc" ]
  }

  deps = [
    ":renderer",
    "//base",
    "//base/test:test_support",
    "//components/autofill_assistant/content/common:mojo_interfaces",
    "//components/autofill_assistant/content/common/proto:proto",
    "//content/public/browser",
    "//content/public/renderer",
    "//content/test:test_support",
  ]
}
