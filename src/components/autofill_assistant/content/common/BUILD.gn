# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

static_library("common") {
  sources = [
    "node_data.cc",
    "node_data.h",
    "switches.cc",
    "switches.h",
  ]
}

mojom("mojo_types") {
  sources = [ "autofill_assistant_types.mojom" ]

  public_deps = [ "//mojo/public/mojom/base" ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "autofill_assistant.mojom.NodeData"
          cpp = "::autofill_assistant::NodeData"
        },
      ]
      traits_headers = [ "autofill_assistant_types_mojom_traits.h" ]
      traits_sources = [ "autofill_assistant_types_mojom_traits.cc" ]
      traits_public_deps = [
        ":common",
        "//base",
      ]
    },
  ]
}

mojom("mojo_interfaces") {
  sources = [
    "autofill_assistant_agent.mojom",
    "autofill_assistant_driver.mojom",
  ]

  public_deps = [ "//mojo/public/mojom/base" ]
  deps = [ ":mojo_types" ]
}
