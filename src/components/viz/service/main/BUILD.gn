# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//components/ui_devtools/devtools.gni")
import("//media/gpu/args.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

source_set("main") {
  output_name = "viz_main_impl"

  defines = []

  sources = [
    "viz_compositor_thread_runner.cc",
    "viz_compositor_thread_runner.h",
    "viz_main_impl.cc",
    "viz_main_impl.h",
  ]

  deps = [
    "//cc",
    "//components/discardable_memory/client",
    "//components/discardable_memory/public/interfaces",
    "//components/ui_devtools",
    "//components/ui_devtools/viz_views",
    "//components/viz/service",
    "//gpu/ipc:gl_in_process_context",
    "//gpu/ipc/common",
    "//gpu/ipc/service",
    "//ipc",
    "//media/gpu:buildflags",
    "//mojo/public/cpp/system",
    "//services/metrics/public/cpp:metrics_cpp",
    "//services/metrics/public/mojom",
    "//services/service_manager/public/cpp",
    "//services/viz/privileged/interfaces",
    "//ui/gfx:memory_buffer",
    "//ui/gl/init",
  ]

  public_deps = [
    "//services/network/public/mojom",
  ]

  if (use_ozone) {
    deps += [ "//ui/ozone:ozone" ]
  }

  if (is_chromeos) {
    deps += [ "//media/gpu" ]
  }

  if (use_vaapi) {
    deps += [ "//media/gpu/vaapi" ]
  }

  if (use_viz_devtools) {
    defines += [ "USE_VIZ_DEVTOOLS" ]
  }
}
