# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

_jni_sources = [
  "java/src/org/chromium/components/crash/browser/ChildProcessCrashObserver.java",
  "java/src/org/chromium/components/crash/browser/CrashDumpManager.java",
  "java/src/org/chromium/components/crash/CrashKeys.java",
]

generate_jni("jni_headers") {
  sources = _jni_sources
  jni_package = "crash"
}

java_cpp_enum("java_enums_srcjar") {
  sources = [
    "crash_keys_android.h",
  ]
}

android_library("java") {
  deps = [
    "//base:base_java",
  ]
  srcjar_deps = [ ":java_enums_srcjar" ]
  java_files = _jni_sources
}

source_set("crash_android") {
  sources = [
    "crash_keys_android.cc",
    "crash_keys_android.h",
  ]
  deps = [
    ":jni_headers",
    "//base",
    "//components/crash/core/common:crash_key",
  ]
}

android_library("javatests") {
  testonly = true
  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//third_party/android_support_test_runner:runner_java",
    "//third_party/junit",
  ]
  java_files = [ "javatests/src/org/chromium/components/crash/browser/CrashDumpManagerTest.java" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "crash_keys_android_unittest.cc",
  ]
  deps = [
    ":crash_android",
    "//components/crash/core/common:crash_key",
    "//testing/gtest",
  ]
}
