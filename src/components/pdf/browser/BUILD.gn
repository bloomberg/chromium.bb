# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//pdf/features.gni")

assert(enable_pdf)

static_library("browser") {
  sources = [
    "pdf_web_contents_helper.cc",
    "pdf_web_contents_helper.h",
    "pdf_web_contents_helper_client.h",
  ]

  deps = [
    "//base",
    "//content/public/browser",
    "//ui/base",
    "//ui/touch_selection",
  ]

  public_deps = [
    "//ipc",
    "//pdf/mojom",
  ]
}

static_library("interceptors") {
  friend = [ ":unit_tests" ]

  public = [
    "pdf_navigation_throttle.h",
    "pdf_stream_delegate.h",
    "pdf_url_loader_request_interceptor.h",
  ]

  sources = [
    "pdf_navigation_throttle.cc",
    "pdf_stream_delegate.cc",
    "pdf_url_loader_request_interceptor.cc",
    "plugin_response_writer.cc",
    "plugin_response_writer.h",
  ]

  deps = [
    "//base",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//net",
    "//services/network/public/cpp:cpp_base",
    "//services/network/public/mojom",
    "//skia",
    "//ui/base",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "fake_pdf_stream_delegate.cc",
    "fake_pdf_stream_delegate.h",
    "mock_url_loader_client.cc",
    "mock_url_loader_client.h",
    "pdf_navigation_throttle_unittest.cc",
    "pdf_url_loader_request_interceptor_unittest.cc",
    "plugin_response_writer_unittest.cc",
  ]

  deps = [
    ":interceptors",
    "//base",
    "//base/test:test_support",
    "//content/public/browser",
    "//content/test:test_support",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//net",
    "//services/network/public/cpp:cpp_base",
    "//services/network/public/mojom",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]
}
