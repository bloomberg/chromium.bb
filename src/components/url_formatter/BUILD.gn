# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

jumbo_static_library("skeleton_generator") {
  sources = [
    "spoof_checks/skeleton_generator.cc",
    "spoof_checks/skeleton_generator.h",
  ]
  deps = [
    "//base",
    "//base:i18n",
  ]
}

jumbo_static_library("url_formatter") {
  sources = [
    "elide_url.cc",
    "elide_url.h",
    "spoof_checks/idn_spoof_checker.cc",
    "spoof_checks/idn_spoof_checker.h",
    "url_fixer.cc",
    "url_fixer.h",
    "url_formatter.cc",
    "url_formatter.h",
    "url_formatter_android.cc",
  ]

  configs += [
    # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
    "//build/config/compiler:no_size_t_to_int_warning",

    "//build/config/compiler:noshadowing",
  ]

  deps = [
    "//base",
    "//base:i18n",
    "//components/url_formatter:skeleton_generator",
    "//components/url_formatter/spoof_checks/top_domains:common",
    "//components/url_formatter/spoof_checks/top_domains:generate_top_domains_trie",
    "//net",
    "//net:preload_decoder",
    "//ui/gfx",
    "//url",
  ]

  public_deps = [ "//third_party/icu" ]

  if (is_android) {
    deps += [
      "android:jni_headers",
      "//url:gurl_android",
    ]
  }
}

if (is_android) {
  java_cpp_enum("url_formatter_java_enums_srcjar") {
    sources = [ "elide_url.h" ]
  }
}

jumbo_source_set("unit_tests") {
  testonly = true
  sources = [
    "elide_url_unittest.cc",
    "spoof_checks/idn_spoof_checker_unittest.cc",
    "url_fixer_unittest.cc",
    "url_formatter_unittest.cc",
  ]

  deps = [
    ":url_formatter",
    "//base",
    "//base/test:test_support",
    "//components/url_formatter/spoof_checks/top_domains:generate_top_domains_test_trie",
    "//net",
    "//net:preload_decoder",
    "//testing/gtest",
    "//ui/gfx",
    "//url",
  ]

  if (is_android) {
    sources += [ "url_formatter_android_unittest.cc" ]
    deps += [
      "android:native_j_unittests_jni_headers",
      "android:native_java_unittests_java",
    ]
  }
}
