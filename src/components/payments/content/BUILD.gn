# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")

jumbo_static_library("content") {
  sources = [
    "can_make_payment_query_factory.cc",
    "can_make_payment_query_factory.h",
    "payment_details_converter.cc",
    "payment_details_converter.h",
    "payment_event_response_util.cc",
    "payment_event_response_util.h",
    "payment_handler_host.cc",
    "payment_handler_host.h",
    "payment_request_converter.cc",
    "payment_request_converter.h",
  ]

  deps = [
    ":content_common",
    ":utils",
    "//components/autofill/core/browser",
    "//components/keyed_service/content",
    "//components/payments/content/utility",
    "//components/payments/core",
    "//components/payments/core:error_strings",
    "//components/payments/mojom",
    "//components/prefs",
    "//components/strings:components_strings_grit",
    "//components/ukm/content",
    "//components/url_formatter",
    "//content/public/browser",
    "//third_party/blink/public:blink_headers",
    "//url",
  ]

  if (!is_android) {
    sources += [
      "autofill_payment_app_factory.cc",
      "autofill_payment_app_factory.h",
      "content_payment_request_delegate.h",
      "initialization_task.cc",
      "initialization_task.h",
      "payment_app_factory.cc",
      "payment_app_factory.h",
      "payment_app_service.cc",
      "payment_app_service.h",
      "payment_app_service_factory.cc",
      "payment_app_service_factory.h",
      "payment_request.cc",
      "payment_request.h",
      "payment_request_dialog.h",
      "payment_request_display_manager.cc",
      "payment_request_display_manager.h",
      "payment_request_spec.cc",
      "payment_request_spec.h",
      "payment_request_state.cc",
      "payment_request_state.h",
      "payment_request_web_contents_manager.cc",
      "payment_request_web_contents_manager.h",
      "payment_response_helper.cc",
      "payment_response_helper.h",
      "service_worker_payment_app.cc",
      "service_worker_payment_app.h",
      "service_worker_payment_app_factory.cc",
      "service_worker_payment_app_factory.h",
    ]

    deps += [ "//components/payments/core:method_strings" ]
  }
}

# Files used by content and utility.
jumbo_static_library("content_common") {
  sources = [
    "web_app_manifest.cc",
    "web_app_manifest.h",
  ]

  deps = [
    "//content/public/browser",
    "//skia",
  ]
}

jumbo_static_library("utils") {
  sources = [
    "developer_console_logger.cc",
    "developer_console_logger.h",
    "installable_payment_app_crawler.cc",
    "installable_payment_app_crawler.h",
    "manifest_verifier.cc",
    "manifest_verifier.h",
    "payment_manifest_web_data_service.cc",
    "payment_manifest_web_data_service.h",
    "payment_method_manifest_table.cc",
    "payment_method_manifest_table.h",
    "service_worker_payment_app_finder.cc",
    "service_worker_payment_app_finder.h",
    "web_app_manifest_section_table.cc",
    "web_app_manifest_section_table.h",
  ]

  deps = [
    ":content_common",
    "//components/autofill/core/browser",
    "//components/payments/content/icon",
    "//components/payments/content/utility",
    "//components/payments/core",
    "//components/payments/core:error_strings",
    "//components/payments/core:method_strings",
    "//components/strings",
    "//components/webdata/common",
    "//content/public/browser",
    "//net",
    "//sql",
    "//third_party/blink/public:blink_headers",
    "//ui/base",
  ]

  public_deps = [
    "//third_party/icu",
    "//third_party/libaddressinput",
  ]
}

jumbo_source_set("unit_tests") {
  testonly = true
  sources = [
    "payment_method_manifest_table_unittest.cc",
    "service_worker_payment_app_finder_unittest.cc",
    "test_content_payment_request_delegate.cc",
    "test_content_payment_request_delegate.h",
    "web_app_manifest_section_table_unittest.cc",
  ]

  if (!is_android) {
    sources += [
      "payment_app_unittest.cc",
      "payment_request_spec_unittest.cc",
      "payment_request_state_unittest.cc",
      "payment_response_helper_unittest.cc",
      "service_worker_payment_app_unittest.cc",
    ]
  }

  deps = [
    ":content",
    ":utils",
    "//base",
    "//base/test:test_support",
    "//components/autofill/core/browser",
    "//components/autofill/core/browser:test_support",
    "//components/payments/core",
    "//components/payments/core:error_strings",
    "//components/payments/core:test_support",
    "//components/strings:components_strings_grit",
    "//components/webdata/common",
    "//content/test:test_support",
    "//services/metrics/public/cpp:metrics_cpp",
    "//sql",
    "//testing/gtest",
    "//third_party/blink/public:blink_headers",
    "//third_party/icu",
    "//third_party/libaddressinput:test_support",
  ]
}
