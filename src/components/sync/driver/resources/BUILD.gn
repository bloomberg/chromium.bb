# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

grd_file = "$target_gen_dir/resources.grd"

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/preprocess"
  out_manifest = "$target_gen_dir/manifest.json"
  in_files = [
    "sync_index.js",
    "sync_node_browser.css",
    "sync_search.css",
  ]
}

generate_grd("build_grd") {
  deps = [ ":preprocess" ]
  grd_prefix = "sync_driver_sync_internals"
  out_grd = grd_file
  input_files = [
    "about.css",
    "about.js",
    "chrome_sync.js",
    "data.js",
    "index.html",
    "invalidations.css",
    "invalidations.js",
    "search.js",
    "star_small.png",
    "sync_log.js",
    "sync_node_browser.js",
    "sync_search.js",
    "traffic_log.css",
    "traffic_log.js",
    "user_events.js",
  ]
  input_files_base_dir = rebase_path("./", "//")
  manifest_files = [ "$target_gen_dir/manifest.json" ]
}

grit("resources") {
  source = grd_file

  # Required because the .grd is generated.
  enable_input_discovery_for_gn_analyze = false

  outputs = [
    "grit/sync_driver_sync_internals_resources.h",
    "grit/sync_driver_sync_internals_resources_map.cc",
    "grit/sync_driver_sync_internals_resources_map.h",
    "sync_driver_sync_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/components"
  deps = [ ":build_grd" ]
}

js_type_check("closure_compile") {
  deps = [
    #TODO(crbug.com/986001): Fix compilation errors and enable all targets.

    #":about",
    ":chrome_sync",

    #":data",
    ":search",
    ":sync_index",
    ":sync_log",

    #":sync_node_browser",
    #":sync_search",
    ":invalidations",
    ":traffic_log",
    ":user_events",
  ]
}

js_library("about") {
  deps = [
    "//third_party/jstemplate:jstemplate",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("chrome_sync") {
  deps = [
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("data") {
}

js_library("search") {
  deps = [
    "//ui/webui/resources/js:util.m",
    "//ui/webui/resources/js/cr/ui:splitter",
  ]
}

js_library("sync_index") {
  deps = [
    "//ui/webui/resources/js:util.m",
    "//ui/webui/resources/js/cr/ui:tabs",
  ]
}

js_library("sync_log") {
  deps = [
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js/cr:event_target.m",
  ]
}

js_library("sync_node_browser") {
  deps = [
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js/cr/ui:tree",
  ]
}

js_library("sync_search") {
  deps = [
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js/cr/ui:array_data_model.m",
    "//ui/webui/resources/js/cr/ui:list.m",
  ]
}

js_library("traffic_log") {
  deps = [
    "//third_party/jstemplate:jstemplate",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("user_events") {
  deps = [
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("invalidations") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
}
