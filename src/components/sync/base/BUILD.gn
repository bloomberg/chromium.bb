# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/features.gni")

declare_args() {
  # Controls the product part of the user agent calculated in sync_util.cc.
  sync_user_agent_product = "Chrome"
}

static_library("base") {
  sources = [
    "bind_to_task_runner.h",
    "client_tag_hash.cc",
    "client_tag_hash.h",
    "command_line_switches.cc",
    "command_line_switches.h",
    "data_type_histogram.cc",
    "data_type_histogram.h",
    "extensions_activity.cc",
    "extensions_activity.h",
    "features.cc",
    "features.h",
    "hash_util.cc",
    "hash_util.h",
    "invalidation_adapter.cc",
    "invalidation_adapter.h",
    "invalidation_helper.cc",
    "invalidation_helper.h",
    "legacy_directory_deletion.cc",
    "legacy_directory_deletion.h",
    "model_type.cc",
    "model_type.h",
    "passphrase_enums.cc",
    "passphrase_enums.h",
    "pref_names.h",
    "progress_marker_map.cc",
    "progress_marker_map.h",
    "report_unrecoverable_error.cc",
    "report_unrecoverable_error.h",
    "stop_source.h",
    "sync_invalidation.cc",
    "sync_invalidation.h",
    "sync_mode.h",
    "sync_prefs.cc",
    "sync_prefs.h",
    "sync_stop_metadata_fate.h",
    "sync_util.cc",
    "sync_util.h",
    "syncer_error.cc",
    "syncer_error.h",
    "time.cc",
    "time.h",
    "unique_position.cc",
    "unique_position.h",
    "user_selectable_type.cc",
    "user_selectable_type.h",
    "weak_handle.cc",
    "weak_handle.h",
  ]

  public_deps = [
    "//base",
    "//components/invalidation/public",
    "//components/sync/protocol",
    "//third_party/metrics_proto",
  ]
  deps = [
    "//base",
    "//base:i18n",
    "//build:chromeos_buildflags",
    "//components/pref_registry",
    "//components/prefs",
    "//components/version_info",
    "//components/version_info:generate_version_info",
    "//crypto",
    "//net",
    "//third_party/zlib",
    "//ui/base",
  ]

  if (is_chromeos_ash) {
    deps += [ "//ash/constants" ]
  }

  defines = [ "SYNC_USER_AGENT_PRODUCT=$sync_user_agent_product" ]
}

# TODO(crbug.com/948661): The other sync targets have their test support targets
# declared outside of the production code directory. Do the same for this one.
static_library("test_support") {
  testonly = true
  sources = [
    "../test/mock_invalidation.cc",
    "../test/mock_invalidation.h",
    "../test/mock_invalidation_tracker.cc",
    "../test/mock_invalidation_tracker.h",
    "../test/trackable_mock_invalidation.cc",
    "../test/trackable_mock_invalidation.h",
    "model_type_test_util.cc",
    "model_type_test_util.h",
  ]

  public_deps = [
    "//base",
    "//components/sync/base",
    "//testing/gmock",
    "//testing/gtest",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]
}
