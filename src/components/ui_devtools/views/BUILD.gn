# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

source_set("views") {
  cflags = []
  if (is_win) {
    cflags += [ "/wd4800" ]  # Value forced to bool.
  }

  sources = [
    "devtools_server_util.cc",
    "devtools_server_util.h",
    "dom_agent_views.cc",
    "dom_agent_views.h",
    "element_utility.cc",
    "element_utility.h",
    "overlay_agent_views.cc",
    "overlay_agent_views.h",
    "page_agent_views.cc",
    "page_agent_views.h",
    "view_element.cc",
    "view_element.h",
    "widget_element.cc",
    "widget_element.h",
  ]

  deps = [
    "//components/ui_devtools",
    "//extensions/common",
    "//skia",
    "//ui/views",
  ]

  if (use_aura) {
    sources += [
      "dom_agent_aura.cc",
      "dom_agent_aura.h",
      "overlay_agent_aura.cc",
      "overlay_agent_aura.h",
      "window_element.cc",
      "window_element.h",
    ]
    deps += [
      "//ui/aura",
      "//ui/wm:wm",
    ]
  }
  if (is_mac) {
    sources += [
      "dom_agent_mac.h",
      "dom_agent_mac.mm",
      "overlay_agent_mac.h",
      "overlay_agent_mac.mm",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  cflags = []
  if (is_win) {
    cflags += [ "/wd4800" ]  # Value forced to bool.
  }

  sources = [
    "dom_agent_unittest.cc",
    "overlay_agent_unittest.cc",
    "page_agent_views_unittest.cc",
    "view_element_unittest.cc",
    "widget_element_unittest.cc",
  ]

  deps = [
    ":views",
    "//components/ui_devtools",
    "//components/ui_devtools:test_support",
    "//skia",
    "//testing/gtest",
    "//ui/events:test_support",
    "//ui/views",
    "//ui/views:test_support",
  ]

  if (use_aura) {
    sources += [ "window_element_unittest.cc" ]
    deps += [
      "//ui/aura",
      "//ui/aura:test_support",
      "//ui/wm:wm",
    ]
  }

  configs += [ "//build/config:precompiled_headers" ]
}
