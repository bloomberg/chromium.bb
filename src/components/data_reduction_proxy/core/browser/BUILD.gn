# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}

browser_sources = [
  "data_reduction_proxy_metrics.h",
  "data_reduction_proxy_settings.cc",
  "data_reduction_proxy_settings.h",
]

if (is_android) {
  static_library("browser_small") {
    sources = browser_sources

    deps = [
      "//base",
      "//components/data_reduction_proxy/core/common",
      "//components/pref_registry",
      "//components/prefs",
      "//crypto",
      "//google_apis",
      "//net:net",
      "//url:url",
    ]
  }
}

static_library("browser") {
  sources = browser_sources

  public_deps = [ "//components/data_reduction_proxy/core/common" ]
  deps = [
    "//base",
    "//components/pref_registry",
    "//components/prefs",
    "//crypto",
    "//google_apis",
    "//net",
    "//services/network:network_service",
    "//third_party/leveldatabase",
    "//url",
  ]

  defines = [ "USE_GOOGLE_API_KEYS" ]
  if (!is_ios) {
    defines += [ "USE_GOOGLE_API_KEYS_FOR_AUTH_KEY" ]
  }
}
