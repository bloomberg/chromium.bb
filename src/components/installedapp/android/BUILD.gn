# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

source_set("android") {
  sources = [ "installed_app_verifier.cc" ]
  deps = [
    "//base",
    "//components/digital_asset_links",
    "//components/embedder_support/android:browser_context",
    "//content/public/browser",
  ]
  public_deps = [ ":jni_headers" ]
}

android_library("java") {
  sources = [
    "java/src/org/chromium/components/installedapp/InstalledAppProviderImpl.java",
    "java/src/org/chromium/components/installedapp/PackageHash.java",
    "java/src/org/chromium/components/installedapp/PackageManagerDelegate.java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]

  deps = [
    "//base:base_java",
    "//components/embedder_support/android:browser_context_java",
    "//components/webapk/android/libs/client:java",
    "//content/public/android:content_java",
    "//mojo/public/java:system_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/blink/public/mojom:android_mojo_bindings_java",
    "//url:gurl_java",
    "//url/mojom:url_mojom_gurl_java",
  ]
}

generate_jni("jni_headers") {
  sources = [
    "java/src/org/chromium/components/installedapp/InstalledAppProviderImpl.java",
    "java/src/org/chromium/components/installedapp/PackageHash.java",
  ]
}

android_library("javatests") {
  testonly = true

  sources = [ "java/src/org/chromium/components/installedapp/InstalledAppProviderTest.java" ]
  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//components/embedder_support/android:browser_context_java",
    "//content/public/android:content_java",
    "//content/public/test/android:content_java_test_support",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/blink/public/mojom:android_mojo_bindings_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
    "//url:gurl_java",
    "//url/mojom:url_mojom_gurl_java",
  ]
}

java_library("junit") {
  # Skip platform checks since Robolectric depends on requires_android targets.
  bypass_platform_checks = true
  testonly = true
  sources =
      [ "java/src/org/chromium/components/installedapp/PackageHashTest.java" ]
  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//base/test:test_support_java",
    "//components/embedder_support/android:browser_context_java",
    "//third_party/junit",
  ]
}
