# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

action_foreach("package_sb_files") {
  script = "package_sb_file.py"
  sources = [
    "audio.sb",
    "cdm.sb",
    "common.sb",
    "common_v2.sb",
    "gpu.sb",
    "gpu_v2.sb",
    "nacl_loader.sb",
    "pdf_compositor.sb",
    "ppapi.sb",
    "ppapi_v2.sb",
    "renderer.sb",
    "renderer_v2.sb",
    "utility.sb",
  ]
  outputs = [
    "$target_gen_dir/{{source_name_part}}.sb.h",
    "$target_gen_dir/{{source_name_part}}.sb.cc",
  ]
  args = [
    "{{source}}",
    rebase_path(target_gen_dir, root_build_dir),
  ]
}

source_set("packaged_sb_files") {
  sources = get_target_outputs(":package_sb_files")
  defines = [ "SERVICE_MANAGER_SANDBOX_IMPL" ]
  deps = [
    ":package_sb_files",
  ]
}
