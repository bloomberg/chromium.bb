# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//net/features.gni")

# This target provides a net::CertVerifier that transparently proxies cert
# verification requests over a Mojo connection to the actual service, which is
# implemented in terms of concrete implementations of net::CertVerifier.
source_set("mojo_cert_verifier") {
  sources = [
    "mojo_cert_verifier.cc",
    "mojo_cert_verifier.h",
  ]

  deps = [
    "//base",
    "//net",
    "//services/network/public/mojom",
  ]
}

source_set("cert_verifier_creation") {
  sources = [
    "cert_verifier_creation.cc",
    "cert_verifier_creation.h",
  ]

  if (is_chromeos) {
    sources += [
      "system_trust_store_provider_chromeos.cc",
      "system_trust_store_provider_chromeos.h",
    ]
  }

  if (trial_comparison_cert_verifier_supported) {
    sources += [
      "trial_comparison_cert_verifier_mojo.cc",
      "trial_comparison_cert_verifier_mojo.h",
    ]
  }

  deps = [
    "//base",
    "//net",
    "//services/network/public/mojom",
  ]

  public_deps = [ "//services/network/public/mojom" ]
}

source_set("cert_verifier_tests") {
  testonly = true
  sources = []

  deps = [
    ":cert_verifier_creation",
    ":mojo_cert_verifier",
    "//base",
    "//base/test:test_support",
    "//mojo/public/cpp/test_support:test_utils",
    "//net",
    "//net:test_support",
    "//testing/gtest",
  ]

  sources += [
    "cert_verifier_mojom_traits_unittest.cc",
    "mojo_cert_verifier_unittest.cc",
  ]

  if (trial_comparison_cert_verifier_supported) {
    sources += [ "trial_comparison_cert_verifier_mojo_unittest.cc" ]
  }
}
