# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("cpp") {
  sources = [
    "access_token_fetcher.cc",
    "access_token_fetcher.h",
    "access_token_info.cc",
    "access_token_info.h",
    "accounts_cookie_mutator.h",
    "accounts_cookie_mutator_impl.cc",
    "accounts_cookie_mutator_impl.h",
    "accounts_in_cookie_jar_info.cc",
    "accounts_in_cookie_jar_info.h",
    "accounts_mutator.h",
    "diagnostics_provider.h",
    "diagnostics_provider_impl.cc",
    "diagnostics_provider_impl.h",
    "identity_manager.cc",
    "identity_manager.h",
    "primary_account_access_token_fetcher.cc",
    "primary_account_access_token_fetcher.h",
    "primary_account_mutator.h",
  ]

  if (!is_chromeos) {
    sources += [
      "primary_account_mutator_impl.cc",
      "primary_account_mutator_impl.h",
    ]
  }

  if (!is_android && !is_ios) {
    sources += [
      "accounts_mutator_impl.cc",
      "accounts_mutator_impl.h",
    ]
  }

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  public_deps = [
    "//components/signin/core/browser:internals",
    "//components/signin/core/browser:shared",
    "//components/signin/core/browser:signin_buildflags",
    "//services/identity/public/cpp:cpp_types",
    "//services/network/public/cpp",
  ]

  deps = [
    "//components/prefs:prefs",
  ]
}

# A source_set for types which the public interfaces depend on for typemapping.
# This cannot itself depend on the main public interfaces target.
source_set("cpp_types") {
  output_name = "identity_cpp_types"

  sources = [
    "account_state.cc",
    "account_state.h",
    "scope_set.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "identity_test_environment.cc",
    "identity_test_environment.h",
    "identity_test_utils.cc",
    "identity_test_utils.h",
    "test_identity_manager_observer.cc",
    "test_identity_manager_observer.h",
  ]

  public_deps = [
    ":cpp",
    "//components/signin/core/browser:internals_test_support",
    "//components/sync_preferences:test_support",
    "//google_apis:test_support",
  ]

  deps = [
    "//components/image_fetcher/core:test_support",
    "//components/signin/core/browser",
  ]

  if (is_ios) {
    deps += [ "//components/signin/ios/browser" ]
  }
}

source_set("tests") {
  testonly = true
  sources = [
    "accounts_cookie_mutator_unittest.cc",
    "accounts_mutator_unittest.cc",
    "diagnostics_provider_unittest.cc",
    "primary_account_mutator_unittest.cc",
  ]

  deps = [
    ":cpp",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//components/signin/core/browser:internals_test_support",
    "//components/sync_preferences:test_support",
    "//services/network:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("manifest") {
  sources = [
    "manifest.cc",
    "manifest.h",
  ]

  deps = [
    "//base",
    "//services/identity/public/mojom",
    "//services/service_manager/public/cpp",
  ]
}
