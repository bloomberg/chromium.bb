# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//services/content/public/features.gni")
import("//services/service_manager/public/service_manifest.gni")

component("simple_browser") {
  public = [
    "simple_browser_service.h",
  ]

  sources = [
    "simple_browser_service.cc",
    "window.cc",
    "window.h",
  ]

  defines = [ "IS_SIMPLE_BROWSER_IMPL" ]

  public_deps = [
    "//base",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//services/content/simple_browser/public/mojom",
    "//services/service_manager/public/cpp",
  ]

  deps = [
    "//services/content/public/cpp",
    "//services/content/public/mojom",
  ]

  if (toolkit_views) {
    deps += [ "//ui/views" ]
  }

  if (use_aura) {
    deps += [ "//ui/aura" ]
  }

  if (enable_remote_navigable_contents_view) {
    deps += [ "//ui/views/mus" ]
  }

  if (is_linux) {
    public_deps += [
      "//components/services/font/public/cpp",
      "//components/services/font/public/interfaces",
    ]
  }
}

service_manifest("manifest") {
  name = "simple_browser"
  source = "manifest.json"
}
