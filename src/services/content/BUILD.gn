# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//services/content/public/features.gni")
import("//services/service_manager/public/service_manifest.gni")

source_set("impl") {
  visibility = [
    ":tests",
    "//content/browser",
  ]

  public = [
    "navigable_contents_delegate.h",
    "service.h",
    "service_delegate.h",
  ]

  sources = [
    "navigable_contents_factory_impl.cc",
    "navigable_contents_factory_impl.h",
    "navigable_contents_impl.cc",
    "navigable_contents_impl.h",
    "service.cc",
  ]

  public_deps = [
    "//base",
    "//services/content/public/cpp:buildflags",
    "//services/service_manager/public/cpp",
    "//ui/gfx",
  ]

  deps = [
    "//mojo/public/cpp/bindings",
    "//services/content/public/cpp",
    "//services/content/public/mojom",
  ]

  if (toolkit_views) {
    deps += [
      "//ui/base",
      "//ui/views",
    ]

    if (use_aura) {
      deps += [ "//ui/aura" ]
    }

    if (enable_remote_navigable_contents_view) {
      deps += [ "//ui/views/mus/remote_view:remote_view_provider" ]
    }
  }
}

source_set("tests") {
  testonly = true

  sources = [
    "service_unittest.cc",
  ]

  deps = [
    ":impl",
    "//base",
    "//base/test:test_support",
    "//services/content/public/cpp",
    "//services/content/public/mojom",
    "//services/service_manager/public/cpp/test:test_support",
    "//testing/gtest",
    "//url",
  ]
}

service_manifest("manifest") {
  name = "content"
  source = "manifest.json"
}
