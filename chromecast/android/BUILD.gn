# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromecast/chromecast.gni")
import("//media/media_options.gni")

# These targets shall only be referenced on Android builds.
assert(is_android)

# This source_set should only contain headers for internal code. These must be
# built separately from public stub implementations of this code to keep the
# dependency tree clean.
source_set("platform_jni_loader") {
  sources = [
    "platform_jni_loader.h",
  ]
}

shared_library("libcast_shell_android") {
  sources = [
    "//chromecast/app/android/cast_jni_loader.cc",
    "cast_jni_registrar.cc",
    "cast_jni_registrar.h",
  ]

  deps = [
    ":platform_jni_loader",
    "//base",
    "//chromecast:cast_shell_jni_registration",
    "//chromecast:cast_shell_lib",
    "//chromecast:chromecast_features",
    "//chromecast/app",
    "//chromecast/app:cast_crash_client",
    "//chromecast/base",
    "//chromecast/base:cast_sys_info",
    "//chromecast/base:jni_headers",
    "//chromecast/base/metrics",
    "//chromecast/browser",
    "//content/public/app:both",
    "//content/public/browser",
    "//skia",
  ]

  if (chromecast_branding == "public") {
    sources += [ "platform_jni_loader_stub.cc" ]
  }

  if (is_cast_using_cma_backend) {
    deps += [ "//chromecast/media/cma/backend/android:cast_media_android" ]
  }
}
