# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromecast/chromecast.gni")
import("//mojo/public/tools/bindings/mojom.gni")

cast_source_set("common") {
  sources = [
    "cast_content_client.cc",
    "cast_content_client.h",
    "cast_resource_delegate.cc",
    "cast_resource_delegate.h",
    "global_descriptors.h",
  ]

  deps = [
    "//base",
    "//chromecast:chromecast_buildflags",
    "//chromecast/base",
    "//chromecast/base:cast_version",
    "//chromecast/common/media",
    "//content/public/common",
    "//ui/base",
    "//ui/gfx",
    "//url:url",
  ]

  if (enable_chromecast_extensions) {
    sources += [
      "cast_extensions_client.cc",
      "cast_extensions_client.h",
    ]

    deps += [
      "//chromecast/common/extensions_api:api",
      "//chromecast/common/extensions_api:api_registration",
      "//chromecast/common/extensions_api:extensions_features",
      "//components/version_info",
      "//extensions:extensions_resources_grd_grit",
      "//extensions/common",
      "//extensions/common/api:generated_api_bundle_generator_schema",
      "//extensions/shell:resources",
      "//extensions/shell:resources_grit",
    ]
  }
}

mojom("interfaces") {
  sources = [
    "application_media_capabilities.mojom",
  ]

  if (!is_android) {
    sources += [ "memory_pressure.mojom" ]
  }
}
