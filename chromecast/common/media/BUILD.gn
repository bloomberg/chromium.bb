# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# CMA is not used on Android; exclude CMA-specific targets.
enable_cma = !is_android

# GYP target: chromecast.gyp:cast_shell_media
source_set("media") {
  sources = [
    "shared_memory_chunk.cc",
    "shared_memory_chunk.h",
  ]

  public_deps = [
    ":cast_messages",
  ]
  if (enable_cma) {
    public_deps += [ ":cma_messages" ]
  }

  deps = [
    "//base",
    "//chromecast/media/cma/ipc",
  ]
}

# GYP target: chromecast.gyp:cast_shell_common
source_set("cast_messages") {
  sources = [
    "cast_message_generator.cc",
    "cast_message_generator.h",
    "cast_messages.h",
  ]
  deps = [
    "//ipc",
  ]
}

if (enable_cma) {
  # GYP target: chromecast.gyp:cast_shell_media
  source_set("cma_messages") {
    sources = [
      "cma_ipc_common.h",
      "cma_message_generator.cc",
      "cma_message_generator.h",
      "cma_messages.h",
      "cma_param_traits.cc",
      "cma_param_traits.h",
      "cma_param_traits_macros.h",
    ]

    deps = [
      "//chromecast/media/cma/pipeline",
      "//content/public/common",
      "//media",
      "//media:shared_memory_support",
      "//ui/gfx/ipc",
    ]
  }
}
