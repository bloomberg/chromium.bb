# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/crypto.gni")
import("//chromecast/chromecast.gni")

declare_args() {
  libcast_media_target = ""
}

source_set("base") {
  sources = [
    "decrypt_context.cc",
    "decrypt_context.h",
    "decrypt_context_clearkey.cc",
    "decrypt_context_clearkey.h",
    "key_systems_common.cc",
    "key_systems_common.h",
    "media_caps.cc",
    "media_caps.h",
    "media_codec_support.cc",
    "media_codec_support.h",
    "media_message_loop.cc",
    "media_message_loop.h",
    "switching_media_renderer.cc",
    "switching_media_renderer.h",
  ]

  configs += [ "//chromecast:config" ]

  deps = [
    "//base",
    "//crypto",
    "//crypto:platform",
    "//media",
    "//third_party/widevine/cdm:version_h",
  ]

  if (chromecast_branding != "public") {
    deps += [
      "${libcast_media_target}",
      # TODO(gyp): add dependency on internal/chromecast_internal:media_base_internal
    ]
  } else {
    sources += [ "key_systems_common_simple.cc" ]

    deps += [ ":libcast_media_default" ]
  }
}

shared_library("libcast_media_default") {
  output_name = "libcast_media_1.0"

  sources = [
    "cast_media_default.cc",
  ]

  configs += [ "//chromecast:config" ]

  deps = [
    "//chromecast/public",
  ]
}
