# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromecast/chromecast.gni")
import("//testing/test.gni")

source_set("browser") {
  sources = [
    "cast_browser_context.cc",
    "cast_browser_context.h",
    "cast_browser_main_parts.cc",
    "cast_browser_main_parts.h",
    "cast_browser_process.cc",
    "cast_browser_process.h",
    "cast_content_browser_client.cc",
    "cast_content_browser_client.h",
    "cast_content_window.cc",
    "cast_content_window.h",
    "cast_download_manager_delegate.cc",
    "cast_download_manager_delegate.h",
    "cast_http_user_agent_settings.cc",
    "cast_http_user_agent_settings.h",
    "cast_net_log.cc",
    "cast_net_log.h",
    "cast_network_delegate.cc",
    "cast_network_delegate.h",
    "cast_permission_manager.cc",
    "cast_permission_manager.h",
    "cast_quota_permission_context.cc",
    "cast_quota_permission_context.h",
    "cast_resource_dispatcher_host_delegate.cc",
    "cast_resource_dispatcher_host_delegate.h",
    "devtools/cast_dev_tools_delegate.cc",
    "devtools/cast_dev_tools_delegate.h",
    "devtools/remote_debugging_server.cc",
    "devtools/remote_debugging_server.h",
    "geolocation/cast_access_token_store.cc",
    "geolocation/cast_access_token_store.h",
    "metrics/cast_metrics_prefs.cc",
    "metrics/cast_metrics_prefs.h",
    "metrics/cast_metrics_service_client.cc",
    "metrics/cast_metrics_service_client.h",
    "metrics/cast_stability_metrics_provider.cc",
    "metrics/cast_stability_metrics_provider.h",
    "metrics/platform_metrics_providers.h",
    "pref_service_helper.cc",
    "pref_service_helper.h",
    "service/cast_service.cc",
    "service/cast_service.h",
    "service/cast_service_android.cc",
    "service/cast_service_android.h",
    "url_request_context_factory.cc",
    "url_request_context_factory.h",
  ]

  if (chromecast_branding == "public") {
    sources += [
      "cast_content_browser_client_simple.cc",
      "cast_network_delegate_simple.cc",
      "devtools/remote_debugging_server_simple.cc",
      "pref_service_helper_simple.cc",
    ]

    if (!is_android) {
      sources += [
        "metrics/platform_metrics_providers_simple.cc",
        "service/cast_service_simple.cc",
        "service/cast_service_simple.h",
      ]
    }
  }

  deps = [
    "//base",
    "//base:prefs",
    "//base:i18n",
    "//cc",
    "//chromecast/app:chromecast_settings",
    "//chromecast/app:resources",
    "//chromecast/base",
    "//chromecast/base:cast_sys_info",
    "//chromecast/common",
    "//chromecast/graphics",
    "//chromecast/media",
    "//chromecast/net",
    "//chromecast/public",
    "//components/crash/app",
    "//components/crash/browser",
    "//components/devtools_http_handler:devtools_http_handler",
    "//components/metrics",
    "//components/metrics:gpu",
    "//components/metrics:net",
    "//components/metrics:profiler",
    "//components/network_hints/browser",
    "//content",
    "//content/public/browser",
    "//content/public/common",
    "//content/public/utility",
    "//media",
    "//ipc",
    "//net",
    "//ui/base",
    "//ui/compositor",
    "//ui/gl",
  ]

  if (!is_android) {
    sources += [
      "metrics/external_metrics.cc",
      "metrics/external_metrics.h",
    ]

    public_deps = [
      "//chromecast/browser/media",
    ]

    deps += [ "//components/metrics:serialization" ]
  }
}

source_set("test_support") {
  testonly = true

  sources = [
    "test/chromecast_browser_test.cc",
    "test/chromecast_browser_test.h",
    "test/chromecast_browser_test_runner.cc",
  ]

  public_deps = [
    ":browser",
    "//chromecast:cast_shell_common",
  ]

  deps = [
    "//base",
    "//chromecast/app",
    "//chromecast/base",
    "//content/public/browser",
    "//content/public/common",
    "//content/test:test_support",
    "//testing/gtest",
  ]
}

config("browser_test_config") {
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

test("cast_shell_browser_test") {
  sources = [
    "test/chromecast_shell_browser_test.cc",
  ]

  configs += [ ":browser_test_config" ]

  deps = [
    ":test_support",
    "//base",
    "//testing/gtest",
    "//url",
  ]
}
