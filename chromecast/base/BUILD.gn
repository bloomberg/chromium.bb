# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/version.gni")  # TODO layering violation!
import("//chromecast/chromecast.gni")
import("//testing/test.gni")

source_set("base") {
  sources = [
    "android/dumpstate_writer.cc",
    "android/dumpstate_writer.h",
    "android/system_time_change_notifier_android.cc",
    "android/system_time_change_notifier_android.h",
    "cast_paths.cc",
    "cast_paths.h",
    "cast_resource.cc",
    "cast_resource.h",
    "chromecast_switches.cc",
    "chromecast_switches.h",
    "device_capabilities.h",
    "device_capabilities_impl.cc",
    "device_capabilities_impl.h",
    "error_codes.cc",
    "error_codes.h",
    "path_utils.cc",
    "path_utils.h",
    "pref_names.cc",
    "pref_names.h",
    "process_utils.cc",
    "process_utils.h",
    "serializers.cc",
    "serializers.h",
    "system_time_change_notifier.cc",
    "system_time_change_notifier.h",
    "task_runner_impl.cc",
    "task_runner_impl.h",
  ]

  public_deps = [
    "//chromecast/base/metrics",
    "//chromecast/public",
  ]

  deps = [
    "//base",
  ]
}

test("cast_base_unittests") {
  sources = [
    "device_capabilities_impl_unittest.cc",
    "error_codes_unittest.cc",
    "path_utils_unittest.cc",
    "process_utils_unittest.cc",
    "serializers_unittest.cc",
    "system_time_change_notifier_unittest.cc",
  ]

  deps = [
    ":base",
    "//base",
    "//base/test:run_all_unittests",
    "//base/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("cast_sys_info") {
  sources = [
    "cast_sys_info_dummy.cc",
    "cast_sys_info_dummy.h",
    "cast_sys_info_util.h",
  ]

  if (chromecast_branding == "public" && !is_android) {
    sources += [ "cast_sys_info_util_simple.cc" ]
  }

  # TODO(mbjorge): put cast_sys_info_android in here

  deps = [
    "//base",
    "//chromecast/public",
  ]
}

process_version("cast_version") {
  template_file = "version.h.in"
  output = "$target_gen_dir/version.h"
  extra_args = [
    "-e",
    "VERSION_FULL=\"%s.%s.%s.%s\"%(MAJOR,MINOR,BUILD,PATCH)",

    # TODO(slan): Populate the fields below with real values
    "-e",
    "CAST_BUILD_INCREMENTAL=20150608.181153",
    "-e",
    "CAST_BUILD_RELEASE=1.15",
    "-e",
    "CAST_IS_DEBUG_BUILD=1",
    "-e",
    "CAST_PRODUCT_TYPE=0",
  ]
}
