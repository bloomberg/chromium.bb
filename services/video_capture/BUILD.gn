# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/shell/public/cpp/service.gni")
import("//services/shell/public/service_manifest.gni")
import("//testing/test.gni")

service("video_capture") {
  sources = [
    "service_main.cc",
    "video_capture_service.cc",
    "video_capture_service.h",
  ]

  deps = [
    ":lib",
    "//media/capture:capture",
    "//mojo/public/cpp/system",
    "//services/shell/public/cpp",
    "//services/video_capture/public/interfaces",
  ]

  data_deps = [
    ":manifest",
  ]
}

service_manifest("manifest") {
  name = "video_capture"
  source = "service_manifest.json"
}

source_set("lib") {
  sources = [
    "device_client_media_to_mojo_adapter.cc",
    "device_client_media_to_mojo_adapter.h",
    "device_client_mojo_to_media_adapter.cc",
    "device_client_mojo_to_media_adapter.h",
    "device_mock_to_media_adapter.cc",
    "device_mock_to_media_adapter.h",
    "mojo_media_conversions.cc",
    "mojo_media_conversions.h",
    "video_capture_device_factory_impl.cc",
    "video_capture_device_factory_impl.h",
    "video_capture_device_proxy_impl.cc",
    "video_capture_device_proxy_impl.h",
  ]

  deps = [
    "//base",
    "//media/capture:capture",
    "//media/mojo/common:common",
    "//media/mojo/interfaces:image_capture",
    "//mojo/common:common_base",
    "//services/shell/public/cpp",
    "//services/video_capture/public/interfaces",
  ]
}

test("video_capture_unittests") {
  sources = [
    "fake_device_descriptor_test.cc",
    "fake_device_descriptor_test.h",
    "fake_device_descriptor_unittest.cc",
    "fake_device_test.cc",
    "fake_device_test.h",
    "fake_device_unittest.cc",
    "mock_device_descriptor_receiver.cc",
    "mock_device_descriptor_receiver.h",
    "mock_device_video_capture_service_test.cc",
    "mock_device_video_capture_service_test.h",
    "mock_device_video_capture_service_unittest.cc",
    "mock_video_capture_device_client.cc",
    "mock_video_capture_device_client.h",
    "mock_video_capture_device_impl.cc",
    "mock_video_capture_device_impl.h",
    "video_capture_service_test.cc",
    "video_capture_service_test.h",
    "video_capture_service_unittest.cc",
  ]

  deps = [
    ":video_capture",
    "//base",
    "//base/test:test_support",
    "//media/mojo/common:common",
    "//services/shell/public/cpp",
    "//services/shell/public/cpp:service_test_support",
    "//services/shell/public/cpp/test:run_all_shelltests",
    "//services/video_capture/public/interfaces",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/gfx:test_support",
  ]

  data_deps = [
    ":unittest_manifest",
  ]
}

service_manifest("unittest_manifest") {
  name = "video_capture_unittests"
  source = "service_unittest_manifest.json"
}
