# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//services/service_manager/public/tools/test/service_test.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "ime_registrar_impl.cc",
    "ime_registrar_impl.h",
    "ime_server_impl.cc",
    "ime_server_impl.h",
  ]

  deps = [
    "//base",
    "//mojo/common",
    "//mojo/public/cpp/bindings",
    "//services/service_manager/public/cpp",
    "//services/tracing/public/cpp",
    "//services/ui/public/interfaces",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":mus_ime_unittests",
  ]
}

service_test("mus_ime_unittests") {
  sources = [
    "ime_unittest.cc",
  ]

  catalog = ":mus_ime_unittests_catalog"

  deps = [
    "//base",
    "//mojo/common",
    "//services/service_manager/public/cpp:service_test_support",
    "//services/service_manager/public/cpp:sources",
    "//services/ui/public/interfaces",
  ]

  data_deps = [
    ":lib",
    "//services/ui",
    "//services/ui/ime/test_ime_driver",
  ]
}

service_manifest("test_manifest") {
  name = "mus_ime_unittests"
  source = "test_manifest.json"
}

catalog("mus_ime_unittests_catalog") {
  embedded_services = [ ":test_manifest" ]
  standalone_services = [
    "//services/ui:manifest",
    "//services/ui/ime/test_ime_driver:manifest",
  ]
}
