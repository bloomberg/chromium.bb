# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

mojom("interfaces") {
  sources = [
    "accessibility_manager.mojom",
    "clipboard.mojom",
    "display_manager.mojom",
    "event_matcher.mojom",
    "gpu.mojom",
    "mus_constants.mojom",
    "remote_event_dispatcher.mojom",
    "user_access_manager.mojom",
    "user_activity_monitor.mojom",
    "window_manager.mojom",
    "window_manager_constants.mojom",
    "window_manager_window_tree_factory.mojom",
    "window_server_test.mojom",
    "window_tree.mojom",
    "window_tree_constants.mojom",
    "window_tree_host.mojom",
  ]

  import_dirs = [
    get_path_info("../../../..", "abspath"),
    "//mojo/services",
  ]

  public_deps = [
    ":constants",
    "//gpu/ipc/common:interfaces",
    "//media/gpu/mojo:jpeg_decoder",
    "//media/mojo/interfaces",
    "//mojo/common:common_custom_types",
    "//services/ui/public/interfaces/cursor",
    "//services/ui/public/interfaces/display",
    "//services/ui/public/interfaces/ime",
    "//services/viz/public/interfaces",
    "//skia/public/interfaces",
    "//ui/base/mojo:mojo_bindings",
    "//ui/display/mojo:interfaces",
    "//ui/events/mojo:interfaces",
    "//ui/gfx/geometry/mojo",
    "//ui/gfx/mojo",
    "//ui/platform_window/mojo:interfaces",
  ]

  # TODO(crbug.com/714018): Convert the implementation to use OnceCallback.
  use_once_callback = false
}

mojom("constants") {
  sources = [
    "constants.mojom",
  ]
}

source_set("tests") {
  testonly = true

  sources = [
    "cursor/cursor_struct_traits_unittest.cc",
    "ime/ime_struct_traits_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//services/ui/public/interfaces/cursor",
    "//services/ui/public/interfaces/ime:test_interfaces",
    "//skia/public/interfaces",
    "//testing/gtest",
    "//ui/display/types",
    "//ui/gfx:test_support",
  ]
}
