# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("input_devices") {
  sources = [
    "input_device_client.cc",
    "input_device_client.h",
  ]

  deps = [
    "//base",
    "//services/service_manager/public/cpp",
    "//ui/events/devices",
  ]

  public_deps = [
    "//services/ui/public/interfaces/input_devices",
  ]

  if (is_chromeos) {
    sources += [
      "input_device_controller_client.cc",
      "input_device_controller_client.h",
    ]
    public_deps += [
      "//services/ui/public/interfaces:constants",
      "//ui/ozone",
    ]
  }
}

if (is_chromeos) {
  source_set("input_device_controller") {
    sources = [
      "input_device_controller.cc",
      "input_device_controller.h",
    ]

    deps = [
      "//base",
      "//services/service_manager/public/cpp",
      "//ui/events:dom_keycode_converter",
      "//ui/ozone",
    ]

    public_deps = [
      "//services/ui/public/interfaces/input_devices",
    ]

    # This target is really an implementation detail of the ui service, but
    # until Chrome is switched over to mus it needs to be visible to Chrome too.
    visibility = [
      "//chrome/browser",
      "//services/ui:lib",
    ]
  }
}
