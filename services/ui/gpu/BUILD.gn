# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

source_set("gpu") {
  output_name = "mus_gpu"

  sources = [
    "gpu_main.cc",
    "gpu_main.h",
    "gpu_service.cc",
    "gpu_service.h",
  ]

  defines = [ "NOTIMPLEMENTED_POLICY=5" ]

  deps = [
    "//cc",
    "//components/viz/service",
    "//gpu/ipc:command_buffer",
    "//gpu/ipc/common",
    "//gpu/ipc/service",
    "//ipc",
    "//media/gpu/ipc/service",
    "//mojo/public/cpp/system",
    "//services/service_manager/public/cpp",
    "//services/ui/gpu/interfaces",
    "//services/ui/public/interfaces",
    "//ui/gfx:memory_buffer",
    "//ui/gl/init",
  ]

  if (use_ozone) {
    deps += [ "//ui/ozone:ozone" ]
  }
}

if (use_aura) {
  source_set("tests") {
    testonly = true

    sources = [
      "gpu_service_unittest.cc",
    ]

    deps = [
      ":gpu",
      "//base",
      "//gpu",
      "//ipc",
      "//services/ui/gpu/interfaces",
      "//testing/gtest",
      "//ui/gfx:memory_buffer",
      "//ui/gfx/geometry",
    ]

    data_deps = [
      "//services/ui",
    ]
  }

  service_manifest("unittest_manifest") {
    name = "mus_gpu_unittests"
    source = "test_manifest.json"
  }

  catalog("tests_catalog") {
    testonly = true
    embedded_services = [ ":unittest_manifest" ]
  }
}
