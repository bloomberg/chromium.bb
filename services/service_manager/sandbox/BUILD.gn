# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_mac) {
  action_foreach("package_sb_files") {
    script = "mac/package_sb_file.py"
    sources = [
      "mac/common.sb",
      "mac/gpu.sb",
      "mac/nacl_loader.sb",
      "mac/ppapi.sb",
      "mac/renderer.sb",
      "mac/renderer_v2.sb",
      "mac/utility.sb",
    ]
    outputs = [
      "$target_gen_dir/{{source_name_part}}.sb.h",
      "$target_gen_dir/{{source_name_part}}.sb.cc",
    ]
    args = [
      "{{source}}",
      rebase_path(target_gen_dir, root_build_dir),
    ]
  }

  source_set("packaged_sb_files") {
    sources = get_target_outputs(":package_sb_files")
    defines = [ "SERVICE_MANAGER_SANDBOX_IMPL" ]
    deps = [
      ":package_sb_files",
    ]
  }
}

component("sandbox") {
  sources = [
    "export.h",
    "sandbox_type.cc",
    "sandbox_type.h",
    "switches.cc",
    "switches.h",

    # Duplicated here to keep private.
    "//services/service_manager/embedder/service_manager_embedder_export.h",
  ]
  defines = [ "SERVICE_MANAGER_SANDBOX_IMPL" ]
  public_deps = [
    "//services/service_manager/embedder:embedder_switches",
  ]
  deps = [
    "//base",
  ]
  if (is_mac) {
    sources += [
      "mac/sandbox_mac.h",
      "mac/sandbox_mac.mm",
    ]
    deps += [ "//sandbox/mac:seatbelt" ]
    public_deps += [ ":packaged_sb_files" ]
    libs = [
      "AppKit.framework",
      "CoreFoundation.framework",
      "CoreGraphics.framework",
      "Foundation.framework",
      "IOSurface.framework",
    ]
  }
}
