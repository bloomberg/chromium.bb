# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Generates a Service binary. The parameters of this template are those of an
# executable.
#
# TODO: We should consider removing this. At this point all it does is bring a
# single dep for an entry point, and change the executable extension. Both are
# of marginal value, and having another special target type may be unnecessarily
# confusing for developers when these are really just executables.
template("service") {
  if (defined(invoker.resources)) {
    # TODO(rockot): Remove this once all existing service targets have stopped
    # setting |resources|. This target serves no purpose other than to ensure
    # that |resources| is actually used, avoiding GN complaints.
    source_set("${target_name}__unused_resources_target") {
      testonly = true
      sources = invoker.resources
      deps = invoker.deps
    }
  }

  executable(target_name) {
    forward_variables_from(invoker, "*", [ "configs" ])

    # Executable targets already have a default set of configs which we should
    # not overwrite. Hence we do not forward but manually append.
    if (defined(invoker.configs)) {
      configs += invoker.configs
    }

    if (is_win) {
      output_extension = "service.exe"
    } else {
      output_extension = "service"
    }

    if (!defined(deps)) {
      deps = []
    }

    if (defined(use_cpp_main) && use_cpp_main) {
      deps += [
        "//services/service_manager/public/cpp/standalone_service:main_cpp",
      ]
    } else {
      deps +=
          [ "//services/service_manager/public/cpp/standalone_service:main" ]
    }
  }
}
