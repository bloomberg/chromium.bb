# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//testing/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":service_manager_unittests",
  ]
}

test("service_manager_unittests") {
  sources = [
    "test_support_unittest.cc",
  ]

  deps = [
    ":interfaces",
    "//base",
    "//base/test:test_support",
    "//mojo/core/test:run_all_unittests",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//services/catalog:lib",
    "//services/catalog:unittests",
    "//services/service_manager",
    "//services/service_manager/background:lib",
    "//services/service_manager/background/tests:unittests",
    "//services/service_manager/embedder:unittests",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp:tests",
    "//services/service_manager/public/cpp/test:test_support",
    "//services/service_manager/public/mojom",
    "//services/service_manager/runner/host:unittests",
    "//services/service_manager/tests/connect",
    "//services/service_manager/tests/lifecycle",
    "//services/service_manager/tests/sandbox",
    "//services/service_manager/tests/service_manager",
    "//services/service_manager/tests/shutdown",
    "//testing/gtest",
  ]
}

mojom("interfaces") {
  sources = [
    "test.mojom",
  ]
}

source_set("util") {
  sources = [
    "util.cc",
    "util.h",
  ]
  deps = [
    "//base",
    "//base:base_static",
    "//mojo/public/cpp/platform",
    "//mojo/public/cpp/system",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/mojom",
    "//services/service_manager/runner/common",
  ]
}

catalog("catalog") {
  testonly = true
  catalog_deps = [
    "//services/service_manager/background/tests:catalog",
    "//services/service_manager/tests/connect:catalog",
    "//services/service_manager/tests/lifecycle:catalog",
    "//services/service_manager/tests/service_manager:catalog",
    "//services/service_manager/tests/shutdown:catalog",
  ]
}

catalog_cpp_source("catalog_source") {
  testonly = true
  catalog = ":catalog"
  generated_function_name = "service_manager::test::CreateTestCatalog"
}
