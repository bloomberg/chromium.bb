# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "ime_driver_bridge.cc",
    "ime_driver_bridge.h",
    "ime_registrar_impl.cc",
    "ime_registrar_impl.h",
  ]

  deps = [
    "//base",
    "//mojo/public/cpp/bindings",
    "//services/service_manager/public/cpp",
    "//services/ws/public/mojom",
  ]
}

source_set("tests") {
  testonly = true

  sources = [
    "ime_unittest.cc",
  ]

  deps = [
    ":tests_catalog_source",
    "//base",
    "//base/test:test_support",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp/test:test_support",
    "//services/ws/ime/test_ime_driver/public/mojom",
    "//services/ws/public/mojom",
    "//testing/gtest",
  ]

  data_deps = [
    ":lib",
    "//services/ws/ime/test_ime_driver",
  ]
}

service_manifest("unittest_manifest") {
  name = "ime_unittests"
  source = "test_manifest.json"
}

catalog("tests_catalog") {
  testonly = true
  embedded_services = [ ":unittest_manifest" ]
  standalone_services = [
    "//services/ws/ime/test_ime_driver:manifest",
    "//services/ws/test_ws:manifest",
  ]
}

catalog_cpp_source("tests_catalog_source") {
  testonly = true
  catalog = ":tests_catalog"
  generated_function_name = "ws::test::CreateImeTestCatalog"
}
