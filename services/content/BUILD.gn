# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/content/public/features.gni")
import("//services/service_manager/public/service_manifest.gni")

source_set("impl") {
  visibility = [
    ":tests",
    "//content/browser",
  ]

  public = [
    "service.h",
    "service_delegate.h",
    "view_delegate.h",
  ]

  sources = [
    "service.cc",
    "view_factory_impl.cc",
    "view_factory_impl.h",
    "view_impl.cc",
    "view_impl.h",
  ]

  public_deps = [
    "//base",
    "//services/content/public/cpp:buildflags",
    "//services/service_manager/public/cpp",
    "//ui/gfx",
  ]

  deps = [
    "//mojo/public/cpp/bindings",
    "//services/content/public/cpp",
    "//services/content/public/mojom",
  ]

  if (enable_aura_content_view_embedding) {
    deps += [
      "//ui/aura",
      "//ui/views",
      "//ui/views/mus/remote_view:remote_view_provider",
    ]
  }
}

source_set("tests") {
  testonly = true

  sources = [
    "service_unittest.cc",
  ]

  deps = [
    ":impl",
    "//base",
    "//base/test:test_support",
    "//services/content/public/cpp",
    "//services/content/public/mojom",
    "//services/service_manager/public/cpp/test:test_support",
    "//testing/gtest",
    "//url",
  ]
}

service_manifest("manifest") {
  name = "content"
  source = "manifest.json"
}
