# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

source_set("lib") {
  # This should be visible only to embedders of the Device Service, and the
  # dependence should only be for the purpose of embedding the Device Service.
  visibility = [
    ":test_support",
    "//content/browser",
  ]
  sources = [
    "device_service.cc",
    "device_service.h",
  ]

  deps = [
    "//base",
    "//device/battery:mojo_bindings",
    "//device/generic_sensor",
    "//device/sensors",
    "//device/sensors/public/interfaces",
    "//device/vibration:mojo_bindings",
    "//device/wake_lock",
    "//services/device/fingerprint",
    "//services/device/power_monitor",
    "//services/device/public/cpp:device_features",
    "//services/device/screen_orientation",
    "//services/device/time_zone_monitor",
    "//services/service_manager/public/cpp",
    "//ui/gfx",
  ]

  if (is_android) {
    sources += [
      "//services/device/android/register_jni.cc",
      "//services/device/android/register_jni.h",
    ]
    deps += [ ":device_service_jni_headers" ]
  } else {
    deps += [
      "//device/battery",
      "//device/vibration",
    ]
  }
}

source_set("tests") {
  testonly = true

  sources = [
    "power_monitor/power_monitor_message_broadcaster_unittest.cc",
    "public/cpp/power_monitor/power_monitor_broadcast_source_unittest.cc",
    "vibration/vibration_manager_impl_unittest.cc",
  ]

  deps = [
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//device/vibration:mojo_bindings",
    "//mojo/public/cpp/bindings",
    "//services/device/power_monitor",
    "//services/device/public/cpp/power_monitor",
    "//testing/gtest",
  ]

  if (is_chromeos) {
    sources += [ "fingerprint/fingerprint_chromeos_unittest.cc" ]
    deps += [ "//services/device/fingerprint" ]
  }

  if (is_android) {
    deps += [
      ":device_service_jni_headers",
      "//device/vibration/android:vibration_jni_headers",
    ]
  } else {
    deps += [
      "//device/battery",
      "//device/vibration",
    ]
  }
}

service_manifest("manifest") {
  name = "device"
  source = "manifest.json"
}

service_manifest("unittest_manifest") {
  name = "device_unittests"
  source = "unittest_manifest.json"
  packaged_services = [ ":manifest" ]
}

catalog("tests_catalog") {
  testonly = true
  embedded_services = [ ":unittest_manifest" ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "device_service_test_base.cc",
    "device_service_test_base.h",
  ]

  deps = [
    ":lib",
    "//base",
    "//base/test:test_support",
    "//mojo/public/cpp/bindings",
    "//services/device/public/interfaces:constants",
    "//services/service_manager/public/cpp:service_test_support",
    "//services/service_manager/public/cpp:sources",
  ]
}

if (is_android) {
  generate_jni("device_service_jni_headers") {
    sources = [
      "android/java/src/org/chromium/services/device/InterfaceRegistrar.java",
    ]
    jni_package = "device_service"
  }

  android_library("java") {
    # This should be visible only to embedders of the Device Service, and the
    # dependence should only be for the purpose of embedding the Device Service.
    # //content/public/android:* here actually wants to identify the
    # //content/public/android:content_java target and all of its generated
    # targets which also need to see this target as well.
    # //services:* identifies //services:service_unittests and all of its
    # generated targets.
    visibility = [
      "//content/public/android:*",
      "//services:*",
    ]
    java_files = [ "android/java/src/org/chromium/services/device/InterfaceRegistrar.java" ]
    deps = [
      "//base:base_java",
      "//device/battery:mojo_bindings_java",
      "//device/battery/android:battery_monitor_java",
      "//device/vibration:mojo_bindings_java",
      "//device/vibration/android:vibration_manager_java",
      "//mojo/android:system_java",
      "//mojo/public/java:bindings_java",
      "//mojo/public/java:system_java",
      "//services/device/screen_orientation:java",
      "//services/device/time_zone_monitor:java",
      "//services/service_manager/public/interfaces:interfaces_java",
      "//services/service_manager/public/java:service_manager_java",
    ]
  }
}
