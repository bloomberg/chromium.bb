# Copyright 2009, Google Inc.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
#     * Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above
# copyright notice, this list of conditions and the following disclaimer
# in the documentation and/or other materials provided with the
# distribution.
#     * Neither the name of Google Inc. nor the names of its
# contributors may be used to endorse or promote products derived from
# this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
# "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
# LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
# A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
# OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
# LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.


import socket
import time

Import('env')

env.SConscript('icu38_o3d.scons', exports=['env'])
env.ApplySConscript([
    '$ICU38_DIR/using_icu38.scons',
])

# For FRIEND_TEST in gtest_prod.h
env.Append(CPPPATH = '$GTEST_DIR/include')

chrome_base_inputs = [
  'at_exit',
  'base_paths',
  'base_switches',
  'command_line',
  'debug_util',
  'dynamic_annotations',
  'file_path',
  'file_util',
  'histogram',
  'lazy_instance',
  'lock',
  'logging',
  'message_loop',
  'message_pump_default',
  'path_service',
  'pickle',
  'ref_counted',
  'stats_table',
  'string_piece',
  'string_util',
  'string_util_icu',
  'system_monitor',
  'thread_collision_warner',
  'third_party/dmg_fp/dtoa',
  'third_party/dmg_fp/g_fmt',
  'third_party/nspr/prtime',
  'thread',
  'tracked',
  'tracked_objects',
]

inputs = [
]

if env.Bit('windows'):
  env.Append(CCFLAGS=['/FIstdlib.h'])
  chrome_base_inputs += [
    'base_paths_win',
    'condition_variable_win',
    'debug_util_win',
    'file_util_win',
    'lock_impl_win',
    'message_pump_win',
    'platform_thread_win',
    'process_util_win',
    'registry',
    'shared_memory_win',
    'sys_info_win',
    'sys_string_conversions_win',
    'system_monitor_win',
    'thread_local_storage_win',
    'thread_local_win',
    'time_win',
    'waitable_event_win',
    'win_util',
  ]
  # The following are cross-platform, but are not needed on linux or mac, and
  # pull in more third-party dependencies on those platforms, so
  # they are not built on all platforms currently.
  # TODO: get the dependencies straightened out.
  chrome_base_inputs += [
    'cpu',
    'time',
    'timer',
  ]

chrome_base_inputs_posix = [
  'atomicops_internals_x86_gcc',
  'condition_variable_posix',
  'debug_util_posix',
  'file_util_posix',
  'lock_impl_posix',
  'message_pump_libevent',
  'platform_thread_posix',
  'process_util_posix',
  'shared_memory_posix',
  'string16',
  'sys_info_posix',
  'time',
  'time_posix',
  'thread_local_posix',
  'thread_local_storage_posix',
  'waitable_event_posix',
]
chrome_base_inputs_mm = []

if env.Bit('linux'):
  chrome_base_inputs += chrome_base_inputs_posix
  chrome_base_inputs += [
    'base_paths_linux',
    'file_util_linux',
    'message_pump_glib',
    'process_util_linux',
    'sys_string_conversions_linux',
  ]

if env.Bit('mac'):
  chrome_base_inputs += chrome_base_inputs_posix
  chrome_base_inputs_mm += [
    'base_paths_mac',
    'mac_util',
    'file_util_mac',
    'message_pump_mac',
    'platform_thread_mac',
    'process_util_mac',
    'scoped_nsautorelease_pool',
    'sys_string_conversions_mac',
  ]

inputs += env.MakeObjects(chrome_base_inputs, '$CHROME_SRC_DIR/base', 'cc')
inputs += env.MakeObjects(chrome_base_inputs_mm, '$CHROME_SRC_DIR/base', 'mm')

# Build a library called 'o3d_base' from the input sources.
o3d_base_lib = env.ComponentLibrary('o3d_base', inputs)
