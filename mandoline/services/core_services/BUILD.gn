# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
# core_services should be thought of as a bundle of many of the services which
# we ship with.

import("//build/config/ui.gni")
import("//mojo/public/mojo_application.gni")

if (is_android) {
  import("//build/config/android/rules.gni")

  java_library_path = "$target_out_dir/java_library.dex.jar"

  mojo_android_application("core_services") {
    input_so = "$root_out_dir/lib.stripped/libcore_services.so"
    input_dex_jar = java_library_path
    deps = [
      ":java_library",
      ":native_library",
    ]
  }

  shared_library("native_library") {
    output_name = "core_services"

    sources = [
      "android_hooks.cc",
    ]

    deps = [
      ":sources",
      "//base",
      "//components/resource_provider:jni_headers",
      "//net",
      "//third_party/mojo/src/mojo/public/c/system:for_shared_library",
    ]
  }

  android_standalone_library("java_library") {
    dex_path = java_library_path

    deps = [
      "//components/resource_provider:java_library",
      "//net/android:net_java",
    ]
  }
} else {
  mojo_native_application("core_services") {
    deps = [
      ":sources",
    ]
    if (is_win) {
      copy("copy_files") {
        sources = [
          "$root_out_dir/icudtl.dat",
          "$root_out_dir/libEGL.dll",
          "$root_out_dir/libGLESv2.dll",
          "$root_out_dir/ui_test.pak",
        ]
        outputs = [
          "$root_out_dir/core_services/{{source_file_part}}",
        ]
      }
      deps += [ ":copy_files" ]
    }
  }
}

source_set("sources") {
  sources = [
    "core_services_application_delegate.cc",
    "main.cc",
  ]

  deps = [
    "//base",
    "//components/clipboard:lib",
    "//components/filesystem:lib",
    "//components/resource_provider:lib",
    "//components/view_manager:lib",
    "//components/view_manager/surfaces:lib",
    "//mandoline/ui/browser:lib",
    "//mojo/application/public/cpp",
    "//mojo/common:tracing_impl",
    "//mojo/services/network:lib",
    "//mojo/services/tracing:lib",
  ]

  if (use_aura) {
    deps += [
      "//components/view_manager/public/cpp",
      "//mandoline/ui/omnibox:lib",
    ]
  }
}
