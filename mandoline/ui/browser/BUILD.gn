# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/mojo_application_package.gni")
import("//third_party/mojo/src/mojo/public/tools/bindings/mojom.gni")

mojo_application_package("browser") {
  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
    "//mandoline/services/navigation/public/interfaces",
    "//mojo/application/public/cpp",
    "//third_party/icu:icudata",
    "//ui/resources:ui_test_pak",
  ]
  resources = [
    "$root_out_dir/icudtl.dat",
    "$root_out_dir/ui_test.pak",
  ]
}

source_set("lib") {
  sources = [
    "browser.cc",
    "browser.h",
    "navigator_host_impl.cc",
    "navigator_host_impl.h",
  ]

  deps = [
    ":interfaces",
    "//base",
    "//components/view_manager/public/cpp",
    "//mandoline/services/navigation/public/interfaces",
    "//mandoline/tab",
    "//mojo/application/public/interfaces",
    "//mojo/converters/geometry",
    "//skia",
    "//third_party/mojo/src/mojo/public/cpp/bindings",
    "//ui/gfx/geometry",
    "//ui/mojo/events:interfaces",
  ]

  if (is_mac || use_aura) {
    sources += [
      "desktop/desktop_ui.cc",
      "desktop/desktop_ui.h",
    ]
    deps += [ "//mandoline/ui/aura" ]
  } else {
    assert(is_android)
    sources += [
      "android/android_ui.cc",
      "android/android_ui.h",
    ]
  }
}

mojom("interfaces") {
  sources = [
    "omnibox.mojom",
    "view_embedder.mojom",
  ]

  import_dirs = [ "//mojo/services" ]

  deps = [
    "//mojo/application/public/interfaces",
    "//mojo/services/network/public/interfaces",
  ]
}
