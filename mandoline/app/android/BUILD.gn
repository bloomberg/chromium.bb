# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_android)

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

group("android") {
  deps = [
    ":mandoline_apk",
  ]
}

executable("mandoline_runner") {
  deps = [
    ":jni_headers",
    "//base",
    "//build/config/sanitizers:deps",
    "//mandoline/ui/desktop_ui/public/interfaces",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//mojo/shell",
    "//mojo/shell/package_manager",
    "//mojo/shell/standalone:lib",
    "//mojo/shell/standalone:main_lib",
    "//mojo/shell/standalone:register_local_aliases_fwd",
  ]

  sources = [
    "../register_local_aliases.cc",
    "mandoline_activity.cc",
    "mandoline_context_init.cc",
  ]

  # On android, the executable is also the native library used by the apk.
  # It means dynamic symbols must be preserved and exported.
  ldflags = [ "-Wl,--export-dynamic" ]
}

copy("copy_mandoline_runner") {
  deps = [
    ":mandoline_runner",
  ]
  sources = [
    "$root_out_dir/mandoline_runner",
  ]
  outputs = [
    "$root_shlib_dir/${shlib_prefix}mandoline_runner$shlib_extension",
  ]
}

android_assets("mandoline_assets") {
  deps = [
    "//components/devtools_service:devtools_service_assets",
    "//components/html_viewer:html_viewer_assets",
    "//components/resource_provider:resource_provider_assets",
    "//mandoline/services/core_services:core_services_assets",
    "//mojo/services/network:network_assets",
    "//mojo/shell/standalone:android_assets",
  ]

  if (use_aura) {
    deps += [
      "//mandoline/ui/desktop_ui:desktop_ui_assets",
      "//mandoline/ui/omnibox:omnibox_assets",
    ]
  }
}

android_library("java") {
  java_files = [ "apk/src/org/chromium/mandoline/MandolineActivity.java" ]

  deps = [
    "//base:base_java",
    "//mojo/shell/standalone:java",
  ]
}

generate_jni("jni_headers") {
  sources = [
    "apk/src/org/chromium/mandoline/MandolineActivity.java",
  ]
  jni_package = "mandoline"
}

android_resources("mandoline_apk_resources") {
  custom_package = "org.chromium.mandoline"
  resource_dirs = [ "apk/res" ]
}

android_apk("mandoline_apk") {
  apk_name = "Mandoline"

  android_manifest = "apk/AndroidManifest.xml"
  native_libs = [ "${shlib_prefix}mandoline_runner$shlib_extension" ]
  write_asset_list = true

  deps = [
    ":copy_mandoline_runner",
    ":java",
    ":mandoline_apk_resources",
    ":mandoline_assets",
    "//base:base_java",
    "//mojo/shell/standalone:java",
    "//mojo/shell/standalone:resources",
    "//ui/platform_window/android:platform_window_java",
    google_play_services_resources,
  ]
}
