# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

group("scheduler") {
  # TODO(xingliu): Change this to a source set when we have code to be used by
  # NotificationScheduleServiceFactory.
  deps = [
    ":lib",
  ]

  public_deps = [
    ":public",
  ]
}

source_set("public") {
  sources = [
    "notification_background_task_scheduler.h",
    "notification_data.cc",
    "notification_data.h",
    "notification_params.cc",
    "notification_params.h",
    "notification_schedule_service.h",
    "notification_scheduler_types.h",
    "schedule_params.cc",
    "schedule_params.h",
  ]

  deps = [
    "//base",
    "//components/keyed_service/core",
  ]
}

# Internal library that embedders should not directly depend on.
source_set("lib") {
  visibility = [
    ":scheduler",
    ":unit_tests",
  ]
  sources = [
    "collection_store.h",
    "icon_entry.h",
    "icon_store.cc",
    "icon_store.h",
    "impression_entry.cc",
    "impression_entry.h",
    "notification_entry.cc",
    "notification_entry.h",
    "notification_schedule_service_impl.cc",
    "notification_schedule_service_impl.h",
    "notification_scheduler_context.cc",
    "notification_scheduler_context.h",
    "proto_conversion.cc",
    "proto_conversion.h",
    "scheduler_model.h",
    "scheduler_model_impl.cc",
    "scheduler_model_impl.h",
    "type_state.cc",
    "type_state.h",
  ]

  # This target should not depend on anything in //chrome/* except the proto library.
  deps = [
    ":public",
    "//base",
    "//chrome/browser/notifications/proto",
    "//components/keyed_service/core",
    "//components/leveldb_proto",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "icon_store_unittest.cc",
    "proto_conversion_unittest.cc",
  ]

  deps = [
    ":lib",
    ":public",
    "//chrome/browser/notifications/proto",
    "//components/leveldb_proto:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
