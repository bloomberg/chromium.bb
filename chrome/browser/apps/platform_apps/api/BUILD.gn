# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//chrome/common/apps/platform_apps/api/api_sources.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")

assert(enable_extensions)

function_registration("api_registration") {
  sources = chrome_apps_api_schema_files
  impl_dir = "//chrome/browser/apps/platform_apps/api"
  configs = [ "//build/config:precompiled_headers" ]
  bundle_name = "ChromeApps"
  root_namespace = apps_api_root_namespace

  deps = [
    "//extensions/browser",
  ]
}

source_set("api") {
  sources = [
    "browser/browser_api.cc",
    "browser/browser_api.h",
    "browser_context_keyed_service_factories.cc",
    "browser_context_keyed_service_factories.h",
  ]

  deps = [
    "//chrome/browser/extensions",
    "//chrome/common/apps/platform_apps/api",
    "//extensions/browser",
  ]

  if (is_chromeos) {
    sources += [
      "easy_unlock_private/easy_unlock_private_api.cc",
      "easy_unlock_private/easy_unlock_private_api.h",
      "easy_unlock_private/easy_unlock_private_connection.cc",
      "easy_unlock_private/easy_unlock_private_connection.h",
      "easy_unlock_private/easy_unlock_private_connection_manager.cc",
      "easy_unlock_private/easy_unlock_private_connection_manager.h",
    ]

    deps += [
      "//chrome:strings",
      "//chrome/browser/chromeos",
      "//chrome/browser/ui",
      "//chromeos/components/proximity_auth",
      "//chromeos/components/proximity_auth/logging",
      "//components/account_id",
      "//components/cryptauth",
      "//components/strings:components_strings_grit",
      "//components/user_manager",
      "//device/bluetooth",
      "//ui/chromeos",
    ]
  }

  visibility = [ "//chrome/browser" ]
}
