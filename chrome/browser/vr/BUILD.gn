# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//device/vr/features/features.gni")
import("//testing/test.gni")
import("//tools/grit/repack.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

assert(enable_vr)

static_library("vr_common") {
  sources = [
    "animation_player.cc",
    "animation_player.h",
    "browser_ui_interface.h",
    "color_scheme.cc",
    "color_scheme.h",
    "controller_mesh.cc",
    "controller_mesh.h",
    "databinding/binding.h",
    "databinding/binding_base.cc",
    "databinding/binding_base.h",
    "databinding/vector_binding.h",
    "databinding/vector_element_binding.h",
    "elements/button.cc",
    "elements/button.h",
    "elements/button_texture.cc",
    "elements/button_texture.h",
    "elements/content_element.cc",
    "elements/content_element.h",
    "elements/controller.cc",
    "elements/controller.h",
    "elements/draw_phase.h",
    "elements/exclusive_screen_toast.cc",
    "elements/exclusive_screen_toast.h",
    "elements/exclusive_screen_toast_texture.cc",
    "elements/exclusive_screen_toast_texture.h",
    "elements/exit_prompt.cc",
    "elements/exit_prompt.h",
    "elements/exit_prompt_backplane.cc",
    "elements/exit_prompt_backplane.h",
    "elements/exit_prompt_texture.cc",
    "elements/exit_prompt_texture.h",
    "elements/exit_warning_texture.cc",
    "elements/exit_warning_texture.h",
    "elements/full_screen_rect.cc",
    "elements/full_screen_rect.h",
    "elements/grid.cc",
    "elements/grid.h",
    "elements/invisible_hit_target.cc",
    "elements/invisible_hit_target.h",
    "elements/laser.cc",
    "elements/laser.h",
    "elements/linear_layout.cc",
    "elements/linear_layout.h",
    "elements/rect.cc",
    "elements/rect.h",
    "elements/render_text_wrapper.cc",
    "elements/render_text_wrapper.h",
    "elements/reticle.cc",
    "elements/reticle.h",
    "elements/simple_textured_element.h",
    "elements/spinner.cc",
    "elements/spinner.h",
    "elements/system_indicator.cc",
    "elements/system_indicator.h",
    "elements/system_indicator_texture.cc",
    "elements/system_indicator_texture.h",
    "elements/text.cc",
    "elements/text.h",
    "elements/textured_element.cc",
    "elements/textured_element.h",
    "elements/throbber.cc",
    "elements/throbber.h",
    "elements/transient_element.cc",
    "elements/transient_element.h",
    "elements/ui_element.cc",
    "elements/ui_element.h",
    "elements/ui_element_iterator.h",
    "elements/ui_element_name.cc",
    "elements/ui_element_name.h",
    "elements/ui_element_transform_operations.cc",
    "elements/ui_element_transform_operations.h",
    "elements/ui_texture.cc",
    "elements/ui_texture.h",
    "elements/url_bar.cc",
    "elements/url_bar.h",
    "elements/url_bar_texture.cc",
    "elements/url_bar_texture.h",
    "elements/vector_icon.cc",
    "elements/vector_icon.h",
    "elements/vector_icon_button_texture.cc",
    "elements/vector_icon_button_texture.h",
    "elements/viewport_aware_root.cc",
    "elements/viewport_aware_root.h",
    "elements/webvr_url_toast.cc",
    "elements/webvr_url_toast.h",
    "elements/webvr_url_toast_texture.cc",
    "elements/webvr_url_toast_texture.h",
    "exit_vr_prompt_choice.h",
    "font_fallback.cc",
    "font_fallback.h",
    "fps_meter.cc",
    "fps_meter.h",
    "gltf_asset.cc",
    "gltf_asset.h",
    "gltf_parser.cc",
    "gltf_parser.h",
    "model/controller_model.h",
    "model/model.cc",
    "model/model.h",
    "model/omnibox_suggestions.cc",
    "model/omnibox_suggestions.h",
    "model/reticle_model.h",
    "model/web_vr_timeout_state.h",
    "service/vr_device_manager.cc",
    "service/vr_device_manager.h",
    "service/vr_display_host.cc",
    "service/vr_display_host.h",
    "service/vr_service_impl.cc",
    "service/vr_service_impl.h",
    "speech_recognizer.cc",
    "speech_recognizer.h",
    "target_property.cc",
    "target_property.h",
    "toolbar_helper.cc",
    "toolbar_helper.h",
    "toolbar_state.cc",
    "toolbar_state.h",
    "transition.cc",
    "transition.h",
    "ui.cc",
    "ui.h",
    "ui_browser_interface.h",
    "ui_element_renderer.h",
    "ui_input_manager.cc",
    "ui_input_manager.h",
    "ui_interface.h",
    "ui_renderer.cc",
    "ui_renderer.h",
    "ui_scene.cc",
    "ui_scene.h",
    "ui_scene_manager.cc",
    "ui_scene_manager.h",
    "ui_unsupported_mode.h",
    "vr_gl_util.cc",
    "vr_gl_util.h",
    "vr_shell_renderer.cc",
    "vr_shell_renderer.h",
    "web_contents_event_forwarder.cc",
    "web_contents_event_forwarder.h",
  ]

  public_deps = [
    "//chrome/browser/resources:vr_shell_resources",
  ]

  deps = [
    "//base",
    "//cc/animation",
    "//cc/paint",
    "//chrome/app:generated_resources",
    "//chrome/browser/vr/vector_icons",
    "//chrome/common:constants",
    "//components/omnibox/browser",
    "//components/security_state/core",
    "//components/strings",
    "//components/toolbar",
    "//components/url_formatter",
    "//components/vector_icons",
    "//content/public/browser",
    "//content/public/common",
    "//device/vr:mojo_bindings",
    "//device/vr:vr",
    "//skia",
    "//ui/base",
    "//ui/display",
    "//ui/gl",
    "//ui/gl/init",
  ]
}

test("vr_common_unittests") {
  sources = [
    "animation_player_unittest.cc",
    "databinding/binding_unittest.cc",
    "databinding/vector_binding_unittest.cc",
    "elements/exit_prompt_unittest.cc",
    "elements/linear_layout_unittest.cc",
    "elements/rect_unittest.cc",
    "elements/spinner_unittest.cc",
    "elements/throbber_unittest.cc",
    "elements/transient_element_unittest.cc",
    "elements/ui_element_iterator_unittest.cc",
    "elements/ui_element_unittest.cc",
    "elements/url_bar_texture_unittest.cc",
    "elements/vector_icon_button_texture_unittest.cc",
    "elements/vector_icon_unittest.cc",
    "elements/viewport_aware_root_unittest.cc",
    "fps_meter_unittest.cc",
    "gltf_parser_unittest.cc",
    "service/vr_device_manager_unittest.cc",
    "speech_recognizer_unittest.cc",
    "test/animation_utils.cc",
    "test/animation_utils.h",
    "test/fake_ui_element_renderer.cc",
    "test/fake_ui_element_renderer.h",
    "test/gl_test_environment_unittest.cc",
    "test/mock_browser_interface.cc",
    "test/mock_browser_interface.h",
    "test/mock_content_input_delegate.cc",
    "test/mock_content_input_delegate.h",
    "test/paths.cc",
    "test/paths.h",
    "test/run_all_unittests.cc",
    "test/ui_pixel_test.cc",
    "test/ui_pixel_test.h",
    "test/ui_scene_manager_test.cc",
    "test/ui_scene_manager_test.h",
    "ui_input_manager_unittest.cc",
    "ui_pixeltest.cc",
    "ui_renderer_unittest.cc",
    "ui_scene_manager_unittest.cc",
    "ui_scene_unittest.cc",
    "vr_gl_util_unittest.cc",
  ]

  # TODO(mthiesse, crbug.com/769373): The dependency on device/vr:fakes requires
  # some workarounds for tests, since we are static-linking to the fakes instead
  # of loading them from a dll on Windows.
  defines = [ "DEVICE_VR_IMPLEMENTATION" ]

  deps = [
    ":vr_test_support",
    "//cc:test_support",
    "//mojo/common",
    "//mojo/public/cpp/bindings",
    "//testing/gmock",
    "//ui/gfx:test_support",
    "//ui/gfx/geometry",
    "//ui/gl",
  ]

  if (is_android) {
    deps += [ "//ui/android:ui_java" ]
  }
}

test("vr_common_perftests") {
  sources = [
    "test/run_all_perftests.cc",
    "text_perftest.cc",
  ]
  deps = [
    ":vr_test_support",
    "//testing/perf",
  ]

  if (is_android) {
    deps += [ "//ui/android:ui_java" ]
  }

  # Needed for isolate script to execute
  data = [
    "//testing/scripts/common.py",
    "//testing/xvfb.py",
    "//testing/scripts/run_gtest_perf_test.py",
    "//tools/perf/generate_legacy_perf_dashboard_json.py",
  ]
}

source_set("vr_test_support") {
  testonly = true

  sources = [
    "test/constants.h",
    "test/gl_test_environment.cc",
    "test/gl_test_environment.h",
    "test/vr_common_test_suite.cc",
    "test/vr_common_test_suite.h",
  ]

  public_deps = [
    ":vr_common",
    ":vr_test_pak",
    "//base/test:test_support",
    "//components/security_state/core",
    "//components/toolbar:vector_icons",
    "//content/test:test_support",
    "//mojo/edk/system",
    "//skia",
    "//testing/gtest",
    "//ui/accessibility:ax_gen",
    "//ui/gl:test_support",

    # TODO(mthiesse, crbug.com/769373): Remove dependency on device/vr:fakes.
    "//device/vr:fakes",
  ]

  data = [
    "test/data/",
    "$root_out_dir/vr_test.pak",
  ]
}

repack("vr_test_pak") {
  sources = [
    "$root_gen_dir/chrome/generated_resources_en-US.pak",
    "$root_gen_dir/chrome/vr_shell_resources.pak",
    "$root_gen_dir/components/components_resources.pak",
    "$root_gen_dir/components/strings/components_strings_en-US.pak",
  ]

  output = "$root_out_dir/vr_test.pak"

  deps = [
    "//chrome/app:generated_resources",
    "//chrome/browser/resources:vr_shell_resources",
    "//components/resources:components_resources",
    "//components/strings",
  ]
}
