# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

local_ntp_resources = "//chrome/browser/resources/local_ntp"

action("local_ntp_code_generate") {
  script = "tools/generate_integrity_header.py"
  header_path = "$target_gen_dir/local_ntp_js_integrity.h"
  local_ntp_js = local_ntp_resources + "/local_ntp.js"
  voice_js = local_ntp_resources + "/voice.js"

  inputs = [
    local_ntp_js,
    voice_js,
  ]

  outputs = [
    header_path,
  ]

  args = [
    "--output_path=" + rebase_path(header_path, root_build_dir),
    rebase_path(local_ntp_js, root_build_dir),
    rebase_path(voice_js, root_build_dir),
  ]
}

source_set("generated") {
  sources = get_target_outputs(":local_ntp_code_generate")

  public_deps = [
    ":local_ntp_code_generate",
  ]
}
