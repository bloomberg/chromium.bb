# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("components") {
  sources = [
    "pending_app_manager.cc",
    "pending_app_manager.h",
    "web_app_helpers.cc",
    "web_app_helpers.h",
    "web_app_shortcut.cc",
    "web_app_shortcut.h",
    "web_app_shortcut_chromeos.cc",
    "web_app_shortcut_mac.h",
    "web_app_shortcut_mac.mm",
    "web_app_shortcut_win.cc",
    "web_app_shortcut_win.h",

    # TODO(nigeltao): move these two files from
    # //chrome/browser/web_applications/components to a stand-alone
    # //components/web_app_icon_downloader?
    #
    # There's also //components/favicon, //components/image_fetcher as well as
    # code split between //content/public/browser/manifest_icon_downloader.h
    # and //content/browser/manfest/manifest_icon_downloader.cc. Some or all of
    # those might be similar enough to merge.
    "web_app_icon_downloader.cc",
    "web_app_icon_downloader.h",
  ]

  if (is_desktop_linux) {
    # Desktop linux, doesn't count ChromeOS.
    sources += [
      "web_app_shortcut_linux.cc",
      "web_app_shortcut_linux.h",
    ]
  }

  deps = [
    "//chrome/browser/web_applications:web_app_group",
    "//content/public/browser",
    "//skia",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "web_app_helpers_unittest.cc",
    "web_app_icon_downloader_unittest.cc",
    "web_app_shortcut_mac_unittest.mm",
    "web_app_shortcut_unittest.cc",
  ]

  if (is_desktop_linux) {
    # Desktop linux, doesn't count ChromeOS.
    sources += [ "web_app_shortcut_linux_unittest.cc" ]
  }

  deps = [
    ":components",
    "//chrome/app/theme:theme_resources",
    "//chrome/browser/web_applications:web_app_group",
    "//content/public/browser",
    "//skia",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
