# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")

js_type_check("closure_compile") {
  deps = [
    ":viewer-bookmark",
    ":viewer-error-screen",
    ":viewer-page-indicator",
    ":viewer-page-selector",
    ":viewer-password-screen",
    ":viewer-pdf-toolbar",
    ":viewer-toolbar-dropdown",
    ":viewer-zoom-button",
    ":viewer-zoom-toolbar",
  ]
  if (is_chromeos) {
    deps += [
      ":viewer-form-warning",
      ":viewer-ink-host",
      ":viewer-pen-options",
    ]
  }
}

js_library("viewer-bookmark") {
  deps = [
    "//third_party/polymer/v1_0/components-chromium/iron-a11y-keys-behavior:iron-a11y-keys-behavior-extracted",
  ]
}

js_library("viewer-error-screen") {
  deps = [
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog",
  ]
}

if (is_chromeos) {
  js_library("viewer-form-warning") {
    deps = [
      "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog",
      "//ui/webui/resources/js:promise_resolver",
    ]
  }

  js_library("viewer-ink-host") {
    deps = [
      "//chrome/browser/resources/pdf:metrics",
      "//chrome/browser/resources/pdf:viewport",
      "//chrome/browser/resources/pdf/ink:ink_api",
    ]
  }
}

js_library("viewer-page-indicator") {
  deps = [
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:util",
  ]
}

js_library("viewer-page-selector") {
  deps = [
    "//ui/webui/resources/cr_elements/cr_input:cr_input",
  ]
}

js_library("viewer-password-screen") {
  deps = [
    "//ui/webui/resources/cr_elements/cr_input:cr_input",
  ]
}

js_library("viewer-pdf-toolbar") {
  deps = [
    ":viewer-bookmark",
    ":viewer-page-selector",
    ":viewer-toolbar-dropdown",
    "..:annotation_tool",
  ]
  externs_list = [ "$externs_path/pending.js" ]
}

js_library("viewer-pen-options") {
  externs_list = [ "$externs_path/pending_polymer.js" ]
}

js_library("viewer-toolbar-dropdown") {
  deps = []
}

js_library("viewer-zoom-toolbar") {
  deps = [
    ":viewer-zoom-button",
    "..:pdf_fitting_type",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:util",
  ]
}

js_library("viewer-zoom-button") {
  deps = []
}

polymer_modulizer("shared-vars") {
  js_file = "shared-vars.m.js"
  html_file = "shared-vars.html"
  html_type = "custom-style"
}

group("polymer3_elements") {
  deps = [
    ":shared-vars_module",
  ]
}
