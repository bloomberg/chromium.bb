# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# TODO(csharrison): Move subresource_filter_browsertests here.

if (is_android) {
  import("//build/config/android/rules.gni")

  generate_jni("jni_headers") {
    sources = [
      "../../android/javatests/src/org/chromium/chrome/browser/subresource_filter/TestRulesetPublisher.java",
    ]
    jni_package = "chrome_subresource_filter"
  }

  # This library must be included by the apk_under_test in order for the JNI
  # shim to function correctly.
  android_library("subresource_filter_java_test_support") {
    testonly = true
    java_files = [ "../../android/javatests/src/org/chromium/chrome/browser/subresource_filter/TestRulesetPublisher.java" ]
    deps = [
      "//base:base_java",
    ]
  }

  # This library must be included by the instrumentation_test. It must not be
  # included by the apk_under_test, since in debug builds, java classes that
  # appear in the apk_under_test are stripped from the instrumentation_test.
  # This library contains dependencies that must not be stripped from the
  # instrumentation_test.
  android_library("subresource_filter_javatests") {
    testonly = true
    java_files = [ "../../android/javatests/src/org/chromium/chrome/browser/subresource_filter/SubresourceFilterTest.java" ]
    deps = [
      ":subresource_filter_java_test_support",
      "//base:base_java",
      "//base:base_java_test_support",
      "//chrome/android:chrome_java",
      "//chrome/android:chrome_test_java",
      "//chrome/test/android:chrome_java_test_support",
      "//content/public/test/android:content_java_test_support",
      "//third_party/android_support_test_runner:rules_java",
      "//third_party/android_support_test_runner:runner_java",
      "//third_party/junit",
    ]
  }

  source_set("subresource_filter_test_support") {
    testonly = true
    sources = [
      "android_test_ruleset_publisher.cc",
    ]
    deps = [
      ":jni_headers",
      "//components/subresource_filter/core/common:test_support",
      "//net",
      "//third_party/protobuf:protobuf_lite",
    ]
  }
}
