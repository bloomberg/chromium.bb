# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//build/config/ui.gni")
import("//chrome/common/features.gni")

assert(enable_extensions)

gypi_values =
    exec_script("//build/gypi_to_gn.py",
                [ rebase_path("../../chrome_browser_extensions.gypi") ],
                "scope",
                [ "../../chrome_browser_extensions.gypi" ])

# GYP version: chrome/chrome_browser_extensions.gypi:browser_extensions
static_library("extensions") {
  sources = []
  sources += rebase_path(gypi_values.chrome_browser_extensions_enabled_sources,
                         ".",
                         "//chrome")

  configs += [
    "//build/config:precompiled_headers",
    "//build/config/compiler:wexit_time_destructors",
  ]
  defines = []

  # Since browser and browser_extensions actually depend on each other,
  # we must omit the dependency from browser_extensions to browser.
  # However, this means browser_extensions and browser should more or less
  # have the same dependencies. Once browser_extensions is untangled from
  # browser, then we can clean up these dependencies.
  public_deps = [
    "//chrome/common/extensions/api",
    "//content/public/browser",
  ]
  deps = [
    "//chrome:extra_resources",
    "//chrome:resources",
    "//chrome:strings",
    "//chrome/app:command_ids",
    "//chrome/app/resources:platform_locale_settings",
    "//chrome/app/theme:theme_resources",
    "//chrome/browser/devtools",
    "//chrome/browser/media/router",
    "//chrome/common",
    "//chrome/common/extensions/api:api_registration",
    "//chrome/common/extensions/api:extensions_features",
    "//chrome/common/safe_browsing:proto",
    "//chrome/installer/util:with_no_strings",
    "//components/app_modal",
    "//components/autofill/content/browser",
    "//components/bookmarks/browser",
    "//components/bookmarks/managed",
    "//components/browser_sync/browser",
    "//components/browsing_data/core",
    "//components/bubble",
    "//components/content_settings/core/browser",
    "//components/crx_file",
    "//components/data_reduction_proxy/core/browser",
    "//components/dom_distiller/core",
    "//components/favicon/content",
    "//components/feedback",
    "//components/gcm_driver",
    "//components/guest_view/browser",
    "//components/history/core/browser",
    "//components/infobars/core",
    "//components/keyed_service/content",
    "//components/navigation_interception",
    "//components/net_log",
    "//components/omnibox/browser",
    "//components/onc",
    "//components/password_manager/core/browser",
    "//components/pdf/browser",
    "//components/policy:generated",
    "//components/policy/core/browser",
    "//components/pref_registry",
    "//components/proximity_auth",
    "//components/proximity_auth/ble",
    "//components/proximity_auth/cryptauth",
    "//components/proximity_auth/logging",
    "//components/proxy_config",
    "//components/rappor",
    "//components/resources",
    "//components/safe_browsing_db:database_manager",
    "//components/safe_browsing_db:util",
    "//components/safe_json",
    "//components/search_engines",
    "//components/sessions",
    "//components/signin/core/browser",
    "//components/spellcheck/browser",
    "//components/storage_monitor",
    "//components/strings",
    "//components/sync",
    "//components/sync_sessions",
    "//components/syncable_prefs",
    "//components/translate/core/browser",
    "//components/undo",
    "//components/update_client",
    "//components/url_matcher",
    "//components/user_prefs",
    "//components/web_modal",
    "//components/zoom",
    "//content/app/resources",
    "//content/public/common",
    "//crypto",
    "//device/bluetooth",
    "//device/hid",
    "//extensions:extensions_resources",
    "//extensions/browser",
    "//extensions/common/api",
    "//extensions/common/api:api_registration",
    "//extensions/strings",
    "//net",
    "//skia",
    "//sql",
    "//storage/browser",
    "//storage/common",
    "//third_party/WebKit/public:image_resources",
    "//third_party/WebKit/public:resources",
    "//third_party/cacheinvalidation",
    "//third_party/icu",
    "//third_party/leveldatabase",
    "//third_party/libaddressinput:util",
    "//third_party/re2",
    "//third_party/webrtc/modules/desktop_capture",
    "//ui/accessibility:ax_gen",
    "//ui/base",
    "//ui/base/ime",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/native_theme",
    "//ui/resources",
    "//ui/shell_dialogs",
    "//ui/strings",
    "//url",
  ]

  if (is_chromeos) {
    sources +=
        rebase_path(gypi_values.chrome_browser_extensions_chromeos_sources,
                    ".",
                    "//chrome")
    sources -= [ "api/music_manager_private/device_id_linux.cc" ]
    if (use_dbus) {
      configs += [ "//build/config/linux/dbus" ]
    }
    deps += [
      "//components/chrome_apps",
      "//components/constrained_window",
      "//components/drive",
      "//components/user_manager",
      "//remoting/host",
      "//remoting/host/it2me:common",
      "//third_party/protobuf:protobuf_lite",
      "//ui/app_list",
      "//ui/chromeos",
      "//ui/file_manager",
      "//ui/views/",
    ]
  } else {
    sources += [
      "default_apps.cc",
      "default_apps.h'",
    ]
    sources +=
        rebase_path(gypi_values.chrome_browser_extensions_non_chromeos_sources,
                    ".",
                    "//chrome")
  }

  # chromeos uses its own global_shortcut_listener, _x11 is not necessary.
  if (is_chromeos || !use_x11) {
    sources -= [
      "global_shortcut_listener_x11.cc",
      "global_shortcut_listener_x11.h",
    ]
  }

  if (is_chromeos && use_ozone) {
    sources -= [ "global_shortcut_listener_chromeos.cc" ]
  }
  if (!use_ozone) {
    sources -= [ "global_shortcut_listener_ozone.cc" ]
  }

  if (is_linux) {
    deps += [ "//build/linux:fontconfig" ]

    if (use_dbus) {
      deps += [ "//dbus" ]
    }
    if (use_udev) {
      deps += [ "//device/udev_linux" ]
    }

    if (use_x11) {
      configs += [ "//build/config/linux:x11" ]
      deps += [
        "//ui/events:events_base",
        "//ui/events/platform",
        "//ui/events/platform/x11",
      ]
    }
    if (!is_chromeos) {
      sources += rebase_path(
              gypi_values.chrome_browser_extensions_input_ime_linux_win_sources,
              ".",
              "//chrome")
    }
  }

  if (!is_android && !is_ios) {
    # Non-mobile.
    #deps += [ "//apps" ]
  }

  if (is_win || is_mac) {
    deps += [ "//components/wifi" ]
    sources += rebase_path(
            gypi_values.chrome_browser_extensions_networking_private_sources_winmac,
            ".",
            "//chrome")
  }

  if (is_win) {
    deps += [
      "//third_party/iaccessible2",
      "//third_party/isimpledom",
    ]
    sources += rebase_path(
            gypi_values.chrome_browser_extensions_input_ime_linux_win_sources,
            ".",
            "//chrome")
  } else if (use_aura && !is_chromeos) {
    sources += [
      "display_info_provider_aura.cc",
      "display_info_provider_aura.h",
    ]
  }

  if (enable_app_list) {
    sources +=
        rebase_path(gypi_values.chrome_browser_extensions_app_list_sources,
                    ".",
                    "//chrome")
  }

  if (enable_hotwording) {
    defines += [ "ENABLE_HOTWORDING" ]
  }

  if (enable_service_discovery) {
    sources += rebase_path(
            gypi_values.chrome_browser_extensions_service_discovery_sources,
            ".",
            "//chrome")
  }

  if (enable_task_manager) {
    sources += rebase_path(
            gypi_values.chrome_browser_extensions_task_manager_enabled_sources,
            ".",
            "//chrome")
  }

  if (enable_rlz_support) {
    deps += [ "//rlz:rlz_lib" ]
  }

  if (enable_webrtc) {
    sources += [ "api/webrtc_logging_private/webrtc_logging_private_api.cc" ]
  } else {
    sources +=
        [ "api/webrtc_logging_private/webrtc_logging_private_api_stub.cc" ]
  }

  if (toolkit_views) {
    deps += [ "//ui/views" ]
  }

  if (use_ash) {
    sources += [
      "api/tabs/ash_panel_contents.cc",
      "api/tabs/ash_panel_contents.h",
    ]
    deps += [ "//ash" ]
  }

  if (use_aura) {
    deps += [
      "//ui/aura",
      "//ui/keyboard",
      "//ui/keyboard:keyboard_with_content",
      "//ui/keyboard:resources",
    ]
  }

  if (use_brlapi) {
    deps += [ "//build/linux/libbrlapi" ]
    sources += rebase_path(gypi_values.chrome_browser_extensions_brlapi_sources,
                           ".",
                           "//chrome")
  } else {
    sources += [ "api/braille_display_private/braille_controller_stub.cc" ]
  }
}
