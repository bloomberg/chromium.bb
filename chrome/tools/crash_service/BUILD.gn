# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win)

executable("crash_service") {
  sources = [
    "main.cc",
  ]

  configs -= [ "//build/config/win:console" ]
  configs += [ "//build/config/win:windowed" ]

  deps = [
    "//base",
    "//chrome/common:constants",
    "//chrome/installer/util:with_no_strings",
    "//components/crash/content/tools:crash_service",
    "//content/public/common:static_switches",
  ]
}

if (target_cpu == "x86" && current_cpu == "x86") {
  # Cross-compile a 64-bit version when compiling a 64-bit target.
  copy("crash_service_win64") {
    if (is_clang) {
      maybe_clang = "clang_"
    } else {
      maybe_clang = ""
    }
    crash_service_64_target =
        ":crash_service(//build/toolchain/win:${maybe_clang}x64)"

    sources = [
      # It would be nice if get_target_outputs would work on the executable,
      # but instead we have to manually compute the executable name.
      get_label_info(crash_service_64_target, "root_out_dir") +
          "/crash_service.exe",
    ]
    outputs = [
      "$root_build_dir/crash_service_win64.exe",
    ]

    deps = [
      crash_service_64_target,
    ]
  }
}
