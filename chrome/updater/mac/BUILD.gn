# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("mac") {
  deps = [
    ":updater",
  ]
}

executable("updater") {
  sources = [
    "main.cc",
  ]

  deps = [
    ":network_fetcher_sources",
    "//chrome/updater:common",
  ]
}

source_set("network_fetcher_sources") {
  sources = [
    "net/network.h",
    "net/network_fetcher.h",
    "net/network_fetcher.mm",
  ]

  deps = [
    "//base",
    "//components/update_client",
    "//net",
  ]
}

source_set("updater_tests") {
  testonly = true

  sources = [
    "net/network_unittest.mm",
  ]

  deps = [
    ":network_fetcher_sources",
    "//base/test:test_support",
    "//net:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

bundle_data("info_plist") {
  sources = [
    "Info.plist",
  ]
  outputs = [
    "{{bundle_contents_dir}}/Info.plist",
  ]
}

create_bundle("GoogleUpdate.bundle") {
  deps = [
    ":info_plist",
  ]
  bundle_root_dir = "${root_build_dir}/GoogleUpdate.bundle"
  bundle_contents_dir = "${bundle_root_dir}/Contents"
  bundle_resources_dir = "${bundle_contents_dir}/Resources"
  bundle_executable_dir = "${bundle_contents_dir}/MacOS"
}
