# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//build/config/allocator.gni")
import("//build/config/clang/clang.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/dcheck_always_on.gni")
import("//build/nocompile.gni")
import("//chrome/process_version_rc_template.gni")
import("//testing/libfuzzer/fuzzer_test.gni")
import("//testing/test.gni")

process_version_rc_template("version") {
  template_file = "gcp_setup.rc.version"
  output = "$root_out_dir/gcp_setup_version.rc"
}

source_set("common") {
  sources = [
    "setup_lib.cc",
    "setup_lib.h",
  ]
  public_deps = [
    "//base:base",
  ]
  deps = [
    ":version",
    "../gaiacp:common",
    "//chrome/installer/util:with_no_strings",
  ]
}

# Builds a self extracting 7-zip binary using the 7-zip SFX sources but with
# the credential provider's version resources.
executable("gcp_sfx") {
  deps = [
    ":version",
    "//third_party/lzma_sdk/Util/SfxSetup:7zS2_source",
  ]
  configs += [ "//build/config/win:windowed" ]
}

executable("gcp_setup") {
  sources = [
    "setup.cc",
  ]
  deps = [
    ":common",
    "../eventlog:gcp_eventlog_messages",
    "../gaiacp:common",
    "//chrome/common:version_header",
  ]
  configs += [ "//build/config/win:windowed" ]
}
