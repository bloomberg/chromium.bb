# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/sysroot.gni")

assert(is_mac)

config("gcapi_config") {
  cflags = [
    "-isysroot",
    sysroot,
    "-mmacosx-version-min=10.5",
  ]
  ldflags = [
    "-isysroot",
    sysroot,
    "-mmacosx-version-min=10.5",
  ]
}

static_library("gcapi_lib") {
  complete_static_lib = true
  configs -= [ "//build/config/compiler:thin_archive" ]
  sources = [
    "gcapi.h",
    "gcapi.mm",
  ]

  libs = [ "Cocoa.framework" ]

  # Don't use runtime_library, to be able to pick a custom mmacosx-version-min.
  configs -= [ "//build/config/compiler:runtime_library" ]
  configs += [ ":gcapi_config" ]
}

executable("gcapi_example") {
  sources = [
    "gcapi_example_client.mm",
  ]

  deps = [
    ":gcapi_lib",
  ]

  # Don't use runtime_library, to be able to pick a custom mmacosx-version-min.
  configs -= [ "//build/config/compiler:runtime_library" ]
  configs += [ ":gcapi_config" ]
}
