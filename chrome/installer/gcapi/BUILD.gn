# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

assert(is_win)

shared_library("gcapi") {
  output_name = "gcapi_dll"

  sources = [
    "gcapi.def",
    "gcapi_dll.cc",
  ]

  deps = [
    ":lib",
    "//base",
  ]
}

source_set("lib") {
  sources = [
    "gcapi.cc",
    "gcapi.h",
    "gcapi_omaha_experiment.cc",
    "gcapi_omaha_experiment.h",
    "gcapi_reactivation.cc",
    "gcapi_reactivation.h",
  ]

  deps = [
    "//base",
    "//chrome/installer/util",
    "//chrome/installer/launcher_support",
    "//components/variations",
    "//google_update",
  ]
}

test("gcapi_test") {
  sources = [
    "gcapi_last_run_test.cc",
    "gcapi_omaha_experiment_test.cc",
    "gcapi_reactivation_test.cc",
    "gcapi_test.cc",
    "gcapi_test.rc",
    "gcapi_test_registry_overrider.cc",
    "gcapi_test_registry_overrider.h",
    "resource.h",
  ]

  deps = [
    ":lib",
    "//base/test:test_support",
    "//chrome/installer/util",
    "//components/variations",
    "//testing/gtest",
  ]

  data_deps = [
    ":gcapi",  # Runtime dependency on this DLL.
  ]
}
