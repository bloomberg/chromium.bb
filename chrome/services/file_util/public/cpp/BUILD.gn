# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

source_set("cpp") {
  public_deps = [
    "//chrome/services/file_util/public/interfaces",
    "//services/service_manager/public/cpp",
  ]

  if (safe_browsing_mode == 1) {
    sources = [
      "sandboxed_dmg_analyzer_mac.cc",
      "sandboxed_dmg_analyzer_mac.h",
      "sandboxed_zip_analyzer.cc",
      "sandboxed_zip_analyzer.h",
    ]
    if (is_chromeos) {
      sources += [
        "zip_file_creator.cc",
        "zip_file_creator.h",
      ]
    }
    public_deps += [ "//chrome/common/safe_browsing" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "sandboxed_dmg_analyzer_mac_unittest.cc",
    "sandboxed_zip_analyzer_unittest.cc",
  ]

  deps = [
    ":cpp",
    "//chrome/services/file_util:lib",
    "//crypto:platform",
    "//services/service_manager/public/cpp/test:test_support",
    "//skia",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

if (is_chromeos) {
  source_set("browser_tests") {
    testonly = true

    sources = [
      "zip_file_creator_browsertest.cc",
    ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    deps = [
      "//base",
      "//chrome/test:test_support",
      "//content/public/common",
      "//content/test:test_support",
      "//testing/gtest",
      "//third_party/zlib",
    ]
  }
}
