# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//printing/buildflags/buildflags.gni")
import("//services/service_manager/public/service_manifest.gni")

source_set("lib") {
  sources = [
    "cups_ipp_parser_service.cc",
    "cups_ipp_parser_service.h",
    "ipp_parser.h",
  ]

  # Hide real impl if libCUPS not present
  if (use_cups && is_chromeos) {
    configs += [ "//printing:cups" ]
    sources += [
      "ipp_parser.cc",
      "ipp_parser_util.cc",
      "ipp_parser_util.h",
    ]
  } else {
    sources += [ "fake_ipp_parser.cc" ]
  }

  deps = [
    "//base",
    "//mojo/public/cpp/bindings",
  ]

  public_deps = [
    "//chrome/services/cups_ipp_parser/public/mojom",
    "//mojo/public/mojom/base",
    "//printing",
    "//services/service_manager/public/cpp",
  ]
}

service_manifest("manifest") {
  name = "cups_ipp_parser"
  source = "manifest.json"
}
