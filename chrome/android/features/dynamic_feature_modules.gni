# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/compiler/compiler.gni")
import("//device/vr/buildflags/buildflags.gni")

# This file aggregates data used when pulling DFM-related dependencies into
# bundle builds. As DFMs move to describe their parameters in a standard way,
# this file may evolve to an aggregation of individual DFM configurations.

# Mapping that controls package IDs assigned to modules.
resource_packages_id_mapping = [
  "ar=0x7e",
  "vr=0x7d",
  "tab_ui=0x7c",
  "autofill_assistant=0x7b",
  "devtools=0x7a",
]

# This variable indicates whether the native feature module system is engaged.
# Currently, this implies a build configuration that supports native modules,
# and that at least one feature is using a native module.
if (is_clang && use_lld && !is_component_build) {
  use_native_modules = enable_vr && modularize_vr_native
} else {
  use_native_modules = false
}

# This block aggregates parameters from multiple modules, for use by targets
# responsible for assembling bundles. In the future, these lists may be
# replaced by a list of module descriptors, or a similar restructuring. See
# http://crbug.com/950056.
native_feature_modules = []
feature_module_base_lib_deps = []
native_feature_module_entrypoint_files = []

if (enable_vr && modularize_vr_native) {
  native_feature_modules += [ "vr" ]
  feature_module_base_lib_deps += [ "//chrome/browser/vr:vr_ui" ]
  native_feature_module_entrypoint_files +=
      [ "//chrome/browser/vr/module_exports.lst" ]
}
