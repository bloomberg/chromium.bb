# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//build/config/locales.gni")
import("//chrome/android/features/dynamic_feature_modules.gni")
import("//chrome/android/features/tab_ui/buildflags.gni")

template("tab_ui_module_tmpl") {
  assert(defined(invoker.version_code))
  assert(defined(invoker.version_name))

  android_app_bundle_module(target_name) {
    forward_variables_from(invoker,
                           [
                             "base_module_target",
                             "manifest_package",
                             "module_name",
                             "uncompress_shared_libraries",
                             "version_code",
                             "version_name",
                           ])
    android_manifest = "//chrome/android/features/tab_ui/AndroidManifest.xml"
    min_sdk_version = 21
    target_sdk_version = android_sdk_version
    proguard_enabled = !is_java_debug
    aapt_locale_whitelist = locales
    package_name = "tab_ui"
    package_name_to_id_mapping = resource_packages_id_mapping
    deps = [
      "//chrome/android/features/tab_ui:java",
    ]

    # If tab_ui is set as async DFM, generate list of dependencies on base for keep
    # rule generation.
    if (async_tab_ui) {
      enable_class_deps_output = "tabUiConstantPoolDeps.txt"
    }
  }
}
