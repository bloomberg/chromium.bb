# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//build/config/android/rules.gni")
import("//chrome/android/modules/buildflags.gni")

android_library("java") {
  deps = [
    ":base_module_java",
    "//base:base_java",
    "//chrome/android/features/test_dummy/public:java",
    "//third_party/android_deps:android_support_v7_appcompat_java",
    "//third_party/android_deps:androidx_annotation_annotation_java",
  ]
  java_files =
      [ "java/src/org/chromium/chrome/features/test_dummy/TestDummyImpl.java" ]
}

source_set("native") {
  sources = [
    "test_dummy.cc",
  ]

  deps = [
    "//base",
    "//chrome/android/features/test_dummy/public:native",
  ]
}

# Java code that should go into the base module. If this were a normal feature,
# this target would reside in the client code using the module. Since this is a
# test dummy module, it has no pre-existing client, and hence the target is
# squatting here for convenience. The same is true for the corresponding native
# target.
android_library("base_module_java") {
  deps = [
    "//base:base_java",
    "//chrome/android/modules/test_dummy/provider:java",
    "//third_party/android_deps:android_support_v7_appcompat_java",
  ]
  java_files = [
    "java/src/org/chromium/chrome/features/test_dummy/TestDummyActivity.java",
    "java/src/org/chromium/chrome/features/test_dummy/TestDummySupport.java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  deps += [ "//base:jni_java" ]
}

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "USE_NATIVE_MODULES=$use_native_modules" ]
}

source_set("base_module_native") {
  sources = [
    "test_dummy_client.cc",
  ]
  deps = [
    ":test_dummy_jni_headers",
    "//base",
  ]
  public_deps = [
    ":buildflags",
  ]
}

generate_jni("test_dummy_jni_headers") {
  sources = [
    "java/src/org/chromium/chrome/features/test_dummy/TestDummySupport.java",
  ]
}
