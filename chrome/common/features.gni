# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This is the GN version of //chrome/chrome_features.gypi.
# Please keep in sync!

import("//build/config/chrome_build.gni")
import("//build/config/chromecast_build.gni")

# Please keep features in alphabetical order.
declare_args() {
  # Whether the Java UI is being used (Java infobars and popups, Java native
  # settings and first run experience, sign-in etc.).
  # Default to true if compiling for android, but allow this being overriden
  # through the environment.
  android_java_ui = is_android

  # Enables support for background apps.
  enable_background = !is_ios && !is_android && !is_chromecast

  enable_one_click_signin =
      is_win || is_mac || (is_linux && !is_chromeos && !is_chromecast)

  # Google Now is disabled to prepare for its removal.
  # http://crbug.com/539674
  enable_google_now = false

  # 'Ok Google' hotwording is disabled by default. Set to true to enable. (This
  # will download a closed-source NaCl module at startup.) Chrome-branded
  # ChromeOS builds have this enabled by default.
  enable_hotwording = is_chrome_branded && is_chromeos

  # Set to true to bundle all the mash related mojo services into chrome.
  # Specify --mash to chrome to have chrome start the mash environment.
  enable_package_mash_services = is_chromeos

  # Enables vr shell.
  enable_vr_shell = false

  # Use vulcanized HTML/CSS/JS resources to speed up WebUI (chrome://)
  # pages. https://github.com/polymer/vulcanize
  use_vulcanize = true
}

chrome_grit_defines = [
  "enable_background=$enable_background",
  "enable_google_now=$enable_google_now",
  "enable_hotwording=$enable_hotwording",
  "use_vulcanize=$use_vulcanize",
]
