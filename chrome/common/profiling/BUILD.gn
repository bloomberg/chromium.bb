# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//mojo/public/tools/bindings/mojom.gni")

if (enable_oop_heap_profiling) {
  static_library("profiling") {
    sources = [
      "memlog_allocator_shim.cc",
      "memlog_allocator_shim.h",
      "memlog_client.cc",
      "memlog_client.h",
      "memlog_sender_pipe.h",
      "memlog_sender_pipe_posix.cc",
      "memlog_sender_pipe_posix.h",
      "memlog_sender_pipe_win.cc",
      "memlog_sender_pipe_win.h",
      "memlog_stream.cc",
      "memlog_stream.h",
      "profiling_constants.cc",
      "profiling_constants.h",
    ]

    public_deps = [
      ":interfaces",
    ]
    deps = [
      "//base",
      "//base:debugging_flags",
      "//base/allocator:features",
      "//chrome/common:constants",
      "//content/public/common",
      "//mojo/edk/system",
    ]
  }

  mojom("interfaces") {
    sources = [
      "constants.mojom",
      "memlog.mojom",
      "memlog_client.mojom",
    ]
    deps = [
      "//mojo/common:common_custom_types",
    ]
  }
} else {
  # Dummy group so this target can be unconditionally depended on.
  group("profiling") {
  }
}
