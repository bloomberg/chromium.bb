# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Runs the version processing script over the given template file to produce
# an output file. This is used for generating various forms of files that
# incorporate the product name and version.
#
# This template automatically includes VERSION, LASTCHANGE, and BRANDING. It
# automatically uses the template file .
# GYP parameterizes this template file but all current invocations use this
# same one. If in the future we need to set it, this should be added as an
# optional argument.
#
# In GYP this is a rule that runs once per ".ver" file. In GN this just
# processes one file per invocation of the template so you may have to have
# multiple targets.
#
# You must specify either sources or a template_file, or both.
#
# Parameters:
#   sources (optional):
#     List of file names to read. When converting a GYP target, this should
#     list the 'source' (see above) as well as any extra_variable_files.
#
#   output:
#     File name of file to write. In GYP this is unspecified and it will
#     make up a file name for you based on the input name, and tack on
#     "_version.rc" to the end. But in GN you need to specify the full name.
#
#   template_file (optional):
#     Template file to use (not a list). Defaults to
#     //chrome/app/chrome_version.rc.version if unspecified.
#
#   extra_args (optional):
#     Extra arguments to pass to version.py. Any "-f <filename>" args should
#     use sources instead.
#
#   visibility (optional)
#
# Example:
#   process_version("myversion") {
#     sources = [ "myfile.h.in" ]
#     output = "$target_gen_dir/myfile.h"
#     extra_args = ["-e", "FOO=42"]
#     extra_files = [ "foo/BRANDING" ]
#   }
template("process_version") {
  assert(defined(invoker.sources) || defined(invoker.template_file),
         "Either sources or template_file must be defined for $target_name")
  assert(defined(invoker.output), "Output must be defined for $target_name")

  action(target_name) {
    if (defined(invoker.visibility)) {
      visibility = invoker.visibility
    }
    script = "//build/util/version.py"

    lastchange_path = "//build/util/LASTCHANGE"
    version_path = "//chrome/VERSION"
    if (is_chrome_branded) {
      branding_path = "//chrome/app/theme/google_chrome/BRANDING"
    } else {
      branding_path = "//chrome/app/theme/chromium/BRANDING"
    }
    if (defined(invoker.template_file)) {
      template_path = invoker.template_file
    } else {
      template_path = "//chrome/app/chrome_version.rc.version"
    }

    inputs = [
      version_path,
      lastchange_path,
      branding_path,
      template_path,
    ]

    outputs = [
      invoker.output,
    ]

    args = []

    if (defined(invoker.sources)) {
      inputs += invoker.sources
      foreach(i, invoker.sources) {
        args += [
          "-f",
          rebase_path(i, root_build_dir),
        ]
      }
    }

    args += [
      "-f",
      rebase_path(version_path, root_build_dir),
      "-f",
      rebase_path(branding_path, root_build_dir),
      "-f",
      rebase_path(lastchange_path, root_build_dir),
    ]
    if (defined(invoker.extra_args)) {
      args += invoker.extra_args
    }
    args += [
      rebase_path(template_path, root_build_dir),
      rebase_path(invoker.output, root_build_dir),
    ]
  }
}
