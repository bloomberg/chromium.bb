# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/service_manifest.gni")

source_set("mash") {
  sources = [
    "mash_runner.cc",
    "mash_runner.h",
  ]
  deps = [
    ":catalog_cpp_source",
    "//base:i18n",
    "//components/tracing:startup_tracing",
    "//content/public/common",
    "//mash/package",
    "//mash/session/public/interfaces:constants",
    "//services/catalog/public/interfaces",
    "//services/service_manager",
    "//services/service_manager/background:lib",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp/standalone_service",
    "//services/service_manager/public/interfaces",
    "//services/service_manager/runner:init",
    "//services/service_manager/runner/common",
    "//services/service_manager/runner/host:lib",
    "//services/service_manager/standalone",
    "//url",
  ]

  if (is_chromeos) {
    sources += [
      "mash_crash_reporter_client.cc",
      "mash_crash_reporter_client.h",
    ]
    deps += [
      "//ash/resources",
      "//components/crash/content/app",
      "//components/crash/core/common",
      "//components/upload_list",
      "//components/version_info:generate_version_info",
    ]
  }

  if (is_linux && !is_android) {
    deps += [ "//components/font_service:lib" ]
  }
}

service_manifest("mash_manifest") {
  name = "chrome_mash"
  source = "chrome_mash_manifest.json"
  packaged_services = [
    "//ash/autoclick/mus:manifest",
    "//ash/touch_hud/mus:manifest",
    "//mash/catalog_viewer:manifest",
    "//mash/quick_launch:manifest",
    "//mash/session:manifest",
    "//mash/task_viewer:manifest",
    "//services/ui:manifest",
    "//services/ui/ime/test_ime_driver:manifest",
  ]

  if (is_chromeos) {
    packaged_services += [ "//ash/mus:manifest" ]
  }

  if (is_linux && !is_android) {
    packaged_services += [ "//components/font_service:manifest" ]
  }
}

catalog("catalog") {
  embedded_services = [ ":mash_manifest" ]
  catalog_deps = [ "//chrome/app:catalog" ]
}

catalog_cpp_source("catalog_cpp_source") {
  catalog = ":catalog"
  output_symbol_name = "kChromeMashCatalogContents"
}
