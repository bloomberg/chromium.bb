# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/nacl/config.gni")
import("//chrome/common/features.gni")
import("//components/nacl/features.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/service_manifest.gni")

assert(enable_package_mash_services)

source_set("embedded_services") {
  sources = [
    "embedded_services.cc",
    "embedded_services.h",
  ]

  deps = [
    ":chrome_mash_catalog",
    "//base:i18n",
    "//chrome/common:constants",
    "//components/tracing:startup_tracing",
    "//content/public/common",
    "//mash/catalog_viewer:lib",
    "//mash/catalog_viewer/public/interfaces:constants",
    "//mash/common",
    "//mash/quick_launch:lib",
    "//mash/quick_launch/public/interfaces:constants",
    "//mash/session:lib",
    "//mash/session/public/interfaces:constants",
    "//mash/task_viewer:lib",
    "//mash/task_viewer/public/interfaces:constants",
    "//services/catalog/public/interfaces",
    "//services/service_manager",
    "//services/service_manager/background:lib",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp/standalone_service",
    "//services/service_manager/public/interfaces",
    "//services/service_manager/runner:init",
    "//services/service_manager/runner/common",
    "//services/service_manager/runner/host:lib",
    "//services/service_manager/standalone",
    "//services/ui:lib",
    "//services/ui/ime/test_ime_driver:lib",
    "//services/ui/public/interfaces:constants",
    "//url",
  ]

  if (is_chromeos) {
    deps += [
      ":chrome_mus_catalog",
      "//ash/autoclick/mus:lib",
      "//ash/mus:lib",
      "//ash/resources",
      "//ash/touch_hud/mus:lib",
    ]
  }

  if (is_posix) {
    deps += [ "//chrome/app:shutdown_signal_handlers" ]
  }

  if (is_linux && !is_android) {
    deps += [ "//components/font_service:lib" ]
  }
}

catalog("catalog_common") {
  embedded_services = [
    "//ash/autoclick/mus:manifest",
    "//ash/touch_hud/mus:manifest",
    "//mash/catalog_viewer:manifest",
    "//mash/quick_launch:manifest",
    "//mash/session:manifest",
    "//mash/task_viewer:manifest",
    "//services/ui:manifest",
    "//services/ui/ime/test_ime_driver:manifest",
  ]
  standalone_services = [
    "//mash/example/views_examples:manifest",
    "//mash/simple_wm:manifest",
  ]

  if (is_chromeos) {
    embedded_services += [ "//ash/mus:manifest" ]
  }

  if (is_linux && !is_android) {
    embedded_services += [ "//components/font_service:manifest" ]
  }

  if (enable_nacl && is_chromeos) {
    embedded_services += [ "//components/nacl/loader:nacl_loader_manifest" ]
  }
}

catalog("catalog") {
  catalog_deps = [
    ":catalog_common",
    "//chrome/app:catalog_for_mash",
  ]
}

catalog_cpp_source("chrome_mash_catalog") {
  catalog = ":catalog"
  generated_function_name = "CreateChromeMashCatalog"
}

catalog("catalog_test") {
  catalog_deps = [
    ":catalog_common",
    "//chrome/app:catalog_for_tests_mash",
  ]
}

catalog_cpp_source("chrome_test_catalog") {
  catalog = ":catalog_test"
  generated_function_name = "CreateChromeTestCatalog"
}

if (is_chromeos) {
  catalog("catalog_mus") {
    catalog_deps = [ "//chrome/app:catalog" ]
    embedded_services = [
      # TODO(sky): verify if we need this.
      "//services/ui/ime/test_ime_driver:manifest",
    ]

    if (enable_nacl) {
      embedded_services += [ "//components/nacl/loader:nacl_loader_manifest" ]
    }
  }

  catalog_cpp_source("chrome_mus_catalog") {
    catalog = ":catalog_mus"
    generated_function_name = "CreateChromeMusCatalog"
  }
}
