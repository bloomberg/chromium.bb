# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

action("aggregate_vector_icons") {
  visibility = [ ":*" ]

  script = "//ui/gfx/vector_icons/aggregate_vector_icons.py"

  icons = [
    "autologin.icon",
    "bluetooth_connected.icon",
    "browser_tools.icon",
    "browser_tools_error.icon",
    "browser_tools_update.icon",
    "caret_down.1x.icon",
    "caret_down.icon",
    "caret_up.1x.icon",
    "caret_up.icon",
    "crashed_tab.icon",
    "credit_card.1x.icon",
    "credit_card.icon",
    "folder.1x.icon",
    "folder.icon",
    "folder_managed.1x.icon",
    "folder_managed.icon",
    "folder_supervised.1x.icon",
    "folder_supervised.icon",
    "incognito.1x.icon",
    "incognito.icon",
    "laptop.icon",
    "navigate_back.1x.icon",
    "navigate_back.icon",
    "navigate_forward.1x.icon",
    "navigate_forward.icon",
    "navigate_home.1x.icon",
    "navigate_home.icon",
    "navigate_reload.1x.icon",
    "navigate_reload.icon",
    "navigate_stop.1x.icon",
    "navigate_stop.icon",
    "overflow_chevron.1x.icon",
    "overflow_chevron.icon",
    "smartphone.icon",
    "tab.icon",
    "tab_audio.1x.icon",
    "tab_audio.icon",
    "tab_audio_muting.1x.icon",
    "tab_audio_muting.icon",
    "tab_bluetooth_connected.icon",
    "tab_close_hovered_pressed.1x.icon",
    "tab_close_hovered_pressed.icon",
    "tab_close_normal.1x.icon",
    "tab_close_normal.icon",
    "tab_media_capturing.icon",
    "tab_media_recording.icon",
    "tab_usb_connected.1x.icon",
    "tab_usb_connected.icon",
    "tablet.icon",
    "translate.icon",
    "zoom_minus.icon",
    "zoom_plus.icon",
  ]

  output_cc = "$target_gen_dir/vector_icons.cc"
  output_h = "$target_gen_dir/vector_icons.h"

  inputs = icons
  inputs += [
    "vector_icons.cc.template",
    "vector_icons.h.template",
  ]
  outputs = [
    output_cc,
    output_h,
  ]

  response_file_contents = rebase_path(icons, root_build_dir)

  args = [
    "--working_directory=" + rebase_path("./"),
    "--file_list={{response_file_name}}",
    "--output_cc=" + rebase_path(output_cc, root_build_dir),
    "--output_h=" + rebase_path(output_h, root_build_dir),
  ]
}

source_set("vector_icons") {
  sources = get_target_outputs(":aggregate_vector_icons")
  sources += [ "//ui/gfx/vector_icon_types.h" ]

  deps = [
    ":aggregate_vector_icons",
    "//base",
    "//skia",
  ]
}
