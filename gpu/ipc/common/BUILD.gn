# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//ui/ozone/ozone.gni")

group("common") {
  if (is_component_build) {
    public_deps = [
      "//gpu",
    ]
  } else {
    public_deps = [
      ":ipc_common_sources",
    ]
  }
}

group("command_buffer_traits") {
  if (is_component_build) {
    public_deps = [
      "//gpu",
    ]
  } else {
    public_deps = [
      ":command_buffer_traits_sources",
    ]
  }
}

source_set("command_buffer_traits_sources") {
  # External code should depend on this via
  # //gpu/ipc/common:command_buffer_traits above rather than depending on this
  # directly or the component build will break.
  visibility = [ "//gpu/*" ]

  sources = [
    "gpu_command_buffer_traits.cc",
    "gpu_command_buffer_traits.h",
    "gpu_command_buffer_traits_multi.h",
  ]

  configs += [
    "//gpu:gpu_implementation",
    "//third_party/khronos:khronos_headers",
  ]

  public_deps = [
    "//gpu/command_buffer/common:common_sources",
    "//ipc",
    "//ui/gfx/ipc",
  ]

  deps = [
    "//base",
    "//ui/gfx/ipc/buffer_types",
    "//ui/gfx/ipc/geometry",
  ]
}

source_set("ipc_common_sources") {
  # External code should depend on this via //gpu/ipc/common above rather than
  # depending on this directly or the component build will break.
  visibility = [ "//gpu/*" ]

  sources = [
    "command_buffer_id.h",
    "gpu_client_ids.h",
    "gpu_memory_buffer_impl.cc",
    "gpu_memory_buffer_impl.h",
    "gpu_memory_buffer_impl_shared_memory.cc",
    "gpu_memory_buffer_impl_shared_memory.h",
    "gpu_memory_buffer_support.cc",
    "gpu_memory_buffer_support.h",
    "gpu_message_generator.cc",
    "gpu_message_generator.h",
    "gpu_messages.h",
    "gpu_param_traits.cc",
    "gpu_param_traits.h",
    "gpu_param_traits_macros.h",
    "gpu_watchdog_timeout.h",
    "memory_stats.cc",
    "memory_stats.h",
  ]

  if (is_mac) {
    sources += [
      "gpu_memory_buffer_impl_io_surface.cc",
      "gpu_memory_buffer_impl_io_surface.h",
    ]
    libs = [ "IOSurface.framework" ]
  }
  if (is_win) {
    sources += [
      "gpu_memory_buffer_impl_dxgi.cc",
      "gpu_memory_buffer_impl_dxgi.h",
    ]
  }
  if (is_linux || use_ozone) {
    sources += [
      "gpu_memory_buffer_impl_native_pixmap.cc",
      "gpu_memory_buffer_impl_native_pixmap.h",
    ]
  }
  if (is_android) {
    sources += [
      "gpu_memory_buffer_impl_android_hardware_buffer.cc",
      "gpu_memory_buffer_impl_android_hardware_buffer.h",
    ]
    visibility += [ "//media/gpu:gpu" ]
  }

  configs += [
    "//gpu:gpu_implementation",
    "//third_party/khronos:khronos_headers",
  ]

  public_deps = [
    ":command_buffer_traits_sources",
    ":surface_handle_type",
    ":vulkan_ycbcr_info",
    "//ipc",
    "//url/ipc:url_ipc",
  ]

  deps = [
    "//base",
    "//components/viz/common:resource_format",
    "//gpu/command_buffer/common:common_sources",
    "//gpu/config:config_sources",
    "//ui/base",
    "//ui/gfx:color_space",
    "//ui/gfx/geometry",
    "//ui/gfx/ipc",
    "//ui/gfx/ipc/buffer_types",
    "//ui/gfx/ipc/color",
    "//ui/gfx/ipc/geometry",
    "//ui/gl",
  ]

  if (is_android) {
    sources += [
      "gpu_surface_lookup.cc",
      "gpu_surface_lookup.h",
      "gpu_surface_tracker.cc",
      "gpu_surface_tracker.h",
    ]
    libs = [ "android" ]
  }

  if (use_ozone) {
    deps += [ "//ui/ozone" ]
  }
}

if (is_android) {
  source_set("android_image_reader_utils") {
    sources = [
      "android/android_image_reader_utils.cc",
      "android/android_image_reader_utils.h",
    ]
    configs += [ "//gpu:gpu_implementation" ]
    deps = [
      "//base",
      "//ui/gl",
    ]
    visibility = [
      "//gpu/*",
      "//media/gpu:gpu",
    ]
  }
}

if (is_android) {
  component("android_texture_owner") {
    sources = [
      "android/image_reader_gl_owner.cc",
      "android/image_reader_gl_owner.h",
      "android/scoped_surface_request_conduit.cc",
      "android/scoped_surface_request_conduit.h",
      "android/surface_texture_gl_owner.cc",
      "android/surface_texture_gl_owner.h",
      "android/texture_owner.cc",
      "android/texture_owner.h",
    ]
    configs += [ "//gpu:gpu_implementation" ]
    deps = [
      ":android_image_reader_utils",
      "//base",
      "//gpu/command_buffer/service:gles2_sources",
      "//gpu/command_buffer/service:service_sources",
      "//ui/gl",
    ]
    libs = [ "android" ]
    visibility = [
      "//gpu/*",
      "//media/gpu:gpu",
      "//content/browser:browser",
    ]
  }
}

if (is_android) {
  jumbo_static_library("android_texture_owner_test_support") {
    testonly = true
    sources = [
      "android/mock_abstract_texture.cc",
      "android/mock_abstract_texture.h",
      "android/mock_texture_owner.cc",
      "android/mock_texture_owner.h",
    ]
    deps = [
      ":android_texture_owner",
      "//base/test:test_support",
      "//gpu:test_support",
      "//testing/gmock",
      "//testing/gtest",
      "//ui/gl",
      "//ui/gl/init",
    ]
  }
}

if (is_android) {
  source_set("android_texture_owner_unittests") {
    testonly = true
    sources = [
      "android/image_reader_gl_owner_unittest.cc",
      "android/surface_texture_gl_owner_unittest.cc",
    ]

    deps = [
      ":android_texture_owner",
      ":android_texture_owner_test_support",
      "//base/test:test_support",
      "//gpu:test_support",
      "//media/base:base",
      "//testing/gmock",
      "//testing/gtest",
      "//ui/gl",
      "//ui/gl/init",
    ]
  }
}

# Depend on this to use surface_handle.h without pulling in all of gpu ipc.
source_set("surface_handle_type") {
  public = [
    "surface_handle.h",
  ]

  public_deps = [
    "//ui/gfx:native_widget_types",
  ]
}

source_set("vulkan_ycbcr_info") {
  sources = [
    "vulkan_ycbcr_info.cc",
    "vulkan_ycbcr_info.h",
  ]
  deps = [
    "//base",
  ]
}

mojom("interfaces") {
  sources = [
    "capabilities.mojom",
    "context_result.mojom",
    "dx_diag_node.mojom",
    "gpu_extra_info.mojom",
    "gpu_feature_info.mojom",
    "gpu_info.mojom",
    "mailbox.mojom",
    "mailbox_holder.mojom",
    "memory_stats.mojom",
    "surface_handle.mojom",
    "sync_token.mojom",
    "vulkan_ycbcr_info.mojom",
  ]

  public_deps = [
    ":gpu_preferences_interface",
    "//mojo/public/mojom/base",
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
  ]
}

mojom("gpu_preferences_interface") {
  sources = [
    "gpu_preferences.mojom",
  ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//ui/gfx/mojom",
  ]

  enabled_features = []
  if (use_ozone) {
    enabled_features += [ "use_ozone" ]
  }
}

mojom("test_interfaces") {
  testonly = true
  sources = [
    "traits_test_service.mojom",
  ]

  public_deps = [
    ":gpu_preferences_interface",
    ":interfaces",
  ]
}

source_set("mojom_traits") {
  sources = [
    "context_result_mojom_traits.h",
    "mailbox_holder_mojom_traits.h",
    "mailbox_mojom_traits.h",
    "memory_stats_mojom_traits.h",
    "surface_handle_mojom_traits.h",
    "sync_token_mojom_traits.h",
  ]
  deps = [
    ":interfaces_shared_cpp_sources",
    ":surface_handle_type",
    ":vulkan_ycbcr_info",
    "//gpu/command_buffer/common",
    "//gpu/ipc/common",
    "//mojo/public/cpp/bindings:bindings",
  ]
  if (is_android) {
    sources += [ "vulkan_ycbcr_info_mojom_traits.h" ]
  }
}
