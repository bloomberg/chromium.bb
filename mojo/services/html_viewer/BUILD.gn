# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")
import("//third_party/mojo/src/mojo/public/mojo.gni")
import("//third_party/mojo/src/mojo/public/mojo_application.gni")

action("generate_blink_resource_map") {
  script = "//mojo/services/html_viewer/generate_blink_resource_map.py"
  args = [
    "--pak-file",
    rebase_path("$root_out_dir/gen/blink/public/resources/blink_resources.pak"),
    "--header",
    rebase_path("$target_gen_dir/blink_resource_map.h"),
    "--cpp",
    rebase_path("$target_gen_dir/blink_resource_map.cc"),
  ]
  outputs = [
    "$target_gen_dir/blink_resource_map.cc",
    "$target_gen_dir/blink_resource_map.h",
  ]
  public_deps = [
    "//third_party/WebKit/public:resources",
  ]
}

source_set("lib") {
  sources = [
    "$target_gen_dir/blink_resource_map.cc",
    "$target_gen_dir/blink_resource_map.h",
    "ax_provider_impl.cc",
    "ax_provider_impl.h",
    "blink_basic_type_converters.cc",
    "blink_basic_type_converters.h",
    "blink_input_events_type_converters.cc",
    "blink_input_events_type_converters.h",
    "blink_platform_impl.cc",
    "blink_platform_impl.h",
    "blink_resource_constants.h",
    "blink_url_request_type_converters.cc",
    "blink_url_request_type_converters.h",
    "discardable_memory_allocator.cc",
    "discardable_memory_allocator.h",
    "html_document.cc",
    "html_document.h",
    "mojo_blink_platform_impl.cc",
    "mojo_blink_platform_impl.h",
    "webclipboard_impl.cc",
    "webclipboard_impl.h",
    "webcookiejar_impl.cc",
    "webcookiejar_impl.h",
    "weblayertreeview_impl.cc",
    "weblayertreeview_impl.h",
    "webmediaplayer_factory.cc",
    "webmediaplayer_factory.h",
    "webmimeregistry_impl.cc",
    "webmimeregistry_impl.h",
    "webscheduler_impl.cc",
    "webscheduler_impl.h",
    "websockethandle_impl.cc",
    "websockethandle_impl.h",
    "webstoragenamespace_impl.cc",
    "webstoragenamespace_impl.h",
    "webthemeengine_impl.cc",
    "webthemeengine_impl.h",
    "webthread_impl.cc",
    "webthread_impl.h",
    "weburlloader_impl.cc",
    "weburlloader_impl.h",
  ]

  include_dirs = [ "third_party/WebKit" ]

  deps = [
    "//base",
    "//base/third_party/dynamic_annotations",
    "//cc",
    "//cc/blink",
    "//cc/surfaces",
    "//gin",
    "//media",
    "//media/blink",
    "//media/mojo",
    "//mojo/application",
    "//mojo/cc",
    "//mojo/common",
    "//mojo/converters/surfaces",
    "//mojo/services/network/public/cpp",
    "//mojo/services/network/public/interfaces",
    "//net",
    "//skia",
    "//third_party/mojo/src/mojo/public/c/system:for_shared_library",
    "//third_party/mojo/src/mojo/public/cpp/utility",
    "//third_party/mojo/src/mojo/public/interfaces/application",
    "//third_party/mojo_services/src/accessibility/public/interfaces",
    "//third_party/mojo_services/src/clipboard/public/interfaces",
    "//third_party/mojo_services/src/content_handler/public/interfaces",
    "//third_party/mojo_services/src/gpu/public/interfaces",
    "//third_party/mojo_services/src/input_events/public/interfaces",
    "//third_party/mojo_services/src/navigation/public/interfaces",
    "//third_party/mojo_services/src/surfaces/public/interfaces",
    "//ui/gfx/geometry",
    "//ui/native_theme",
    "//url",
  ]

  public_deps = [
    "//third_party/WebKit/public:blink",
    "//third_party/mojo/src/mojo/public/cpp/bindings",
    "//third_party/mojo_services/src/view_manager/public/cpp",
    ":generate_blink_resource_map",
  ]
}

if (is_android) {
  import("//build/config/android/rules.gni")

  java_library_path = "$target_out_dir/java_library.dex.jar"

  mojo_android_application("html_viewer") {
    input_so = "$root_out_dir/lib.stripped/libhtml_viewer_lib.so"
    input_dex_jar = java_library_path
  }

  shared_library("html_viewer_lib") {
    sources = [
      "android/android_hooks.cc",
      "html_viewer.cc",
    ]

    deps = [
      ":html_viewer_jni_headers",
      ":lib",
      "//base",
      "//ui/gfx:gfx_jni_headers",
    ]
  }

  generate_jni("html_viewer_jni_headers") {
    sources = [
      "android/java/org/chromium/html_viewer/Main.java",
    ]
    jni_package = "mojo/services/html_viewer"
  }

  android_library("html_viewer_java_classes") {
    java_files = [ "android/java/org/chromium/html_viewer/Main.java" ]

    deps = [
      "//base:base_java",
    ]
  }

  android_standalone_library("java_library") {
    dex_path = java_library_path

    deps = [
      ":html_viewer_java_classes",

      # TODO(sky): this is WAY more than we need. We really only need
      # DeviceDisplayInfo. Refactor to make this clearer.
      "//ui/android:ui_java",
    ]
  }
} else {
  mojo_native_application("html_viewer") {
    sources = [
      "html_viewer.cc",
    ]
    deps = [
      ":lib",
    ]
  }
}

test("tests") {
  output_name = "html_viewer_unittests"
  sources = [
    "ax_provider_impl_unittest.cc",
    "discardable_memory_allocator_unittest.cc",
  ]
  deps = [
    ":lib",
    "//base/test:run_all_unittests",
  ]
}
