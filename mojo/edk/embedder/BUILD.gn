# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("embedder") {
  # This isn't really a standalone target; it must be linked into the
  # mojo_system_impl component.
  visibility = [
    "//mojo/edk/system",
    "//components/nacl:nacl",
  ]

  sources = [
    "configuration.h",
    "embedder.cc",
    "embedder.h",
    "embedder_internal.h",
    "entrypoints.cc",

    # Test-only code:
    # TODO(vtl): It's a little unfortunate that these end up in the same
    # component as non-test-only code. In the static build, this code should
    # hopefully be dead-stripped.
    "test_embedder.cc",
    "test_embedder.h",
  ]

  defines = [
    "MOJO_SYSTEM_IMPL_IMPLEMENTATION",
    "MOJO_SYSTEM_IMPLEMENTATION",
  ]

  configs += [ "//mojo/edk/system:system_config" ]

  public_deps = [
    ":delegates",
    ":platform",
    "//mojo/public/cpp/system",
  ]

  deps = [
    "//base",
    "//crypto",
  ]
}

source_set("platform") {
  # This isn't really a standalone target; it must be linked into the
  # mojo_system_impl component.
  visibility = [
    ":embedder",
    "//mojo/edk/system",
  ]

  sources = [
    "platform_channel_pair.cc",
    "platform_channel_pair.h",
    "platform_channel_pair_posix.cc",
    "platform_channel_pair_win.cc",
    "platform_channel_utils_posix.cc",
    "platform_channel_utils_posix.h",
    "platform_handle.cc",
    "platform_handle.h",
    "platform_handle_utils.h",
    "platform_handle_utils_posix.cc",
    "platform_handle_utils_win.cc",
    "platform_handle_vector.h",
    "platform_shared_buffer.h",
    "platform_support.h",
    "scoped_platform_handle.h",
    "simple_platform_shared_buffer.cc",
    "simple_platform_shared_buffer.h",
    "simple_platform_shared_buffer_android.cc",
    "simple_platform_shared_buffer_posix.cc",
    "simple_platform_shared_buffer_win.cc",
    "simple_platform_support.cc",
    "simple_platform_support.h",
  ]

  defines = [ "MOJO_SYSTEM_IMPL_IMPLEMENTATION" ]

  configs += [ "//mojo/edk/system:system_config" ]

  public_deps = [
    "//mojo/public/cpp/system",
  ]

  deps = [
    "//base",
  ]

  if (is_android) {
    deps += [ "//third_party/ashmem" ]
  }
}

source_set("delegates") {
  # This isn't really a standalone target; it must be linked into the
  # mojo_system_impl component.
  visibility = [
    ":embedder",
    "//mojo/edk/system",
  ]

  sources = [
    "process_delegate.h",
  ]

  defines = [ "MOJO_SYSTEM_IMPL_IMPLEMENTATION" ]

  configs += [ "//mojo/edk/system:system_config" ]

  public_deps = [
    "//mojo/public/cpp/system",
  ]
}

# TODO(use_chrome_edk): remove "2"
source_set("embedder_unittests2") {
  testonly = true

  # TODO(use_chrome_edk): remove "2". Also enable this visibility check when we
  # figure out why it's failing just on Android.
  #visibility = [ "//mojo/edk/system:mojo_system_unittests2" ]

  sources = [
    "embedder_unittest.cc",
    "platform_channel_pair_posix_unittest.cc",
    "simple_platform_shared_buffer_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//mojo/message_pump",

    # TODO(use_chrome_edk): temporary since the Mojo wrapper primitives are
    # declared in third party only for now.
    "//mojo/edk/system:test_utils",
    "//mojo/edk/test:test_support",
    "//testing/gtest",
    "//third_party/mojo/src/mojo/edk/system",

    # TODO(use_chrome_edk): temporary since the Mojo wrapper primitives are
    # declared in third party only for now.
    "//third_party/mojo/src/mojo/edk/system",
  ]
}
