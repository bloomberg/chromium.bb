# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

# TODO(hansmuller): The organization of tests in this directory is weird:
#   * Really, js_unittests tests public stuff, so that should live in public
#     and be reworked as some sort of apptest.
#   * Both js_unittests and js_integration_tests should auto-generate their
#     tests somehow. The .cc files are just test runner stubs, including
#     explicit lists of .js files.

group("tests") {
  testonly = true
  deps = [
    ":mojo_js_integration_tests",
    ":mojo_js_unittests",
  ]
}

test("mojo_js_integration_tests") {
  deps = [
    ":js_to_cpp_bindings",
    "//base/test:test_support",
    "//gin:gin_test",
    "//mojo/common",
    "//mojo/edk/js",
    "//mojo/edk/test:run_all_unittests",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//mojo/public/js:bindings",
  ]

  sources = [
    "js_to_cpp_tests.cc",
  ]

  data = [
    "js_to_cpp_tests.js",
  ]

  configs += [ "//v8:external_startup_data" ]
}

mojom("js_to_cpp_bindings") {
  sources = [
    "js_to_cpp.mojom",
  ]
}

test("mojo_js_unittests") {
  deps = [
    "//base",
    "//gin:gin_test",
    "//mojo/edk/js",
    "//mojo/edk/test:run_all_unittests",
    "//mojo/edk/test:test_support",
    "//mojo/public/cpp/system",
    "//mojo/public/interfaces/bindings/tests:test_associated_interfaces",
    "//mojo/public/interfaces/bindings/tests:test_interfaces",
    "//mojo/public/js:tests",
  ]

  sources = [
    "//mojo/edk/js/handle_unittest.cc",
    "run_js_unittests.cc",
  ]
}
