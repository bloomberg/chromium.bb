# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

# GYP version: mojo.gyp:mojo_application_manager
component("application_manager") {
  output_name = "mojo_application_manager"
  sources = [
    "application_loader.cc",
    "application_loader.h",
    "application_manager.cc",
    "application_manager.h",
    "application_manager_export.h",
    "background_shell_application_loader.cc",
    "background_shell_application_loader.h",
  ]

  defines = [
    "MOJO_APPLICATION_MANAGER_IMPLEMENTATION",
  ]

  public_deps = [
    "//base",
    "//mojo/common",
    "//mojo/public/interfaces/application:application",
    "//mojo/services/public/interfaces/network:network",
    "//url",
  ]
  deps = [
    "//base/third_party/dynamic_annotations",
    "//net",
    "//url",
    "//mojo/environment:chromium",
    "//mojo/services/public/interfaces/content_handler:content_handler",
    "//mojo/system",
  ]
}

# GYP version: mojo.gyp:mojo_application_manager_unittests
test("mojo_application_manager_unittests") {
  sources = [
    "application_manager_unittest.cc",
    "background_shell_application_loader_unittest.cc",
  ]

  deps = [
    ":application_manager",
    ":test_bindings",
    "//base",
    "//mojo/application",
    "//mojo/common",
    "//mojo/common/test:run_all_unittests",
    "//mojo/environment:chromium",
    "//mojo/public/cpp/bindings",
    "//testing/gtest",
    "//url",
  ]
}

mojom("test_bindings") {
  sources = [ "test.mojom" ]
}
