# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

# GYP version: mojo.gyp:mojo_application_manager
component("application_manager") {
  output_name = "mojo_application_manager"
  sources = [
    "application_loader.cc",
    "application_loader.h",
    "application_manager.cc",
    "application_manager.h",
    "application_manager_export.h",
    "background_shell_application_loader.cc",
    "background_shell_application_loader.h",
  ]

  defines = [
    "MOJO_APPLICATION_MANAGER_IMPLEMENTATION",
  ]

  deps = [
    "//base",
    "//base/third_party/dynamic_annotations",
    "//net",
    "//url",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//mojo/public/interfaces/application:application",
    "//mojo/services/public/interfaces/content_handler:content_handler",
    "//mojo/services/public/interfaces/network:network",
    "//mojo/system",
  ]

  forward_dependent_configs_from = [
    "//mojo/public/interfaces/application:application",
  ]
}

# GYP version: mojo.gyp:mojo_application_manager_unittests
test("mojo_application_manager_unittests") {
  deps = [
    ":application_manager",
    ":test_bindings",
    "//base",
    "//testing/gtest",
    "//url",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//mojo/public/cpp/application:chromium",
    "//mojo/common/test:run_all_unittests",
    "//mojo/public/cpp/bindings",
  ]

  sources = [
    "application_manager_unittest.cc",
    "background_shell_application_loader_unittest.cc",
  ]
}

mojom("test_bindings") {
  sources = [ "test.mojom" ]
}
