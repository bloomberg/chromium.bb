# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("client") {
  # Disabled the source filters because there are _mac files that need to
  # be compiled on all platforms.
  set_sources_assignment_filter([])

  sources = [
    "audio_player.cc",
    "audio_player.h",
    "chromoting_client.cc",
    "chromoting_client.h",
    "chromoting_session.cc",
    "chromoting_session.h",
    "client_context.cc",
    "client_context.h",
    "client_telemetry_logger.cc",
    "client_telemetry_logger.h",
    "client_user_interface.h",
    "connect_to_host_info.cc",
    "connect_to_host_info.h",
    "cursor_shape_stub_proxy.cc",
    "cursor_shape_stub_proxy.h",
    "desktop_viewport.cc",
    "desktop_viewport.h",
    "direct_input_strategy.cc",
    "direct_input_strategy.h",
    "dual_buffer_frame_consumer.cc",
    "dual_buffer_frame_consumer.h",
    "empty_cursor_filter.cc",
    "empty_cursor_filter.h",
    "fling_animation.cc",
    "fling_animation.h",
    "fling_tracker.cc",
    "fling_tracker.h",
    "gesture_interpreter.cc",
    "gesture_interpreter.h",
    "host_experiment_sender.cc",
    "host_experiment_sender.h",
    "key_event_mapper.cc",
    "key_event_mapper.h",
    "native_device_keymap.cc",
    "native_device_keymap.h",
    "normalizing_input_filter_cros.cc",
    "normalizing_input_filter_cros.h",
    "normalizing_input_filter_mac.cc",
    "normalizing_input_filter_mac.h",
    "normalizing_input_filter_win.cc",
    "normalizing_input_filter_win.h",
    "queued_task_poster.cc",
    "queued_task_poster.h",
    "server_log_entry_client.cc",
    "server_log_entry_client.h",
    "software_video_renderer.cc",
    "software_video_renderer.h",
    "touch_input_scaler.cc",
    "touch_input_scaler.h",
    "view_matrix.cc",
    "view_matrix.h",
  ]
  set_sources_assignment_filter(sources_assignment_filter)

  configs += [
    "//build/config/compiler:wexit_time_destructors",
    "//remoting/build/config:version",
    "//remoting/build/config:enable_webrtc_remoting_client",
  ]

  deps = [
    "//remoting/base",
    "//remoting/codec:decoder",
    "//remoting/protocol",
    "//third_party/libyuv",
    "//third_party/webrtc/modules/desktop_capture:primitives",
    "//ui/events:dom_keycode_converter",
  ]

  libs = []

  if (is_nacl) {
    sources -= [
      "client_telemetry_logger.cc",
      "server_log_entry_client.cc",
    ]
  } else {
    sources += [
      "chromoting_client_runtime.cc",
      "chromoting_client_runtime.h",
    ]
  }
  if (is_android) {
    sources += [
      "audio_player_android.cc",
      "audio_player_android.h",
      "native_device_keymap_android.cc",
    ]
    sources -= [ "native_device_keymap.cc" ]
    libs += [
      "android",
      "OpenSLES",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  # Disabled the source filters because there are _mac files that need to
  # be compiled on all platforms.
  set_sources_assignment_filter([])
  sources = [
    "audio_player_unittest.cc",
    "chromoting_client_runtime_unittest.cc",
    "client_telemetry_logger_unittest.cc",
    "desktop_viewport_unittest.cc",
    "dual_buffer_frame_consumer_unittest.cc",
    "empty_cursor_filter_unittest.cc",
    "fling_animation_unittest.cc",
    "fling_animation_unittest.h",
    "key_event_mapper_unittest.cc",
    "normalizing_input_filter_cros_unittest.cc",
    "normalizing_input_filter_mac_unittest.cc",
    "normalizing_input_filter_win_unittest.cc",
    "queued_task_poster_unittest.cc",
    "server_log_entry_client_unittest.cc",
    "software_video_renderer_unittest.cc",
    "touch_input_scaler_unittest.cc",
  ]
  set_sources_assignment_filter(sources_assignment_filter)

  configs += [
    "//remoting/build/config:version",
    "//remoting/build/config:enable_webrtc_remoting_client",
  ]

  deps = [
    ":client",
    "//remoting/proto",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/webrtc/base:rtc_base_approved",
  ]
}
