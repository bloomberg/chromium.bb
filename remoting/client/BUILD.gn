# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/remoting_srcs.gni")

source_set("client") {
  sources = rebase_path(remoting_srcs_gypi_values.remoting_client_sources,
                        ".",
                        "//remoting")

  configs += [
    "//build/config/compiler:wexit_time_destructors",
    "//remoting:version",
  ]

  deps = [
    "//remoting/base",
    "//remoting/codec",
    "//remoting/protocol",
    "//third_party/libyuv",
    "//third_party/webrtc/modules/desktop_capture",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "audio_player_unittest.cc",
    "client_status_logger_unittest.cc",
    "key_event_mapper_unittest.cc",
    "plugin/empty_cursor_filter_unittest.cc",
    "plugin/normalizing_input_filter_mac_unittest.cc",
    "server_log_entry_client_unittest.cc",
  ]

  if (is_chromeos) {
    sources += [ "plugin/normalizing_input_filter_cros_unittest.cc" ]
  }

  deps = [
    ":client",
    "//remoting/proto",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/webrtc",
  ]
}
