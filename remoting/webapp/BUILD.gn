# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Keep in sync with targets in remoting/remoting_client.gypi.

import("//build/config/features.gni")
import("//remoting/webapp/build_template.gni")

group("webapp") {
  deps = [
    ":webapp_v1",
    ":ar_sample_app",
    ":credits",
  ]

  if (enable_nacl) {
    deps += [ ":webapp_v2" ]
  }
}

desktop_remoting_webapp("webapp_v1") {
  webapp_type = "v1"
  output_dir = "remoting/remoting-webapp"
  zip_path = "remoting/remoting-webapp.zip"
  extra_files = []
}

desktop_remoting_webapp("webapp_v2") {
  webapp_type = "v2_pnacl"
  output_dir = "remoting/remoting-webapp.v2"
  zip_path = "remoting/remoting-webapp.v2.zip"
  extra_files = [
    "crd/remoting_client_pnacl.nmf.jinja2",
    # TODO(garykac): Get correct path to this.
    #"<(PRODUCT_DIR)/remoting_client_plugin_newlib.pexe",
  ]
}

app_remoting_webapp("ar_sample_app") {
  app_key = "Sample_App"
  app_id = "ljacajndfccfgnfohlgkdphmbnpkjflk"
  app_client_id = "sample_client_id"
  app_name = "App Remoting Client"
  app_description = "App Remoting client"
  app_capabilities = [ "GOOGLE_DRIVE" ]
  manifest_key = "remotingdevbuild"
}

# GYP version: remoting/remoting_client:remoting_credits
action("credits") {
  about_credits_file = "$target_gen_dir/credits.html"
  script = "//tools/licenses.py"

  inputs = [
    "base/html/credits.tmpl",
    "base/html/credits_entry.tmpl",
  ]

  outputs = [
    about_credits_file,
  ]

  args = [
    "credits",
    rebase_path(about_credits_file, root_build_dir),
    "--file-template",
    rebase_path("base/html/credits.tmpl", root_build_dir),
    "--entry-template",
    rebase_path("base/html/credits_entry.tmpl", root_build_dir),
  ]
}
