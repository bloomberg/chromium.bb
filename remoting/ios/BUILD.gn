# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/build/config/remoting_build.gni")

group("all") {
  testonly = true

  deps = [
    ":ios_core",
    "./app:all",
  ]
}

group("all_test") {
  testonly = true

  deps = [
    ":ios_core_unit_tests",
  ]
}

source_set("ios_core") {
  sources = [
    "client_gestures.h",
    "client_gestures.mm",
    "host_preferences.h",
    "host_preferences.mm",
    "host_preferences_persistence.h",
    "key_input.h",
    "key_input.mm",
    "key_map_us.h",
    "keychain_wrapper.h",
    "keychain_wrapper.mm",
  ]

  if (!is_chrome_branded) {
    sources += [ "host_preferences_persistence_chromium.mm" ]
  } else {
    # TODO(nicholss): Until we have a private implementation of this, stub it
    # with the chromium version. We still want the logic of is_chrome_branded
    # but to get the release builds building we will just reuse the file.
    sources += [ "host_preferences_persistence_chromium.mm" ]
  }

  public_deps = [
    "//remoting/ios/domain",
    "//remoting/ios/facade",
    "//remoting/ios/session",
    "//third_party/webrtc/base:rtc_base",
    "//third_party/webrtc/modules/desktop_capture:primitives",
  ]

  deps = [
    "//base",
    "//remoting/protocol",
  ]

  libs = [
    "CoreGraphics.framework",
    "GLKit.framework",
    "OpenGLES.framework",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("ios_core_unit_tests") {
  testonly = true

  include_dirs = [
    "$root_gen_dir",
    "//third_party/protobuf/src",
  ]

  sources = [
    "key_input_unittest.mm",
  ]

  deps = [
    ":ios_core",
    "//testing/gtest",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  data = []
}
