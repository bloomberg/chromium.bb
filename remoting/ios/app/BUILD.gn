# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/ios/app/remoting_ios_tmpl.gni")

group("all") {
  testonly = true

  deps = [
    ":ios_remoting_app",
  ]
}

# source set to be used by both external and internal app.
source_set("common_source_set") {
  sources = [
    "app_delegate.h",
    "app_delegate.mm",
    "client_connection_view_controller.h",
    "client_connection_view_controller.mm",
    "host_collection_header_view.h",
    "host_collection_header_view.mm",
    "host_collection_view_cell.h",
    "host_collection_view_cell.mm",
    "host_collection_view_controller.h",
    "host_collection_view_controller.mm",
    "host_view_controller.h",
    "host_view_controller.mm",
    "pin_entry_view.h",
    "pin_entry_view.mm",
    "remoting_menu_view_controller.h",
    "remoting_menu_view_controller.mm",
    "remoting_theme.h",
    "remoting_theme.mm",
    "remoting_view_controller.h",
    "remoting_view_controller.mm",
  ]

  deps = [
    "//base",
    "//google_apis",
    "//remoting/base",
    "//remoting/client",
    "//remoting/client/input",
    "//remoting/ios:ios_core",
    "//remoting/ios/app/settings",
    "//remoting/ios/mdc",
    "//remoting/protocol",
    "//ui/base",
    "//ui/gfx",
    "//ui/resources",
  ]

  public_deps = [
    "//ios/third_party/material_components_ios",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("main") {
  sources = [
    "main.mm",
  ]
  deps = [
    ":common_source_set",
    "//remoting/ios/app/resources:system_icons",
  ]
  configs += [ "//build/config/compiler:enable_arc" ]
}

ios_remoting_app_tmpl("ios_remoting_app") {
  output_name = "chromoting"
  entitlements_path = "resources/Remoting.entitlements"
  remoting_source_set = ":main"
}
