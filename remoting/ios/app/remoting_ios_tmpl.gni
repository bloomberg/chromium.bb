# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/ios/rules.gni")
import("//build/mac/tweak_info_plist.gni")
import("//build/util/process_version.gni")
import("//remoting/build/config/remoting_build.gni")

remoting_ios_app_source_dir = get_path_info("./", "abspath")

# Arguments
#
#   output_name:
#     string, the filename of the generated .app folder
#
#   entitlements_path:
#     string, ath of the .entitlements file
#
#   remoting_source_set:
#     The source set to be compiled
#
#   bundle_id:
#     (optional) string, the bundle_id. If this is not set, it will come from
#     either branding_Chromium or branding_Chrome
template("ios_remoting_app_tmpl") {
  info_plist_target_name = "${target_name}_tweak_info_plist"
  tweak_info_plist(info_plist_target_name) {
    info_plist = "//remoting/ios/app/resources/Info.plist"
    args = [ "--platform=ios" ]
  }

  launchscreen_storyboard_target_name = "${target_name}_launchscreen_storyboard"
  bundle_data_ib_file(launchscreen_storyboard_target_name) {
    source = rebase_path("resources/LaunchScreen.storyboard",
                         ".",
                         remoting_ios_app_source_dir)
  }

  ios_app_bundle(target_name) {
    output_name = invoker.output_name

    entitlements_path = invoker.entitlements_path
    info_plist_target = ":$info_plist_target_name"

    if (defined(invoker.bundle_id)) {
      bundle_id = invoker.bundle_id
    } else {
      bundle_id = remoting_ios_bundle_id
    }

    extra_substitutions = [
      "BUNDLE_IDENTIFIER=$bundle_id",
      "DISPLAY_NAME=$remoting_ios_display_name",
      "EXECUTABLE_NAME=$remoting_ios_executable_name",
      "MINIMUM_OS_VERSION=7.0",
      "PRODUCT_NAME=$remoting_ios_product_name",
      "VERSION_FULL=$remoting_version_full",
      "VERSION_SHORT=$remoting_version_short",
    ]

    libs = [
      "Accelerate.framework",
      "AudioToolbox.framework",
      "CoreAudio.framework",
      "CoreData.framework",
      "CoreMIDI.framework",
      "CoreVideo.framework",
      "GLKit.framework",
      "OpenGLES.framework",
      "Webkit.framework",
      "SafariServices.framework",
      "SystemConfiguration.framework",
    ]

    deps = [
      "//base",
      "//remoting/ios/app/resources:assets",
      "//remoting/ios/app/resources:launchscreen_assets",
      "//remoting/ios/app/resources:remoting_icons",
      "//remoting/ios/display",
    ]

    deps += [ invoker.remoting_source_set ]

    bundle_deps = [ ":$launchscreen_storyboard_target_name" ]
  }
}
