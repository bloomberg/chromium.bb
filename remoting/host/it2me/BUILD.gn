# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//remoting/remoting_enable.gni")
import("//remoting/remoting_srcs.gni")

source_set("common") {
  sources = rebase_path(
          remoting_host_srcs_gypi_values.remoting_it2me_host_static_sources,
          ".",
          "//remoting")

  configs += [
    "//build/config/compiler:wexit_time_destructors",
    "//remoting:version",
  ]

  deps = [
    "//base:i18n",
    "//net:net",
    "//remoting/base",
    "//remoting/host",
    "//remoting/protocol",
    "//remoting/resources",
  ]
}

if (!is_chromeos && !is_android && enable_remoting_host) {
  if (is_win) {
    # GYP version:
    # //remoting/remoting_host_win.gypi:remoting_it2me_native_messaging_host
    executable("remote_assistance_host") {
      configs += [ "//build/config/compiler:wexit_time_destructors" ]

      deps = [
        "//base/allocator",
        "//remoting/host:remoting_core",
        "//remoting/host:remoting_windows_resources",
      ]

      sources = [
        "$root_gen_dir/remoting/version.rc",
        "it2me_native_messaging_host_entry_point.cc",
      ]

      defines = [ "BINARY=BINARY_REMOTE_ASSISTANCE_HOST" ]

      ldflags = [
        "/MANIFEST:EMBED",
        "/MANIFESTINPUT:" +
            rebase_path("../win/common-controls.manifest", root_build_dir),
        "/MANIFESTINPUT:" +
            rebase_path("../win/dpi_aware.manifest", root_build_dir),

        # "/NODEFAULTLIB", TODO(zijiehe): Why IgnoreAllDefaultLibraries: true in
        # GYP does not take effect?
        "comctl32.lib",
      ]
    }
  } else {
    executable("remote_assistance_host") {
      sources = [
        "it2me_native_messaging_host_entry_point.cc",
        "it2me_native_messaging_host_main.cc",
        "it2me_native_messaging_host_main.h",
      ]

      configs += [
        "//build/config/compiler:wexit_time_destructors",
        "//remoting:version",
      ]

      deps = [
        ":common",
        "//build/config/sanitizers:deps",
        "//remoting/host",
        "//remoting/host/native_messaging",
        "//remoting/proto",
        "//ui/gfx",
      ]

      if (enable_webrtc) {
        deps += [ "//third_party/libjingle:libjingle_webrtc" ]
      }

      if (is_desktop_linux) {
        deps += [ "//build/config/linux/gtk2" ]
      }
    }
  }
}
