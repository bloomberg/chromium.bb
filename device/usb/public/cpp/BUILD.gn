# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_ids = "//third_party/usb_ids/usb.ids"
generated_ids = "$target_gen_dir/usb_ids_gen.cc"

action("usb_device_ids") {
  script = "//device/usb/public/tools/usb_ids.py"
  inputs = [
    source_ids,
  ]
  outputs = [
    generated_ids,
  ]
  args = [
    "-i",
    rebase_path(source_ids, root_build_dir),
    "-o",
    rebase_path(generated_ids, root_build_dir),
  ]

  # Only the device_usb_public_cpp target can depend on us.
  visibility = [ ":cpp" ]
}

source_set("cpp") {
  sources = [
    "usb_ids.cc",
    "usb_ids.h",
    "usb_utils.cc",
    "usb_utils.h",
    generated_ids,
  ]

  deps = [
    ":usb_device_ids",
    "//device/usb/public/mojom",
  ]
}

static_library("test_support") {
  testonly = true

  sources = [
    "fake_usb_device.cc",
    "fake_usb_device.h",
    "fake_usb_device_info.cc",
    "fake_usb_device_info.h",
    "fake_usb_device_manager.cc",
    "fake_usb_device_manager.h",
    "mock_usb_mojo_device.cc",
    "mock_usb_mojo_device.h",
  ]

  deps = [
    ":cpp",
  ]

  public_deps = [
    "//base",
    "//device/usb/public/mojom",
    "//testing/gmock",
    "//url:url",
  ]
}
