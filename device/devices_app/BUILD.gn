# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")

source_set("lib") {
  sources = [
    "devices_app.cc",
    "devices_app.h",
    "usb/device_impl.cc",
    "usb/device_impl.h",
    "usb/device_manager_impl.cc",
    "usb/device_manager_impl.h",
    "usb/type_converters.cc",
    "usb/type_converters.h",
  ]

  deps = [
    "//device/core",
    "//device/devices_app/usb/public/interfaces",
    "//device/usb",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/bindings:callback",
    "//net",
    "//url",
  ]

  public_deps = [
    "//base",
    "//mojo/application/public/cpp",
    "//mojo/application/public/interfaces",
  ]
}

if (!is_component_build) {
  mojo_native_application("devices") {
    sources = [
      "main.cc",
    ]

    deps = [
      "//base",
      "//device/devices_app/public/cpp:factory",
      "//mojo/application/public/cpp",
    ]

    public_deps = [
      ":lib",
    ]
  }

  mojo_native_application("apptests") {
    output_name = "devices_apptests"

    testonly = true

    sources = [
      "devices_apptest.cc",
    ]

    deps = [
      "//base",
      "//mojo/application/public/cpp:test_support",
    ]

    public_deps = [
      ":lib",
      "//device/devices_app/usb/public/interfaces",
    ]

    data_deps = [
      ":devices",
    ]
  }
}
