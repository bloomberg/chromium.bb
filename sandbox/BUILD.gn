# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//sandbox/features.gni")

# Several targets want to include this header file. We separate it out
# here so multiple targets can depend on it.
source_set("sandbox_export") {
  sources = [
    "sandbox_export.h",
  ]
}

# Meta-target that forwards to the proper platform one.
group("sandbox") {
  if (is_win) {
    public_deps = [
      "//sandbox/win:sandbox",
    ]
  } else if (is_mac) {
    public_deps = [
      "//sandbox/mac:sandbox",
      "//sandbox/mac:seatbelt",
    ]
  } else if (is_linux || is_android) {
    public_deps = [
      "//sandbox/linux:sandbox",
    ]
  }
}

buildflag_header("sandbox_features") {
  header = "sandbox_features.h"
  flags = [ "USE_SECCOMP_BPF=$use_seccomp_bpf" ]
}
