# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")
import("//build/config/ui.gni")

jumbo_component("clipboard_types") {
  output_name = "ui_base_clipboard_types"
  sources = [
    "clipboard_constants.cc",
    "clipboard_constants.h",
    "clipboard_constants_mac.mm",
    "clipboard_format_type_android.cc",
    "clipboard_format_type_mac.mm",
    "clipboard_format_type_win.cc",
    "clipboard_types.h",
  ]

  if (!is_ios) {
    if (use_aura) {
      if ((use_x11 && is_desktop_linux) || !is_win) {
        sources += [ "clipboard_format_type_aura.cc" ]
      }
    }

    sources += [ "clipboard_format_type.h" ]
  }

  defines = [ "IS_BASE_CLIPBOARD_TYPES_IMPL" ]

  deps = [
    "//base",
  ]

  libs = []
  if (is_mac) {
    libs += [
      "AppKit.framework",
      "CoreFoundation.framework",
    ]
  }
}

jumbo_component("clipboard") {
  output_name = "ui_base_clipboard"

  sources = [
    "clipboard.cc",
    "clipboard.h",
    "clipboard_android.cc",
    "clipboard_android.h",
    "clipboard_mac.h",
    "clipboard_mac.mm",
    "clipboard_monitor.cc",
    "clipboard_monitor.h",
    "clipboard_observer.h",
    "clipboard_util_mac.h",
    "clipboard_util_mac.mm",
    "clipboard_util_win.cc",
    "clipboard_util_win.h",
    "clipboard_win.cc",
    "clipboard_win.h",
    "custom_data_helper.cc",
    "custom_data_helper.h",
    "scoped_clipboard_writer.cc",
    "scoped_clipboard_writer.h",
  ]

  defines = [ "IS_BASE_CLIPBOARD_IMPL" ]

  deps = [
    "//base",
    "//net",
    "//skia",
    "//ui/events/platform",
    "//ui/gfx",
    "//ui/gfx/geometry",
  ]

  public_deps = [
    ":clipboard_types",
  ]

  if (use_aura) {
    # Aura clipboard.
    # Chromecast uses clipboard_aura now.
    if (is_desktop_linux && !is_chromecast) {
      if (use_ozone) {
        sources += [
          "clipboard_ozone.cc",
          "clipboard_ozone.h",
        ]
      } else if (use_x11) {
        sources += [
          "clipboard_aurax11.cc",
          "clipboard_aurax11.h",
        ]
        configs += [ "//build/config/linux:x11" ]
        deps += [
          "//ui/base",
          "//ui/base/x",
          "//ui/gfx/x",
        ]
      }
    } else if (!is_win) {
      # This file is used for all non-X11, non-Windows aura Builds.
      sources += [
        "clipboard_aura.cc",
        "clipboard_aura.h",
      ]
    }
  }

  if (is_android) {
    deps += [ "//ui/base:ui_base_jni_headers" ]
  }

  if (is_mac) {
    deps += [ "//third_party/mozilla" ]
    libs = [
      "AppKit.framework",
      "CoreFoundation.framework",
    ]
  }

  if (use_ozone) {
    deps += [ "//ui/ozone" ]
  }
}

jumbo_source_set("clipboard_test_support") {
  testonly = true

  if (!is_ios) {
    sources = [
      "test/test_clipboard.cc",
      "test/test_clipboard.h",
    ]
  }

  public_deps = [
    ":clipboard",
    "//base",
    "//skia",
  ]
}

source_set("clipboard_test") {
  testonly = true

  output_name = "ui_base_clipboard_test"
  sources = [
    "clipboard_mac_unittest.mm",
    "clipboard_util_mac_unittest.mm",
  ]

  if (!is_ios) {
    sources += [
      "custom_data_helper_unittest.cc",
      "test/test_clipboard_unittest.cc",
    ]
  }

  deps = [
    ":clipboard_test_support",
    "//base/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/events/platform",
    "//ui/gfx:test_support",
  ]

  if (is_mac) {
    deps += [ "//third_party/mozilla" ]
  }
}
