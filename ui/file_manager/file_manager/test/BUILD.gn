# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")

action("create_test_main") {
  testonly = true
  script = "//ui/file_manager/file_manager/test/scripts/create_test_main.py"
  output = "$target_gen_dir/../test.html"
  sources = [
    "../../../webui/resources/css/text_defaults.css",
    "../background/js/background_common_scripts.js",
    "../background/js/background_scripts.js",
    "../foreground/elements/elements_bundle.html",
    "../foreground/elements/files_quick_view.html",
    "../foreground/elements/files_safe_media.html",
    "../foreground/elements/files_safe_media.js",
    "../foreground/js/elements_importer.js",
    "../foreground/js/main_scripts.js",
    "../main.html",
    "check_select.js",
    "crostini.js",
    "quick_view.js",
    "uma.js",
  ]
  args = [
    "--output=" + rebase_path(output, root_build_dir),
    "--file_manager_strings=" +
        rebase_path("$root_out_dir/file_manager_strings"),
  ]
  outputs = [
    output,
    "$target_gen_dir/gen/css/text_defaults.css",
    "$target_gen_dir/gen/elements/elements_bundle.html",
    "$target_gen_dir/gen/elements/files_quick_view.html",
    "$target_gen_dir/gen/elements/files_safe_media.html",
    "$target_gen_dir/gen/elements/files_safe_media.js",
    "$target_gen_dir/gen/js/elements_importer.js",
  ]
  deps = [
    "//chrome/browser/chromeos/extensions/file_manager/test:file_manager_strings",
  ]
}

js_type_check("closure_compile") {
  deps = [
    ":check_select",
    ":closure_compile_externs",
    ":crostini",
    ":quick_view",
    ":uma",
  ]
}

js_library("closure_compile_externs") {
  sources = []
  externs_list = [
    "js/externs.js",
    "$externs_path/command_line_private.js",
    "$externs_path/metrics_private.js",
    "../../externs/app_window_common.js",
    "../../externs/background/file_browser_background.js",
    "../../externs/entry_location.js",
    "../../externs/volume_info.js",
    "../../externs/volume_info_list.js",
    "../../externs/volume_manager.js",
    "//third_party/analytics/externs.js",
  ]
}

js_library("check_select") {
  deps = [
    "js:test_util",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("crostini") {
  deps = [
    "js:test_util",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("quick_view") {
  deps = [
    "js:test_util",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}

js_library("uma") {
  deps = [
    "js:test_util",
    "//ui/webui/resources/js:webui_resource_test",
  ]
}
