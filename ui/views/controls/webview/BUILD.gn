# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")

# Reset sources_assignment_filter for the BUILD.gn file to prevent
# regression during the migration of Chromium away from the feature.
# See docs/no_sources_assignment_filter.md for more information.
# TODO(crbug.com/1018739): remove this when migration is done.
set_sources_assignment_filter([])

jumbo_component("webview") {
  sources = [
    "unhandled_keyboard_event_handler.cc",
    "unhandled_keyboard_event_handler.h",
    "web_contents_set_background_color.cc",
    "web_contents_set_background_color.h",
    "web_dialog_view.cc",
    "web_dialog_view.h",
    "webview.cc",
    "webview.h",
    "webview_export.h",
  ]

  if (is_mac) {
    sources += [ "unhandled_keyboard_event_handler_mac.mm" ]
  }

  if (is_win) {
    sources += [ "unhandled_keyboard_event_handler_win.cc" ]
  }

  defines = [ "WEBVIEW_IMPLEMENTATION" ]

  if (is_mac) {
    libs = [ "CoreFoundation.framework" ]
  }

  deps = [
    "//base:i18n",
    "//base/third_party/dynamic_annotations",
    "//ipc",
    "//skia",
    "//ui/accessibility",
    "//ui/base",
    "//ui/content_accelerators",
    "//ui/events",
    "//ui/events:events_base",
    "//ui/web_dialogs",
    "//url",
  ]

  public_deps = [
    "//base",
    "//content/public/browser",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/views",
  ]

  if (is_linux || is_android || is_fuchsia) {
    sources += [ "unhandled_keyboard_event_handler_default.cc" ]
  }
}

static_library("test_support") {
  testonly = true
  sources = [
    "../../test/webview_test_helper.cc",
    "../../test/webview_test_helper.h",
  ]

  public_deps = [ ":webview" ]
  deps = [
    "//base",
    "//content",
    "//content/test:test_support",
    "//ipc:test_support",
    "//skia",
    "//testing/gtest",
    "//ui/base",
    "//ui/events",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/views",
    "//ui/views:test_support",
  ]
}
