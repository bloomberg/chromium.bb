# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

config("views_bridge_mac_warnings") {
  if (is_mac) {
    # TODO(thakis): Remove this once http://crbug.com/383820 is figured out
    cflags = [ "-Wno-nonnull" ]
  }
}

component("views_bridge_mac") {
  assert(is_mac)

  configs += [ ":views_bridge_mac_warnings" ]
  sources = [
    "alert.h",
    "alert.mm",
    "bridged_native_widget_host_helper.h",
    "cocoa_mouse_capture.h",
    "cocoa_mouse_capture.mm",
    "cocoa_mouse_capture_delegate.h",
    "drag_drop_client.h",
    "views_bridge_mac_export.h",
  ]
  defines = [ "VIEWS_BRIDGE_MAC_IMPLEMENTATION" ]
  deps = [
    ":mojo",
    "//base",
    "//base:i18n",
    "//mojo/public/cpp/bindings",
    "//ui/accelerated_widget_mac",
    "//ui/base",
    "//ui/events",
    "//ui/gfx",
  ]
  libs = [ "Cocoa.framework" ]
}

mojom("mojo") {
  assert(is_mac)

  sources = [
    "mojo/alert.mojom",
    "mojo/bridge_factory.mojom",
    "mojo/bridged_native_widget.mojom",
    "mojo/bridged_native_widget_host.mojom",
    "mojo/text_input_host.mojom",
  ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//ui/base/accelerators/mojo:interfaces",
    "//ui/base/mojo",
    "//ui/display/mojo:interfaces",
    "//ui/events/mojo:interfaces",
    "//ui/gfx/geometry/mojo",
    "//ui/gfx/mojo",
    "//ui/gfx/range/mojo",
  ]
}
