# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//testing/test.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

component("touch_selection") {
  output_name = "ui_touch_selection"

  sources = [
    "selection_event_type.h",
    "touch_handle.cc",
    "touch_handle.h",
    "touch_handle_orientation.h",
    "touch_selection_controller.cc",
    "touch_selection_controller.h",
    "ui_touch_selection_export.h",
  ]

  defines = [ "UI_TOUCH_SELECTION_IMPLEMENTATION" ]

  deps = [
    "//base:base",
    "//ui/base:base",
    "//ui/events:events",
    "//ui/events:gesture_detection",
    "//ui/gfx/geometry:geometry",
  ]
}

test("ui_touch_selection_unittests") {
  sources = [
    "touch_handle_unittest.cc",
    "touch_selection_controller_unittest.cc",
  ]

  deps = [
    ":touch_selection",
    "//base/test:run_all_unittests",
    "//testing/gmock:gmock",
    "//testing/gtest:gtest",
    "//ui/base:base",
    "//ui/events:test_support",
    "//ui/gfx:gfx",
    "//ui/gfx:test_support",
  ]
}

if (is_android) {
  java_cpp_enum("ui_touch_selection_enums_srcjar") {
    sources = [
      "selection_event_type.h",
    ]
    outputs = [
      "org/chromium/ui/touch_selection/SelectionEventType.java",
    ]
  }
  java_cpp_enum("ui_touch_handle_orientation_srcjar") {
    sources = [
      "touch_handle_orientation.h",
    ]
    outputs = [
      "org/chromium/ui/touch_selection/TouchHandleOrientation.java",
    ]
  }
}
