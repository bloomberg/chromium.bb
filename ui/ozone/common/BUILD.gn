# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//ui/ozone/ozone.gni")

assert(use_ozone)

source_set("common") {
  sources = [
    "egl_util.cc",
    "egl_util.h",
    "gl_ozone_egl.cc",
    "gl_ozone_egl.h",
    "gl_ozone_osmesa.cc",
    "gl_ozone_osmesa.h",
    "gl_surface_egl_readback.cc",
    "gl_surface_egl_readback.h",
    "gpu/ozone_gpu_message_generator.cc",
    "gpu/ozone_gpu_message_generator.h",
    "gpu/ozone_gpu_message_params.cc",
    "gpu/ozone_gpu_message_params.h",
    "gpu/ozone_gpu_messages.h",
    "stub_overlay_manager.cc",
    "stub_overlay_manager.h",
  ]

  public_deps = [
    "//ui/ozone:ozone_base",
  ]

  deps = [
    "//ui/gfx/ipc/color",
    "//ui/gl",
    "//ui/gl:gl_features",
  ]

  data_deps = [
    "//third_party/mesa:osmesa",
  ]

  visibility = [ "//ui/ozone/platform/*" ]
}

if (ozone_platform_gbm) {
  source_set("linux") {
    assert(ozone_platform_gbm)

    sources = [
      "linux/drm_util_linux.cc",
      "linux/drm_util_linux.h",
      "linux/gbm_device_linux.cc",
      "linux/gbm_device_linux.h",
      "linux/overlay_plane.cc",
      "linux/overlay_plane.h",
      "linux/scanout_buffer.h",
    ]

    deps = [
      "//base:base",
      "//third_party/libdrm",
      "//third_party/minigbm",
      "//ui/gfx:buffer_types",
      "//ui/gfx:memory_buffer",
      "//ui/gfx/geometry:geometry",
    ]

    visibility = [ "//ui/ozone/platform/drm/*" ]
  }
}
