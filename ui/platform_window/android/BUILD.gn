# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//build/config/jumbo.gni")
import("//testing/test.gni")

assert(is_android)

jumbo_component("android") {
  output_name = "android_window"
  sources = [
    "android_window_export.h",
    "platform_ime_controller_android.cc",
    "platform_ime_controller_android.h",
    "platform_window_android.cc",
    "platform_window_android.h",
  ]

  defines = [ "ANDROID_WINDOW_IMPLEMENTATION" ]

  deps = [
    ":jni_headers",
    ":platform_window_java",
    "//base",
    "//skia",
    "//ui/events:dom_keycode_converter",
    "//ui/events:events",
    "//ui/events:events_base",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/platform_window",
    "//ui/platform_window/stub",
  ]

  libs = [ "android" ]
}

generate_jni("jni_headers") {
  sources = [
    "java/src/org/chromium/ui/PlatformImeControllerAndroid.java",
    "java/src/org/chromium/ui/PlatformWindowAndroid.java",
  ]
  jni_package = "android_window"
}

android_library("platform_window_java") {
  java_files = [
    "java/src/org/chromium/ui/PlatformImeControllerAndroid.java",
    "java/src/org/chromium/ui/PlatformWindowAndroid.java",
  ]
  deps = [
    "//base:base_java",
  ]
}
