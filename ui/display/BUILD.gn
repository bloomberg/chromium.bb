# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//testing/test.gni")

if (is_chromeos) {
  component("display") {
    sources = [
      "chromeos/apply_content_protection_task.cc",
      "chromeos/apply_content_protection_task.h",
      "chromeos/configure_displays_task.cc",
      "chromeos/configure_displays_task.h",
      "chromeos/display_configurator.cc",
      "chromeos/display_configurator.h",
      "chromeos/display_layout_manager.h",
      "chromeos/display_snapshot_virtual.cc",
      "chromeos/display_snapshot_virtual.h",
      "chromeos/display_util.cc",
      "chromeos/display_util.h",
      "chromeos/ozone/display_configurator_ozone.cc",
      "chromeos/query_content_protection_task.cc",
      "chromeos/query_content_protection_task.h",
      "chromeos/update_display_configuration_task.cc",
      "chromeos/update_display_configuration_task.h",
      "chromeos/x11/display_configurator_x11.cc",
      "chromeos/x11/display_mode_x11.cc",
      "chromeos/x11/display_mode_x11.h",
      "chromeos/x11/display_snapshot_x11.cc",
      "chromeos/x11/display_snapshot_x11.h",
      "chromeos/x11/display_util_x11.cc",
      "chromeos/x11/display_util_x11.h",
      "chromeos/x11/native_display_delegate_x11.cc",
      "chromeos/x11/native_display_delegate_x11.h",
      "chromeos/x11/native_display_event_dispatcher_x11.cc",
      "chromeos/x11/native_display_event_dispatcher_x11.h",
      "display_export.h",
      "display_switches.cc",
      "display_switches.h",
    ]

    defines = [ "DISPLAY_IMPLEMENTATION" ]

    deps = [
      "//base",
      "//ui/display/types",
      "//ui/display/util",
      "//ui/gfx",
      "//ui/gfx/geometry",
    ]

    if (use_x11) {
      sources -= [ "chromeos/ozone/display_configurator_ozone.cc" ]
      configs += [
        "//build/config/linux:x11",
        "//build/config/linux:xext",
        "//build/config/linux:xi",
        "//build/config/linux:xrandr",
      ]
      deps += [ "//ui/events/platform" ]
    }

    deps += []
    if (use_x11) {
      deps += [ "//ui/gfx/x" ]
    }

    if (use_ozone) {
      deps += [ "//ui/ozone" ]
      sources -= [
        "chromeos/x11/display_configurator_x11.cc",
        "chromeos/x11/display_mode_x11.cc",
        "chromeos/x11/display_mode_x11.h",
        "chromeos/x11/display_snapshot_x11.cc",
        "chromeos/x11/display_snapshot_x11.h",
        "chromeos/x11/display_util_x11.cc",
        "chromeos/x11/display_util_x11.h",
        "chromeos/x11/native_display_delegate_x11.cc",
        "chromeos/x11/native_display_delegate_x11.h",
        "chromeos/x11/native_display_event_dispatcher_x11.cc",
        "chromeos/x11/native_display_event_dispatcher_x11.h",
      ]
    }
  }

  component("test_util") {
    output_name = "display_test_util"
    sources = [
      "chromeos/test/test_display_snapshot.cc",
      "chromeos/test/test_display_snapshot.h",
    ]

    defines = [ "DISPLAY_IMPLEMENTATION" ]

    public_deps = [
      ":display",
    ]
    deps = [
      "//base",
      "//ui/display/types",
      "//ui/gfx",
      "//ui/gfx/geometry",
    ]
  }

  source_set("test_support") {
    testonly = true
    sources = [
      "chromeos/test/action_logger.cc",
      "chromeos/test/action_logger.h",
      "chromeos/test/action_logger_util.cc",
      "chromeos/test/action_logger_util.h",
      "chromeos/test/test_display_layout_manager.cc",
      "chromeos/test/test_display_layout_manager.h",
      "chromeos/test/test_native_display_delegate.cc",
      "chromeos/test/test_native_display_delegate.h",
    ]

    public_deps = [
      ":display",
    ]
    deps = [
      "//base",
      "//ui/display/types",
      "//ui/gfx",
      "//ui/gfx:test_support",
      "//ui/gfx/geometry",
    ]
  }
}

# TODO(GYP): Delete this after we've converted everything to GN.
# The _run targets exist only for compatibility w/ GYP.
group("display_unittests_run") {
  testonly = true
  deps = [
    ":display_unittests",
  ]
}

# This test covers the ChromeOS "display" target as well as the cross-platform
# //display/util target.
test("display_unittests") {
  sources = [
    "chromeos/apply_content_protection_task_unittest.cc",
    "chromeos/configure_displays_task_unittest.cc",
    "chromeos/display_configurator_unittest.cc",
    "chromeos/query_content_protection_task_unittest.cc",
    "chromeos/update_display_configuration_task_unittest.cc",
    "chromeos/x11/display_util_x11_unittest.cc",
    "chromeos/x11/native_display_event_dispatcher_x11_unittest.cc",
    "util/display_util_unittest.cc",
    "util/edid_parser_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:run_all_unittests",
    "//testing/gtest",
    "//ui/display/util",
    "//ui/gfx/geometry",
  ]

  if (is_chromeos) {
    deps += [
      ":display",
      ":test_support",
      ":test_util",
      "//base/test:test_support",
      "//ui/display/types",
    ]
  }

  if (use_ozone && is_chromeos) {
    sources -= [
      "chromeos/x11/display_util_x11_unittest.cc",
      "chromeos/x11/native_display_event_dispatcher_x11_unittest.cc",
    ]
  }
}
