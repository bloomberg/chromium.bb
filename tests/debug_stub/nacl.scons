# -*- python -*-
# Copyright 2012 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can
# be found in the LICENSE file.

Import('env')

if not env.AllowInlineAssembly():
  Return()

if env.UnderWindowsCoverage():
  Return()

test_prog = env.ComponentProgram(
    'debugger_test', 'debugger_test.c',
    EXTRA_LIBS=['${PTHREAD_LIBS}', '${NONIRT_LIBS}'])

sel_ldr_command = env.AddBootstrap(env.GetSelLdr(), ['-v', '-f', test_prog])

is_broken = not env.Bit('nacl_static_link') or env.GetSelLdr() is None

if env.Bit('build_arm'):
  # Use the system's 'nm' tool.  The NaCl toolchain will not be
  # available on the ARM hardware bots, but they will be running Linux
  # with a normal GNU toolchain available.
  nm_tool = 'nm'
else:
  # Use the NaCl toolchain's 'nm' tool since it is not available
  # natively on Windows and Mac.
  nm_tool = '${NM}'

node = env.CommandTest(
    'debug_stub_test.out',
    command=(['${PYTHON}', env.File('debug_stub_test.py'), '-v']
             + ['--', env.get('TARGET_FULLARCH'), nm_tool] + sel_ldr_command),
    extra_deps=[env.File('gdb_rsp.py')])
env.AddNodeToTestSuite(node, ['medium_tests', 'nonpexe_tests'],
                       'run_debug_stub_test', is_broken=is_broken)
env.TestBindsFixedTcpPort(node)
