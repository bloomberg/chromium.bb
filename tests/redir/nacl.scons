# -*- python -*-
# Copyright (c) 2011 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import atexit
import os
import tempfile

Import('env')

env.ComponentProgram('redir_test.nexe', 'redir_test.c')

# windows builds swallow stdout and stderr, so we cannot check
# against the golden files when stdout and stderr are not redirected
# to files.
if not env.Bit('host_windows'):
  node = env.CommandSelLdrTestNacl(
      'redir_test_basic.out',
      command=[env.File('redir_test.nexe')],
      stdout_golden=env.File('redir_test.stdout'),
      stderr_golden=env.File('redir_test.stderr'),
      filter_regex="'^REALOUTPUT'",
      )

  env.AddNodeToTestSuite(node,
                         ['small_tests', 'sel_ldr_tests'],
                         'run_redir_basic_test')

# we immediately close to ensure that python is not holding on to a
# file handle, so that on Windows, the Cleanup use of unlink will not
# fail due to us still having an open file handle.
(_, outfile)=tempfile.mkstemp()
os.close(_)
(_, errfile)=tempfile.mkstemp()
os.close(_)

def CleanupBinder(fname):
  def Cleanup():
    try:
      os.unlink(fname)
    except OSError:
      pass
  return Cleanup

atexit.register(CleanupBinder(outfile))
atexit.register(CleanupBinder(errfile))

node = env.CommandSelLdrTestNacl(
    'redir_test.out',
    command=[env.File('redir_test.nexe')],
    filter_regex="'^REALOUTPUT'",
    osenv=['NACL_EXE_STDOUT=%s' % outfile,
           'NACL_EXE_STDERR=%s' % errfile])

env.AddNodeToTestSuite(node,
                       ['small_tests', 'sel_ldr_tests'],
                       'run_redir_test')

node = env.Command('redir_test_output_compare',
                   ['${PYTHON}',
                    env.File('${SCONSTRUCT_DIR}/tools/file_cmp_test.py'),
                    outfile,
                    env.File('redir_test.stdout')],
                   [env.File(outfile)])

env.AddNodeToTestSuite(node,
                       ['small_tests', 'sel_ldr_tests'],
                       'run_redir_test')
