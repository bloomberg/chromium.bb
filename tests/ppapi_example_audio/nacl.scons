# -*- python -*-
# Copyright 2011 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can
# be found in the LICENSE file.

# This is a pepper audio example from examples/audio.

Import('env')

env.Prepend(CPPDEFINES=['XP_UNIX'])

nexe = env.ComponentProgram('ppapi_example_audio.nexe',
                            [ 'audio.cc' ],
                            EXTRA_LIBS=['ppruntime',
                                        'ppapi_cpp',
                                        'imc',
                                        'platform',
                                        'gio',
                                        'pthread',
                                        'm',
                                        'srpc'])

validate = env.CommandValidatorTestNacl('validate_ppapi_example_audio.out',
                                        image=[nexe])
# since we do not have an automated test - at least validate the image
env.AddNodeToTestSuite(validate,
                       ['validator_tests', 'small_tests'],
                       'validate_ppapi_example_audio')

# Note that the html is required to run this program.
env.Publish('ppapi_example_audio.nexe', 'run',
            ['ppapi_example_audio.html' ])

test = env.SelUniversalTest(
    'ppapi_example_audio.out',
     sel_universal_flags=['--command_file',
                          env.File('${SCONSTRUCT_DIR}/tests/ppapi_proxy'
                                   '/sel_universal_ppapi_replay_prolog.stdin'),
                          '--command_file',
                          env.File('ppapi_example_audio.stdin'),
                          ],
    command=[nexe],
    stdout_golden=env.File('ppapi_example_audio.stdout'),
    )

# Note: we use the sysv shm feature in sel_universal which is only available
# on linux.
is_broken = not env.Bit('host_linux')
env.AddNodeToTestSuite(test,
                       ['small_tests', 'sel_ldr_tests'],
                       'run_ppapi_example_audio_test',
                       is_broken=is_broken)
