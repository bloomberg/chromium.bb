# -*- python -*-
# Copyright 2009 The Native Client Authors.  All rights reserved.  Use
# of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

if 'TRUSTED_ENV' not in env:
  Return()

if env.Bit('target_arm'):
  # The test fails in our ARM cross-compile/QEMU environment, but only
  # because the environment lacks libraries, which causes the dlopen()
  # to fail.
  Return()

if env.Bit('disable_hardy64_vmware_failures'):
  # Platform qualification check is unreliable on vmware test bots.
  # Ideally, this test would accept the sel_ldr -Q option, but for now,
  # we skip this test on hardy64 bots.
  Return()

trusted_env = env['TRUSTED_ENV'].Clone()

if trusted_env.get('COVERAGE_ENABLED') and trusted_env.Bit('windows'):
  # Disable the coverage test on Windows.  This is related to bug:
  # http://code.google.com/p/nativeclient/issues/detail?id=1030
  # TODO(dspringer): re-enable this test once the bug is fixed.
  Return()

if not trusted_env.Bit('mac'):
  trusted_env['COMPONENT_STATIC'] = False


if trusted_env.Bit('linux'):
    trusted_env.Append(
        CCFLAGS=['-fPIC', '-Wno-long-long',],
        # Catch unresolved symbols in libraries.
        LINKFLAGS=['-Wl,-z,defs'],
        )

    # We usually try to build things statically, but the plugin is a .so
    trusted_env.FilterOut(LINKFLAGS=['-static'])
    trusted_env.Append(LIBS=['dl', 'pthread'])

if trusted_env.Bit('windows'):
    trusted_env.Append(
        CCFLAGS = ['/EHsc',],
        CPPDEFINES = ['WIN32', '_WINDOWS'],
    )
    trusted_env.Tool('atlmfc_vc80')


trusted_env.Append(LIBS=['ppapi_browser', 'ppapi_plugin', 'platform', 'gio'])

fake_browser = trusted_env.ComponentProgram('fake_browser_ppapi',
                                            ['fake_core.cc',
                                             'fake_file_io.cc',
                                             'fake_file_io_trusted.cc',
                                             'fake_file_ref.cc',
                                             'fake_host.cc',
                                             'fake_instance.cc',
                                             'fake_object.cc',
                                             'fake_resource.cc',
                                             'fake_url_loader.cc',
                                             'fake_url_request_info.cc',
                                             'fake_url_response_info.cc',
                                             'fake_window.cc',
                                             'main.cc',
                                             'test_scriptable.cc'])

ppapi_basic_object_nexe = env.File('${STAGING_DIR}/ppapi_basic_object.nexe')

if trusted_env.Bit('mac'):
  plugin = trusted_env.File('${STAGING_DIR}/ppNaClPlugin')
else:
  plugin = trusted_env.File('${STAGING_DIR}/${SHLIBPREFIX}'
                            'ppNaClPlugin${SHLIBSUFFIX}')
sel_ldr = trusted_env.File('${STAGING_DIR}/${PROGPREFIX}sel_ldr${PROGSUFFIX}')
log_file = env.File('fake_browser_ppapi.out').abspath

node = env.CommandTest('fake_browser_ppapi.out',
                       [fake_browser,
                        plugin,
                        'http://localhost:5103/basic_object.html',
                        # Must use relative nexe url for the test to work.
                        # See TODO in URLLoader::FinishStreamingToFile().
                        '"id=nacl_module;src=ppapi_basic_object.nexe"',
                        '${STAGING_DIR}'
                       ],
                       'medium',
                       osenv=('NACL_SEL_LDR=%s,NACLLOG=%s'
                              % (sel_ldr, log_file)),
                       extra_deps=[sel_ldr, ppapi_basic_object_nexe])

env.AddNodeToTestSuite(node, ['medium_tests'], 'run_fake_browser_ppapi_test')
