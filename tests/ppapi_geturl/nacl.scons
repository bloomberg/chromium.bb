# -*- python -*-
# Copyright (c) 2011 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This is a C PPAPI-based version of ../npapi_geturl.
#
# ppapi_geturl.html - test driver that loads the nexe and scripts it
# ppapi_geturl_success.html - to be url-loaded and displayed w/n driver html
#
# ppapi_geturl.cc - implementation of PPP interface
# module.h/cc - implementation of PPP_Instance interface
# scriptable_object.h/cc - implementation of the scripting interface
# url_load_request.h/cc - url loading helper
# nacl_file_main.cc - uses main() to test NaClFile interface

Import('env')

env.Prepend(CPPDEFINES=['XP_UNIX'])

ppapi_geturl_target = 'ppapi_geturl_%s.nexe' % env.get('TARGET_FULLARCH')
env.Alias('ppapi_geturl.nexe', ['$STAGING_DIR/%s' % ppapi_geturl_target])

ppapi_geturl_nexe = env.ComponentProgram(ppapi_geturl_target,
                                         ['nacl_file_main.cc',
                                          'module.cc',
                                          'ppapi_geturl.cc',
                                          'scriptable_object.cc',
                                          'url_load_request.cc',
                                          ],
                                         EXTRA_LIBS=[
                                             'nacl_file',
                                             'ppruntime',
                                             'srpc',
                                             'imc',
                                             'imc_syscalls',
                                             'platform',
                                             'gio',
                                             'pthread',
                                             'm',
                                             '${NON_PPAPI_BROWSER_LIBS}'],
                                         EXTRA_LINKFLAGS=['-Wl,--wrap=read',
                                                          '-Wl,--wrap=open',
                                                          '-Wl,--wrap=lseek',
                                                          '-Wl,--wrap=close'])

# Note that the html and a .nmf manifest is required to run this program.
dest_copy = env.Replicate('$STAGING_DIR',
            ['ppapi_geturl.html',
             'ppapi_geturl.nmf',
             'ppapi_geturl_success.html',
             env.File('${SCONSTRUCT_DIR}/tools/browser_tester/browserdata/'
                      'nacltest.js'),
             ])
env.Depends(ppapi_geturl_target, dest_copy)

node = env.PPAPIBrowserTester(
    'ppapi_geturl_browser_test.out',
     url='ppapi_geturl.html',
     files=[ppapi_geturl_nexe,
            env.File('ppapi_geturl.html'),
            env.File('ppapi_geturl.nmf'),
            env.File('ppapi_geturl_success.html'),
            env.File('${SCONSTRUCT_DIR}/tools/browser_tester/browserdata/'
                     'nacltest.js'),
            ],
     args=['--allow_404', '--enable_experimental_js'],
     )
env.AddNodeToTestSuite(node,
                       ['chrome_browser_tests'],
                       'run_ppapi_geturl_browser_test',
                       is_broken=env.PPAPIBrowserTesterIsBroken()
                       )
