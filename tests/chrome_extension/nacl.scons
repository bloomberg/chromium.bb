# -*- python -*-
# Copyright (c) 2011 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Test loading a NaCl-enabled Chrome Extension.

Import('env')

# BUG(adonovan): test is breaking on ARM.  Disable in haste...
if env.Bit('target_arm'):
  Return()


nexe = 'ppapi_test_example_${TARGET_FULLARCH}${PROGSUFFIX}'

# Note: this test uses the nexe, manifest.json, background.html and
# test_bridge.js, but these files are loaded from the disk as part of the
# installed Chrome extension and not loaded through the tester's web server.
# These files need their own install command.
dest_copy = env.Replicate('$STAGING_DIR/chrome_extension',
                          [env.File('$STAGING_DIR/%s' % nexe),
                           env.File('$STAGING_DIR/ppapi_test_example.nmf'),
                           'background.html',
                           'manifest.json',
                           'test_bridge.js'])

node = env.PPAPIBrowserTester(
    'chrome_extension_test.out',
    url='chrome_extension_test.html',
    extensions=[env.Dir('$STAGING_DIR/chrome_extension')],
    files=[env.File('chrome_extension_test.html')]
    )
env.Depends(node, dest_copy)
env.Alias('all_programs', dest_copy)

env.AddNodeToTestSuite(
    node,
    ['chrome_browser_tests'],
    'run_chrome_extension_test',
    is_broken=env.PPAPIBrowserTesterIsBroken())
