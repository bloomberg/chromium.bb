# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/compiler/compiler.gni")

group("telemetry_chrome_test") {
  data_deps = [
    # TODO(kbr): this used to be "//chrome". Had to change this to
    # chrome_initial temporarily to work around crbug.com/536192.
    "//chrome:chrome_initial",
    "//third_party/catapult/telemetry:bitmaptools",
  ]

  data = [
    "//tools/perf/chrome_telemetry_build/",
    "//third_party/catapult/",
  ]

  if (is_win) {
    data_deps += [ "//chrome/tools/crash_service" ]
    data += [ "//components/crash/content/tools/generate_breakpad_symbols.py" ]
  }

  if (is_linux) {
    data_deps += [ "//tools/xdisplaycheck" ]
  }

  if (is_win && (symbol_level == 1 || symbol_level == 2)) {
    # TODO(GYP): These should be provided automatically through data_deps.
    data += [ "$root_out_dir/chrome.exe.pdb" ]
    if (is_component_build) {
      data += [
        "$root_out_dir/base.dll.pdb",
        "$root_out_dir/blink_platform.dll.pdb",
        "$root_out_dir/blink_web.dll.pdb",
        "$root_out_dir/content.dll.pdb",
      ]
    }
  }
}
