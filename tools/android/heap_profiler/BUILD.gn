# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

shared_library("heap_profiler") {
  sources = [
    "heap_profiler_hooks_android.c",
  ]
  public_deps = [
    ":heap_profiler_core",
  ]
}

source_set("heap_profiler_core") {
  sources = [
    "heap_profiler.c",
    "heap_profiler.h",
  ]

  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [ "//build/config/compiler:no_chromium_code" ]
}

executable("heap_dump") {
  sources = [
    "heap_dump.c",
  ]

  deps = [
    ":heap_profiler_core",
  ]

  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [ "//build/config/compiler:no_chromium_code" ]
}

test("heap_profiler_unittests") {
  sources = [
    "heap_profiler_unittest.cc",
  ]
  deps = [
    ":heap_profiler_core",
    "//testing/gtest",
    "//testing/gtest:gtest_main",
  ]
}

executable("heap_profiler_integrationtest") {
  testonly = true

  sources = [
    "heap_profiler_integrationtest.cc",
  ]
  deps = [
    "//testing/gtest",
    ":heap_profiler",
  ]

  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [ "//build/config/compiler:no_chromium_code" ]
}
