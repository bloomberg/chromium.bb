# Copyright (c) 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/crypto.gni")
import("//build/config/linux/pkg_config.gni")

config("ssl_preprocessor_flags") {
  if (use_openssl) {
    defines = [ "USE_OPENSSL" ]
  } else if (use_nss) {
    defines = [ "USE_NSS" ]
  }
}

# This meta-target will include the SSL library according to the build flags.
group("metassl") {
  direct_dependent_configs = [ ":ssl_preprocessor_flags" ]

  if (use_openssl) {
    assert(is_linux)
    deps = [ "//third_party/openssl" ]
  } else {
    deps = [ "//net/third_party/nss:ssl" ]
  }

  forward_dependent_configs_from = deps
}
