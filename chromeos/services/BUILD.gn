# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//chromeos/assistant/assistant.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/tools/test/service_test.gni")
import("//testing/test.gni")

# This is modeled after //services:services_unittests
# One Big Target for services to register their unit test sources. This exists
# to avoid having to maintain a separate test binary for every service.
#
# To add tests for a new service, please define a "tests" source_set in the
# service subdirectory and add it as a dependency here. If your unit tests
# use the ServiceTest framework, you must also include corresponding catalog
# entries in the "chromeos_services_unittests_catalog" target below.
service_test("chromeos_services_unittests") {
  deps = []

  if (enable_cros_assistant) {
    deps += [ "//chromeos/services/assistant:tests" ]
  }

  data_deps = [
    "//testing/buildbot/filters:services_unittests_filters",
  ]

  catalog = ":chromeos_services_unittests_catalog"
}

catalog("chromeos_services_unittests_catalog") {
  testonly = true

  catalog_deps = []

  if (enable_cros_assistant) {
    catalog_deps += [ "//chromeos/services/assistant:tests_catalog" ]
  }
}
