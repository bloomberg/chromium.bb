# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromeos/services/ime/public/features.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")

assert(is_chromeos, "Non-ChromeOS builds cannot depend on //chromeos")

source_set("lib") {
  sources = [
    "ime_service.cc",
    "ime_service.h",
    "input_engine.cc",
    "input_engine.h",
    "rulebased/controller.cc",
    "rulebased/controller.h",
    "rulebased/def/ar.h",
    "rulebased/def/ckb_ar.h",
    "rulebased/def/ckb_en.h",
    "rulebased/def/fa.h",
    "rulebased/def/km.h",
    "rulebased/def/lo.h",
    "rulebased/def/ne_inscript.h",
    "rulebased/def/ru_phone_aatseel.h",
    "rulebased/def/ru_phone_yazhert.h",
    "rulebased/def/ta_inscript.h",
    "rulebased/def/ta_typewriter.h",
    "rulebased/def/th.h",
    "rulebased/def/th_pattajoti.h",
    "rulebased/def/th_tis.h",
    "rulebased/rules_data.cc",
    "rulebased/rules_data.h",
  ]

  deps = [
    "//base",
    "//chromeos/services/ime/public/cpp:buildflags",
    "//chromeos/services/ime/public/mojom",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/mojom",
  ]

  if (enable_cros_ime_decoder) {
    sources += [
      "decoder/decoder_engine.cc",
      "decoder/decoder_engine.h",
    ]
  }
}

service_manifest("manifest") {
  name = "ime"
  source = "manifest.json"
}

source_set("unit_tests") {
  testonly = true
  deps = [
    ":lib",
    "//base",
    "//testing/gmock",
    "//testing/gtest:gtest",
  ]
  sources = [
    "rulebased/rulebased_unittest.cc",
  ]
}

source_set("services_unittests") {
  testonly = true
  deps = [
    ":lib",
    "//base",
    "//chromeos/services/ime/public/mojom",
    "//mojo/public/cpp/bindings",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp:service_test_support",
    "//testing/gmock",
  ]
  sources = [
    "ime_service_unittest.cc",
  ]
}

service_manifest("unittest_manifest") {
  name = "ime_unittests"
  source = "test_manifest.json"
  packaged_services = [ ":manifest" ]
}

catalog("tests_catalog") {
  testonly = true
  embedded_services = [ ":unittest_manifest" ]
}
