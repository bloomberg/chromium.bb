# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_chromeos, "Non-ChromeOS builds cannot depend on //chromeos")

component("constants") {
  output_name = "chromeos_ime_constants"
  defines = [ "IS_CHROMEOS_IME_CONSTANTS_IMPL" ]
  deps = [
    "//base",
    "//build:branding_buildflags",
  ]
  sources = [
    "constants.cc",
    "constants.h",
  ]
}

source_set("lib") {
  sources = [
    "decoder/decoder_engine.cc",
    "decoder/decoder_engine.h",
    "decoder/downloader_impl.cc",
    "decoder/downloader_impl.h",
    "ime_service.cc",
    "ime_service.h",
    "input_engine.cc",
    "input_engine.h",
  ]

  deps = [
    ":constants",
    "//base",
    "//chromeos/constants",
    "//chromeos/services/ime/public/cpp:buildflags",
    "//chromeos/services/ime/public/cpp:rulebased",
    "//chromeos/services/ime/public/cpp/shared_lib:interfaces",
    "//chromeos/services/ime/public/mojom",
  ]
}

source_set("sandbox_hook") {
  sources = [
    "ime_sandbox_hook.cc",
    "ime_sandbox_hook.h",
  ]

  deps = [
    ":constants",
    "//base",
    "//chromeos/services/ime/public/cpp:buildflags",
    "//sandbox/linux:sandbox_services",
    "//services/service_manager/sandbox:sandbox",
  ]
}

source_set("unit_tests") {
  testonly = true
  deps = [ "//chromeos/services/ime/public/cpp:rulebased_unit_tests" ]
}

source_set("services_unittests") {
  testonly = true
  deps = [
    ":lib",
    "//base",
    "//base/test:test_support",
    "//chromeos/services/ime/public/mojom",
    "//mojo/public/cpp/bindings",
    "//services/network:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
  sources = [ "ime_service_unittest.cc" ]
}
