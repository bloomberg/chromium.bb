# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")
import("//third_party/closure_compiler/compile_js.gni")

assert(is_chromeos, "Media App is Chrome OS only")

static_library("media_app_ui") {
  sources = [
    "media_app_guest_ui.cc",
    "media_app_guest_ui.h",
    "media_app_ui.cc",
    "media_app_ui.h",
    "url_constants.cc",
    "url_constants.h",
  ]

  deps = [
    "//chromeos/constants",
    "//chromeos/resources:media_app_bundle_resources",
    "//chromeos/resources:media_app_resources",
    "//content/public/browser",
    "//ui/webui",
  ]
}

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [
    "test/media_app_guest_ui_browsertest.js",
    "test/media_app_ui_browsertest.js",
  ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    ":browser_test_support",
    "//chromeos/constants",
  ]
}

source_set("browser_test_support") {
  testonly = true
  sources = [
    "test/media_app_ui_browsertest.cc",
    "test/media_app_ui_browsertest.h",
  ]

  deps = [
    ":media_app_ui",
    "//base/test:test_support",
    "//chrome/test:test_support_ui",
    "//chromeos/constants:constants",
    "//content/test:test_support",
  ]

  data = [
    "test/driver.js",
    "test/driver_api.js",
    "test/guest_query_receiver.js",
  ]
}

group("closure_compile") {
  testonly = true
  deps = [
    ":closure_compile_tests",
    "resources/js:closure_compile",
    "resources/mock/js:closure_compile",
  ]
}

# Note we compile with reportUnknownTypes while it works, but if dependencies
# get more complex, we should remove it and only enable in developer builds.
js_type_check("closure_compile_tests") {
  testonly = true
  closure_flags = default_closure_args + [
                    "jscomp_error=strictCheckTypes",
                    "jscomp_error=reportUnknownTypes",
                  ]
  deps = [
    ":test_driver_api_js",
    ":test_driver_js",
    ":test_guest_query_receiver_js",
  ]
}

js_library("test_driver_api_js") {
  testonly = true
  sources = [
    "test/driver_api.js",
  ]
}

js_library("test_guest_query_receiver_js") {
  testonly = true
  sources = [
    "test/guest_query_receiver.js",
  ]
  deps = [
    ":test_driver_api_js",
  ]
}

js_library("test_driver_js") {
  testonly = true
  sources = [
    "test/driver.js",
  ]
  deps = [
    ":test_driver_api_js",
    "//ui/webui/resources/js:assert",
  ]
}
